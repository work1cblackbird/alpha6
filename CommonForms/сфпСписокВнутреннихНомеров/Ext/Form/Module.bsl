#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события фомы "ПриСозданииНаСервере"
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ТекущийНомер") Тогда
		ЭтаФорма.ТекущийНомер = Параметры.ТекущийНомер;
	КонецЕсли;	
	// АльфаАвто
	Если Параметры.Свойство("сфпИспользоватьСофтФон") Тогда
		сфпИспользоватьСофтФон = Параметры.сфпИспользоватьСофтФон;
	КонецЕсли;
	// Конец АльфаАвто
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события фомы "ПриОткрытии"
//
Процедура ПриОткрытии(Отказ)
	Если сфпСофтФонПроКлиент.сфпПроверитьДоступностьСофтФон(Истина, сфпИспользоватьСофтФон) Тогда // АльфаАвто
		сфпСофтФонПроКлиент.сфпПолучитьСостоянияЛиний();
	КонецЕсли;	
КонецПроцедуры // ПриОткрытии()

&НаКлиенте
// Процедура - обработчик события фомы "ОбработкаОповещения"
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "СофтФон_OnAllLinesInfo" Тогда
		ЗаполнитьТаблицуПоМассивуНомеров(Параметр);
	КонецЕсли;
КонецПроцедуры // ОбработкаОповещения()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
// Процедура - обработчик события "Выбор" таблицы формы "ТаблицаТелефонныеНомера"
//
Процедура ТаблицаТелефонныеНомераВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыбореИЗакрыть();
КонецПроцедуры // ТаблицаТелефонныеНомераВыбор()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик команды формы "ОК"
//
Процедура ОК(Команда)
	ОповеститьОВыбореИЗакрыть();
КонецПроцедуры // ОК()

&НаКлиенте
// Процедура - обработчик команды формы "Отмена"
//
Процедура Отмена(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры // Отмена()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
// Процедура заполняет таблицу по массиву номеров
//
// Параметры:
//	МассивНомеров	- Массив	- Массив структур с данными номеров
//
Процедура ЗаполнитьТаблицуПоМассивуНомеров(МассивНомеров)
	Для Каждого ЭлементМассива Из МассивНомеров Цикл
		Если ПустаяСтрока(ЭлементМассива.Number) Тогда Продолжить КонецЕсли;
		ОчищенныйНомер = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(ЭлементМассива.Number);
		Если СтрДлина(ОчищенныйНомер) = СтрДлина(ЭлементМассива.Number) Тогда
			НоваяСтрока					= ЭтаФорма.ТаблицаТелефонныеНомера.Добавить();
			НоваяСтрока.Состояние		= ЭлементМассива.LineState - 1;
			НоваяСтрока.ИмяЛинии		= ЭлементМассива.LineName;
			НоваяСтрока.НомерТелефона	= ЭлементМассива.Number;
		КонецЕсли;
	КонецЦикла;
	Если НЕ ПустаяСтрока(ЭтаФорма.ТекущийНомер) Тогда
		НайденныеСтроки = ЭтаФорма.ТаблицаТелефонныеНомера.НайтиСтроки(Новый Структура("НомерТелефона", ЭтаФорма.ТекущийНомер));	
		Если НайденныеСтроки.Количество() = 1 Тогда
			ЭтаФорма.Элементы.ТаблицаТелефонныеНомера.ТекущаяСтрока = ЭтаФорма.ТаблицаТелефонныеНомера.Индекс(НайденныеСтроки[0]);
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры // ЗаполнитьТаблицуПоМассивуНомеров()

&НаКлиенте
// Процедура оповещеает о выборе номера и закрывает форму
//
// Параметры:
//	Нет.
//
Процедура ОповеститьОВыбореИЗакрыть()
	ТД = Элементы.ТаблицаТелефонныеНомера.ТекущиеДанные;
	Если ТД = Неопределено Тогда Возврат; КонецЕсли;	
	Если НЕ (ТД.НомерТелефона = ТекущийНомер) Тогда
		Оповестить("сфпВыбранТелефонныйНомерПользователя", ТД.НомерТелефона);
	КонецЕсли;	
	ЭтаФорма.Закрыть();
КонецПроцедуры // ОповеститьОВыбореИЗакрыть()	

#КонецОбласти