
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Номенклатура", ТекущаяНоменклатура);
	
	Если ТекущаяНоменклатура.Пустая() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Истина;
	
	// Заполним параметры поиска источников обеспечения
	Параметры.Свойство("СкладКомпании"        , СкладКомпании);
	Параметры.Свойство("ПодразделениеКомпании", ПодразделениеКомпании);
	Параметры.Свойство("Контрагент"           , Контрагент);
	Параметры.Свойство("Валюта"               , Валюта);
	Параметры.Свойство("ТипЦен"               , ТипЦен);
	Параметры.Свойство("ТекущаяСтрока"        , ТекущаяСтрока);
	
	ПоискВПрайсЛистах.СписокИсточниковОбеспеченияПоНоменклатуре(
		ТекущаяНоменклатура, ПараметрыПоискаИсточниковИЦен(), ИсточникиОбеспеченияНоменклатуры);
	
	Заголовок = СтрШаблон("Источники обеспечения <%1>", ТекущаяНоменклатура);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсточникиОбеспеченияНоменклатуры

&НаКлиенте
Процедура ИсточникиОбеспеченияНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Выбрать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда = Неопределено)
	
	ТекущиеДанные = Элементы.ИсточникиОбеспеченияНоменклатуры.ТекущиеДанные;
	
	Результат = Новый Структура("Цена,Поставщик,НаправлениеПоставки,КлючСтрокиПоставщика,СрокПоставкиВСтроке,ТекущаяСтрока");
	ЗаполнитьЗначенияСвойств(Результат, ТекущиеДанные);
	
	Результат.СрокПоставкиВСтроке = ТекущиеДанные.СрокПоставки;
	Результат.ТекущаяСтрока       = ТекущаяСтрока;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыПоискаИсточниковИЦен()
	
	Результат = Новый Структура;
	
	Результат.Вставить("СкладКомпании"           , СкладКомпании);
	Результат.Вставить("ПодразделениеКомпании"   , ПодразделениеКомпании);
	Результат.Вставить("Контрагент"              , Контрагент);
	Результат.Вставить("Валюта"                  , Валюта);
	Результат.Вставить("ДобавлятьСлужебныеСтроки", Ложь);
	Результат.Вставить("ВыводитьАналоги"         , Ложь);
	Результат.Вставить("РежимДерева"             , Ложь);
	
	Если НЕ ТипЦен.Пустая() Тогда
		Результат.Вставить("ТипЦен", ТипЦен);
	Иначе
		Результат.Вставить("ТипЦен", ПраваИНастройкиПользователя.Значение("ОсновнойТипЦенПродажи"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти