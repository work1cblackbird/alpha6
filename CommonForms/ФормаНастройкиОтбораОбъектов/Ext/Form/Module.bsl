
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектОтбора               = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ОбъектОтбора");
	НастройкаОтбора            = ПолучитьЗначениеПараметраСтруктуры(Параметры, "НастройкаОтбора");
	ИдентификаторОсновнойФормы = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ИдентификаторОсновнойФормы");
	
	Если ОбъектОтбора = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПолноеИмяОбъекта = ОбъектОтбора.ПолноеИмя;
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
	
	МассивОбщихРеквизитов = Новый Массив;
	МассивИменаРеквизитов = Новый Массив;
	Для Каждого МетаРеквизит Из Метаданные.ОбщиеРеквизиты Цикл
		ЗначениеСостава = МетаРеквизит.Состав.Найти(ОбъектМетаданных);
		Если ЗначениеСостава=Неопределено ИЛИ (НЕ ЗначениеСостава.Использование=Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать) Тогда
			Продолжить;
		КонецЕсли;
		МассивОбщихРеквизитов.Добавить(МетаРеквизит);
		МассивИменаРеквизитов.Добавить(МетаРеквизит.Имя);
	КонецЦикла;
	
	СхемаКомпоновки = Новый СхемаКомпоновкиДанных;
	ИсточникДанных = СхемаКомпоновки.ИсточникиДанных.Добавить();
	ИсточникДанных.ТипИсточникаДанных = "Local";
	
	НаборДанных = СхемаКомпоновки.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	МассивИменаРеквизитов.Добавить(ДоступныеРеквизитыОтбора(ОбъектМетаданных));
	НаборДанных.Запрос = "ВЫБРАТЬ " + СтрСоединить(МассивИменаРеквизитов, ",") + " ИЗ " + ПолноеИмяОбъекта;
	НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
	
	АдресСхемыКомпоновки = ПоместитьВоВременноеХранилище(СхемаКомпоновки, УникальныйИдентификатор);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновки));
	
	Если НЕ НастройкаОтбора = Неопределено И НЕ ПустаяСтрока(НастройкаОтбора)
		И ЭтоАдресВременногоХранилища(НастройкаОтбора) Тогда
		КомпоновкаНастройкиДанных = ПолучитьИзВременногоХранилища(НастройкаОтбора);
		Если НЕ КомпоновкаНастройкиДанных = Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(КомпоновкаНастройкиДанных);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Закрыть(АдресНастроекКомпоновщикаОтбора());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДоступныеРеквизитыОтбора(ОбъектМетаданных)
	МассивРеквизитов = Новый Массив;
	Для Каждого РеквизитМетаданные Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
		Если НЕ РеквизитМетаданные.Тип.СодержитТип(Тип("ХранилищеЗначения")) Тогда
			МассивРеквизитов.Добавить(РеквизитМетаданные.Имя);
		КонецЕсли
	КонецЦикла;
	Для Каждого РеквизитМетаданные Из ОбъектМетаданных.Реквизиты Цикл
		Если НЕ РеквизитМетаданные.Тип.СодержитТип(Тип("ХранилищеЗначения")) Тогда
			МассивРеквизитов.Добавить(РеквизитМетаданные.Имя);
		КонецЕсли
	КонецЦикла;
	Для Каждого ТабличнаяЧастьМетаданные Из ОбъектМетаданных.ТабличныеЧасти Цикл
		Если НЕ РеквизитМетаданные.Тип.СодержитТип(Тип("ХранилищеЗначения")) Тогда
			МассивРеквизитов.Добавить(ТабличнаяЧастьМетаданные.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(МассивРеквизитов, ",");
КонецФункции

&НаСервере
Функция АдресНастроекКомпоновщикаОтбора()
	Возврат ПоместитьВоВременноеХранилище(КомпоновщикНастроек.Настройки, ИдентификаторОсновнойФормы);
КонецФункции

#КонецОбласти

