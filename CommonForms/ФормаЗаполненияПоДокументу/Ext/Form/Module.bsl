#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтотОбъект,
		,
		"Товары.ВладелецХарактеристики"
	);
	
	ПараметрыЗакрытия = Новый Структура("ИмяТабличнойЧасти,ИмяРеквизитаКоличество,ЕстьЯчейка,ПолноеИмяОбъекта");
	ЗаполнитьЗначенияСвойств(ПараметрыЗакрытия, Параметры);
	Элементы.ТоварыЦена.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ДокументОснованиеПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТоварыНоменклатураПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекстОшибки = "";
	Если НЕ ПроверитьДанныеДляВыгрузки(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	АдресХранилища = ВДокумент();
	
	ПараметрыЗакрытия.Вставить("Корзина", АдресХранилища);
	ПараметрыЗакрытия.Вставить("ПараметрыДействия", Новый Структура);
	Если ВыгружатьЦену Тогда
		ПараметрыЗакрытия.Вставить("ЗагружатьЦены");
	КонецЕсли;
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьЦенуПриИзменении(Элемент)
	
	Элементы.ТоварыЦена.Видимость = ВыгружатьЦену;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ТоварыНоменклатураПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	ОбработатьИзменениеНоменклатура(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьДанныеДляВыгрузки(ТекстОшибки)
	
	Если Товары.Количество() = 0 Тогда
		
		ТекстОшибки = НСтр("ru = 'Нет данных для загрузки в документ'");
		Возврат Ложь;
		
	КонецЕсли;
	
	СтрокаВыбрана = Ложь;
	Для Каждого Строка Из Товары Цикл
		
		Если Строка.Пометка Тогда
			
			СтрокаВыбрана = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ СтрокаВыбрана Тогда
		
		ТекстОшибки = НСтр("ru = 'Не выбраны данные для загрузки в документ'");
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ДокументОснованиеПриИзмененииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	СкладКомпании = Неопределено;
	Если ЕстьРеквизит(ДокументОснование, "СкладКомпании") Тогда
		СкладКомпании = ДокументОснование.СкладКомпании;
	КонецЕсли;
	
	Товары.Очистить();
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Товары.Коэффициент КАК Коэффициент,
	|	СУММА(%2) КАК Количество,
	|	МАКСИМУМ(%3) КАК Цена,
	|	ИСТИНА КАК Пометка,
	|	&СкладКомпании КАК СкладКомпании
	|ИЗ
	|	Документ.%1.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.ЕдиницаИзмерения,
	|	Товары.Коэффициент";
	
	ЕстьМатериалы = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОснование, "Материалы");
	Если ЕстьМатериалы Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОбъединеннаяТаблица.Номенклатура КАК Номенклатура,
		|	ОбъединеннаяТаблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОбъединеннаяТаблица.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ОбъединеннаяТаблица.Коэффициент КАК Коэффициент,
		|	ОбъединеннаяТаблица.СкладКомпании КАК СкладКомпании,
		|	СУММА(ОбъединеннаяТаблица.Количество) КАК Количество,
		|	МАКСИМУМ(ОбъединеннаяТаблица.Цена) КАК Цена,
		|	ИСТИНА КАК Пометка
		|ИЗ
		|	(ВЫБРАТЬ
		|		Товары.Номенклатура КАК Номенклатура,
		|		Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Товары.Коэффициент КАК Коэффициент,
		|		Товары.СкладКомпании КАК СкладКомпании,
		|		%2 КАК Количество,
		|		%3 КАК Цена
		|	ИЗ
		|		Документ.%1.Товары КАК Товары
		|	ГДЕ
		|		Товары.Ссылка = &Ссылка
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Материалы.Номенклатура,
		|		Материалы.ХарактеристикаНоменклатуры,
		|		Материалы.ЕдиницаИзмерения,
		|		Материалы.Коэффициент,
		|		Материалы.СкладКомпании,
		|		%4,
		|		%5
		|	ИЗ
		|		Документ.%1.Материалы КАК Материалы
		|	ГДЕ
		|		Материалы.Ссылка = &Ссылка) КАК ОбъединеннаяТаблица
		|СГРУППИРОВАТЬ ПО
		|	ОбъединеннаяТаблица.Номенклатура,
		|	ОбъединеннаяТаблица.ХарактеристикаНоменклатуры,
		|	ОбъединеннаяТаблица.ЕдиницаИзмерения,
		|	ОбъединеннаяТаблица.Коэффициент,
		|	ОбъединеннаяТаблица.СкладКомпании";
		Если НЕ ЕстьРеквизит(ДокументОснование, "СкладКомпании", "Товары") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Товары.СкладКомпании", "&СкладКомпании");
		КонецЕсли;
		Если НЕ ЕстьРеквизит(ДокументОснование, "СкладКомпании", "Материалы") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Материалы.СкладКомпании", "&СкладКомпании");
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = СтрШаблон(
		ТекстЗапроса,
		ДокументОснование.Метаданные().Имя,
		?(ЕстьРеквизит(ДокументОснование, "Количество", "Товары"), "Товары.Количество", "0"),
		?(ЕстьРеквизит(ДокументОснование, "Цена", "Товары"), "Товары.Цена", "0"),
		?(ЕстьРеквизит(ДокументОснование, "Количество", "Материалы"), "Материалы.Количество", "0"),
		?(ЕстьРеквизит(ДокументОснование, "Цена", "Материалы"), "Материалы.Цена", "0")
	);
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("ЕстьКоличество", ЕстьРеквизит(ДокументОснование, "Количество", "Товары"));
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		ПараметрыДействия = Новый Структура;
		ОбработатьИзменениеНоменклатура(НоваяСтрока);
		Если НоваяСтрока.Количество = 0 Тогда
			НоваяСтрока.Количество = 1;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВДокумент()
	
	ТаблицаТоваров = РеквизитФормыВЗначение("Товары");
	ТаблицаТоваров = ТаблицаТоваров.Скопировать(Новый Структура("Пометка", Истина));
	
	Если НЕ ВыгружатьЦену Тогда
		Для Каждого Строка Из ТаблицаТоваров Цикл
			Строка.Цена = 0;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаТоваров));
	
КонецФункции

Процедура ОбработатьИзменениеНоменклатура(Строка)
	
	Номенклатура = Строка.Номенклатура;
	
	ИспользованиеХарактеристик   = Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик;
	ИспользованиеЕдиницИзмерения = Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения;
	
	Если ИспользованиеХарактеристик = 1 Тогда
		Строка.ВладелецХарактеристики = Номенклатура.ТипНоменклатуры;
	ИначеЕсли ИспользованиеХарактеристик = 2 Тогда
		Строка.ВладелецХарактеристики = Номенклатура;
	Иначе
		Строка.ВладелецХарактеристики = Неопределено;
	КонецЕсли;
	
	Если ИспользованиеЕдиницИзмерения = 1 Тогда
		Строка.ВладелецЕдиницыИзмерения = Номенклатура.ТипНоменклатуры;
	ИначеЕсли ИспользованиеЕдиницИзмерения = 2 Тогда
		Строка.ВладелецЕдиницыИзмерения = Номенклатура;
	Иначе
		Строка.ВладелецЕдиницыИзмерения = Неопределено;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти