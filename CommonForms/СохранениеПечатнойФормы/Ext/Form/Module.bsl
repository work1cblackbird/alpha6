///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивОграниченийФорматовСохранения = СтрРазделить(Параметры.ОграничениеФорматовСохранения, ",", Ложь);
	
	// заполнение списка форматов
	Для Каждого ФорматСохранения Из УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента() Цикл
		Если Не МассивОграниченийФорматовСохранения.Количество()
			ИЛИ МассивОграниченийФорматовСохранения.Найти(ФорматСохранения.Расширение) <> Неопределено Тогда
				
			ВыбранныеФорматыСохранения.Добавить(Строка(ФорматСохранения.ТипФайлаТабличногоДокумента), ФорматСохранения.Представление, Ложь, ФорматСохранения.Картинка);
		КонецЕсли;
	КонецЦикла;
	ВыбранныеФорматыСохранения[0].Пометка = Истина; // По умолчанию выбран только первый формат из списка.
	
	Если ВыбранныеФорматыСохранения.Количество() = 1 Тогда
		Элементы.ГруппаВыборФорматов.Видимость = Ложь;
		СтандартныеПодсистемыСервер.УстановитьКлючНазначенияФормы(ЭтотОбъект, "БезВыбораФормата");
	ИначеЕсли ВыбранныеФорматыСохранения.Количество() > 1 Тогда
		СтандартныеПодсистемыСервер.УстановитьКлючНазначенияФормы(ЭтотОбъект, "");
	КонецЕсли;
	
	// Место сохранения по умолчанию.
	ВариантСохранения = "СохранитьВПапку";
	
	// настройка видимости
	ЕстьВозможностьСохранения = Параметры.ОбъектыПечати.Количество() > 0;
	
	ОбъектыПрикрепления = ПолучитьОбъектыДляПрикрепления(Параметры.ОбъектыПечати);
	Если Параметры.ОбъектыПечати.Количество() = 1 Тогда
		ЕстьВозможностьПрисоединения = ОбъектыПрикрепления[0].Пометка;
	ИначеЕсли ЕстьВозможностьСохранения Тогда
		ЕстьВозможностьПрисоединения = Ложь;
		Для Каждого ОбъектДляПрикрепления Из ОбъектыПрикрепления Цикл
			ЕстьВозможностьПрисоединения = ЕстьВозможностьПрисоединения Или ОбъектДляПрикрепления.Пометка;			
		КонецЦикла;
	Иначе
		ЕстьВозможностьПрисоединения = Ложь;
	КонецЕсли;
	
	ЭлементПрисоединения = Элементы.ВариантСохранения.СписокВыбора.НайтиПоЗначению("Присоединить");
	
	Если Не ЕстьВозможностьПрисоединения Тогда
		Элементы.ВариантСохранения.СписокВыбора.Удалить(ЭлементПрисоединения);
		ЭлементПрисоединения = Неопределено;
		ВариантСохранения = "СохранитьВПапку";
		Элементы.ВариантСохранения.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Элементы.ВыборМестаСохраненияФайла.Видимость = Параметры.РасширениеДляРаботыСФайламиПодключено 
		Или ЕстьВозможностьСохранения;
	Элементы.ВариантСохранения.Видимость = ЕстьВозможностьСохранения;
	Если Не ЕстьВозможностьСохранения Тогда
		Элементы.ПапкаДляСохраненияФайлов.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	КонецЕсли;
	Элементы.ПапкаДляСохраненияФайлов.Видимость = Параметры.РасширениеДляРаботыСФайламиПодключено;
	
	Если Параметры.ОбъектыПечати.Количество() > 1 И ЭлементПрисоединения <> Неопределено Тогда
		ЭлементПрисоединения.Представление = НСтр("ru='Присоединить к документам'")
				+ " (" + Формат(Параметры.ОбъектыПечати.Количество(), "ЧДЦ=0;") + ")";
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Авто;
		Элементы.КнопкаСохранить.Отображение = ОтображениеКнопки.Картинка;
		Элементы.Подписать.Видимость = Ложь;
	КонецЕсли;
	
	// АльфаАвто
	// Добавим элементы формы
	СохранениеПечатнойФормыАльфаАвто.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец АльфаАвто
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если ВыбранныеФорматыСохранения.Количество() <> 1 Тогда
		ФорматыСохраненияИзНастроек = Настройки["ВыбранныеФорматыСохранения"];
		Если ФорматыСохраненияИзНастроек <> Неопределено Тогда
			Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл 
				ФорматИзНастроек = ФорматыСохраненияИзНастроек.НайтиПоЗначению(ВыбранныйФормат.Значение);
				Если ФорматИзНастроек <> Неопределено Тогда
					ВыбранныйФормат.Пометка = ФорматИзНастроек.Пометка;
				КонецЕсли;
			КонецЦикла;
			Настройки.Удалить("ВыбранныеФорматыСохранения");
		КонецЕсли;
	Иначе
		Настройки.Удалить("ВыбранныеФорматыСохранения");
	КонецЕсли;
	
	Если Элементы.ВариантСохранения.ТолькоПросмотр Тогда
		Настройки["ВариантСохранения"] = "СохранитьВПапку"; 
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Настройки["Подписать"] = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	Если ВыбранныеФорматыСохранения.Количество() = 1 Тогда
		Настройки.Удалить("ВыбранныеФорматыСохранения");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьСтраницуМестаСохранения();
	// АльфаАвто
	ИмяФайла = ?(Тип(ВладелецФормы) = Тип("ФормаКлиентскогоПриложения"), ВладелецФормы.Заголовок, "");
	ИмяФайлаНеКорректно(ИмяФайла);
	// Конец АльфаАвто
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантСохраненияПриИзменении(Элемент)
	УстановитьСтраницуМестаСохранения();
	ОчиститьСообщения();
КонецПроцедуры

&НаКлиенте
Процедура ПапкаДляСохраненияФайловНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ВыбраннаяПапка = Элемент.ТекстРедактирования;
	ФайловаяСистемаКлиент.ВыбратьКаталог(Новый ОписаниеОповещения("ПапкаДляСохраненияФайловЗавершениеВыбора", ЭтотОбъект), , ВыбраннаяПапка);
	
КонецПроцедуры

// Обработчик завершения выбора каталога сохраненных файлов.
//  См. Синтакс-помощник: ДиалогВыбораФайла.Показать().
//
&НаКлиенте
Процедура ПапкаДляСохраненияФайловЗавершениеВыбора(Папка, ДополнительныеПараметры) Экспорт 
	Если Не ПустаяСтрока(Папка) Тогда 
		ВыбраннаяПапка = Папка;
		ОчиститьСообщения();
	КонецЕсли;
КонецПроцедуры

// АльфаАвто

&НаКлиенте
Процедура Подключаемый_ИмяФайлаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ИмяФайлаНеКорректно(Текст);
КонецПроцедуры

// Конец АльфаАвто

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Элементы.ПапкаДляСохраненияФайлов.Видимость Тогда
		Если ВариантСохранения = "СохранитьВПапку" И ПустаяСтрока(ВыбраннаяПапка) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите папку.'"),,"ВыбраннаяПапка");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// АльфаАвто
	#Если Не ВебКлиент Тогда
		// Проверим существование пути
		Директория = Новый Файл(ВыбраннаяПапка);
		Если НЕ Директория.Существует() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Выбранного каталога не существует.'"),,"ВыбраннаяПапка");
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	// Если указаны запрещенные символы в имени файла
	Если ИмяФайлаНеКорректно(ЭтотОбъект.ИмяФайла, Истина) Тогда
		Возврат;
	КонецЕсли;
	// Конец АльфаАвто
	
	ФорматыСохранения = Новый Массив;
	
	Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл
		Если ВыбранныйФормат.Пометка Тогда
			ФорматыСохранения.Добавить(ВыбранныйФормат.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ФорматыСохранения.Количество() = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Укажите как минимум один из предложенных форматов.'"));
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("УпаковатьВАрхив", УпаковатьВАрхив);
	РезультатВыбора.Вставить("ФорматыСохранения", ФорматыСохранения);
	РезультатВыбора.Вставить("ВариантСохранения", ВариантСохранения);
	РезультатВыбора.Вставить("ПапкаДляСохранения", ВыбраннаяПапка);
	РезультатВыбора.Вставить("ПереводитьИменаФайловВТранслит", ПереводитьИменаФайловВТранслит);
	РезультатВыбора.Вставить("Подписать", Подписать);
	// АльфаАвто
	Если ВариантСохранения = "СохранитьВПапку" Тогда
		РезультатВыбора.Вставить("ИмяФайла", ЭтотОбъект.ИмяФайла);
		ОповеститьОВыборе(РезультатВыбора);
	ИначеЕсли ВариантСохранения = "Присоединить" Тогда
	// Конец АльфаАвто
		СтрокаОшибки = "";
		ОбъектыДляПрикрепления = Новый Соответствие; 
		Для Каждого ОбъектПрикрепления Из ОбъектыПрикрепления Цикл
			Если Не ОбъектПрикрепления.Пометка Тогда
				СтрокаОшибки = СтрокаОшибки + ОбъектПрикрепления.Значение;
			Иначе
				ОбъектыДляПрикрепления.Вставить(ОбъектПрикрепления.Значение, Истина);
			КонецЕсли;
		КонецЦикла;
		
		РезультатВыбора.Вставить("ОбъектыДляПрикрепления", ОбъектыДляПрикрепления);
		
		Если СтрокаОшибки <> "" Тогда
			ОписаниеОповещенияПродолжениеСохранения = Новый ОписаниеОповещения("ПродолжениеСохранения", ЭтотОбъект, РезультатВыбора);
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Прикрепление не будет выполнено для объектов:
			| %1'"), СтрокаОшибки);
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить("Отменить", НСтр("ru = 'Отменить'"));
			Кнопки.Добавить("Продолжить", НСтр("ru = 'Продолжить'"));
			
			ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
			ПараметрыВопроса.Заголовок = НСтр("ru = 'Недостаточно прав для прикрепления'");
			ПараметрыВопроса.БлокироватьВесьИнтерфейс = Истина;
			ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
			
			СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(ОписаниеОповещенияПродолжениеСохранения, ТекстВопроса, Кнопки, ПараметрыВопроса);
		Иначе
			ОповеститьОВыборе(РезультатВыбора);
		КонецЕсли;
	Иначе
		ОповеститьОВыборе(РезультатВыбора);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьСтраницуМестаСохранения()
	
	Если Элементы.ПапкаДляСохраненияФайлов.Видимость Тогда
		Элементы.ПапкаДляСохраненияФайлов.Доступность = Не ВариантСохранения = "Присоединить";
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеСохранения(РезультатВопроса, РезультатВыбора) Экспорт
	Если РезультатВопроса.Значение = "Отменить" Тогда
		Закрыть();
	Иначе
		ОповеститьОВыборе(РезультатВыбора);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОбъектыДляПрикрепления(ОбъектыПечати)
	Результат = Новый СписокЗначений;
	МодульУправлениеДоступом = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
	КонецЕсли;
	
	Для Каждого ОбъектПечати Из ОбъектыПечати Цикл
		Результат.Добавить(ОбъектПечати.Значение,,?(МодульУправлениеДоступом <> Неопределено, 
			МодульУправлениеДоступом.ИзменениеРазрешено(ОбъектПечати.Значение), Истина)); 
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// АльфаАвто

&НаКлиенте
Функция ИмяФайлаНеКорректно(Текст, Сохранение = Ложь)
	
	ИмяФайлаНеКорректно = Ложь;
	
	// Проверяем имя только для сохранения в папку
	Если ВариантСохранения = "Присоединить" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат                   = Новый Массив(4);
	ЗапрещенныеСимволы          = "\/:*?<>|+%!@" + Символ(34); // Символ(34) = "
	НайденныеЗапрещенныеСимволы = Новый Массив;
	
	// Убираем ненужные пробелы
	Текст = СокрЛП(Текст);
	
	// Удалим рекурсивно запрещенные символы
	Для Счет = 1 По СтрДлина(ЗапрещенныеСимволы) Цикл
		СимволПоиска = Сред(ЗапрещенныеСимволы, Счет, 1);
		Если СтрНайти(Текст, СимволПоиска) > 0 Тогда
			НайденныеЗапрещенныеСимволы.Добавить(СимволПоиска);
			Текст = УдалитьЗапрещенныеСимволы(Текст, СимволПоиска);
		КонецЕсли;
	КонецЦикла;
	
	// Подготовим сообщения
	Если НайденныеЗапрещенныеСимволы.Количество() > 0 Тогда
		ИмяФайлаНеКорректно = Истина;
		Результат.Вставить(0, СтрСоединить(НайденныеЗапрещенныеСимволы, " "));
		Результат.Вставить(1, " - в любом месте имени файла");
	КонецЕсли;
	
	// Подготовим сообщения
	Если Прав(Текст,1) = "." Тогда
		ИмяФайлаНеКорректно = Истина;
		Результат.Вставить(2,".");
		Результат.Вставить(3," - в конце имени файла");
		Текст = УдалитьСимволКонца(Текст, ".");
	КонецЕсли;
	
	Если ИмяФайлаНеКорректно Тогда
		ПоказатьПредупреждение(,
			СтрШаблон(
				НСтр("ru = 'При создании имени файла запрещены некоторые служебные символы:
					|%1%2%3%4.
					|При сохранении документа эти символы будут автоматически удалены из имени файла.'"),
				Результат[0],
				Результат[1],
				?(ЗначениеЗаполнено(Результат[1]) И ЗначениеЗаполнено(Результат[3]), ";"+Символы.ПС+Результат[2], Результат[2]),
				Результат[3]),,
			НСтр("ru = 'Некорректное имя файла'"));
	КонецЕсли;
	
	// Убираем ненужные пробелы после удаления служебных символов
	Текст = СокрЛП(Текст);
	Если ПустаяСтрока(Текст) И Сохранение Тогда
		ИмяФайлаНеКорректно = Истина;
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не заполнено имя файла для сохранения'"),
			,
			"ИмяФайла"
		);
	КонецЕсли;
	
	ИмяФайла = Текст;
	
	Возврат ИмяФайлаНеКорректно;
	
КонецФункции

&НаКлиенте
Функция УдалитьЗапрещенныеСимволы(Текст, СимволПоиска)
	
	// Удалим запрещенный символ
	НовоеИмяФайла = СтрЗаменить(Текст, СимволПоиска, "");
	
	// Возможно этот символ был в названии не один раз, а больше
	Если СтрДлина(НовоеИмяФайла) <> СтрДлина(Текст) Тогда
		НовоеИмяФайла = УдалитьЗапрещенныеСимволы(НовоеИмяФайла, СимволПоиска);
	КонецЕсли;
	
	Возврат НовоеИмяФайла;
	
КонецФункции

&НаКлиенте
Функция УдалитьСимволКонца(Текст, СимволПоиска)
	
	// Удалим запрещенный символ
	НомерСимвола = СтрНайти(Текст, ".", НаправлениеПоиска.СКонца);
	Если НомерСимвола = СтрДлина(Текст) Тогда
		НовоеИмяФайла = Лев(Текст, СтрДлина(Текст)-1);
	КонецЕсли;
	
	// Возможно этот символ был в названии не один раз, а больше
	Если СтрДлина(НовоеИмяФайла) <> СтрДлина(Текст) Тогда
		НовоеИмяФайла = УдалитьЗапрещенныеСимволы(НовоеИмяФайла, СимволПоиска);
	КонецЕсли;
	
	Возврат НовоеИмяФайла;
	
КонецФункции

// Конец АльфаАвто

#КонецОбласти
