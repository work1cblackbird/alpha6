// Модуль формы "Просмотр текущих скидок и маркетинговых программ"

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДеревоТекущихСкидок(Параметры.Источник);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоТекущихСкидок(Источник)
	Запрос = Новый Запрос;
	
	Если ЭтоАдресВременногоХранилища(Источник) Тогда
		Запрос.УстановитьПараметр("Т", ПолучитьИзВременногоХранилища(Источник));
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Т.Скидка КАК Скидка,
		|	Т.Значение КАК ЗНачение
		|ПОМЕСТИТЬ Источник
		|ИЗ
		|	&Т КАК Т
		|;";
	Иначе
		Запрос.УстановитьПараметр("Ссылка", Источник);
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗаказНаАвтомобильСкидкиИМаркетинговыеПрограммы.Скидка КАК Скидка,
		|	ЗаказНаАвтомобильСкидкиИМаркетинговыеПрограммы.Значение КАК Значение
		|ПОМЕСТИТЬ Источник
		|ИЗ
		|	Документ.ЗаказНаАвтомобиль.СкидкиИМаркетинговыеПрограммы КАК ЗаказНаАвтомобильСкидкиИМаркетинговыеПрограммы
		|ГДЕ
		|	ЗаказНаАвтомобильСкидкиИМаркетинговыеПрограммы.Ссылка = &Ссылка
		|;";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса +
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Источник.Скидка) КАК Представление,
	|	Источник.Значение КАК Значение,
	|	ЛОЖЬ КАК Выделять
	|ИЗ
	|	Источник КАК Источник
	|ИТОГИ
	|	""Итого"" КАК Представление,
	|	СУММА(Значение) КАК Значение,
	|	ИСТИНА КАК Выделять
	|ПО
	|	ОБЩИЕ";
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам), "ТекущиеСкидки");
	
КонецПроцедуры

#КонецОбласти

