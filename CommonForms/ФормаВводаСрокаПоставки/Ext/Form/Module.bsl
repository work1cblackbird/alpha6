
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СрокПоставкиМаксимальный = Параметры.СрокПоставкиМаксимальный;
	СрокПоставкиМинимальный  = Параметры.СрокПоставкиМинимальный;
	ЭтоИнтервал              = Параметры.ЭтоИнтервал;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазобратьСрокПоставки();
	УстановитьПредставлениеСрокаПоставки();
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СрокПоставкиМинимальныйПриИзменении(Элемент)
	СрокПоставкиМинимальный = МинДней*24*60*60 + МинЧасов*60*60 + МинМинут*60;
	СрокПоставкиМаксимальный = ?(ЭтоИнтервал, Макс(СрокПоставкиМаксимальный, СрокПоставкиМинимальный), СрокПоставкиМинимальный);
	МаксДней = Цел(СрокПоставкиМаксимальный/(24*60*60));
	МаксЧасов = Цел((СрокПоставкиМаксимальный - МаксДней*24*60*60)/(60*60));
	МаксМинут = Цел((СрокПоставкиМаксимальный - МаксДней*24*60*60 - МаксЧасов*60*60)/60);
	УстановитьДоступность();
	УстановитьПредставлениеСрокаПоставки();
КонецПроцедуры

&НаКлиенте
Процедура СрокПоставкиМаксимальныйПриИзменении(Элемент)
	СрокПоставкиМаксимальный = МаксДней*24*60*60 + МаксЧасов*60*60 + МаксМинут*60;
	СрокПоставкиМинимальный  = Мин(СрокПоставкиМаксимальный,СрокПоставкиМинимальный);
	МинДней	 = Цел(СрокПоставкиМинимальный/(24*60*60));
	МинЧасов = Цел((СрокПоставкиМинимальный - МинДней*24*60*60)/(60*60));
	МинМинут = Цел((СрокПоставкиМинимальный - МинДней*24*60*60 - МинЧасов*60*60)/60);
	УстановитьДоступность();
	УстановитьПредставлениеСрокаПоставки();
КонецПроцедуры

&НаКлиенте
Процедура МинДнейПриИзменении(Элемент)
	Если МинДней >= 1 Тогда
		МинЧасов = 0;
		МинМинут = 0;
	КонецЕсли;
	СрокПоставкиМинимальныйПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура МаксДнейПриИзменении(Элемент)
	Если МаксДней >= 1 Тогда
		МаксЧасов = 0;
		МаксМинут = 0;
	КонецЕсли;
	СрокПоставкиМаксимальныйПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЭтоИнтервалПриИзменении(Элемент)
	Если Не ЭтоИнтервал Тогда
		МаксДней = МинДней;
		МаксЧасов = МинЧасов;
		МаксМинут = МинМинут;
		СрокПоставкиМаксимальный = СрокПоставкиМинимальный;
		УстановитьПредставлениеСрокаПоставки();
	КонецЕсли;
	УстановитьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	Результат = Новый Структура;
	Результат.Вставить("СрокПоставкиМинимальный"   , СрокПоставкиМинимальный);
	Результат.Вставить("СрокПоставкиМаксимальный"  , СрокПоставкиМаксимальный);
	Результат.Вставить("СрокПоставкиПредставление" , СрокПоставкиПредставление);
	Результат.Вставить("ДатаПоставки" , СформироватьБлижайщуюДатуПоставки());
	
	ЭтотОбъект.Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РазобратьСрокПоставки()
	Если СрокПоставкиМаксимальный < СрокПоставкиМинимальный Тогда
		СрокПоставкиМаксимальный = СрокПоставкиМинимальный;
	КонецЕсли;
	
	Если ПроверитьЗначениеСрокаПоставки(СрокПоставкиМинимальный) ИЛИ ПроверитьЗначениеСрокаПоставки(СрокПоставкиМаксимальный) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Срок поставки имел недопустимое значение и был преобразован.'"));
	КонецЕсли;
	
	// Выполним проверки и преобразования
	МинДней	  = Мин(Цел(СрокПоставкиМинимальный/(24*60*60)), 365);
	МинЧасов  = Цел((СрокПоставкиМинимальный - МинДней*24*60*60)/(60*60));
	МинМинут  = Цел((СрокПоставкиМинимальный - МинДней*24*60*60 - МинЧасов*60*60)/60);
	МаксДней  = Мин(Цел(СрокПоставкиМаксимальный/(24*60*60)), 365);
	МаксЧасов = Цел((СрокПоставкиМаксимальный - МаксДней*24*60*60)/(60*60));
	МаксМинут = Цел((СрокПоставкиМаксимальный - МаксДней*24*60*60 - МаксЧасов*60*60)/60);
	
	ЭтоИнтервал = (СрокПоставкиМинимальный <> СрокПоставкиМаксимальный);
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗначениеСрокаПоставки(СрокПоставкиЧисло)
	БылаКорректировка = Ложь;
	
	Если СрокПоставкиЧисло > 0 И СрокПоставкиЧисло < 180 Тогда
		СрокПоставкиЧисло = СрокПоставкиЧисло * (24*60*60);
		БылаКорректировка = Истина;
	КонецЕсли;
	
	Возврат БылаКорректировка;
КонецФункции

&НаКлиенте
Процедура УстановитьДоступность()
	Элементы.МинЧасов.Доступность = МинДней < 1;
	Элементы.МинМинут.Доступность = МинДней < 1;
	
	Если ЭтоИнтервал Тогда
		Элементы.МаксДней.Доступность        = Истина;
		Элементы.ДекорациямаксДн.Доступность = Истина;
		Элементы.МаксЧасов.Доступность       = МаксДней < 1;
		Элементы.МаксМинут.Доступность       = МаксДней < 1;
	Иначе
		Элементы.МаксДней.Доступность         = Ложь;
		Элементы.ДекорациямаксДн.Доступность  = Ложь;
		Элементы.МаксЧасов.Доступность        = Ложь;
		Элементы.МаксМинут.Доступность        = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СформироватьБлижайщуюДатуПоставки()
	
	Возврат ПрайсЛистыКонтрагентовКлиентСервер.РассчитатьСрокПоставки(ТекущаяДатаСеанса(), СрокПоставкиМаксимальный, Истина);
	
КонецФункции

&НаСервере
Процедура УстановитьПредставлениеСрокаПоставки()
	СрокПоставкиПредставление = ПрайсЛистыКонтрагентовКлиентСервер.
		ПредставлениеСрокаПоставкиИнтервал(СрокПоставкиМинимальный, СрокПоставкиМаксимальный);
КонецПроцедуры

#КонецОбласти

