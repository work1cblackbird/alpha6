#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СКД = Новый СхемаКомпоновкиДанных();
	
	Источник = СКД.ИсточникиДанных.Добавить();
	Источник.Имя = "ИсточникДанных";
	Источник.СтрокаСоединения = "";
	Источник.ТипИсточникаДанных = "local";
	
	Счетчик = 1;
	ТекстЗапроса = "";

	МассивСправочников = Новый Массив();
	МассивСправочников.Добавить("КонтактныеЛицаПартнеров");
	МассивСправочников.Добавить("КонтактныеЛица");
	МассивСправочников.Добавить("Партнеры");
	МассивСправочников.Добавить("Контрагенты");
	МассивСправочников.Добавить("Картотека");
	МассивСправочников.Добавить("Лиды");
	
	Для Каждого ИмяСправочника Из МассивСправочников Цикл
		Если Метаданные.Справочники.Найти(ИмяСправочника) <> Неопределено Тогда
			ТекстЗапроса = ТекстЗапроса + ?(ТекстЗапроса = "", "ВЫБРАТЬ", ",") + "
			|	ЗНАЧЕНИЕ(Справочник." + ИмяСправочника + ".ПустаяСсылка) КАК Контакт" + Счетчик;
			
			Счетчик = Счетчик + 1;
		КонецЕсли;
	КонецЦикла;
	
	НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Запрос = ТекстЗапроса;
	НаборДанных.Имя = "Запрос";
	НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
	НаборДанных.ИсточникДанных = "ИсточникДанных";
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных();
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СКД));

	ДополнительнаяИнформацияКонтактов = Константы.сфпДополнительнаяИнформацияКонтактов.Получить();
	
	МассивСохраненныхСтрок = Новый Массив();
	Если ЗначениеЗаполнено(ДополнительнаяИнформацияКонтактов) Тогда
		МассивСохраненныхСтрок = сфпСофтФонПроСервер.сфпРазложитьСтрокуВМассивПодстрок(ДополнительнаяИнформацияКонтактов, ";");
	КонецЕсли;
	
	ДеревоРеквизитовКонтактов = РеквизитФормыВЗначение("ДеревоРеквизитов");
	ДеревоРеквизитовКонтактов.Строки.Очистить();
	
	МассивИсключаемыхРеквизитов = Новый Массив();
	МассивИсключаемыхРеквизитов.Добавить("Наименование");
	МассивИсключаемыхРеквизитов.Добавить("ПометкаУдаления");
	МассивИсключаемыхРеквизитов.Добавить("ВерсияДанных");
	МассивИсключаемыхРеквизитов.Добавить("Предопределенный");
	МассивИсключаемыхРеквизитов.Добавить("ЭтоГруппа");
	МассивИсключаемыхРеквизитов.Добавить("ИмяПредопределенныхДанных");
	
	Для Каждого ТипКонтакта Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если НЕ ЗначениеЗаполнено(ТипКонтакта.ТипЗначения) Тогда
			Продолжить;
		КонецЕсли;
		
		МетаданныеКонтакта = ТипКонтакта.ТипЗначения.ПривестиЗначение(Неопределено).Метаданные();
		ИмяМетаданныхКонтакта = МетаданныеКонтакта.Имя;
		
		СтрокаКонтакта = ДеревоРеквизитовКонтактов.Строки.Добавить();
		СтрокаКонтакта.Использование = 0;
		СтрокаКонтакта.Объект = ИмяМетаданныхКонтакта;
		СтрокаКонтакта.Представление = МетаданныеКонтакта.Представление();
		СтрокаКонтакта.ЭтоГруппа = Истина;
		
		Для Каждого РеквизитКонтакта Из ТипКонтакта.Элементы Цикл
			ИмяРеквизита = СтрЗаменить(РеквизитКонтакта.Поле, "" + ТипКонтакта.Поле + ".", "");
			
			Если РеквизитКонтакта.Таблица Тогда
				Продолжить;
				
			ИначеЕсли МассивИсключаемыхРеквизитов.Найти(ИмяРеквизита) <> Неопределено Тогда
				Продолжить;
				
			ИначеЕсли Найти(ИмяРеквизита, "Удалить") = 1 Тогда
				Продолжить;
			КонецЕсли;	

			ПутьКДаннымРеквизита = СтрокаКонтакта.Объект + "." + ИмяРеквизита;
			ПредставлениеРеквизита = СтрЗаменить(РеквизитКонтакта.Заголовок, "" + ТипКонтакта.Поле + ".", "");
			
			Если СтрокаКонтакта.Строки.Найти(ПутьКДаннымРеквизита, "Объект") = Неопределено Тогда
				СтрокаРеквизита = СтрокаКонтакта.Строки.Добавить();
				СтрокаРеквизита.Использование = 0;
				СтрокаРеквизита.Объект = ПутьКДаннымРеквизита;
				СтрокаРеквизита.Представление = ПредставлениеРеквизита;
				
				Для Каждого СохраненнаяСтрока Из МассивСохраненныхСтрок Цикл
					Если СохраненнаяСтрока = СтрокаРеквизита.Объект Тогда
						СтрокаРеквизита.Использование = 1;
						
						СтрокаКонтакта.Использование = 2;
						СтрокаКонтакта.Представление = СтрокаКонтакта.Представление + ": " + СтрокаРеквизита.Представление;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	ДеревоРеквизитовКонтактов.Строки.Сортировать("Представление", Истина);
	
	ЗначениеВРеквизитФормы(ДеревоРеквизитовКонтактов, "ДеревоРеквизитов");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоРеквизитов

&НаКлиенте
Процедура ДеревоРеквизитовИспользованиеПриИзменении(Элемент = Неопределено)
	
	ТекДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;

	ТекРодитель = ТекДанные.ПолучитьРодителя();
	Если ТекРодитель = Неопределено Тогда
		Если ТекДанные.Использование = 2 Тогда
			ТекДанные.Использование = 1;
			
		ИначеЕсли ТекДанные.Использование = 1 Тогда
			ТекДанные.Использование = 0;
			
		Иначе
			ТекДанные.Использование = 2;
		КонецЕсли;
		
	Иначе	
		ТекДанные.Использование = ?(ТекДанные.Использование = 2, 0, ТекДанные.Использование);
		
		ТекРодитель.Использование = ?(ТекДанные.Использование = 1, 2, 0);
		
		Позиция = Найти(ТекРодитель.Представление, ": ");
		Если Позиция = 0 Тогда
			  ТекРодитель.Представление = ТекРодитель.Представление + ": " + ТекДанные.Представление;
		Иначе ТекРодитель.Представление = Лев(ТекРодитель.Представление, Позиция - 1) + ": " + ТекДанные.Представление;
		КонецЕсли;
		
		Если ТекДанные.Использование Тогда
			СтрокиРеквизитов = ТекРодитель.ПолучитьЭлементы();
			Для Каждого ТекСтрока Из СтрокиРеквизитов Цикл
				Если ТекСтрока <> ТекДанные И ТекСтрока.Использование = 1 Тогда
					ТекСтрока.Использование = 0;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Элементы.ДеревоРеквизитов.Свернуть(ТекРодитель.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	
	ТекРодитель = ТекДанные.ПолучитьРодителя();
	Если ТекРодитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные.Использование = ?(ТекДанные.Использование = 0, 1, 0);
	
	ДеревоРеквизитовИспользованиеПриИзменении();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура КомандаСохранить(Команда)
	
	СтрокаСохранения = "";
	
	СтрокиКонтактов = ДеревоРеквизитов.ПолучитьЭлементы();
	Для Каждого СтрокаКонтакта Из СтрокиКонтактов Цикл
		СтрокиРеквизитов = СтрокаКонтакта.ПолучитьЭлементы();
		Для Каждого СтрокаРеквизита Из СтрокиРеквизитов Цикл
			Если СтрокаРеквизита.Использование = 1 Тогда
				СтрокаСохранения = СтрокаСохранения + ?(СтрокаСохранения = "", "", ";") + СтрокаРеквизита.Объект;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
	КонецЦикла;
	
	ЗаписатьЗначениеКонстанты(СтрокаСохранения);
	
	ЭтаФорма.Закрыть();

КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначениеКонстанты(СтрокаСохранения)
	Константы.сфпДополнительнаяИнформацияКонтактов.Установить(СтрокаСохранения);
КонецПроцедуры

#КонецОбласти