
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Номенклатура");
	ОткрытьБезНоменклатуры = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ОткрытьБезНоменклатуры", Ложь);
	
	Если ОткрытьБезНоменклатуры Тогда 
		Заголовок = НСтр("ru = 'Сканирование кода маркировки'");
		Возврат;
	КонецЕсли;
		
	// Откроем форму только если передан элемент справочника Номенклатура
	Если НЕ ЗначениеЗаполнено(Номенклатура) ИЛИ ТипЗнч(Номенклатура) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Проверким маркировку
	ДанныеМаркировкиТовара = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Номенклатура,
		"ТипНоменклатуры.ВедетсяМаркировка");
	
	ОткрыватьФормуСканированияМаркировки = ПраваИНастройкиПользователя.Значение("ОткрыватьФормуСканированияМаркировки");
	
	Если НЕ (ОткрыватьФормуСканированияМаркировки И ДанныеМаркировкиТовара.ТипНоменклатурыВедетсяМаркировка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Заголовок = Строка(Номенклатура);
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Принимаем только внешние события от сканера ШК
	Если НЕ ВводДоступен() ИЛИ ИмяСобытия <> "ScanData" Тогда
		Возврат;
	КонецЕсли;
	
	ПолученныйШтрихкод = ШтрихкодированиеКлиент.ПолучитьШтрихкодИзПараметровОборудования(ИмяСобытия, Параметр);
	Если НЕ ЗначениеЗаполнено(ПолученныйШтрихкод) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыДействия = Новый Структура;
	ШтрихкодированиеКлиент.ОбработатьПолныйШтрихкод(ПолученныйШтрихкод, ПараметрыДействия);
	
	// Разберем считанный ШК
	ТекстОшибки = "";
	Маркировка = МаркировкаТоваровКлиентСервер.ПолучитьКодМаркировки(ПараметрыДействия.Штрихкод, ТекстОшибки);
	
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		// Это не маркировка.
		ПоказатьПредупреждение(, ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ШтрихКод = Маркировка;
	
	// Необходима небольшая задержка перед закрытием формы, может возникнуть ситуация,
	// что из формы документы будет снова дергнута процедура записи маркировки в документ.
	ПодключитьОбработчикОжидания("Подключаемый_ЗавершитьСканированиеМаркировки", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ЗавершитьСканированиеМаркировки()
	
	// Возвращаем считанный ШК.
	Закрыть(ШтрихКод);
	
КонецПроцедуры // Подключаемый_ЗавершитьСканированиеМаркировки()

#КонецОбласти
