
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Заголовок = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Заголовок", НСтр("ru = 'Таблица'"));
	Адрес = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Адрес");
	
	Если НЕ ЭтоАдресВременногоХранилища(Адрес) Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Таблица = ПолучитьИзВременногоХранилища(Адрес);
	СоздатьРеквизитыТабличнойЧасти(Таблица);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьРеквизитыТабличнойЧасти(Таблица)
	
	// Создадим реквизиты
	НовыеРеквизиты = Новый Массив;
	НовыеРеквизиты.Добавить(Новый РеквизитФормы("ОтображаемаяТаблица", Новый ОписаниеТипов("ТаблицаЗначений")));
	
	Для Каждого Колонка Из Таблица.Колонки Цикл
		
		НовыеРеквизиты.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, "ОтображаемаяТаблица", Колонка.Заголовок));
		
	КонецЦикла;
	
	ИзменитьРеквизиты(НовыеРеквизиты);
	
	// Создадим элементы формы
	ТаблицаФормы = Элементы.Добавить("ОтображаемаяТаблица", Тип("ТаблицаФормы"));
	ТаблицаФормы.ПутьКДанным = "ОтображаемаяТаблица";
	ТаблицаФормы.ТолькоПросмотр = Истина;
	ТаблицаФормы.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	
	Для Каждого Колонка Из Таблица.Колонки Цикл
		
		НоваяКолонка = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), ТаблицаФормы);
		НоваяКолонка.ПутьКДанным = "ОтображаемаяТаблица." + Колонка.Имя;
		
	КонецЦикла;
	
	ЭтотОбъект["ОтображаемаяТаблица"].Загрузить(Таблица);
	
КонецПроцедуры

#КонецОбласти