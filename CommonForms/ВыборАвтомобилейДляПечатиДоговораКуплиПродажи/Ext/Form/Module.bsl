// Модуль общей формы "Выбор автомобилей для печати договора купли-продажи"

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СписокВыбора") Тогда
		ПолучитьСписокВыбораАвтомобилей(Параметры.СписокВыбора);
		ПараметрыПечати = Параметры.ПараметрыПечати;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// если нет автомобилей для печати
	Если ТаблицаЗначенийДляВыбора.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не найдено автомобилей для печати договора купли-продажи'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗащищенныеФункцииКлиент.НастроитьЭлементФормыТабличнойЧасти(ЭтотОбъект, "ТаблицаЗначенийДляВыбора");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаЗначенийДляВыбора

&НаКлиенте
Процедура ТаблицаЗначенийДляВыбораВыбраноПриИзменении(Элемент)
	
	УправлениеДиалогом();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийДляВыбораЗначениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийДляВыбораЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	АвтомобилиДляПечати = Новый Массив;
	АвтомобилиДляПечати.Добавить(ВладелецФормы);
	АвтомобилиДляПечати.Добавить(ПараметрыПечати);
	
	СтрокиАвтомобилей = ТаблицаЗначенийДляВыбора.НайтиСтроки(Новый Структура("Выбрано", Истина));
	Для Каждого Строка Из СтрокиАвтомобилей Цикл
		АвтомобилиДляПечати.Добавить(Строка.Значение);
		АвтомобилиДляПечати.Добавить(Строка.Документ);
	КонецЦикла;
	
	Закрыть(АвтомобилиДляПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	Для Каждого Строка Из ТаблицаЗначенийДляВыбора Цикл
		Строка.Выбрано = Истина;
	КонецЦикла;
	
	УправлениеДиалогом();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделение(Команда)
	
	Для Каждого Строка Из ТаблицаЗначенийДляВыбора Цикл
		Строка.Выбрано = Ложь;
	КонецЦикла;
	
	УправлениеДиалогом();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьСписокВыбораАвтомобилей(ОбъектыПечати)
	
	Если ОбъектыПечати = Неопределено ИЛИ ОбъектыПечати.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОбъектПечати Из ОбъектыПечати Цикл
		Для Каждого СтрокаАвтомобиль Из ОбъектПечати.Автомобили Цикл
			НоваяСтрока = ТаблицаЗначенийДляВыбора.Добавить();
			НоваяСтрока.Выбрано  = Истина;
			НоваяСтрока.Значение = СтрокаАвтомобиль.Автомобиль;
			НоваяСтрока.Документ = ОбъектПечати;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

// Производит настройку параметров отображения элементов управления диалога в зависимости от значений реквизитов объекта.
//
&НаКлиенте
Процедура УправлениеДиалогом()
	
	Для Каждого Строка Из ТаблицаЗначенийДляВыбора Цикл
		Если Строка.Выбрано = Истина Тогда
			Элементы.ФормаПрименить.Доступность = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ФормаПрименить.Доступность = Ложь;
	
КонецПроцедуры // УправлениеДиалогомНаСервере()

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ
