
#Область Общие_Процедуры_И_Функции

&НаСервере
Процедура ЗаполнитьРеквизитыСтрокиДереваАбонентов(СтрокаДерева, Контакт, Владелец, ПредставлениеВидаКонтакта)
	
	СтрокаДерева.Ссылка = Контакт;
	Если ЗначениеЗаполнено(Владелец) Тогда
		СтрокаДерева.Объект = Строка(Контакт) + ?(ЗначениеЗаполнено(Владелец), " <" + Строка(Владелец) + "> ", " ") + ПредставлениеВидаКонтакта;
		СтрокаДерева.Владелец = Владелец;
		
	Иначе
		СтрокаДерева.Объект = Строка(Контакт) + " " + ПредставлениеВидаКонтакта;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура сфпСформироватьДеревоАбонентов()
	
	ДеревоАбонентов.ПолучитьЭлементы().Очистить();
	
	Если СписокОбъектов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КореньДерева = ДеревоАбонентов.ПолучитьЭлементы();
	Для Каждого ЭлементСписка Из СписокОбъектов Цикл
		СсылкаНаЭлемент = ЭлементСписка.Значение;
		
		КонтактИмяМетаданных = СсылкаНаЭлемент.Метаданные().Имя;
		Если КонтактИмяМетаданных = "Партнеры" Тогда
			Если сфпСофтФонПроСервер.сфпРеквизитСуществует(СсылкаНаЭлемент, "CRM_ВидПартнера") Тогда
				Если СсылкаНаЭлемент["CRM_ВидПартнера"] = Перечисления["CRM_ВидПартнера"].ЧастноеЛицо Тогда
					ВеткаДерева = КореньДерева.Добавить();
					ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент,, Нстр("ru = '(партнер, физ. лицо)'"));

				Иначе
					ВеткаДерева = КореньДерева.Добавить();
					ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент, СсылкаНаЭлемент, Нстр("ru = '(партнер, юр. лицо)'"));
				КонецЕсли;
				
			Иначе
				ВеткаДерева = КореньДерева.Добавить();
				ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент, СсылкаНаЭлемент, Нстр("ru = '(партнер)'"));
			КонецЕсли;

		ИначеЕсли КонтактИмяМетаданных = "Контрагенты" Тогда
			Если сфпСофтФонПроСервер.сфпРеквизитСуществует(СсылкаНаЭлемент, "ЮрФизЛицо") Тогда
				Если СсылкаНаЭлемент["ЮрФизЛицо"] = Перечисления["ЮрФизЛицо"].ФизЛицо Тогда
					ВеткаДерева = КореньДерева.Добавить();
					ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент,, Нстр("ru = '(контрагент, физ. лицо)'"));
					
				Иначе
					ВеткаДерева = КореньДерева.Добавить();
					ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент, СсылкаНаЭлемент, Нстр("ru = '(контрагент, юр. лицо)'"));
				КонецЕсли;
				
			Иначе
				ВеткаДерева = КореньДерева.Добавить();
				ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент, СсылкаНаЭлемент, Нстр("ru = '(контрагент)'"));
			КонецЕсли;

		Иначе
			Если КонтактИмяМетаданных = "Пользователи" И НЕ ТолькоКлиентыИКЛ Тогда
				ВеткаДерева = КореньДерева.Добавить();
				ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент,, Нстр("ru = '(пользователь)'"));

			ИначеЕсли КонтактИмяМетаданных = "Организации" И НЕ ТолькоКлиентыИКЛ Тогда
				ВеткаДерева = КореньДерева.Добавить();
				ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент,, Нстр("ru = '(организация)'"));

			ИначеЕсли КонтактИмяМетаданных = "ФизическиеЛица" И НЕ ТолькоКлиентыИКЛ Тогда
				ВеткаДерева = КореньДерева.Добавить();
				ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент,, Нстр("ru = '(физическое лицо)'"));

			ИначеЕсли КонтактИмяМетаданных = "КонтактныеЛицаПартнеров" Тогда
				ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(СсылкаНаЭлемент);
				
				ВеткаДерева = КореньДерева.Добавить();
				ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент, ВладелецКонтакта, Нстр("ru = '(контактное лицо)'"));

			ИначеЕсли КонтактИмяМетаданных = "Лиды" Тогда
				ВеткаДерева = КореньДерева.Добавить();
				ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент,, Нстр("ru = '(лид)'"));

			ИначеЕсли КонтактИмяМетаданных = "Картотека" Тогда
				ВеткаДерева = КореньДерева.Добавить();
				ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент,, Нстр("ru = '(картотека)'"));

			ИначеЕсли КонтактИмяМетаданных = "КонтактныеЛица" Тогда
				Если Метаданные.РегистрыСведений.Найти("СвязиКонтрагентКонтакт") = Неопределено Тогда
					ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(СсылкаНаЭлемент);
					
					ВеткаДерева = КореньДерева.Добавить();
					ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент, ВладелецКонтакта, Нстр("ru = '(контактное лицо)'"));

				Иначе
					Запрос = Новый Запрос("
					|ВЫБРАТЬ Контрагент
					|ИЗ РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних(, Контакт = &Контакт)
					|ГДЕ НЕ СвязьНедействительна");
					Запрос.УстановитьПараметр("Контакт", СсылкаНаЭлемент);
					Выборка = Запрос.Выполнить().Выбрать();
					Пока Выборка.Следующий() Цикл
						ВеткаДерева = КореньДерева.Добавить();
						ЗаполнитьРеквизитыСтрокиДереваАбонентов(ВеткаДерева, СсылкаНаЭлемент, Выборка.Контрагент, Нстр("ru = '(контактное лицо)'"));
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Обработчики_Событий_Формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ИдентификаторЗвонка") ИЛИ НЕ Параметры.Свойство("СписокОбъектов") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗвонка = Параметры.ИдентификаторЗвонка;
	
	СписокОбъектов = Параметры.СписокОбъектов;
	
	Если Параметры.Свойство("НомерТелефона") Тогда
		НомерТелефона = Параметры.НомерТелефона;
	КонецЕсли;	
	
	Если Параметры.Свойство("ТолькоКлиентыИКЛ") Тогда
		ТолькоКлиентыИКЛ = Параметры.ТолькоКлиентыИКЛ;
	КонецЕсли;		
	
	Если Параметры.Свойство("ПодборВСобытие") Тогда
		  Заголовок = "Выберите связанный объект";
	Иначе Заголовок = "Выберите контакт для номера: " + НомерТелефона;
	КонецЕсли;

	сфпСформироватьДеревоАбонентов();

КонецПроцедуры

#КонецОбласти

#Область Обработчики_Событий_Элементов_Формы

&НаКлиенте
Процедура ДеревоАбонентовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьКонтакт();

КонецПроцедуры

#КонецОбласти

#Область Обработчики_Команд_Формы

&НаКлиенте
Процедура ОткрытьОбъект(Команда)
	
	ТекДанные = Элементы.ДеревоАбонентов.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		ПоказатьПредупреждение(, Нстр("ru = 'Не выбран контакт!'"),5);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекДанные.Ссылка) Тогда
		ПоказатьЗначение(, ТекДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКонтакт(Команда = Неопределено)
	
	ТекДанные = Элементы.ДеревоАбонентов.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураКонтакта = Новый Структура("ИдентификаторЗвонка, Контакт, Владелец", ИдентификаторЗвонка, ТекДанные.Ссылка, ТекДанные.Владелец);
	
	Оповестить("сфпВыборКонтакта", СтруктураКонтакта);
	
	Закрыть(СтруктураКонтакта);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СофтФон_УдалитьЗвонок" ИЛИ ИмяСобытия = "сфпКонецРазговора" Тогда
		Если Параметр = ЭтаФорма.ИдентификаторЗвонка Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти