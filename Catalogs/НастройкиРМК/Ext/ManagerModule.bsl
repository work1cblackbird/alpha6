// Модуль менеджера справочника "Настройки РМК"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// 	 Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает основную кассу ККМ рабочего места пользователя по данным настроек РМК
//
// Параметры:
//  РабочееМесто - СправочникСсылка.РабочиеМеста - Касса будет определена для указанного либо текущего рабочего места.
//
// Возвращаемое значение:
//  СправочникСсылка.КассыККМ - если не заданы настройки РМК или не получилось определить текущее рабочее место.
//
Функция ПолучитьПодключенноеОборудование(ТипОборудования, КассаККМ=Неопределено, РабочееМесто=Неопределено) Экспорт
	
	// Проверяем переданные параметры на корректность
	Если НЕ ЗначениеЗаполнено(ТипОборудования) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Произведем уточнение текущего рабочего места
	Если НЕ ЗначениеЗаполнено(РабочееМесто) Тогда
		РабочееМесто = МенеджерОборудованияВызовСервера.РабочееМестоКлиента();
	КонецЕсли;
	
	// Прекращаем обработку, если рабочее место идентифицировать не удалось
	Если НЕ ЗначениеЗаполнено(РабочееМесто) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Формируем запрос к настройкам рабочего места
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиРМККассыККМ.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ИЗ
	|	Справочник.НастройкиРМК.КассыККМ КАК НастройкиРМККассыККМ
	|ГДЕ
	|	НастройкиРМККассыККМ.Ссылка.РабочееМесто = &РабочееМесто
	|	И НастройкиРМККассыККМ.ПодключаемоеОборудование.ТипОборудования = &ТипОборудования
	|	И (&КассаККМ = НЕОПРЕДЕЛЕНО
	|			ИЛИ НастройкиРМККассыККМ.КассаККМ = &КассаККМ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НастройкиРМККассыККМ.НомерСтроки";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТипОборудования", ТипОборудования);
	Запрос.УстановитьПараметр("КассаККМ",        КассаККМ);
	Запрос.УстановитьПараметр("РабочееМесто",    РабочееМесто);
	
	// Выполняем запрос и получаем экземпляр подключенного оборудования
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.ПодключаемоеОборудование, Неопределено);
	
КонецФункции // ПолучитьПодключенноеОборудование()

#Область ПараметрыОбработкиРеквизитовОбъекта

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Массив - Возвращаемый массив содержит строковые идентификаторы реквизитов.
//
Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Массив();
	ОбязательныеРеквизиты.Добавить("РабочееМесто");
	ОбязательныеРеквизиты.Добавить("КассыККМ.КассаККМ");
	ОбязательныеРеквизиты.Добавить("КассыККМ.ПодключаемоеОборудование");
	
	// Возвращаем сформированные параметры проверки
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет уникальности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Структура - Возвращаемая структура содержит строковые идентификаторы реквизитов.
//  Для описания проверки табличных частей используется
//  вложенная структура.
//
Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	// Уникальные поля табличной части
	УникальныеКассыККМ = Новый Массив();
	УникальныеКассыККМ.Добавить("КассаККМ");
	УникальныеКассыККМ.Добавить("ПодключаемоеОборудование");
	
	// Структура уникальных реквизитов
	УникальныеРеквизиты = Новый Структура();
	УникальныеРеквизиты.Вставить("РабочееМесто");
	УникальныеРеквизиты.Вставить("КассыККМ", УникальныеКассыККМ);
	
	// Возвращаем сформированные параметры проверки
	Возврат УникальныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

#КонецОбласти

#КонецОбласти

#КонецЕсли