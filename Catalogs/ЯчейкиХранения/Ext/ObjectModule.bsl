
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	
	ПрефиксацияОбъектов.ПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ПрерватьЗаполнение = Не ОбработкаСобытийСправочникаСервер.ОбработкаЗаполнения(
		ЭтотОбъект,
		ДанныеЗаполнения,
		ТекстЗаполнения,
		СтандартнаяОбработка
	);
	
	Если ПрерватьЗаполнение Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Не ЭтоГруппа И ТипЗнч(Владелец) = Тип("СправочникСсылка.СкладыКомпании") И Не Владелец.Пустая() Тогда

		ПараметрыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Владелец,
			"ШиринаЯчейкиПоУмолчанию,ДлинаЯчейкиПоУмолчанию");
		ШиринаЯчейки = ПараметрыСклада.ШиринаЯчейкиПоУмолчанию;
		ДлинаЯчейки  = ПараметрыСклада.ДлинаЯчейкиПоУмолчанию;
		
		НеДоступна = Ложь;
		
		Код = Справочники.ЯчейкиХранения.КодПоКоординатам(ЭтотОбъект);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ОбработкаСобытийСправочникаСервер.ПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если Не ОбработкаСобытийСправочникаСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты) Тогда
	
		Возврат;
	
	КонецЕсли;

	Если ЭтоГруппа Тогда
		
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Код"));
		
		Если Не ЗначениеЗаполнено(Код) Тогда

			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Поле ""Наименование"" не заполнено'"), ЭтотОбъект, "Код");
			Отказ = Истина;

		КонецЕсли;
		
	ИначеЕсли Справочники.СкладыКомпании.ЭтоСкладСЯчейками(Владелец) Тогда
		
		РазмерыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Владелец, "ДлинаСклада,ШиринаСклада,ЧислоУровней");
		
		Если ДлинаЯчейки  + КоординатаX  > РазмерыСклада.ДлинаСклада Тогда
			
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='- Параметры ячейки превышают длину склада,'"), ЭтотОбъект,
				"ДлинаЯчейки");
			
		КонецЕсли;
		
		Если ШиринаЯчейки + КоординатаY > РазмерыСклада.ШиринаСклада Тогда
			
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='- Параметры ячейки превышают ширину склада,'"), ЭтотОбъект,
				"ШиринаЯчейки");
			
		КонецЕсли;
		
		Если Уровень >= РазмерыСклада.ЧислоУровней Тогда
			
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru='- Уровень ячейки превышает число уровней склада (нумерация уровней начинается с нуля),'"),
				ЭтотОбъект,
				"ШиринаЯчейки"
			);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если Не ОбработкаСобытийСправочникаСервер.ПередЗаписью(ЭтотОбъект, Отказ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Не Отказ И Не ЭтоГруппа И Не ПометкаУдаления = Ссылка.ПометкаУдаления Тогда
		
		Недоступна = ПометкаУдаления;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Не ОбработкаСобытийСправочникаСервер.ПриЗаписи(ЭтотОбъект, Отказ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		
		Возврат;
		
	КонецЕсли;
	
	РегистрыСведений.ЯчейкиХраненияПоУмолчанию.УдалитьЯчейку(Ссылка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
