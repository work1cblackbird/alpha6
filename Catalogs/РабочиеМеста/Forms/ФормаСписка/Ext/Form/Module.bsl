
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)                                            
	
	МенеджерОборудованияКлиент.УстановитьРежимПодключенияРасширенияИнформацииОКомпьютере(Истина);
	МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента();
	
	ТекущееРабочееМесто = МенеджерОборудованияКлиент.РабочееМестоКлиента();
	Список.Параметры.УстановитьЗначениеПараметра("ТекущееРабочееМесто", ТекущееРабочееМесто); 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИдентификаторКлиента = МенеджерОборудованияКлиент.ИдентификаторКлиентаДляРабочегоМеста();
	РабочееМесто = ПолучитьРабочееМестоПоИдентификаторуКлиента(ИдентификаторКлиента);
	
	Если НЕ РабочееМесто = Неопределено Тогда
		
		Отказ = Истина;
			                  
		Текст = НСтр("ru='Создание нового рабочего места не требуется. 
					|Для данного идентификатора клиента оно уже создано.
					|Открыть существующее рабочее место?'");
		Оповещение = Новый ОписаниеОповещения("СписокПередНачаломДобавленияЗавершение", ЭтотОбъект, РабочееМесто);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзмененоРабочееМестоТекущегоСеанса" Тогда
		ТекущееРабочееМесто = МенеджерОборудованияКлиент.РабочееМестоКлиента();
		Список.Параметры.УстановитьЗначениеПараметра("ТекущееРабочееМесто", ТекущееРабочееМесто); 
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

// АльфаАвто
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры // КонецАльфаАвто

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СписокПередНачаломДобавленияЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да И Не ПустаяСтрока(Параметры) Тогда
		ПоказатьЗначение(, Параметры);
	КонецЕсли;  
	
КонецПроцедуры 

&НаСервере
Функция ПолучитьРабочееМестоПоИдентификаторуКлиента(Идентификатор)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|    РабочиеМеста.Ссылка
	|ИЗ
	|    Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|    РабочиеМеста.Код = &Идентификатор");
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// АльфаАвто
&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Список.УсловноеОформление.Элементы.Очистить();
	
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Наименование");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Текущее");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(ШрифтыСтиля.ШрифтТекста,,,Истина));

КонецПроцедуры // Конец АльфаАвто

#КонецОбласти