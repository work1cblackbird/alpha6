
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Скидка", Скидка);
	НаМомент = НачалоДня(ТекущаяДатаСеанса());
	
	ОбновитьПараметрыДинамическогоСписка(ЭтотОбъект);
	
	Если Параметры.Свойство("ОтобразитьВсе") Тогда
		ОтборПоАктивности = НЕ Параметры.ОтобразитьВсе;
	КонецЕсли;
	ОбновитьОтборДинамическогоСписка(Список, ОтборПоАктивности);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаМоментПриИзменении(Элемент)
	
	ОбновитьПараметрыДинамическогоСписка(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоАктивностиПриИзменении(Элемент)
	
	ОбновитьОтборДинамическогоСписка(Список, ОтборПоАктивности);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Действует(Команда)
	
	ОткрытьФормуИзмененияСтатуса(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НеДействует(Команда)
	
	ОткрытьФормуИзмененияСтатуса(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПараметрыДинамическогоСписка(Форма)
	
	Форма.Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", Форма.НаМомент);
	Форма.Список.Параметры.УстановитьЗначениеПараметра("Скидка", Форма.Скидка);
	
	Форма.Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьОтборДинамическогоСписка(ДинамическийСписок, Использование)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ДинамическийСписок,
		"Действует",
		Истина,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Использование,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуИзмененияСтатуса(НовыйСтатус)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Начало", НаМомент);
	ПараметрыОткрытия.Вставить("Скидка", Скидка);
	ПараметрыОткрытия.Вставить("Статус", НовыйСтатус);
	
	Источники = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		Источники.Добавить(ВыделеннаяСтрока);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбрано ни одного источника.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия.Вставить("Источник", Источники);
	ОткрытьФорму(
		"Справочник.СкидкиАвтомобилей.Форма.ФормаУстановкиСтатусаСкидки",
		ПараметрыОткрытия,
		ЭтотОбъект,
		ЭтотОбъект,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

#КонецОбласти