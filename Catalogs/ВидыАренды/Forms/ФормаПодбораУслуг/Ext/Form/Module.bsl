///////////////////////////////////////////////////////////////////////////////
// Модуль формы "Подбор услуг" справочника "Виды аренды"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Документ") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Документ  = Параметры.Документ.Ссылка;
	ВидАренды = ?(ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваров"),
		Параметры.Документ.ДокументОснование.ВидАренды,
		Параметры.Документ.ВидАренды);
	
	ИмяДокумента = Метаданные.НайтиПоТипу(ТипЗнч(Документ)).ПолноеИмя();
	
	ОбновитьНаСервере();
	
	// Установим вариант отображения дополнительных полей "Код" и "Артикул"
	УправлениеДиалогомДокументаСервер.УстановитьВидимостьКолонокКодАртикул(ЭтотОбъект, "Список");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РассчитатьИтогиСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

#Область ОбработчикиСобытийПолейТаблицыФормыСписок

&НаКлиенте
Процедура СписокКоличествоПриИзменении(Элемент)
	
	СписокКоличествоПриИзмененииНаСервере();
	
	РассчитатьИтогиСписка();
	
КонецПроцедуры

&НаСервере
Процедура СписокКоличествоПриИзмененииНаСервере()
	
	ПараметрыДействия = Новый Структура;
	ПараметрыДействия.Вставить("НеРассчитыватьБонусы",          Истина);
	ПараметрыДействия.Вставить("НеУстанавливатьОтборНаОстаток", Истина);
	ПараметрыДействия.Вставить("НеРассчитыватьСкидки",          Истина);
	
	ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяДокумента).
		ТоварыКоличествоПриИзменении(Документ,
		Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока),
		ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПометкаПриИзменении(Элемент)
	
	РассчитатьИтогиСписка();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	
	ЭтотОбъект.ВладелецФормы.Модифицированность = Истина;
	ЭтотОбъект.Закрыть(СформироватьРезультат());
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	УстановитьФлажки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	УстановитьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	
	РассчитатьИтогиСписка();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункцииКлиент

&НаКлиенте
Процедура РассчитатьИтогиСписка()
	
	ИтогПометка    = 0;
	ИтогКоличество = 0;
	ИтогСуммаВсего = 0;
	Для Каждого Строка Из Список Цикл
		Если Строка.Пометка Тогда
			ИтогПометка    = ИтогПометка    + 1;
			ИтогКоличество = ИтогКоличество + Строка.Количество;
			ИтогСуммаВсего = ИтогСуммаВсего + Строка.СуммаВсего;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Значение)
	
	Для Каждого Строка Из Список Цикл
		Строка.Пометка = Значение;
	КонецЦикла;
	
	РассчитатьИтогиСписка();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииСервер

&НаСервере
Функция СформироватьРезультат()
	
	ТЗ = Список.Выгрузить(Новый Структура("Пометка", Истина),
		"Номенклатура, Количество, Цена, ЕдиницаИзмерения, ХарактеристикаНоменклатуры");
	
	ПараметрыПодбора = Новый Структура();
	ПараметрыПодбора.Вставить("ЕстьЯчейка",              Ложь);
	ПараметрыПодбора.Вставить("ЕстьИсточникОбеспечения", Ложь);
	ПараметрыПодбора.Вставить("ПолноеИмяОбъекта",        ИмяДокумента);
	ПараметрыПодбора.Вставить("ИмяТабличнойЧасти",       "Товары");
	ПараметрыПодбора.Вставить("ИмяРеквизитаКоличество",  "Количество");
	ПараметрыПодбора.Вставить("ПараметрыДействия",       Новый Структура());
	ПараметрыПодбора.Вставить("Корзина",                 ПоместитьВоВременноеХранилище(ТЗ, УникальныйИдентификатор));
	
	Возврат ПараметрыПодбора;
	
КонецФункции

&НаСервере
Процедура ОбновитьНаСервере()
	
	Список.Загрузить(ВидАренды.ДополнительныеУслуги.Выгрузить());
	
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяДокумента);
	
	ПараметрыДействия = Новый Структура;
	ПараметрыДействия.Вставить("НеРассчитыватьБонусы",          Истина);
	ПараметрыДействия.Вставить("НеУстанавливатьОтборНаОстаток", Истина);
	ПараметрыДействия.Вставить("НеРассчитыватьСкидки",          Истина);
	
	Для Каждого Строка Из Список Цикл
		МенеджерДокумента.ТоварыНоменклатураПриИзменении(Документ, Строка, ПараметрыДействия);
		Строка.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
