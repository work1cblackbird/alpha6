// Модуль менеджера справочника "Опции"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// 	 Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверим корректность ставки НДС для устранения ситуации выбора ставки НДС 18%
//
Функция ПроверитьКорректностьСтавкиНДС(Объект) Экспорт
	
	// По закону с 01.01.2019 вместо ставки НДС 18% необходимо использовать ставку НДС 20%.
	
	ТекДата = ТекущаяДатаСеанса();
	
	СтавкаНДСНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтавкаНДС, "Ставка");
	СтавкаНДС18 = 18;
	
	Если СтавкаНДСНоменклатуры = СтавкаНДС18 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьСтавкиНДС()

#Область ПараметрыОбработкиРеквизитовОбъекта

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Массив - Возвращаемый массив содержит строковые идентификаторы реквизитов.
//
Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Массив();
	ОбязательныеРеквизиты.Добавить("Наименование");
	
	Если НЕ Объект.ЭтоГруппа Тогда
		ОбязательныеРеквизиты.Добавить("СтавкаНДС");
		ОбязательныеРеквизиты.Добавить("ВалютаУчета");
		Если Автосервис.АртикулОбязательный() Тогда
			ОбязательныеРеквизиты.Добавить("Артикул");
		КонецЕсли;
		Если Объект.ПризнакНабора ИЛИ Объект.ПризнакКомплекта Тогда
			ОбязательныеРеквизиты.Добавить("СоставНабора.Опция");
			ОбязательныеРеквизиты.Добавить("СоставНабора.Количество");
		КонецЕсли;
	КонецЕсли;
	
	// Возвращаем сформированные параметры проверки
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности и уникальности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Структура - Возвращаемая структура содержит строковые идентификаторы реквизитов и вариант проверки его заполнения
//  (1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный). Для описания проверки табличных частей используется
//  вложенная структура.
//
Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	// Структура уникальных реквизитов
	УникальныеРеквизиты = Новый Структура();
	УникальныеРеквизиты.Вставить("Код");
	
	Если НЕ Объект.ЭтоГруппа Тогда
		РежимКонтроля = Автосервис.РежимКонтроляУникальностиАртикула();
		Если РежимКонтроля <> Перечисления.РежимыКонтроляУникальностиНомераПоКаталогу.НеКонтролировать Тогда
			УникальныеРеквизиты.Вставить("Артикул");
		КонецЕсли;
		Если Объект.ПризнакНабора ИЛИ Объект.ПризнакКомплекта Тогда
			УникальныеРеквизитыНабор=Новый Массив;
			УникальныеРеквизитыНабор.Добавить("Опция");
			УникальныеРеквизиты.Вставить("СоставНабора", УникальныеРеквизитыНабор);
		КонецЕсли; 
	КонецЕсли;

	
	// Возвращаем сформированные параметры проверки
	Возврат УникальныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события возникающего при изменении данных реквизита "Наименование".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура НаименованиеПриИзменении(Объект, ПараметрыДействия=Неопределено) Экспорт
	
	Если ПустаяСтрока(Объект.НаименованиеПолное) Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	
КонецПроцедуры // НаименованиеПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Признак комплекта".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ПризнакКомплектаПриИзменении(Объект, ПараметрыДействия=Неопределено) Экспорт
	
	Если Объект.ПризнакКомплекта И Объект.ПризнакНабора Тогда
		Объект.ПризнакНабора=Ложь;
	КонецЕсли; 
	
КонецПроцедуры // ПризнакКомплектаПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Признак набора".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ПризнакНабораПриИзменении(Объект, ПараметрыДействия=Неопределено) Экспорт
	
	Если Объект.ПризнакКомплекта И Объект.ПризнакНабора Тогда
		Объект.ПризнакКомплекта = Ложь; 
	КонецЕсли;
	
КонецПроцедуры // ПризнакНабораПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Ставка НДС".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура СтавкаНДСПриИзменении(Объект, ПараметрыДействия=Неопределено) Экспорт
	
	Если НЕ ПроверитьКорректностьСтавкиНДС(Объект) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Указана недействительная ставка НДС.'"), Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры // СтавкаНДСПриИзменении()

#КонецОбласти

#Область ОбработчикиСобытийПолейТаблицыФормыСоставНабора

// Обработчик события возникающего при изменении данных реквизита "Опция".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура СоставНабораОпцияПриИзменении(Объект, Строка, ПараметрыДействия=Неопределено) Экспорт
	
	Если Строка <> Неопределено Тогда
		Если НЕ ЗначениеЗаполнено(Строка.Количество) Тогда
			Строка.Количество = 1;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СоставНабораОпцияПриИзменении()

#КонецОбласти

#КонецОбласти

#КонецЕсли