#Область ОписаниеПеременных

&НаКлиенте
Перем РедактируемыеРеквизиты; // Массив реквизитов редактируемый в данной форме

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПередЗаписьюОбъекта 			= ПолучитьЗначениеПараметраСтруктуры(Параметры, "ПередЗаписьюОбъекта", "");
	ПриЗаписиОбъекта 				= ПолучитьЗначениеПараметраСтруктуры(Параметры, "ПриЗаписиОбъекта", "");
	ПослеДобавленияСтроки 			= ПолучитьЗначениеПараметраСтруктуры(Параметры, "ПослеДобавленияСтроки", "");
	ПередВыгрузкойОбъекта 			= ПолучитьЗначениеПараметраСтруктуры(Параметры, "ПередВыгрузкойОбъекта", "");
	ВариантИспользованияНастройки 	= ПолучитьЗначениеПараметраСтруктуры(Параметры, "ВариантИспользованияНастройки", Перечисления.ВариантыИспользованияНастройки.ЗагрузкаДанных);
	
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы = Неопределено Тогда
		Отказ = Истина;
		Возврат;                         
	КонецЕсли;
	
	// Определим редактируемые реквизиты
	РедактируемыеРеквизиты = Новый Массив;
	Если ВариантИспользованияНастройки = ПредопределенноеЗначение("Перечисление.ВариантыИспользованияНастройки.ЗагрузкаДанных") Тогда
		РедактируемыеРеквизиты.Добавить("ПередЗаписьюОбъекта");
		РедактируемыеРеквизиты.Добавить("ПриЗаписиОбъекта");
		РедактируемыеРеквизиты.Добавить("ПослеДобавленияСтроки");
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПередЗаписьюОбъекта;
	ИначеЕсли ВариантИспользованияНастройки = ПредопределенноеЗначение("Перечисление.ВариантыИспользованияНастройки.ВыгрузкаДанных") Тогда
		РедактируемыеРеквизиты.Добавить("ПередВыгрузкойОбъекта");
		РедактируемыеРеквизиты.Добавить("ПослеДобавленияСтроки");
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПередВыгрузкойОбъекта;
	Иначе
		ВызватьИсключение НСтр("ru = 'Не указан вариант использования настройки'");
	КонецЕсли;
	
	// Скроем ненужные страницы
	Для каждого Страница Из Элементы.Страницы.ПодчиненныеЭлементы Цикл
		
		ИмяСобытия = СтрЗаменить(Страница.Имя, "Страница", "");
		ПорядковыйНомер = РедактируемыеРеквизиты.Найти(ИмяСобытия);
		Если ПорядковыйНомер = Неопределено Тогда
			Страница.Видимость = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьНадписьТекстВыражения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды "ОК"
//
&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура;
	
	Для Каждого Элемент Из РедактируемыеРеквизиты Цикл
		Результат.Вставить(Элемент, ЭтотОбъект[Элемент]);
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает текст надписи текста выражения
//
&НаКлиенте
Процедура УстановитьНадписьТекстВыражения()
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПослеДобавленияСтроки Тогда
		
		Если ВариантИспользованияНастройки = ПредопределенноеЗначение("Перечисление.ВариантыИспользованияНастройки.ЗагрузкаДанных") Тогда
			Элементы.НадписьПояснения.Заголовок =
			НСтр("ru = 'В тексте выражения можно использовать следующие предопределенные параметры:
			|   Объект         - Записываемый объект
			|   ТекущиеДанные  - Содержит данные загружаемой строки табличной части.
			|   ТекстыЯчеек    - массив текстов ячеек строки
			|Встроенные функции, функции общих модулей.'");
				
		ИначеЕсли ВариантИспользованияНастройки = ПредопределенноеЗначение("Перечисление.ВариантыИспользованияНастройки.ВыгрузкаДанных") Тогда
			Элементы.НадписьПояснения.Заголовок =
			НСтр("ru = 'В тексте выражения можно использовать следующие предопределенные параметры:
			|   Результат       - строка с рассчитанными значениями полей выгружаемого файла обращение
			|                     производится по индексу 0..N-1, где N количество колонок файла
			|   ТекущиеДанные   - структура доступных значений объекта                
			|   Ссылка          - ссылка на документ (только при выгрузке документа)
			|   ПользовательскиеДанные - структура для хранения пользовательских данных
			|Встроенные функции, функции общих модулей.'");
		Иначе
			ВызватьИсключение НСтр("ru = 'Не указан вариант использования настройки'");
		КонецЕсли;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПередВыгрузкойОбъекта Тогда
		
		Элементы.НадписьПояснения.Заголовок =
		НСтр("ru = 'В тексте выражения можно использовать следующие предопределенные параметры:              
		|   Ссылка                 - ссылка на документ (только при выгрузке документа)
		|   ПользовательскиеДанные - структура для хранения пользовательских данных
		|Встроенные функции, функции общих модулей.'");
		
	Иначе
		
		Элементы.НадписьПояснения.Заголовок =
		НСтр("ru = 'В тексте выражения можно использовать следующие предопределенные параметры:
		|   Объект         - Записываемый объект
		|   Отказ          - Признак отказа от записи объекта
		|Встроенные функции, функции общих модулей.'");
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьНадписьТекстВыражения()

// Процедура - обработчик события "ПриСменеСтраницы"
//
&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	УстановитьНадписьТекстВыражения();
	
КонецПроцедуры

#КонецОбласти

