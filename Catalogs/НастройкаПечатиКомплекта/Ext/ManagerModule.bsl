// Модуль менеджера справочника "Настройка печати комплекта"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

 #Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// 	 Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПараметрыОбработкиРеквизитовОбъекта

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Массив - Возвращаемый массив содержит строковые идентификаторы реквизитов.
//
Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	// Обязательные реквизиты объекта
	ОбязательныеРеквизиты = Новый Массив();
	ОбязательныеРеквизиты.Добавить("Наименование");
	ОбязательныеРеквизиты.Добавить("ТипДоступа");
	ОбязательныеРеквизиты.Добавить("СписокПечатныхФорм");
	ОбязательныеРеквизиты.Добавить("СпособФормированияДокументов");
	
	Если НЕ Объект.ТипДоступа = Перечисления.ТипыДоступаКВариантуОтчета.БезОграничения Тогда
		ОбязательныеРеквизиты.Добавить("ОбъектДоступа");
	КонецЕсли;
	
	// Возвращаем сформированные параметры проверки
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет уникальности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Структура - Возвращаемая структура содержит строковые идентификаторы реквизитов.
//  Для описания проверки табличных частей используется
//  вложенная структура.
//
Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	// Структура уникальных реквизитов
	УникальныеРеквизиты = Новый Структура();
	УникальныеРеквизиты.Вставить("Код");
	
	// Возвращаем сформированные параметры проверки
	Возврат УникальныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ИЗМЕНЕНИЯ ДАННЫХ РЕКВИЗИТОВ ШАПКИ

#Область Печать

// Переопределяет настройки печати для объекта.
//
// Параметры:
//  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
//
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
	
	Настройки.ПриДобавленииКомандПечати = Истина;
	
КонецПроцедуры

// Процедура используется для указания доступного данному виду объектов перечня вариантов печати.
//
// Параметры:
//  КомандыПечати - ТаблицаЗначений - Каждая строка таблицы соответствует одному из вариантов печати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	СсылкаНаДокумент = ПолучитьСсылку();
	УправлениеПечатьюПлатформа.ДобавитьКоманду(КомандыПечати,
		"Справочник.НастройкаПечатиКомплекта",
		"ОписьКомплектаДокументов",
		НСтр("ru = 'Опись комплекта документов'"),
		СсылкаНаДокумент);
	
КонецПроцедуры // ДобавитьКомандыПечати()

// Обработчик события формирования печатной формы.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОписьКомплектаДокументов") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
															"ОписьКомплектаДокументов",
															НСтр("ru = 'Опись комплекта документов'"),
															ОписьКомплектаДокументов(МассивОбъектов, ОбъектыПечати,ПараметрыПечати));
	КонецЕсли;
	
КонецПроцедуры // Печать()

Функция ОписьКомплектаДокументов(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;

	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.НастройкаПечатиКомплекта.ОписьКомплектаДокументов");

	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
	ПроверятьСоответствие = Истина;
	Если НЕ ПараметрыПечати.Свойство("ОписьКомплектов") 
		И НЕ ПараметрыПечати.Свойство("Комплект") Тогда
		ЗаполнитьОписьКомплектовИзМассива(МассивОбъектов, ПараметрыПечати);
		ПроверятьСоответствие = Ложь;
	КонецЕсли;
	
	ОбластьЗаголовок    = Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока       = Макет.ПолучитьОбласть("Строка");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("Шапка");
	ОбластьИтоги        = Макет.ПолучитьОбласть("Итоги");
	
	// Заголовок
	ОбластьЗаголовок.Параметры.ТекстЗаголовка = НСтр("ru = 'Опись комплекта документов'");
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	// Готовим шапку
	НомерСтраницы = 2; НомерСтраницыПредыдущий = 2;
		
	НаборПечатаемыхФорм= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПараметрыПечати.ОписьКомплектов);
	ВсегоЛистов = 0;
	Для Каждого Строка Из НаборПечатаемыхФорм Цикл
		
		ОписаниеМакета = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Строка,"|");
		
		// Если описание не соответствует переданному комплекту, пропустим
		Если ПроверятьСоответствие И НЕ ОписаниеМакета[0] = МассивОбъектов Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Документ = ОписаниеМакета[1];
		ОбластьСтрока.Параметры.ПФ = ОписаниеМакета[2];
		ОбластьСтрока.Параметры.Количество = ОписаниеМакета[3];
		ВсегоЛистов = ВсегоЛистов + ОписаниеМакета[3];
		
		НомерСтраницы = УправлениеПечатьюПлатформа.ВывестиГоризонтальнуюОбласть(ТабличныйДокумент, ОбластьСтрока,,, НомерСтраницы,, ПараметрыПечати.Комплект);
		
	КонецЦикла;
	
	ОбластьИтоги.Параметры.КоличествоЛистов = ВсегоЛистов;  
	ТабличныйДокумент.Вывести(ОбластьИтоги); 
	Если ЗначениеЗаполнено(ОбъектыПечати) Тогда 
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			ОбъектыПечати[0].Значение
		);	
	КонецЕсли;   
	
	Возврат ТабличныйДокумент;
	
КонецФункции //ОписьКомплектаДокументов()

Функция ЗаполнитьОписьКомплектовИзМассива(МассивОбъектов, ПараметрыПечати)
	
	Если МассивОбъектов.Количество() > 0 Тогда
		ОписьКомплектов = "";
		Для Каждого Строка из МассивОбъектов[0].СписокПечатныхФорм Цикл 
		   ОписьКомплектов = ОписьКомплектов + ?(ОписьКомплектов = "", "", ",") 
		    					+ " " + "|" + Строка.Представление 
		    					+ "|" + Строка.ИмяМакета + "|" + Строка.Копий;
		КонецЦикла;
		ПараметрыПечати.Вставить("ОписьКомплектов", ОписьКомплектов);
		ПараметрыПечати.Вставить("Комплект", МассивОбъектов[0]);   
	КонецЕсли;
	
	Возврат ПараметрыПечати;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли