#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ИмяЭлементаКоманднойПанели", "ГлобальныеКоманды");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства

	ОбновитьПредложенияНаименований(Объект, Элементы.Наименование.СписокВыбора);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НадоОповеститьОЗаписиНового = Справочники.ХарактеристикиНоменклатуры.ЗаполнитьВладельца(Объект, Параметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
	    ОбновитьЭлементыДополнительныхРеквизитов();
	    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	    Возврат;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	 	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РаботаСФормойКлиент.ЗамерВремениЗапись("ХарактеристикиНоменклатуры");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	// СтандартныеПодсистемы.ПодключаемыеКоманды	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если НадоОповеститьОЗаписиНового Тогда
		ОповеститьОЗаписиНового(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РадиусПриИзменении(Элемент)
	
	ОбновитьПредложенияНаименований(Объект, Элементы.Наименование.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	
	ОбновитьПредложенияНаименований(Объект, Элементы.Наименование.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПрофиляПриИзменении(Элемент)
	
	ОбновитьПредложенияНаименований(Объект, Элементы.Наименование.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СезонПриИзменении(Элемент)
	
	ОбновитьПредложенияНаименований(Объект, Элементы.Наименование.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ШипыПриИзменении(Элемент)
	
	ОбновитьПредложенияНаименований(Объект, Элементы.Наименование.СписокВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
//@skip-warning
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда,
                                                НавигационнаяСсылка = Неопределено,
                                                СтандартнаяОбработка = Неопределено)

	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);

КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПредложенияНаименований(Объект, СписокПредложений)
	
	СписокПредложений.Очистить();
	
	ПоляКЗаполнению = Новый Структура();
	ПоляКЗаполнению.Вставить("Ширина", "-");
	ПоляКЗаполнению.Вставить("Радиус", "-");
	ПоляКЗаполнению.Вставить("Сезон", "-");
	ПоляКЗаполнению.Вставить("Отношение", "-");
	ПоляКЗаполнению.Вставить("Шипы", "");
	КоэффициетПроценты = 100;
	
	Для Каждого КлючЗначение Из ПоляКЗаполнению Цикл
		
		Если КлючЗначение.Ключ = "Отношение" Тогда
			
			Если ЗначениеЗаполнено(Объект.Ширина) И ЗначениеЗаполнено(Объект.ВысотаПрофиля) Тогда
				
				ПоляКЗаполнению.Отношение = Окр(Объект.ВысотаПрофиля / Объект.Ширина * КоэффициетПроценты,
					0, РежимОкругления.Окр15как20);
				
			КонецЕсли;
			
		ИначеЕсли КлючЗначение.Ключ = "Шипы" Тогда
			
			Если Объект.Шипы Тогда
				
				ПоляКЗаполнению.Шипы = Символы.НПП + НСтр("ru = 'Ш'");
				
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(Объект[КлючЗначение.Ключ]) Тогда
				
				ПоляКЗаполнению[КлючЗначение.Ключ] = Объект[КлючЗначение.Ключ];
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстПредложения = СтрШаблон(
		"%1/%2 R%3 %4%5",
		ПоляКЗаполнению.Ширина,
		ПоляКЗаполнению.Отношение,
		ПоляКЗаполнению.Радиус,
		ПоляКЗаполнению.Сезон,
		ПоляКЗаполнению.Шипы);
	СписокПредложений.Добавить(ТекстПредложения);
	
КонецПроцедуры

#КонецОбласти