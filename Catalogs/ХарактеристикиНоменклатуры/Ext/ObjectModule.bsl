
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	
	// Вызываем общий обработчик события
	ПрефиксацияОбъектов.ПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс);
	
КонецПроцедуры // ПриУстановкеНовогоКода()

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ПродолжитьЗаполнение = ОбработкаСобытийСправочникаСервер.ОбработкаЗаполнения(
		ЭтотОбъект,
		ДанныеЗаполнения,
		ТекстЗаполнения,
		СтандартнаяОбработка);
	
	Если НЕ ПродолжитьЗаполнение Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Наименование = Справочники.ХарактеристикиНоменклатуры.СформироватьНаименованиеПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	// Вызываем общий обработчик события
	Если НЕ ОбработкаСобытийСправочникаСервер.ПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = "";
	
КонецПроцедуры // ПриКопировании()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Вызываем общий обработчик события
	Если НЕ ОбработкаСобытийСправочникаСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПередЗаписью(Отказ)
	
	ЗаполнитьНаименование();
	ЗаполнитьСортировку();

	Если НЕ ОбработкаСобытийСправочникаСервер.ПередЗаписью(ЭтотОбъект, Отказ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ ОбработкаСобытийСправочникаСервер.ПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ ОбработкаСобытийСправочникаСервер.ПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередУдалением()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаименование()
	
	Наименование = СокрЛП(Наименование);
	
	Если ПустаяСтрока(Наименование) ИЛИ Наименование="<" + НСтр("ru = 'не заполнены реквизиты'") + ">" Тогда
		
		Наименование = Справочники.ХарактеристикиНоменклатуры.СформироватьНаименованиеПоУмолчанию(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Функция АвтоСписаниеХарактеристик()
	
	ПутьКРеквизиту = "АвтоСписаниеХарактеристик";
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		ПутьКРеквизиту = "ТипНоменклатуры.АвтоСписаниеХарактеристик";
		
	КонецЕсли;
	
	АвтоСписаниеХарактеристик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, ПутьКРеквизиту);
	
	Если НЕ ЗначениеЗаполнено(АвтоСписаниеХарактеристик) Тогда
		
		АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание;
		
	КонецЕсли;
	
	Возврат АвтоСписаниеХарактеристик;
	
КонецФункции

Процедура ЗаполнитьСортировку()
	
	АвтоСписаниеХарактеристик = АвтоСписаниеХарактеристик();
	ПустаяДата = Дата(1, 1, 1);
	ДатаИзготовленияВозр = Перечисления.РежимыАвтоСписанияХарактеристик.ДатаИзготовленияВозр;
	ДатаИзготовленияУбыв = Перечисления.РежимыАвтоСписанияХарактеристик.ДатаИзготовленияУбыв;
	КачествоВозр = Перечисления.РежимыАвтоСписанияХарактеристик.КачествоВозр;
	КачествоУбыв = Перечисления.РежимыАвтоСписанияХарактеристик.КачествоУбыв;
	СрокГодностиВозр = Перечисления.РежимыАвтоСписанияХарактеристик.СрокГодностиВозр;
	СрокГодностиУбыв = Перечисления.РежимыАвтоСписанияХарактеристик.СрокГодностиУбыв;
	
	Если АвтоСписаниеХарактеристик = ДатаИзготовленияВозр Тогда
		
		Сортировка = ДатаИзготовления - ПустаяДата;
		Возврат;
		
	КонецЕсли;
	
	Если АвтоСписаниеХарактеристик = ДатаИзготовленияУбыв Тогда
		
		Сортировка = ПустаяДата - ДатаИзготовления;
		Возврат;
		
	КонецЕсли;
	
	СортировкаТип = Метаданные.Справочники.ХарактеристикиНоменклатуры.Реквизиты.Сортировка.Тип;
	
	Если АвтоСписаниеХарактеристик = КачествоВозр Тогда
		
		Сортировка = СортировкаТип.ПривестиЗначение(Качество.Код);
		Возврат;
		
	КонецЕсли;
	
	Если АвтоСписаниеХарактеристик = КачествоУбыв Тогда
		
		Запрос=Новый Запрос("ВЫБРАТЬ КОЛИЧЕСТВО(*) КАК Кол ИЗ Справочник.Качество");
		КоличествоКачеств = Запрос.Выполнить().Выгрузить().Получить(0).Кол;
		Сортировка = СортировкаТип.ПривестиЗначение(Качество.Код) - КоличествоКачеств;
		Возврат;
		
	КонецЕсли;
	
	Если АвтоСписаниеХарактеристик = СрокГодностиВозр Тогда
		
		Сортировка = СрокГодности - ПустаяДата;
		Возврат;
		
	КонецЕсли;
	
	Если АвтоСписаниеХарактеристик = СрокГодностиУбыв Тогда
		
		Сортировка = ПустаяДата - СрокГодности;
		Возврат;
		
	КонецЕсли;
	
	Сортировка = 0;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
