///////////////////////////////////////////////////////////////////////////////
// Модуль формы секций ПТС
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПолучитьЗначениеПараметраСтруктуры(Параметры, "ПараметрыПечати") <> Неопределено Тогда
		Если ТипЗнч(Параметры.ПараметрыПечати.ОбъектыПечати) = Тип("Массив")
		   И Параметры.ПараметрыПечати.ОбъектыПечати.Количество() > 0 Тогда
			Покупатель = Параметры.ПараметрыПечати.ОбъектыПечати[0].Контрагент;
			Дилер = Параметры.ПараметрыПечати.ОбъектыПечати[0].Организация;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события возникающего на клиенте при открытии формы, до показа окна пользователю.
//
// Параметры:
//  Отказ - Булево - Признак отказа от создания формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.Печать.Доступность = Ложь;
	ВариантПечати = 1;
	Для Инд = 1 По 4 Цикл
		ИмяЭлемента = "Секция" + Инд;
		ИмяНадписи = "тОписание" + Инд;
		ИмяНадписиКонтрагент = "тКонтрагент" + Инд;
		Элементы[ИмяНадписи].Заголовок = "Секция не выбрана";
		Элементы[ИмяНадписиКонтрагент].Заголовок = "нет данных";
		СекцияБулево.Добавить(Ложь);
		ДилерМассив.Добавить(Ложь);
	КонецЦикла;
	
	УстановитьСекции();
	УстановитьДилеров();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события возникающего на клиенте при нажатии на поле картинки ПТС.
//
// Параметры:
//  Элемент	 - ЭлементФормы	 - Элемент, в котором произошло нажатие.
//
&НаКлиенте
Процедура ПолеКартинкиПТСНажатие(Элемент)
	
	ИмяЭлемента = Элемент.Имя;
	
	Если ИмяЭлемента<>Неопределено Тогда
		ВыбСекция = Прав(ИмяЭлемента,1);
	КонецЕсли;
	
	Если НЕ СекцияБулево[ВыбСекция-1].Значение Тогда
		Если ВариантПечати = 2 Тогда
			Список = Новый СписокЗначений;
			Список.Добавить(1,"Секция дилера");
			Список.Добавить(2,"Секция покупателя");
			Оп = Новый ОписаниеОповещения("ВыполнитьПослеВыбора", ЭтотОбъект);
			ПоказатьВыборИзСписка(Оп, Список, Элемент);
		ИначеЕсли ВариантПечати = 1 Тогда
			Список = Новый СписокЗначений;
			Список.Добавить(2,"Секция покупателя");
			ВыполнитьПослеВыбора(Список[0]);
		КонецЕсли;
	Иначе
		Список = Новый СписокЗначений;
		Список.Добавить(2,"Секция покупателя");
		ВыполнитьПослеВыбора(Список[0]);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события возникающего на клиенте при выполнении команды "Печать".
//
// Параметры:
//  Кнопка - КнопкаФормы - Кнопка, при нажатии на которую возникло данное событие.
//
&НаКлиенте
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("МассивСекций",  МассивСекций);
	СтруктураВозврата.Вставить("ВладелецФормы", ЭтотОбъект.ВладелецФормы);
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик события возникающего при выполнении оповещения данной формы о завершении выбора из созанного выпадающего списка.
//
// Параметры:
//  РезультатОповещения     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
&НаКлиенте
Процедура ВыполнитьПослеВыбора(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если РезультатОповещения <> Неопределено Тогда
		ВидСекции = РезультатОповещения.Значение;
	КонецЕсли;
	
	Если ВидСекции <> 1 И ВидСекции <> 2 Тогда
		ВидСекции = 1;
	КонецЕсли;
	
	ВыполнитьПослеВыбораНаСервере(ВидСекции);
	
КонецПроцедуры

// Процедура - Выполнить после выбора на сервере
//
// Параметры:
//  ВидСекции - Число - Вид секции для печати (дилера или покупателя).
//
&НаСервере
Процедура ВыполнитьПослеВыбораНаСервере(ВидСекции)
	
	ЦветДилера     = WebЦвета.Васильковый;
	ЦветПокупателя = WebЦвета.ЦветМорскойВолны;
	
	Для Инд = 1 По 4 Цикл
		ИмяЭлемента = "Секция" + Инд;
		ИмяНадписи = "тОписание" + Инд;
		ИмяНадписиКонтрагент = "тКонтрагент" + Инд;
		Если ВыбСекция = Инд Тогда
			Если НЕ СекцияБулево[Инд-1].Значение Тогда
				Если ВариантПечати = 2 Тогда
					Если ВидСекции = 1 Тогда
						Элементы[ИмяНадписи].Заголовок = Новый ФорматированнаяСтрока("Секция дилера",, ЦветДилера);
						Элементы[ИмяНадписиКонтрагент].Заголовок = Новый ФорматированнаяСтрока(Дилер.Наименование,, ЦветДилера);
						СекцияБулево[Инд-1].Значение = Истина;
						ДилерМассив[Инд-1].Значение = Истина;
						Если Инд <> 4 Тогда
							Элементы["тОписание" + (Инд+1)].Заголовок = Новый ФорматированнаяСтрока("Секция покупателя",, ЦветПокупателя);
							Элементы["тКонтрагент" + (Инд+1)].Заголовок = Новый ФорматированнаяСтрока(Покупатель.Наименование,, ЦветПокупателя);
							СекцияБулево[Инд].Значение = Истина;
							ДилерМассив[Инд].Значение= Ложь;
						КонецЕсли;
					Иначе
						Элементы[ИмяНадписи].Заголовок = Новый ФорматированнаяСтрока("Секция покупателя",, ЦветПокупателя);
						Элементы[ИмяНадписиКонтрагент].Заголовок = Новый ФорматированнаяСтрока(Покупатель.Наименование,, ЦветПокупателя);
						СекцияБулево[Инд-1].Значение = Истина;
						ДилерМассив[Инд-1].Значение = Ложь;
					КонецЕсли;
				Иначе
					Элементы[ИмяНадписи].Заголовок = Новый ФорматированнаяСтрока("Секция покупателя",, ЦветПокупателя);
					Элементы[ИмяНадписиКонтрагент].Заголовок = Новый ФорматированнаяСтрока(Покупатель.Наименование,, ЦветПокупателя);
					СекцияБулево[Инд-1].Значение = Истина;
					ДилерМассив[Инд-1].Значение = Ложь;
				КонецЕсли;
			Иначе
				Элементы[ИмяНадписи].Заголовок = "Секция не выбрана";
				Элементы[ИмяНадписиКонтрагент].Заголовок = "нет данных";
				СекцияБулево[Инд-1].Значение = Ложь;
				ДилерМассив[Инд-1].Значение = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	МассивСекций.Очистить();
	ПустыхСекций = 0;
	Для Инд = 1 По 4 Цикл
		ИмяЭлементаБулево = "СекцияБулево" + Инд;
		ДилерИлиПокупатель = "Дилер" + Инд;
		Если СекцияБулево[Инд-1].Значение Тогда
			Если ДилерМассив[Инд-1].Значение Тогда
				МассивСекций.Добавить("СекцияДилера");
			Иначе
				МассивСекций.Добавить("СекцияПокупателя");
			КонецЕслИ;
		Иначе
			МассивСекций.Добавить("Пустая");
			ПустыхСекций = ПустыхСекций + 1;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.Печать.Доступность = ?(ПустыхСекций = 4, Ложь, Истина);
	
	УстановитьСекции();
	УстановитьДилеров();
	
КонецПроцедуры

// Установить значения использования секции
//
&НаСервере
Процедура УстановитьСекции()
	
	СекцияБулево1 = СекцияБулево[0];
	СекцияБулево2 = СекцияБулево[1];
	СекцияБулево3 = СекцияБулево[2];
	СекцияБулево4 = СекцияБулево[3];
	
КонецПроцедуры

// Установить значения дилеров и покупателей
//
&НаСервере
Процедура УстановитьДилеров()
	
	Дилер1 = ДилерМассив[0];
	Дилер2 = ДилерМассив[1];
	Дилер3 = ДилерМассив[2];
	Дилер4 = ДилерМассив[3];
	
КонецПроцедуры

#КонецОбласти

