
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	
	ПрефиксацияОбъектов.ПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ПродолжатьЗаполнение = ОбработкаСобытийСправочникаСервер.ОбработкаЗаполнения(
		ЭтотОбъект,
		ДанныеЗаполнения,
		ТекстЗаполнения,
		СтандартнаяОбработка
	);
	
	
	Если НЕ ПродолжатьЗаполнение Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоЗаказамНаАвтомобили(ДанныеЗаполнения);
	ЗаполнитьПоМарке(ДанныеЗаполнения);
	
	Если НЕ ЭтоГруппа Тогда
		
		Если ВалютаУчета.Пустая() Тогда
			
			ВалютаУчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			
		КонецЕсли;
		
		Если ВключатьВПрайсЛист.Пустая() Тогда
			
			Если Родитель.ВключатьВПрайсЛист.Пустая() Тогда
				
				ВключатьВПрайсЛист = Перечисления.ВидВключенияВПрайсЛист.ПоУмолчанию;
				
			Иначе
				
				ВключатьВПрайсЛист = Родитель.ВключатьВПрайсЛист;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Автор = Неопределено;
	ДатаРегистрации = Неопределено;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ОбработкаСобытийСправочникаСервер.ПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
	Автор = Неопределено;
	ДатаРегистрации = Неопределено;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ОбработкаСобытийСправочникаСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		РезультатПроверки = АвтомобилиСервер.НомераАвтомобиляКорректны(ЭтотОбъект);
		Отказ = Отказ ИЛИ РезультатПроверки.Ошибка;
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		РезультатПроверки = АвтомобилиСервер.НомераАвтомобиляУникальны(ЭтотОбъект);
		Отказ = Отказ ИЛИ РезультатПроверки.Ошибка;
		
	КонецЕсли;
	
	// Проверим заполнение РНПТ у послеживаемого товара
	УчетПрослеживаемыхТоваровСервер.ПроверитьЗаполнениеРНПТПрослеживаемогоАвтомобиля(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если НЕ ОбработкаСобытийСправочникаСервер.ПередЗаписью(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ЭтоГруппа Тогда
		Если ЗначениеЗаполнено(ВариантКомплектации) Тогда
			ТипКузова    = Справочники.ТипыКузовов.ПустаяСсылка();
			ТипКПП       = Справочники.ТипыКПП.ПустаяСсылка();
		КонецЕсли;
		
		Если НЕ МодельДвигателя.Пустая() Тогда
			ТипДвигателя = Справочники.ТипыДвигателей.ПустаяСсылка();
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НаименованиеПолное) Тогда
			НаименованиеПолное = Наименование;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Автор           = Пользователи.ТекущийПользователь();
		ДатаРегистрации = ТекущаяДатаСеанса();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если НЕ ОбработкаСобытийСправочникаСервер.ПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоЗаказамНаАвтомобили(ДанныеЗаполнения)
	
	Если НЕ ЭтоЗаказСАвтомобилем(ДанныеЗаполнения) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПереносимыеПоля = "Модель,
					  |ВариантКомплектации,
					  |Цвет,
					  |ЦветКод,
					  |ТипСалона";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, ПереносимыеПоля);	
		
	Если ЗначениеЗаполнено(ДанныеЗаполнения.ВариантКомплектации) Тогда
		
		РеквизитыВариантКомплектации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДанныеЗаполнения.ВариантКомплектации, "ТипКузова,ТипДвигателя,ТипКПП");
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыВариантКомплектации);
		
	КонецЕсли;
	
	Если ЭтоЗаказПоставщикуНаАвтомобиль(ДанныеЗаполнения) Тогда
		
		Поставщик = ДанныеЗаполнения.Контрагент;
		
	КонецЕсли;
			
	Наименование       = Справочники.Автомобили.СформироватьНаименованиеПоУмолчанию(ЭтотОбъект);
	НаименованиеПолное = Справочники.Автомобили.СформироватьНаименованиеПолноеПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Функция ЭтоЗаказСАвтомобилем(Документ)
	
	Возврат ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказНаАвтомобиль")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументОбъект.ЗаказНаАвтомобиль")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументОбъект.ЗаказПоставщикуНаАвтомобиль");
	
КонецФункции

Функция ЭтоЗаказПоставщикуНаАвтомобиль(Документ)
	
	Возврат ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль")
		ИЛИ  ТипЗнч(Документ) = Тип("ДокументОбъект.ЗаказПоставщикуНаАвтомобиль");
	
КонецФункции

Процедура ЗаполнитьПоМарке(ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("СправочникСсылка.Марки") Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеПолное = "";
	Марка = ДанныеЗаполнения;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
