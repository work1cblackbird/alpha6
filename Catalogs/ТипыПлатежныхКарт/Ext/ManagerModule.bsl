// Модуль менеджера справочника "Типы платежных карт"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ОбработчикиОбновления 
Процедура УстановкаТипаПлатежнойКарты() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТипыПлатежныхКарт.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТипыПлатежныхКарт КАК ТипыПлатежныхКарт
		|ГДЕ
		|	ТипыПлатежныхКарт.Основной = ИСТИНА";  
	
	РезультатЗапроса = Запрос.Выполнить();  
	
	Если  РезультатЗапроса.Пустой() Тогда   
		Попытка
			ТипКартыОбъект = Справочники.ТипыПлатежныхКарт.Мир.ПолучитьОбъект();
			ТипКартыОбъект.Основной = Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ТипКартыОбъект);
		Исключение 
			 ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка установки основного типа платежных карт'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Предупреждение,
				,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			);

		КонецПопытки;
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// 	 Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции  
 
#Область ОбработчикиСобытийЭлементовШапкиФормы
	
// Обработчик события возникающего при изменении данных реквизита "Наименование".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ОсновнойПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТипыПлатежныхКарт.Представление КАК Представление
		|ИЗ
		|	Справочник.ТипыПлатежныхКарт КАК ТипыПлатежныхКарт
		|ГДЕ
		|	ТипыПлатежныхКарт.Основной = Истина";
		
	РезультатЗапроса = Запрос.Выполнить(); 
	
	Если НЕ РезультатЗапроса.Пустой() Тогда  
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Если Объект.Основной Тогда 
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru='Основным может быть только один тип платежной системы. 
					|Необходимо снять флаг ""Основной"" в типе платежной системы <%1>.'"),
					СокрЛП(Выборка.Представление)	
				)
			);
			Объект.Основной = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ПараметрыОбработкиРеквизитовОбъекта

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Массив - Возвращаемый массив содержит строковые идентификаторы реквизитов.
//
Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	// Обязательные реквизиты объекта
	ОбязательныеРеквизиты = Новый Массив();
	ОбязательныеРеквизиты.Добавить("Наименование");
	
	// Возвращаем сформированные параметры проверки
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет уникальности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Структура - Возвращаемая структура содержит строковые идентификаторы реквизитов.
//  Для описания проверки табличных частей используется
//  вложенная структура.
//
Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	// Структура уникальных реквизитов
	УникальныеРеквизиты = Новый Структура();
	УникальныеРеквизиты.Вставить("Код");
	
	// Возвращаем сформированные параметры проверки
	Возврат УникальныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

#КонецОбласти

// Возвращает тип платежной карты отмеченый как основной
//
// Возвращаемое значение:
//  Ссылка - ссылка на элемент справочника отмеченый как основной.
//
Функция ОсновнойТипПлатежнойКарты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТипыПлатежныхКарт.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТипыПлатежныхКарт КАК ТипыПлатежныхКарт
		|ГДЕ
		|	ТипыПлатежныхКарт.Основной = ИСТИНА";  
	
	РезультатЗапроса = Запрос.Выполнить();  
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();  
		Выборка.Следующий();
		Возврат  Выборка.Ссылка;
		
	Иначе 
		
		Возврат Справочники.ТипыПлатежныхКарт.ПустаяСсылка();
		
	КонецЕсли;

КонецФункции

#КонецОбласти

#КонецЕсли