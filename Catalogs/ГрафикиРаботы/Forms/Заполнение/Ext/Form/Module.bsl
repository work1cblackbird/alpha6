///////////////////////////////////////////////////////////////////////////////
// Модуль формы заполнения регистра "График работ календарный"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ГрафикРаботы") Тогда
		ГрафикРаботы = Параметры.ГрафикРаботы;
	КонецЕсли;
	Если Параметры.Свойство("ГрафикРаботОснование") Тогда
		ГрафикРаботыОснование = Параметры.ГрафикРаботОснование;
	КонецЕсли;
	
	Если Параметры.Свойство("Вытеснить") Тогда
		Вытеснить = Параметры.Вытеснить;
	ИначеЕсли Параметры.Свойство("Очистить") Тогда
		Очистить = Параметры.Очистить;
	ИначеЕсли Параметры.Свойство("ЗаполнитьПоГрафику") Тогда
		ЗаполнитьПоГрафику = Параметры.ЗаполнитьПоГрафику;
	Иначе
		Заполнить = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Обработчик события возникающего на клиенте при открытии формы, до показа окна пользователю.
//
// Параметры:
//  Отказ - Булево - Признак отказа от создания формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Обновляем отображение элементов формы
	УправлениеДиалогом();
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события возникающего на клиенте при изменении данных реквизита "График работы основание".
//
// Параметры:
//  Элемент - ПолеФормы - Элемент управления, в котором возникло данное событие.
//
&НаКлиенте
Процедура ГрафикРаботыОснованиеПриИзменении(Элемент)
	
	Если ТипЗнч(ГрафикРаботыОснование) = Тип("СправочникСсылка.ПроизводственныеКалендари") Тогда
		ЗамещатьТолькоПраздники = Истина;
		ЗамещатьПолныйДень      = Истина;
		Элементы.ЗамещатьТолькоПраздники.Доступность = Ложь;
		Элементы.ЗамещатьПолныйДень.Доступность      = Ложь;
	Иначе
		Элементы.ЗамещатьТолькоПраздники.Доступность = Истина;
		Элементы.ЗамещатьПолныйДень.Доступность      = Истина;
	КонецЕсли;
	
КонецПроцедуры // ГрафикРаботыОснованиеПриИзменении()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события возникающего на клиенте при нажатии кнопки "Применить".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура Применить(Команда)
	
	// Проверим обязательные реквизиты
	ДатаНачала = Период.ДатаНачала;
	ДатаОкончания = Период.ДатаОкончания;
	
	ТекстВозврата = "";
	Если (ЗаполнитьПоГрафику ИЛИ Вытеснить) И НЕ ЗначениеЗаполнено(ГрафикРаботыОснование) Тогда
		ТекстВозврата = ТекстВозврата + НСтр("ru='Не указан шаблон заполнения'") + Символы.ПС;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДатаНачала) ИЛИ НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ТекстВозврата = ТекстВозврата + НСтр("ru='Не указан период заполнения'") + Символы.ПС;
	КонецЕсли;
	Если НЕ ПустаяСтрока(ТекстВозврата) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстВозврата);
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("Период",                  Период);
	ПараметрыВыбора.Вставить("ГрафикРаботыОснование",   ГрафикРаботыОснование);
	ПараметрыВыбора.Вставить("ВоскресеньеВыходной",     ВоскресеньеВыходной);
	ПараметрыВыбора.Вставить("СубботаВыходной",         СубботаВыходной);
	ПараметрыВыбора.Вставить("ЗамещатьТолькоПраздники", ЗамещатьТолькоПраздники);
	ПараметрыВыбора.Вставить("ЗамещатьПолныйДень",      ЗамещатьПолныйДень);
	
	Закрыть(ПараметрыВыбора);
	
КонецПроцедуры // Применить()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Производит настройку параметров отображения элементов управления диалога в зависимости от значений реквизитов
// объекта.
//
&НаКлиенте
Процедура УправлениеДиалогом()
	
	Элементы.ГруппаЗаполнить.Видимость = Заполнить;
	Элементы.ГрафикРаботыОснование.Видимость = ЗаполнитьПоГрафику ИЛИ Вытеснить;
	Элементы.ГрафикРаботыОснование.АвтоОтметкаНезаполненного = Вытеснить;
	Элементы.ГруппаЗамещатьПраздники.Видимость = Вытеснить;
	
КонецПроцедуры // УправлениеДиалогомНаСервере()

#КонецОбласти

