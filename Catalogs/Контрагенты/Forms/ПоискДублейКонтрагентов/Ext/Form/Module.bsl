///////////////////////////////////////////////////////////////////////////////
// Модуль общей формы "Форма найденных дублей"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СписокДублей") Тогда
		
		ОтобразитьДубли(Параметры.СписокДублей);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТаблицаНайденоПриАктивизацииСтроки(Элемент)
	
	Элементы.ТаблицаРасшифровкаНайдено.ОтборСтрок = Новый ФиксированнаяСтруктура("Объект",
		Элементы.ТаблицаНайдено.ТекущиеДанные.Объект); 
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНайденоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(,Элемент.ТекущиеДанные.Объект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Закрыть(Новый Структура("Результат","Продолжить"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНайденному(Команда)
	
	Закрыть(Новый Структура("Результат,Объект","Перейти",Элементы.ТаблицаНайдено.ТекущиеДанные.Объект));
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	Закрыть(Новый Структура("Результат","ПродолжитьНеЗакрывая"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура ОтобразитьДубли(СписокДублей)
	
	ФормаСобственности = Параметры.Объект.ФормаСобственности;
	ЗапретЗаписи = Ложь;
	
	Для Каждого Дубль Из СписокДублей Цикл
		
		Отбор = Новый Структура();
		Отбор.Вставить("Объект", Дубль.Значение.Ссылка);
		
		НайденнаяСтрока = ТаблицаНайдено.НайтиСтроки(Отбор);
		
		Если НайденнаяСтрока.Количество() = 0 Тогда
			
			ТекСтрока = ТаблицаНайдено.Добавить();
			ТекСтрока.Объект = Дубль.Значение.Ссылка;
			ОбъектТаблицыНайдено = Дубль.Значение.Ссылка;
			
		КонецЕсли;
		
		Строка = ТаблицаРасшифровкаНайдено.Добавить();
		Строка.Объект = Дубль.Значение.Ссылка;
		Строка.ПредставлениеНайденоПо = Строка(Дубль.Значение.ИмяРеквизита)
			+ " - "
			+ Строка(Дубль.Значение.ЗначениеРеквизита);
		
		Если ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
				
			Если Дубль.Значение.ИмяРеквизита = НСтр("ru = 'Наименование и НаименованиеПолное и ДатаРождения'") Тогда
				
				СтрокаТаблицыНайдено = ТаблицаНайдено.НайтиСтроки(Новый Структура("Объект",Дубль.Значение.Ссылка))[0];
				СтрокаТаблицыНайдено.ДубльПоИННИКПП = Истина;
				Элементы.ТаблицаНайденоДубльПоИННИКПП.Видимость = Истина;
				ЗапретЗаписи = Истина;
				
			КонецЕсли;

		ИначеЕсли НЕ ФормаСобственности = Перечисления.ФормыСобственности.ПрочаяФормаСобственности Тогда
			
			Если Дубль.Значение.ИмяРеквизита = НСтр("ru = 'ИНН и КПП'") Тогда
				
				СтрокаТаблицыНайдено = ТаблицаНайдено.НайтиСтроки(Новый Структура("Объект",Дубль.Значение.Ссылка))[0];
				СтрокаТаблицыНайдено.ДубльПоИННИКПП = Истина;
				Элементы.ТаблицаНайденоДубльПоИННИКПП.Видимость = Истина;
				ЗапретЗаписи = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПравоПользователя("ЗапретитьСохранениеДублейКонтрагентов",Строка.Объект) И ЗапретЗаписи Тогда
			
			Элементы.Записать.Доступность = Ложь;
			Элементы.ЗаписатьИЗакрыть.Доступность = Ложь;
			Элементы.ПерейтиКНайденному.КнопкаПоУмолчанию = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

