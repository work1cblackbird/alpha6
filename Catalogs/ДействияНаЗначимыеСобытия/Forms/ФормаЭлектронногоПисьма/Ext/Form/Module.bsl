///////////////////////////////////////////////////////////////////////////////
// Модуль формы справочника "Действия на значимые события"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	
	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
	Если Параметры.ДополнительныеПараметры.Свойство("Источник") Тогда
		Источник = Параметры.ДополнительныеПараметры.Источник;
		СсылочныйТипИсточника = НЕ (Источник.ЗначениеПустойСсылки = Неопределено);
	КонецЕсли;
	
	Если Параметры.Свойство("ВидДействия") Тогда
		Объект.ВидДействия = Параметры.ВидДействия;
		Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
			Справочники.ДействияНаЗначимыеСобытия.СформироватьНаименованиеПоУмолчанию(Объект);
		КонецЕсли;
	КонецЕсли;
	
	ВидДействияУст = Объект.ВидДействия;
	
	Текст = ?(ЗначениеЗаполнено(Объект.эпТекстПисьмаПроизвольныйКод),Объект.эпТекстПисьмаПроизвольныйКод,Объект.эпТекстПисьма);
	Если Объект.эпФорматТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTML Тогда
		СтруктураВложенийСодержание = Новый Структура;
		ТекстПисьмаФорматированныйДокумент.УстановитьHTML(Текст, СтруктураВложенийСодержание);
	Иначе
		ТекстПисьма = Текст;
	КонецЕсли;
	
	РаботаСФормой.УстановитьДоступностьПоляКодНаФормеСправочника(ЭтотОбъект, Объект);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		УправлениеДиалогомНаСервере();
	
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Настроим командную панель формы
	ЗащищенныеФункцииКлиент.НастроитьЭлементФормыТабличнойЧасти(ЭтотОбъект,"ПолучателиПисьма");
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
		
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.АдреснаяКнига") Тогда
		
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("Массив") И ТипЗнч(ВыбранноеЗначение) <> Тип("Соответствие") Тогда
			Возврат;
		КонецЕсли;
		
		// Получим список адресатов
		ЗаполнитьПодобранныхПолучателей(ВыбранноеЗначение);
		Возврат;
	КонецЕсли;
		
КонецПроцедуры 

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Подготовим документ HTML из содержимого форматированного документа
	Если Объект.эпФорматТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTML Тогда
		
		// Содержание
		СтруктураВложений = Новый Структура;
		ТекстHTML = "";
		ТекстПисьмаФорматированныйДокумент.ПолучитьHTML(ТекстHTML, СтруктураВложений);
		ТекущийОбъект.эпТекстПисьма = ТекстHTML; 
		ТекущийОбъект.эпТекстПисьмаПроизвольныйКод  = "";
		
	Иначе
		
		Если ПроизвольныйКод Тогда
			ТекущийОбъект.эпТекстПисьмаПроизвольныйКод = ТекстПисьма;
			ТекущийОбъект.эпТекстПисьма = "";
		Иначе
			ТекущийОбъект.эпТекстПисьма = ТекстПисьма;
			ТекущийОбъект.эпТекстПисьмаПроизвольныйКод = "";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АвторНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидовПравил = Новый СписокЗначений;
	Элементы.Автор.СписокВыбора.Очистить();
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение"));
	Если СсылочныйТипИсточника Тогда
		СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.РеквизитОбъектаИсточника"));
	КонецЕсли;
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ПроизвольныйКод"));
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ПродолжитьВыборРеквизита",Элементы.Автор);
	ПараметрыОбработки.Вставить("ИмяФормы","Справочник.Пользователи");
	ПараметрыОбработки.Вставить("ИмяРеквизита",?(ЗначениеЗаполнено(Объект.АвторПроизвольныйКод),Объект.АвторПроизвольныйКод,"Автор"));
	ОписаниеПродолжениеВыбора = Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияВыборРеквизита", ЭтотОбъект, ПараметрыОбработки);
	
	ПоказатьВыборИзСписка(ОписаниеПродолжениеВыбора,СписокВидовПравил, Элемент);
	
КонецПроцедуры 

&НаКлиенте
Процедура АвторОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		Объект.АвторПроизвольныйКод = ВыбранноеЗначение;
		Объект.Автор = "<" + НСтр("ru = 'Произвольное выражение'")+ ">";
		Объект.АвторПутьКДанным = "";
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Пользователи") Тогда
		
		Объект.Автор = ВыбранноеЗначение;
		Объект.АвторПутьКДанным = "";
		Объект.АвторПроизвольныйКод = "";
		
	Иначе
		
		// Проверим соответствие типов
		Если НЕ ВыбранноеЗначение.ТипМетаданного.СодержитТип(Тип("СправочникСсылка.Пользователи")) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю (НСтр("ru='Выбранный реквизит не является ссылкой на элемент справочника ""Пользователи"".'"));
			Возврат;
		КонецЕсли;
				
		Объект.Автор = ВыбранноеЗначение.Поле + " (Владелец)";
		Объект.АвторПутьКДанным = ВыбранноеЗначение.ИмяМетаданного;
		Объект.АвторПроизвольныйКод = "";
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВажностьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидовПравил = Новый СписокЗначений;
	Элементы.Важность.СписокВыбора.Очистить();
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение"));
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ПроизвольныйКод"));
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ПродолжитьВыборРеквизита",Элементы.Важность);
	ПараметрыОбработки.Вставить("ИмяТипа","ВариантыВажностиВзаимодействия");
	ПараметрыОбработки.Вставить("ИмяРеквизита",?(ЗначениеЗаполнено(Объект.ВажностьПроизвольныйКод),Объект.ВажностьПроизвольныйКод,"Важность"));
	ОписаниеПродолжениеВыбора = Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияВыборРеквизита", ЭтотОбъект, ПараметрыОбработки);
	
	ПоказатьВыборИзСписка(ОписаниеПродолжениеВыбора,СписокВидовПравил, Элемент);
	
КонецПроцедуры 

&НаКлиенте
Процедура ВажностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		СтандартнаяОбработка = Ложь;
		Объект.ВажностьПроизвольныйКод = ВыбранноеЗначение;
		Объект.Важность = "<" + НСтр("ru = 'Произвольное выражение'")+ ">";
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура эпУчетнаяЗаписьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидовПравил = Новый СписокЗначений;
	Элементы.эпУчетнаяЗапись.СписокВыбора.Очистить();
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение"));
	Если СсылочныйТипИсточника Тогда
		СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.РеквизитОбъектаИсточника"));
	КонецЕсли;
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ПроизвольныйКод"));
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ПродолжитьВыборРеквизита",Элементы.эпУчетнаяЗапись);
	ПараметрыОбработки.Вставить("ИмяФормы","Справочник.УчетныеЗаписиЭлектроннойПочты");
	ПараметрыОбработки.Вставить("ИмяРеквизита",?(ЗначениеЗаполнено(Объект.эпУчетнаяЗаписьПроизвольныйКод),Объект.эпУчетнаяЗаписьПроизвольныйКод,"УчетнаяЗапись"));
	ОписаниеПродолжениеВыбора = Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияВыборРеквизита", ЭтотОбъект, ПараметрыОбработки);
	
	ПоказатьВыборИзСписка(ОписаниеПродолжениеВыбора,СписокВидовПравил, Элемент);
	
КонецПроцедуры //эпУчетнаяЗаписьНачалоВыбора()

&НаКлиенте
Процедура эпУчетнаяЗаписьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		СтандартнаяОбработка = Ложь;
		Объект.эпУчетнаяЗаписьПроизвольныйКод = ВыбранноеЗначение;
		Объект.эпУчетнаяЗапись = "<" + НСтр("ru = 'Произвольное выражение'")+ ">";
		Объект.эпУчетнаяЗаписьПутьКДанным = "";
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда
		
		Объект.эпУчетнаяЗапись = ВыбранноеЗначение;
		Объект.эпУчетнаяЗаписьПутьКДанным = "";
		Объект.эпУчетнаяЗаписьПроизвольныйКод = "";
		
	Иначе
		
		// Проверим соответствие типов
		Если НЕ ВыбранноеЗначение.ТипМетаданного.СодержитТип(Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты")) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю (НСтр("ru='Выбранный реквизит не является ссылкой на элемент справочника ""Пользователи"".'"));
			Возврат;
		КонецЕсли;
				
		Объект.эпУчетнаяЗапись = ВыбранноеЗначение.Поле + " (Владелец)";
		Объект.эпУчетнаяЗаписьПутьКДанным = ВыбранноеЗначение.ИмяМетаданного;
		Объект.эпУчетнаяЗаписьПроизвольныйКод = "";
		
	КонецЕсли;
	
КонецПроцедуры //эпУчетнаяЗаписьОбработкаВыбора()

&НаКлиенте
Процедура ТемаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидовПравил = Новый СписокЗначений;
	Элементы.Тема.СписокВыбора.Очистить();
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение"));
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ПроизвольныйКод"));
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ПродолжитьВыборРеквизита",Элементы.Тема);
	ПараметрыОбработки.Вставить("ИмяТипа","СтрокаТема");
	ПараметрыОбработки.Вставить("ИмяРеквизита",?(ЗначениеЗаполнено(Объект.ТемаПроизвольныйКод),Объект.ТемаПроизвольныйКод,"Тема"));
	ОписаниеПродолжениеВыбора = Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияВыборРеквизита", ЭтотОбъект, ПараметрыОбработки);
	
	ПоказатьВыборИзСписка(ОписаниеПродолжениеВыбора,СписокВидовПравил, Элемент);
	
КонецПроцедуры 

&НаКлиенте
Процедура ТемаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		СтандартнаяОбработка = Ложь;
		Объект.ТемаПроизвольныйКод = ВыбранноеЗначение;
		Объект.Тема = "<" + НСтр("ru = 'Произвольное выражение'")+ ">";
		
	КонецЕсли;
	
КонецПроцедуры //ТемаОбработкаВыбора()

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидовПравил = Новый СписокЗначений;
	Элементы.Комментарий.СписокВыбора.Очистить();
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение"));
	СписокВидовПравил.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПравил.ПроизвольныйКод"));
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ПродолжитьВыборРеквизита",Элементы.Комментарий);
	ПараметрыОбработки.Вставить("ИмяТипа","СтрокаКомментарий");
	ПараметрыОбработки.Вставить("ИмяРеквизита",?(ЗначениеЗаполнено(Объект.КомментарийПроизвольныйКод),Объект.КомментарийПроизвольныйКод,"Комментарий"));
	ОписаниеПродолжениеВыбора = Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияВыборРеквизита", ЭтотОбъект, ПараметрыОбработки);
	
	ПоказатьВыборИзСписка(ОписаниеПродолжениеВыбора,СписокВидовПравил, Элемент);
	
КонецПроцедуры 

&НаКлиенте
Процедура КомментарийОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		СтандартнаяОбработка = Ложь;
		Объект.КомментарийПроизвольныйКод = ВыбранноеЗначение;
		Объект.Комментарий = "<" + НСтр("ru = 'Произвольное выражение'")+ ">";
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура эпФорматТекстаПриИзменении(Элемент)
	
	Если Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст") И НЕ ПустаяСтрока(ТекстПисьмаФорматированныйДокумент.ПолучитьТекст()) Тогда
		
		ВзаимодействияКлиент.ВопросПриИзмененииФорматаСообщенияНаОбычныйТекст(ЭтотОбъект);
		УстановитьВидимостьПометкаВидовТекста(, ПроизвольныйКод);
		
	ИначеЕсли Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст") Тогда
		
		ТекстПисьма = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
		ТекстПисьмаФорматированныйДокумент.Удалить();
		Элементы.ТекстПисьма.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
		Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаОбычныйТекст;
		
	Иначе
		
		ТекстПисьмаФорматированныйДокумент.Добавить(ТекстПисьма);
		ТекстПисьма = "";
		Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаФорматированныйДокумент;
		УстановитьВидимостьПометкаВидовТекста(Ложь, ПроизвольныйКод);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПисьмаВидПравилаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные; 
	
	Если ТекущиеДанные.ВидПравила=ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение") Тогда
		ТекущиеДанные.АдресЭлектроннойПочты = ""; 
	Иначе
		ТекущиеДанные.АдресЭлектроннойПочты = Неопределено;
	КонецЕсли;
	
	ТекущиеДанные.Представление   = "";
	ТекущиеДанные.ПроизвольныйКод = "";

КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПисьмаАдресЭлектроннойПочтыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
	
	Если ТекущиеДанные.ВидПравила = ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение") Тогда
		
		РедактироватьСписокПолучателей();
		
	ИначеЕсли ТекущиеДанные.ВидПравила = ПредопределенноеЗначение("Перечисление.ВидыПравил.ПроизвольныйКод") Тогда
		
		// Сформируем параметры открытия
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		ПараметрыОткрытия.Вставить("Текст", ?(ЗначениеЗаполнено(ТекущиеДанные.ПроизвольныйКод),ТекущиеДанные.ПроизвольныйКод,"АдресЭлектроннойПочты"));
		
		// Получаем форму, производим ее настройку и открытие
		ОткрытьФорму("Справочник.ДействияНаЗначимыеСобытия.Форма.РедакторВыражений",
			ПараметрыОткрытия,
			Элемент,
			,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
		); 
	КонецЕсли;
		
КонецПроцедуры 

&НаКлиенте
Процедура ПолучателиПисьмаАдресЭлектроннойПочтыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
		ТекущиеДанные.ПроизвольныйКод = ВыбранноеЗначение;
		ТекущиеДанные.АдресЭлектроннойПочты = "<" + НСтр("ru = 'Произвольное выражение'")+ ">";
		ТекущиеДанные.Представление         = "";
	
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура НаименованиеНачалоВыбораНаСервере(ПараметрыДействия=Неопределено)
	
	Справочники.ДействияНаЗначимыеСобытия.СформироватьНаименованиеПоУмолчанию(Объект,ПараметрыДействия);
	
КонецПроцедуры 

&НаКлиенте
Процедура НаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ПустаяСтрока(Объект.Наименование) Тогда
		
		// Формируем описание обработчика перехвата закрытия формы
		ОбработчикВопроса = Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияСформироватьНаименование", ЭтотОбъект);
		
		// Формируем текст вопроса
		ТекстВопроса = НСтр("ru = 'Сформировать новое наименование?'");
		
		// Получаем подтверждение операции от пользователя
		ПоказатьВопрос(ОбработчикВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		// Обработаем событие в контексте сервера
		НаименованиеНачалоВыбораНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры // НаименованиеНачалоВыбора()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ПроизвольнаяТемаПисьмаНаСервере()
	
	ТекстПисьма = Справочники.ДействияНаЗначимыеСобытия.ОчиститьСодержимоеРедактора("ТекстПисьма");
	
КонецПроцедуры 

&НаКлиенте
Процедура ПроизвольнаяТемаПисьма(Команда)
	
	Если НЕ ПроизвольныйКод Тогда
		ТекстПисьма = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
	КонецЕсли;
	ТекстПисьмаФорматированныйДокумент.Удалить();
	Элементы.ТекстПисьма.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
	Если ПустаяСтрока(ТекстПисьма) Тогда
		ПроизвольнаяТемаПисьмаНаСервере();
	КонецЕсли;

	ПроизвольныйКод = Истина;
	Если Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаОбычныйТекст Тогда
		УстановитьВидимостьПометкаВидовТекста(, ПроизвольныйКод); 
	Иначе
		Если Не ТекстПисьмаФорматированныйДокумент.Элементы.Количество() Тогда
			ПроизвольнаяТемаПисьмаНаСервере();
			ТекстПисьмаФорматированныйДокумент.Добавить(ТекстПисьма);
		КонецЕсли;
		
		УстановитьВидимостьПометкаВидовТекста(Ложь, ПроизвольныйКод);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТочнаяТемаПисьма(Команда)
	
	Если Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст") И НЕ ПроизвольныйКод 
		И ТекстПисьмаФорматированныйДокумент.Элементы.Количество() Тогда
		ТекстПисьма = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
		ТекстПисьмаФорматированныйДокумент.Удалить();
	КонецЕсли;
	
	ПроизвольныйКод = Ложь;
	Если Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаОбычныйТекст Тогда
		УстановитьВидимостьПометкаВидовТекста(, ПроизвольныйКод);
	Иначе
		УстановитьВидимостьПометкаВидовТекста(Ложь, ПроизвольныйКод);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВыражений(Команда)
	
	// Сформируем начальный текст 
	Если Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.HTML") Тогда
		Текст = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
	Иначе
		Текст = ТекстПисьма;
	КонецЕсли;
	
	// Сформируем параметры открытия
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("Текст", ?(ЗначениеЗаполнено(Текст),Текст,"ТекстПисьма"));
	
	ОписаниеПродолжениеВыбора = Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияРедакторВыражения", ЭтотОбъект);
	
	// Получаем форму, производим ее настройку и открытие
	ОткрытьФорму("Справочник.ДействияНаЗначимыеСобытия.Форма.РедакторВыражений", ПараметрыОткрытия,,,,,ОписаниеПродолжениеВыбора,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик события возникающего при выполнении оповещения данной формы о прекращении работы подчиненной.
//
// Параметры:
//  РезультатОповещения     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
&НаКлиенте
Процедура ВопросПриИзмененииФорматаПриЗакрытии(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.HTML");
		Возврат;
	КонецЕсли;
		
	ТекстПисьма = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
	Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст");
	ТекстПисьмаФорматированныйДокумент.Удалить();
	Элементы.ТекстПисьма.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
	Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаОбычныйТекст;
	
КонецПроцедуры //ВопросПриИзмененииФорматаПриЗакрытии()

// Обработчик события открывающий форму адресной книги.
//
&НаКлиенте
Процедура РедактироватьСписокПолучателей()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("УчетнаяЗапись", Объект.эпУчетнаяЗапись);
	ПараметрыОткрытия.Вставить("ГруппаПоУмолчанию",НСтр("ru = 'Кому'"));
	
	// Откроем форму для редактирования списка адресатов
	ИмяОбщейФормы = "ОбщаяФорма.АдреснаяКнига";
	
	ОткрытьФорму(ИмяОбщейФормы, ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры //РедактироватьСписокПолучателей()

// Заполнение табличной части получателями электронного письма, подобранными пользователями.
//
&НаКлиенте
Процедура ЗаполнитьПодобранныхПолучателей(Результат)

	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
	ПерваяСтрока = Истина;
	
	Для Каждого Элемент Из Результат Цикл
		
		Если ПерваяСтрока Тогда
			ТекущиеДанные.АдресЭлектроннойПочты = Элемент.Адрес;
			ТекущиеДанные.Представление = Элемент.Представление + " <" +Элемент.Адрес + ">";
			ТекущиеДанные.КодГруппыАдреса = Элемент.Группа;
			ТекущиеДанные.Контакт = Элемент.Контакт;
			ПерваяСтрока = Ложь;
		Иначе
			НоваяСтрока = объект.ПолучателиПисьма.Добавить();
			НоваяСтрока.АдресЭлектроннойПочты = Элемент.Адрес;
			НоваяСтрока.Представление = Элемент.Представление + " <" +Элемент.Адрес + ">";
			НоваяСтрока.Контакт = Элемент.Контакт;
			НоваяСтрока.КодГруппыАдреса = Элемент.Группа;
			НоваяСтрока.ВидПравила = ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры //ЗаполнитьПодобранныхПолучателей()

// Обработчик события возникающего при выполнении оповещения данной формы о прекращении работы подчиненной.
//
// Параметры:
//  РезультатОповещения     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
&НаКлиенте
Процедура ОбработкаРезультатаОповещенияСформироватьНаименование(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если РезультатОповещения = КодВозвратаДиалога.Да Тогда
		
		// Обработаем событие в контексте сервера
		НаименованиеНачалоВыбораНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаРезультатаОповещенияСформироватьНаименование()

// Обработчик события возникающего при выполнении оповещения данной формы о прекращении работы подчиненной.
//
// Параметры:
//  РезультатОповещения     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
&НаКлиенте
Процедура ОбработкаРезультатаОповещенияРедакторВыражения(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	// Проверяем статус закрытия окна параметров
	Если РезультатОповещения=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаВидТекста.Заголовок =  НСтр("ru = 'Произвольный код'");
	Элементы.ТекстПисьмаКонтекстноеМенюПроизвольнаяТемаПисьма.Пометка = Истина;
	Элементы.ТочнаяТемаПисьма.Пометка = Ложь;
	Если Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст") Тогда
		ТекстПисьма = РезультатОповещения;
	Иначе
		ТекстПисьмаФорматированныйДокумент.Удалить();
		ТекстПисьмаФорматированныйДокумент.Добавить(РезультатОповещения);
		ТекстПисьма = "";
	КонецЕсли;
	ПроизвольныйКод = Истина;
	
КонецПроцедуры // ОбработкаРезультатаОповещенияРедакторВыражения()

// Обработчик события возникающего при выполнении оповещения данной формы о прекращении работы подчиненной.
//
// Параметры:
//  РезультатОповещения     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
&НаКлиенте
Процедура ОбработкаРезультатаОповещенияВыборРеквизита(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	// Проверяем статус закрытия окна параметров
	Если РезультатОповещения=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = "ПродолжитьВыборВажности" Тогда
		
		Объект.Важность = РезультатОповещения.Значение;
		Объект.ВажностьПроизвольныйКод = "";
		
	ИначеЕсли ТипЗнч(ДополнительныеПараметры) = Тип("Структура") И ДополнительныеПараметры.Свойство("ПродолжитьВыборРеквизита") Тогда
		
		// Обработаем в зависимости от выбранного варианта
		Если РезультатОповещения.Значение=ПредопределенноеЗначение("Перечисление.ВидыПравил.ТочноеЗначение") Тогда
			
			Если ДополнительныеПараметры.Свойство("ИмяТипа") И ДополнительныеПараметры.ИмяТипа = "ВариантыВажностиВзаимодействия" Тогда
				
				СписокВажностей = Новый СписокЗначений;
				Элементы.Важность.СписокВыбора.Очистить();
				СписокВажностей.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыВажностиВзаимодействия.Низкая"));
				СписокВажностей.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыВажностиВзаимодействия.Обычная"));
				СписокВажностей.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыВажностиВзаимодействия.Высокая"));
				ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияВыборРеквизита", ЭтотОбъект, "ПродолжитьВыборВажности"),СписокВажностей,ДополнительныеПараметры.ПродолжитьВыборРеквизита);
				
			ИначеЕсли ДополнительныеПараметры.Свойство("ИмяТипа") И ДополнительныеПараметры.ИмяТипа = "СтрокаТема" Тогда
				
				Объект.Тема = "";
				Объект.ТемаПроизвольныйКод = "";
				
			ИначеЕсли ДополнительныеПараметры.Свойство("ИмяТипа") И ДополнительныеПараметры.ИмяТипа = "СтрокаКомментарий" Тогда
				
				Объект.Комментарий = "";
				Объект.КомментарийПроизвольныйКод = "";
				
			ИначеЕсли ДополнительныеПараметры.Свойство("ИмяФормы")Тогда
				
				// Сформируем параметры открытия
				ПараметрыОткрытия = Новый Структура;
				ПараметрыОткрытия.Вставить("РежимВыбора",Истина);
				ОткрытьФорму(""+ДополнительныеПараметры.ИмяФормы+".ФормаВыбора",ПараметрыОткрытия,ДополнительныеПараметры.ПродолжитьВыборРеквизита,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				
			КонецЕсли;
		
		ИначеЕсли РезультатОповещения.Значение=ПредопределенноеЗначение("Перечисление.ВидыПравил.РеквизитОбъектаИсточника") Тогда
			
			// Сформируем параметры открытия
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("Источник", Источник);
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			
			// Получаем форму, производим ее настройку и открытие
			ОткрытьФорму("Справочник.ДействияНаЗначимыеСобытия.Форма.ВыборРеквизитаОбъекта", ПараметрыОткрытия,ДополнительныеПараметры.ПродолжитьВыборРеквизита,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		ИначеЕсли РезультатОповещения.Значение=ПредопределенноеЗначение("Перечисление.ВидыПравил.ПроизвольныйКод") Тогда
			
			// Сформируем параметры открытия
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			ПараметрыОткрытия.Вставить("Текст", ДополнительныеПараметры.ИмяРеквизита);
			
			// Получаем форму, производим ее настройку и открытие
			ОткрытьФорму("Справочник.ДействияНаЗначимыеСобытия.Форма.РедакторВыражений", ПараметрыОткрытия, ДополнительныеПараметры.ПродолжитьВыборРеквизита,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаРезультатаОповещенияВыборРеквизита()

Функция УстановитьВидимостьПометкаВидовТекста(ПростойТекст = Истина, ПроизвольныйКод)
	
	ЗаголовокГруппы = ?(ПроизвольныйКод, НСтр("ru = 'Произвольный код'"), НСтр("ru = 'Точный текст'"));
	
	Если ПростойТекст Тогда
		Элементы.ГруппаВидТекста.Заголовок 									= ЗаголовокГруппы;
		Элементы.ТекстПисьмаКонтекстноеМенюПроизвольнаяТемаПисьма.Пометка 	= ПроизвольныйКод;
		Элементы.ТочнаяТемаПисьма.Пометка 									= Не ПроизвольныйКод;
		
		Элементы.РедакторВыражений.Видимость 								= Элементы.ТекстПисьмаКонтекстноеМенюПроизвольнаяТемаПисьма.Пометка;
	Иначе
		Элементы.ГруппаВидТекстаФорматированныйДокумент.Заголовок 									= ЗаголовокГруппы;
		Элементы.ТекстПисьмаКонтекстноеМенюПроизвольнаяТемаПисьмаФорматированныйДокумент.Пометка 	= ПроизвольныйКод;
		Элементы.ТочнаяТемаПисьмаФорматированныйДокумент.Пометка 									= Не ПроизвольныйКод;
		
		Элементы.РедакторВыраженийФорматированныйДокумент.Видимость 								= 
			Элементы.ТекстПисьмаКонтекстноеМенюПроизвольнаяТемаПисьмаФорматированныйДокумент.Пометка;
	КонецЕсли;
	
КонецФункции

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

// Производит настройку параметров отображения элементов управления диалога в зависимости от значений реквизитов
// объекта.
//
&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	// Вызываем общий обработчик действия
	УправлениеДиалогомСправочникаСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	
	Если Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст") Тогда
		Элементы.ТекстПисьма.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
		Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаОбычныйТекст;
	Иначе
		Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаФорматированныйДокумент;
	КонецЕсли;
	
	ПроизвольныйКод = ЗначениеЗаполнено(Объект.эпТекстПисьмаПроизвольныйКод);
	
	Если Объект.эпФорматТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст") Тогда
		УстановитьВидимостьПометкаВидовТекста(, ПроизвольныйКод)
	Иначе
		УстановитьВидимостьПометкаВидовТекста(Ложь, ПроизвольныйКод)
	КонецЕсли;  
	
	Элементы.Наименование.СписокВыбора.Очистить();
	Элементы.Наименование.СписокВыбора.Добавить(
		Справочники.ДействияНаЗначимыеСобытия.ПолучитьНаименованиеПоУмолчанию(Объект)
		, "Автоматическое наименование");
	
КонецПроцедуры // УправлениеДиалогомНаСервере()

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

