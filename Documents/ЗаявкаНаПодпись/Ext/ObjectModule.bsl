#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ФизическоеЛицоПЭП.ПодписаноСогласиеНаРаботуСПЭП Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "ФизическоеЛицоПЭП";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Текст = НСтр("ru = 'Для физического лица не подписано согланияе на работу с ПЭП'");
		Сообщение.Сообщить();
		Отказ = Истина;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Или ПометкаУдаления Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Отказ = Отказ
		Или ОбновитьДанныеДокументаPDF()
		Или ОбновитьСостояниеДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбновитьДанныеДокументаPDF()
	
	ТребуетсяОбновитьДанныеДокументаPDF = Неопределено;
	ДополнительныеСвойства.Свойство("ТребуетсяОбновитьДанныеДокументаPDF", ТребуетсяОбновитьДанныеДокументаPDF);
	
	Если ТребуетсяОбновитьДанныеДокументаPDF Тогда
		
		АдресФайлаВоВременномХранилище = Неопределено;
		ДополнительныеСвойства.Свойство("АдресФайлаВоВременномХранилище", АдресФайлаВоВременномХранилище);
		
		Если ЭтоАдресВременногоХранилища(АдресФайлаВоВременномХранилище) Тогда
			
			ДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайлаВоВременномХранилище);
			
			Если ДанныеФайла <> Неопределено Тогда
				
				Попытка
					
					ЗаявкиНаПодписаниеПЭП.ОбновитьПривязанныйФайл(Ссылка, ДанныеФайла);
					
				Исключение
					
					ЗаписьЖурналаРегистрации(
						Нстр("ru = 'Простая электронная подпись.Обновление PDF файла'"),
						УровеньЖурналаРегистрации.Ошибка,
						,
						ЭтотОбъект,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
					);
					Сообщение = Новый СообщениеПользователю();
					Сообщение.Текст = НСтр("ru = 'Не удалось записать данные файла. Подробнее в журнале регистрации'");
					Сообщение.УстановитьДанные(ЭтотОбъект);
					Сообщение.Сообщить();
					Возврат Истина;
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ОбновитьСостояниеДокумента()
	
	СостояниеДокумента = Неопределено;
	ДополнительныеСвойства.Свойство("СостояниеДокумента", СостояниеДокумента);
	
	Если ЗначениеЗаполнено(СостояниеДокумента) Тогда
		
		Попытка
			
			ЗаявкиНаПодписаниеПЭП.ОбновитьСостояниеДокумента(Ссылка, СостояниеДокумента);
			
		Исключение
			
			ЗаписьЖурналаРегистрации(
				Нстр("ru = 'Простая электронная подпись.Обновление состояния документа'"),
				УровеньЖурналаРегистрации.Ошибка,
				,
				ЭтотОбъект,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			);
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Не удалось обновить состояние документа. Подробнее в журнале регистрации'");
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Возврат Истина;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли
