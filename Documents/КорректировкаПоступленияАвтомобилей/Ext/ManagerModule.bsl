// Модуль менеджера документа "Корректировка поступления автомобилей"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Производит расчет значений итоговых показателей по операции в целом.
//
// Параметры:
//  Объект      - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  Расширенные - Булево               - Признак получения расширенных сведений об итогах операци.
//
// Возвращаемое значение:
//  Структура - значения итоговых показателей по операции в целом:
//    СуммаДокумента	- Число - сумма документа;
//    СуммаНДС			- Число - сумма НДС;
//    СуммаБезНДС		- Число - сумма без НДС.
//
Функция РассчитатьИтогиОперации(Объект, Расширенные = Ложь) Экспорт
	
	// Формируем перечень основной информации об итогах операции
	ИтогиОперации = Новый Структура();
	ИтогиОперации.Вставить("СуммаДокумента", Объект.Автомобили.Итог("СуммаВсего"));
	ИтогиОперации.Вставить("СуммаДокументаДоКорректировки", Объект.Автомобили.Итог("СуммаВсегоПоДокументуПоступления"));
	
	// Производим установку полученных значений в итоговые показатели объекта
	Если НЕ Расширенные Тогда
		Возврат ИтогиОперации;
	КонецЕсли;
	
	// Формируем перечень расширенной информации об итогах операции
	ИтогиОперации.Вставить("СуммаНДС",    Объект.Автомобили.Итог("СуммаНДС"));
	ИтогиОперации.Вставить("СуммаБезНДС", ИтогиОперации.СуммаДокумента - ИтогиОперации.СуммаНДС);
	
	// Возвращаем сведения об итогах операции
	Возврат ИтогиОперации;
	
КонецФункции // РассчитатьИтогиОперации()

// Используеся в механизме взаимодействий. Формирует текст запроса по контактам предмета взаимодействия.
//
// Параметры:
//  Ссылка - Ссылка - Содержит ссылку на передаваемый объект метаданных.
//  ТекстВременнаяТаблица  - Строка    - Строка в которой может находиться часть текста запроса, которая отвечает за
//                                       помещение результата запроса во временную таблицу.
//  Объединить             - Булево    - Признак который указывает на режим формирования запроса. В случае, если данный
//                                       параметр имеет значение Истина, то формируемый в функции запрос являются частью
//                                       другого запроса и должен начинаться с конструкции ОБЪЕДИНИТЬ.
//  ТолькоОсновныеКонтакты - Булево    - Признак который указывает на необходимость выводв только основных контактов.
//
// Возвращаемое значение:
//  Строка - Возращаемая сторка содержащая в себе текст запроса по контактам предмета взаимодействий.
//
Функция ПолучитьТекстЗапросаПоКонтактам(Ссылка, ТекстВременнаяТаблица = "", 
						Объединить = Ложь, ТолькоОсновныеКонтакты = Истина) Экспорт
	
	// Вызываем общий обработчик действия
	Параметры = Новый Структура;
	Параметры.Вставить("ТекстВременнаяТаблица", ТекстВременнаяТаблица);
	Параметры.Вставить("Объединить", Объединить);
	Параметры.Вставить("ТолькоОсновныеКонтакты", ТолькоОсновныеКонтакты);
	
	Возврат УправлениеДиалогомСервер.ПолучитьТекстЗапросаПоКонтактам(Ссылка, Параметры);
	
КонецФункции // ПолучитьТекстЗапросаПоКонтактам()

// Расчет разницы сумм по документу реализации и введенной суммы
//
// Параметры:
//  Объект - ДанныеФормыСтруктура   - Объект, для которого выполняется обработка события.
//  Строка - Строка табличной части - Строка, при изменении поля которой возникло событие.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура РасчетРазницы(Объект, Строка, ПараметрыДействия = Неопределено) Экспорт
	
	Строка.СуммаРазница      = Строка.Сумма      - Строка.СуммаПоДокументуПоступления ;
	Строка.СуммаНДСРазница   = Строка.СуммаНДС   - Строка.СуммаНДСПоДокументуПоступления;
	Строка.СуммаВсегоРазница = Строка.СуммаВсего - Строка.СуммаВсегоПоДокументуПоступления;
	Строка.КоличествоРазница = Строка.Количество - Строка.КоличествоПоДокументуПоступления;
	
КонецПроцедуры // РасчетРазницы()

// Определяет возможен ли ввод корректировки на основании данного документа основания.
//
// Параметры:
//	Основание - документ поступление автомобилей или корректировки поступления автомобилей.
//
Функция КорректировкаНеДоступна(Основание) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИСТИНА КАК ЕстьВозврат
	|ИЗ
	|	Документ.ВозвратПоставщикуАвтомобилей КАК ТаблицаОбъекта
	|ГДЕ
	|	ТаблицаОбъекта.ДокументОснование = &Основание
	|	И ТаблицаОбъекта.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Документ.ВозвратПоставщикуАвтомобилей.Автомобили КАК ТаблицаОбъекта
	|ГДЕ
	|	ТаблицаОбъекта.Партия = &Основание
	|	И ТаблицаОбъекта.Ссылка.Проведен
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Основание", Основание);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции // КорректировкаНеДоступна()

// АПК:299-выкл

// Процедура заполнения реквизита "СуммаДокументаРазница"
//
Процедура ЗаполнитьСуммаДокументаРазница() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КорректировкаПоступленияАвтомобилей.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаПоступленияАвтомобилей КАК КорректировкаПоступленияАвтомобилей
	|ГДЕ
	|	КорректировкаПоступленияАвтомобилей.СуммаДокументаРазница = 0";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		ДокументОбъект.Заблокировать();
		ДокументОбъект.СуммаДокументаРазница = ДокументОбъект.Автомобили.Итог("СуммаВсегоРазница");
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		Попытка
			ДокументОбъект.Записать();
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(
						НСтр("ru = 'Не удалось перезаполнить сумму разницы в %1'"),
						СокрЛП(Строка(ДокументОбъект.Ссылка))
					)
			);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьСуммаДокументаРазница()

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.КорректировкаПоступленияАвтомобилей - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами(Объект) Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = УчетПрослеживаемыхТоваровСервер.ИнициализацияТаблицыОпераций();
	
	ДанныеКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Контрагент, "СтранаРегистрацииЕАЭС");
	
	// Для контрагентов из ЕАЭС другая форма отчетности
	Если ДанныеКонтрагента.СтранаРегистрацииЕАЭС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ОсвобождентОтНДС = ПолучитьПараметрыПлательщикаНДС(Объект.Организация, Неопределено, Объект.Дата).ОсвобожденОтНДС;
		
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Объект.Автомобили.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	Если Объект.ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейИсправлениеВПервичныхДокументах Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.УПД) КАК ВидДокумента,
		|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПоступлениеТоваров) КАК КодОперации,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
		|	СУММА(КорректировкаПоступленияАвтомобилейАвтомобили.Количество) КАК КоличествоПрослеживаемости,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.ГТД КАК РНПТ,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсего - КорректировкаПоступленияАвтомобилейАвтомобили.СуммаНДС) КАК СуммаБезНДС
		|ИЗ
		|	Документ.КорректировкаПоступленияАвтомобилей.Автомобили КАК КорректировкаПоступленияАвтомобилейАвтомобили
		|ГДЕ
		|	КорректировкаПоступленияАвтомобилейАвтомобили.Ссылка = &Ссылка
		|	И КорректировкаПоступленияАвтомобилейАвтомобили.ГТД.РНПТ
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаПоступленияАвтомобилейАвтомобили.Автомобиль,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.ГТД,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.СтавкаНДС";
		Документ = Объект.Сделка;
		Если ЗначениеЗаполнено(Объект.Сделка) Тогда
			ДанныеПоступления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.Сделка, "ВхДокНомер,ВхДокДата,Дата,Номер,Контрагент");
			ПериодОтчета = НачалоКвартала(ДанныеПоступления.Дата);
			НомерДокумента = ДанныеПоступления.ВхДокНомер;
			ДатаДокумента = ДанныеПоступления.ВхДокДата;
			КонтрагентОперации = ДанныеПоступления.Контрагент;
		Иначе
			ПериодОтчета = НачалоКвартала(Объект.Дата);
			КонтрагентОперации = Объект.Контрагент;
		КонецЕсли;
		
	Иначе
		ТекстЗапроса = "ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.УКД) КАК ВидДокумента,
		|	ВЫБОР
		|		КОГДА КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоРазница > 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочннаяСчетФактураУвеличение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочннаяСчетФактураУменьшение)
		|	КОНЕЦ КАК КодОперации,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
		|	СУММА(КорректировкаПоступленияАвтомобилейАвтомобили.КоличествоРазница * ВЫБОР
		|			КОГДА КорректировкаПоступленияАвтомобилейАвтомобили.КоличествоРазница < 0
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК КоличествоПрослеживаемости,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.ГТД КАК РНПТ,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоРазница * ВЫБОР
		|			КОГДА КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоРазница < 0
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ - КорректировкаПоступленияАвтомобилейАвтомобили.СуммаНДСРазница * ВЫБОР
		|			КОГДА КорректировкаПоступленияАвтомобилейАвтомобили.СуммаНДСРазница < 0
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК СуммаБезНДС
		|ИЗ
		|	Документ.КорректировкаПоступленияАвтомобилей.Автомобили КАК КорректировкаПоступленияАвтомобилейАвтомобили
		|ГДЕ
		|	КорректировкаПоступленияАвтомобилейАвтомобили.Ссылка = &Ссылка
		|	И КорректировкаПоступленияАвтомобилейАвтомобили.ГТД.РНПТ
		|	И КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоРазница <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоРазница > 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочннаяСчетФактураУвеличение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочннаяСчетФактураУменьшение)
		|	КОНЕЦ,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.Автомобиль,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.ГТД,
		|	КорректировкаПоступленияАвтомобилейАвтомобили.СтавкаНДС";
		Документ = Объект.Ссылка;
		ПериодОтчета = НачалоКвартала(Объект.Дата);
		НомерДокумента = Объект.ВхДокНомер;
		ДатаДокумента = Объект.ВхДокДата;
		КонтрагентОперации = Объект.Контрагент;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	// Проверим, что в документе есть РНПТ
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаРНПТ = Запрос.Выполнить().Выгрузить();
	
	// Зафиксируем данные для заполнения
	Организация = Объект.Организация;
	
	// Для пересчета валюты в рубли
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРегл, Объект.Дата);
	КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаДокумента = Объект.ВалютаДокумента;
	КурсДокумента = Объект.КурсДокумента;
	ВалютаНеРегл = (ВалютаДокумента <> ВалютаРегл);
	
	// Сформируем таблицу
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.Документ = Документ;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = ДатаДокумента;
		НоваяСтрока.Контрагент = КонтрагентОперации;
		
		Если ВалютаНеРегл Тогда
			НоваяСтрока.СуммаБезНДС = Окр(
				РаботаСКурсамиВалютПлатформа.ПересчетПоВалюте(НоваяСтрока.СуммаБезНДС, ВалютаДокумента,
					КурсДокумента, ВалютаРегл, КурсРегл), 2);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Область ПараметрыОбработкиРеквизитовОбъекта

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Массив из Строка- Возвращаемый массив содержит строковые идентификаторы реквизитов.
//
Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Массив();
	ОбязательныеРеквизиты.Добавить("Организация");
	ОбязательныеРеквизиты.Добавить("ПодразделениеКомпании");
	ОбязательныеРеквизиты.Добавить("Автор");
	ОбязательныеРеквизиты.Добавить("ДокументОснование");
	ОбязательныеРеквизиты.Добавить("ТипЦен");
	ОбязательныеРеквизиты.Добавить("ХозОперация");
	ОбязательныеРеквизиты.Добавить("ВалютаДокумента");
	ОбязательныеРеквизиты.Добавить("КурсДокумента");
	ОбязательныеРеквизиты.Добавить("Контрагент");
	ОбязательныеРеквизиты.Добавить("ДоговорВзаиморасчетов");
	ОбязательныеРеквизиты.Добавить("СкладКомпании");
	
	СпособУчетаВзаиморасчетовПоРасчетнымДокументам =
		РасчетыСКонтрагентамиСервер.ИспользуютсяВзаиморасчетыПоРасчетнымДокументам(Объект.ДоговорВзаиморасчетов);
	Если Не СпособУчетаВзаиморасчетовПоРасчетнымДокументам Тогда
		ОбязательныеРеквизиты.Добавить("СпособЗачетаАвансов");
	КонецЕсли;
	
	Если НЕ ПраваИНастройкиПользователя.Значение("ПроведениеНезаполненныхДокументов", Объект) Тогда
		ОбязательныеРеквизиты.Добавить("Автомобили");
	КонецЕсли;

	ОбязательныеРеквизиты.Добавить("Автомобили.Автомобиль");
	
	// Возвращаем сформированные параметры проверки
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности и уникальности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Структура - Возвращаемая структура содержит строковые идентификаторы реквизитов и вариант проверки его заполнения
//    (1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный). Для описания проверки табличных частей используется
//    вложенная структура.
//
Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	// Структура уникальных реквизитов табличных частей
	УникальныеРеквизиты = Новый Структура();
	
	// Уникальные поля таблицы товаров
	УникальныеАвтомобили = Новый Массив();
	УникальныеАвтомобили.Добавить("Автомобиль");
	УникальныеАвтомобили.Добавить("ГТД");
	УникальныеРеквизиты.Вставить("Автомобили", УникальныеАвтомобили);
	
	// Возвращаем сформированные параметры проверки
	Возврат УникальныеРеквизиты;
	
КонецФункции // ПолучитьУникальныеРеквизиты()

// Производит формирование перечня реквизитов для проверки соответствия Организации и Подразделению.
//
// Параметры:
//  Объект                  - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//  КонтрольПоПодразделению - Булево               - Признак необходимости выполнить контроль по подразделению.
//
// Возвращаемое значение:
//  Структура - Содержит перечень проверяемых реквизитов.
//
Функция ПолучитьРеквизитыКонтроляПоОрганизации(Объект, КонтрольПоПодразделению) Экспорт
	
	// Структура параметров проверки реквизитов объекта
	КонтролируемыеРеквизиты = Новый Структура();
	КонтролируемыеРеквизиты.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
	
	// Возвращаем сформированные параметры проверки
	Возврат КонтролируемыеРеквизиты;
	
КонецФункции // ПолучитьРеквизитыКонтроляПоОрганизации()

// АПК:299-вкл

#КонецОбласти

#Область ОбработчикиИзмененияДанныхОбъекта

// Возвращает предопределенную структуру действий
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
// Возвращаемое значение:
//  Структура - Содержит перечень параметров действия.
//
Функция ПолучитьПараметрыДействия(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Инициализируем параметры обработки действия с объектом
	Если ПараметрыДействия = Неопределено Тогда
		ПараметрыДействия = Новый Структура;
	КонецЕсли;
	
	// Производим добавление параметров расширяемых контекст обработки событий объекта
	ПараметрыДействия.Вставить("ОбъектЗаполнен", Объект.Автомобили.Количество() > 0);
	
	// Возвращаем сформированную структуру параметров
	Возврат ПараметрыДействия;
	
КонецФункции // ПолучитьПараметрыДействия()

// Обработчик события пересчета зависимых показателей объекта при изменении значений ведущих реквизитов.
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ОбработкаПересчетаПоказателейОбъекта(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	ОбработкаТабличнойЧастиАвтомобили.ПерезаполнитьЦеныТабличныхЧастей(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик действия
	ОбработкаРеквизитовДокументаСервер.ОбработкаПересчетаПоказателейОбъекта(Объект, ПараметрыДействия);
	
КонецПроцедуры // ОбработкаПересчетаПоказателейОбъекта()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события возникающего при изменении данных реквизита "Дата".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ДатаПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);

	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ДатаПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры

// Обработчик события возникающего при изменении данных реквизита "Документ основание".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ДокументОснованиеПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	Если Объект.ДокументОснование <> Неопределено И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Если Объект.ДокументОснование = Объект.Ссылка Тогда
			Объект.ДокументОснование = Неопределено;
			ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Ввод корректировки поступления автомобилей на основании самой себя запрещен.'")
			);
			Возврат;
		КонецЕсли;

		Если КорректировкаНеДоступна(Объект.ДокументОснование) Тогда
			Объект.ДокументОснование = Неопределено;
			ТекстСообщения = СтрШаблон(НСтр("ru = 'На основании %1 введен возврат автомобилей поставщику. Ввод корректировки поступления автомобилей невозможен.'"), Объект.ДокументОснование);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "ДокументОснование");
			ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
			Возврат;
		КонецЕсли;
		
		Если НЕ Объект.ДокументОснование.Проведен Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Ввод корректировки поступления автомобилей возможен только на основании проведенного документа.'")
			);
			Объект.ДокументОснование=Неопределено;
			ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
			Возврат;
		КонецЕсли;

		ДокументОснованиеХозОперация = Объект.ДокументОснование.ХозОперация;
		Если ДокументОснованиеХозОперация = Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия Тогда
			Объект.ДокументОснование = Неопределено;
			ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
			ТекстСообщения = НСтр("ru = 'Ввод корректировки поступления автомобилей не возможен на основании поступления автомобилей комиссия.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "ДокументОснование");
			Возврат;
		КонецЕсли;
		Если ДокументОснованиеХозОперация = Справочники.ХозОперации.ПоступлениеАвтомобилейОтветственноеХранение Тогда
			Объект.ДокументОснование = Неопределено;
			ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
			ТекстСообщения = НСтр("ru = 'Ввод корректировки поступления автомобилей не возможен на основании поступления автомобилей на ответственное хранение.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "ДокументОснование");
			Возврат;
		КонецЕсли;
		Если ДокументОснованиеХозОперация = Справочники.ХозОперации.ПереходВСобственностьАвтомобилей Тогда
			Объект.ДокументОснование = Неопределено;
			ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
			ТекстСообщения = НСтр("ru = 'Ввод корректировки поступления автомобилей не возможен на основании перехода автомобилей в собственность.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "ДокументОснование");
			Возврат;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КорректировкаПоступленияАвтомобилей.Ссылка
		|ИЗ
		|	Документ.КорректировкаПоступленияАвтомобилей КАК КорректировкаПоступленияАвтомобилей
		|ГДЕ
		|	КорректировкаПоступленияАвтомобилей.ДокументОснование = &ДокументОснование
		|	И КорректировкаПоступленияАвтомобилей.Ссылка <> &Ссылка";
		Запрос.УстановитьПараметр("ДокументОснование", Объект.ДокументОснование);
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		РезЗапроса = Запрос.Выполнить();
		Если НЕ РезЗапроса.Пустой() Тогда
			Выборка = РезЗапроса.Выбрать();
			Выборка.Следующий();
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'На основании документа %1 уже введен %2'"),
					Объект.ДокументОснование,
					Выборка.Ссылка
				)
			);
			Объект.ДокументОснование = Неопределено;
			ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры

// Обработчик события возникающего при изменении данных реквизита "Подразделение компании".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ПодразделениеКомпанииПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ПодразделениеКомпанииПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // ПодразделениеКомпанииПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Организация".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ОрганизацияПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ОрганизацияПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // ОрганизацияПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Автор".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура АвторПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.АвторПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // АвторПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Проект".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ПроектПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ПроектПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // ПроектПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Хоз. операция".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ХозОперацияПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ХозОперацияПриИзменении(Объект, ПараметрыДействия);
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.КорректировкаПоступленияАвтомобилейИсправлениеВПервичныхДокументах") Тогда
		Если ЗначениеЗаполнено(Объект.ДокументОснование)
			И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
			
			Объект.НомерИсправления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "НомерИсправления") + 1;
		Иначе
			Объект.НомерИсправления = 1;
		КонецЕсли;
	Иначе
		Объект.НомерИсправления = 0;
	КонецЕсли;
	
КонецПроцедуры // ХозОперацияПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Тип цен".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ТипЦенПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ТипЦенПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // ТипЦенПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Валюта".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ВалютаДокументаПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ВалютаДокументаПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // ВалютаДокументаПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Курс документа".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура КурсДокументаПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.КурсДокументаПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // КурсДокументаПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Склад компании".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура СкладКомпанииПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.СкладКомпанииПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // СкладКомпанииПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Контрагент".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура КонтрагентПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.КонтрагентПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // КонтрагентПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Договор взаиморасчетов".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура ДоговорВзаиморасчетовПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.ДоговорВзаиморасчетовПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // ДоговорВзаиморасчетовПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Курс валюты взаиморасчетов".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура КурсВалютыВзаиморасчетовПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// Вызываем общий обработчик события
	ОбработкаРеквизитовДокументаСервер.КурсВалютыВзаиморасчетовПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // КурсВалютыВзаиморасчетовПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Курс валюты взаиморасчетов".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура СуммаДокументаПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
КонецПроцедуры // КурсВалютыВзаиморасчетовПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Карточка".
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура - Объект, для которого выполняется обработка события.
//  ПараметрыДействия - Структура            - Набор параметров, использующихся при выполнения операции.
//
Процедура КарточкаПриИзменении(Объект, ПараметрыДействия = Неопределено) Экспорт
	
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	ОбработкаРеквизитовДокументаСервер.КарточкаПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры // КарточкаПриИзменении()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАвтомобили

// Обработчик события возникающего при изменении данных реквизита "Автомобиль"
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура   - Объект, для которого выполняется обработка события.
//  Строка            - Строка табличной части - Строка, при изменении поля которой возникло событие.
//  ПараметрыДействия - Структура              - Набор параметров, использующихся при выполнения операции.
//
Процедура АвтомобилиАвтомобильПриИзменении(Объект, Строка, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	// подставим VIN
	Строка.VIN = Строка.Автомобиль.VIN;
	Если НЕ ЗначениеЗаполнено(Строка.СтавкаНДС) Тогда
		Строка.СтавкаНДС = ПолучитьПараметрыПлательщикаНДС(Объект.Организация, Объект.ПодразделениеКомпании, Объект.Дата).ОсновнаяСтавка;
	КонецЕсли;
	
	РасчетРазницы(Объект, Строка, ПараметрыДействия);
	
КонецПроцедуры

// Обработчик события возникающего при изменении данных реквизита "Количество"
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура   - Объект, для которого выполняется обработка события.
//  Строка            - Строка табличной части - Строка, при изменении поля которой возникло событие.
//  ПараметрыДействия - Структура              - Набор параметров, использующихся при выполнения операции.
//
Процедура АвтомобилиКоличествоПриИзменении(Объект, Строка, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	АвтомобилиСуммаПриИзменении(Объект, Строка, ПараметрыДействия);
	
КонецПроцедуры // АвтомобилиКоличествоПриИзменении()

// Обработчик события возникающего при изменении данных реквизита "Цена"
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура   - Объект, для которого выполняется обработка события.
//  Строка            - Строка табличной части - Строка, при изменении поля которой возникло событие.
//  ПараметрыДействия - Структура              - Набор параметров, использующихся при выполнения операции.
//
Процедура АвтомобилиЦенаПриИзменении(Объект, Строка, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	Строка.Сумма = Строка.Цена;
	АвтомобилиСуммаПриИзменении(Объект, Строка, ПараметрыДействия);
	
КонецПроцедуры

// Обработчик события возникающего при изменении данных реквизита "Сумма"
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура   - Объект, для которого выполняется обработка события.
//  Строка            - Строка табличной части - Строка, при изменении поля которой возникло событие.
//  ПараметрыДействия - Структура              - Набор параметров, использующихся при выполнения операции.
//
Процедура АвтомобилиСуммаПриИзменении(Объект, Строка, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	Если Строка.Количество = 0 Тогда
		Строка.Сумма = 0;
	КонецЕсли;
	
	Если Строка.Сумма = 0 И Строка.Количество = 1 Тогда
		
		Строка.Сумма = Строка.Цена;
		
	КонецЕсли;
	
	// расчитаем сумму всего 
	ЦенаВключаетНДС = ?(ЗначениеЗаполнено(Объект.ТипЦен), Объект.ТипЦен.ЦенаВключаетНДС, Истина);
	Если ЦенаВключаетНДС Тогда
		Строка.СуммаВсего = Строка.Сумма;
		Строка.СуммаНДС   = Строка.СуммаВсего - (100 * Строка.СуммаВсего) / (100 + Строка.СтавкаНДС.Ставка);
	Иначе
		Строка.СуммаНДС   = (Строка.Сумма * Строка.СтавкаНДС.Ставка) / 100;
		Строка.СуммаВсего = Строка.Сумма + Строка.СуммаНДС;
	КонецЕсли;
	
	РасчетРазницы(Объект, Строка, ПараметрыДействия);
	
КонецПроцедуры

// Обработчик события возникающего при изменении данных реквизита "Сумма всего"
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура   - Объект, для которого выполняется обработка события.
//  Строка            - Строка табличной части - Строка, при изменении поля которой возникло событие.
//  ПараметрыДействия - Структура              - Набор параметров, использующихся при выполнения операции.
//
Процедура АвтомобилиСуммаВсегоПриИзменении(Объект, Строка, ПараметрыДействия = Неопределено) Экспорт
	
	Если Строка.Количество = 0 Тогда
		Строка.СуммаВсего = 0;
	КонецЕсли;
	                               
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	ЦенаВключаетНДС = ?(ЗначениеЗаполнено(Объект.ТипЦен), Объект.ТипЦен.ЦенаВключаетНДС, Истина); 
	Если ЦенаВключаетНДС Тогда
		// Цена уже содержит НДС
		Строка.Сумма    = Строка.СуммаВсего;
		Строка.СуммаНДС = Строка.СуммаВсего - (100 * Строка.СуммаВсего) / (100 + Строка.СтавкаНДС.Ставка);
	Иначе
		// НДС в цену не включен
		Строка.Сумма    = (100 * Строка.СуммаВсего)/(100 + Строка.СтавкаНДС.Ставка);
		Строка.СуммаНДС = Строка.СуммаВсего - Строка.Сумма;
	КонецЕсли; 
	
	Строка.Цена = Строка.Сумма;
	
	РасчетРазницы(Объект, Строка, ПараметрыДействия);
	
КонецПроцедуры

// Обработчик события возникающего при изменении данных реквизита "Ставка НДС"
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура   - Объект, для которого выполняется обработка события.
//  Строка            - Строка табличной части - Строка, при изменении поля которой возникло событие.
//  ПараметрыДействия - Структура              - Набор параметров, использующихся при выполнения операции.
//
Процедура АвтомобилиСтавкаНДСПриИзменении(Объект, Строка, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	АвтомобилиСуммаПриИзменении(Объект, Строка, ПараметрыДействия);
	
КонецПроцедуры

// Обработчик события возникающего при изменении данных реквизита "Сумма НДС"
//
// Параметры:
//  Объект            - ДанныеФормыСтруктура   - Объект, для которого выполняется обработка события.
//  Строка            - Строка табличной части - Строка, при изменении поля которой возникло событие.
//  ПараметрыДействия - Структура              - Набор параметров, использующихся при выполнения операции.
//
Процедура АвтомобилиСуммаНДСПриИзменении(Объект, Строка, ПараметрыДействия = Неопределено) Экспорт
	
	// Устанавливаем параметры выполнения операции
	ПараметрыДействия = ПолучитьПараметрыДействия(Объект, ПараметрыДействия);
	
	РасчетРазницы(Объект, Строка, ПараметрыДействия);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Переопределяет настройки печати для объекта.
//
// Параметры:
//  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
//
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
	
	Настройки.ПриДобавленииКомандПечати = Истина;
	
КонецПроцедуры

// Процедура используется для указания доступного данному виду объектов перечня вариантов печати.
//
// Параметры:
//  КомандыПечати - ТаблицаЗначений - Каждая строка таблицы соответствует одному из вариантов печати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	СсылкаНаДокумент = ПолучитьСсылку();
	
	УправлениеПечатьюПлатформа.ДобавитьКоманду(КомандыПечати,
		"Документ.КорректировкаПоступленияАвтомобилей",
		"ПоступлениеАвтомобилей",
		НСтр("ru = 'Корректировка поступления автомобилей'"),
		СсылкаНаДокумент);
	УправлениеПечатьюПлатформа.ДобавитьКоманду(КомандыПечати,
		"Обработка.ПечатьУКД",
		"ПФ_MXL_УКД",
		НСтр("ru = 'Универсальный корректировочный документ'"),
		СсылкаНаДокумент);
	
КонецПроцедуры // ДобавитьКомандыПечати()

// Обработчик печати документа
//
// Параметры:
//  МассивОбъектов         - Массив           - массив объектов для формирования печатных форм
//  ПараметрыПечати        - Структура        - дополнительные параметры печати
//  КоллекцияПечатныхФорм  - ТаблицаЗначений - таблица с идентификаторами печатных форм и макетами печати
//  ОбъектыПечати          - Структура        - список областей печатной формы в разрезе документов
//  ПараметрыВывода        - Структура        - дополнительтые параметры вывода формы.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПоступлениеАвтомобилей") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
															"ПоступлениеАвтомобилей",
															"Корректировка поступления автомобилей",
															ПечатьКорректировкаПоступленияАвтомобилей(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры // Печать()

// Формирует печатную форму "ПоступлениеАвтомобилей"
// 
// Параметры:
//  МассивОбъектов - Массив - Массив документов для печати.
//  ОбъектыПечати    - Структура        - список областей печатной формы в разрезе документов
//
// Возвращаемое значение:
//	ТабличныйДокумент   - возвращает сформированный табличный документ.
//
Функция ПечатьКорректировкаПоступленияАвтомобилей(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	Для Каждого Документ Из МассивОбъектов Цикл
		Если НЕ ПервыйДокумент Тогда // новый документ должен быть на отдельной странице
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы(
			"Документ.КорректировкаПоступленияАвтомобилей.ПФ_MXL_ПоступлениеАвтомобилей");
		
		ОбластьШапкаТаблицы    = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьЗаголовок       = Макет.ПолучитьОбласть("Заголовок");
		ОбластьСтрока          = Макет.ПолучитьОбласть("Строка");
		ОбластьИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		ОбластьПодвал          = Макет.ПолучитьОбласть("Подвал");
		
		// заголовок
		ОбластьЗаголовок.Параметры.Заполнить(Документ);
		ТекстЗаголовка = УправлениеПечатьюПлатформа.ПолучитьПредставлениеДокумента(Документ);
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		
		ДополнительныеПараметры = УправлениеПечатьюПлатформа.ПолучитьПараметрыВызоваПредставленияСправочника();
		ДополнительныеПараметры.НаДату = Документ.Дата;
		ОбластьЗаголовок.Параметры.ПредставлениеПоставщика = УправлениеПечатьюПлатформа.ПолучитьПредставлениеСправочника(
			Документ.Контрагент, , ДополнительныеПараметры);
		
		ДополнительныеПараметры = УправлениеПечатьюПлатформа.ПолучитьПараметрыВызоваПредставленияСправочника();
		ДополнительныеПараметры.Вставить("ПодразделениеКомпании", Документ.ПодразделениеКомпании);
		ДополнительныеПараметры.ИспользоватьКИПодразделения = Истина;
		ДополнительныеПараметры.ИспользоватьКПППодразделения = Истина;
		ДополнительныеПараметры.НаДату = Документ.Дата;
		ОбластьЗаголовок.Параметры.ПредставлениеПокупателя = УправлениеПечатьюПлатформа.ПолучитьПредставлениеСправочника(
			Документ.Организация, , ДополнительныеПараметры);
		
		ОбластьЗаголовок.Параметры.ПредставлениеСклада =
			УправлениеПечатьюПлатформа.ПолучитьПредставлениеСправочника(Документ.СкладКомпании);
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		// заполняем заголовок колонки НДС по типу цен
		Параметры = Новый Структура("НДС");
		Параметры.НДС = "НДС";
		Если Документ.ТипЦен.ЦенаВключаетНДС
				И НЕ ПолучитьПараметрыПлательщикаНДС(Документ.Организация, Документ.ПодразделениеКомпании, Документ.Дата).ОсвобожденОтНДС  Тогда // Если НДС включен
			Параметры.НДС = НСтр("ru = 'в т.ч. НДС'");
		КонецЕсли;
		ОбластьШапкаТаблицы.Параметры.Заполнить(Параметры);
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		// готовим шапку
		НомерСтраницы = 2; 
		НомерСтраницыПредыдущий = 2;
		СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего", Документ.ВалютаДокумента, 0, 0);
		ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаТаблицы.Параметры.НомерСтраницы  = "Страница: " + НомерСтраницы;
		
		// 
		ВыборкаТабличнойЧасти = Документ.Автомобили;
		Для Каждого СтрокаТЧ Из ВыборкаТабличнойЧасти Цикл
			
			ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры,
				УправлениеПечатьюПлатформа.ЗаполнитьПредставлениеДанныхСтрокиДляАвтомобилей(СтрокаТЧ, Документ));
			
			// доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если Документ.Автомобили.Индекс(СтрокаТЧ) = Документ.Автомобили.Количество() - 1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
			КонецЕсли;
			
			НомерСтраницы = УправлениеПечатьюПлатформа.ВывестиГоризонтальнуюОбласть(
				ТабличныйДокумент,
				ОбластьСтрока,
				ОбластьШапкаТаблицы,
				ОбластьИтогоПоСтранице,
				НомерСтраницы,
				СтруктураИтоговПоСтранице,
				Документ,
				мсвДопОбластиПодвала);
			
			// инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПредыдущий Тогда
				СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего", Документ.ВалютаДокумента, 0, 0);
				НомерСтраницыПредыдущий   = НомерСтраницы;
				ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
			КонецЕсли;
			
			// добавляем итоги
			УправлениеПечатьюПлатформа.ДобавитьИтогиПоСтранице(СтрокаТЧ, СтруктураИтоговПоСтранице);
		КонецЦикла;
		
		// выводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			УправлениеПечатьюПлатформа.ВывестиИтогиПоСтранице(
				ТабличныйДокумент, ОбластьИтогоПоСтранице, СтруктураИтоговПоСтранице, Документ);
		КонецЕсли;
		
		// заполнение подвала и итогов
		ФорматВыводаКоличества = УправлениеПечатьюПлатформа.ПолучитьФорматВыводаКоличества(Документ);
		ФорматВыводаСуммы      = УправлениеПечатьюПлатформа.ПолучитьФорматВыводаСуммы(Документ);
		
		ОбластьПодвал.Параметры.ВалютаДокумента = Документ.ВалютаДокумента;
		СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего, ФорматВыводаСуммы);
		НДСВсего   = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
		ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего, ФорматВыводаСуммы);
		ОбластьПодвал.Параметры.СуммаПрописью = НСтр("ru = 'Всего наименований'") + Символы.НПП
			+ ВыборкаТабличнойЧасти.Количество() + Символы.НПП + НСтр("ru = 'на сумму'") + Символы.НПП
			+ РаботаСКурсамиВалют.СформироватьСуммуПрописью(СуммаВсего, Документ.ВалютаДокумента);
		
		ОбластьПодвал.Параметры.Заполнить(
			УправлениеПечатьюПлатформа.ДанныеОтветственногоЛица(Документ, "ОтпустилКонтрагент", "Отпустил"));
		ОбластьПодвал.Параметры.Заполнить(УправлениеПечатьюПлатформа.ДанныеОтветственногоЛица(Документ, "Получил"));
		
		УправлениеПечатьюПлатформа.ВывестиГоризонтальнуюОбласть(
			ТабличныйДокумент,
			ОбластьПодвал,
			, ,
			НомерСтраницы,
			,
			Документ);
		
		// отметим конец области документа
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Документ);
		
	КонецЦикла;
	
	// Добавим установку параметров печати
	ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаПоступленияАвтомобилей_КорректировкаПоступленияАвтомобилей";
	УправлениеПечатьюПлатформа.УстановитьСтандартныеПараметрыПечати(ИмяПараметровПечати, ТабличныйДокумент);
	ТабличныйДокумент.ИмяПараметровПечати = ИмяПараметровПечати;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьКорректировкаПоступления()

// Функция получения данных для УКД
//
// Параметры:
//  Объект - ДокументСсылка.КорректировкаПоступления - Объект, для которого выполняется обработка события.
//
// Возвращаемое значение:
//  Структура - структура с данными документа.
//
Функция ПолучитьДанныеДляПечатиУКД(Объект) Экспорт
	
	ДокументФактура = Документы.СчетФактураПолученный.НайтиПоРеквизиту("ДокументОснование", Объект.Ссылка);
	ДокументОбъект = ДокументФактура;
	ДанныеОбъекта = Новый Структура();
	
	// Сформируем статус документа
	Если ДокументФактура <> Документы.СчетФактураПолученный.ПустаяСсылка() И НЕ ДокументФактура.ПометкаУдаления Тогда
		Статус = 1;
		ДокументОбъект = ДокументФактура;
	Иначе
		Статус = 2;
		ДокументОбъект = Объект;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Цена КАК Цена,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Сумма КАК Сумма,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СуммаРазница КАК СуммаРазница,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СтавкаНДС КАК СтавкаНДС,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СуммаНДС КАК СуммаНДС,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СуммаНДСРазница КАК СуммаНДСРазница,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсего КАК СуммаВсего,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоРазница КАК СуммаВсегоРазница,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Количество КАК Количество,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.КоличествоРазница КАК КоличествоРазница,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СуммаНДСПоДокументуПоступления КАК СуммаНДСПоДокументуПоступления,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоПоДокументуПоступления КАК СуммаВсегоПоДокументуПоступления,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.ГТД КАК ГТДПослеИзменения,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Количество - КорректировкаПоступленияАвтомобилейАвтомобили.КоличествоРазница КАК КоличествоДо,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Количество КАК КоличествоПосле,
	|	0 КАК НомерСтроки,
	|	0 КАК ИсходныйНомерСтроки,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.СтавкаНДСПоДокументуПоступления КАК СтавкаНДСДоИзменения,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.ГТДПоДокументуПоступления КАК ГТД
	|ИЗ
	|	Документ.КорректировкаПоступленияАвтомобилей.Автомобили КАК КорректировкаПоступленияАвтомобилейАвтомобили
	|ГДЕ
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Ссылка = &Ссылка
	|	И (КорректировкаПоступленияАвтомобилейАвтомобили.Сумма <> КорректировкаПоступленияАвтомобилейАвтомобили.СуммаПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияАвтомобилейАвтомобили.СтавкаНДС <> КорректировкаПоступленияАвтомобилейАвтомобили.СтавкаНДСПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияАвтомобилейАвтомобили.СуммаНДС <> КорректировкаПоступленияАвтомобилейАвтомобили.СуммаНДСПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсего <> КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияАвтомобилейАвтомобили.ГТД <> КорректировкаПоступленияАвтомобилейАвтомобили.ГТДПоДокументуПоступления)";
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	// данные документа
	ДанныеОбъекта.Вставить("Дата"					, ДокументОбъект.Дата);
	ДанныеОбъекта.Вставить("Номер"					, УправлениеПечатьюПлатформа.ПолучитьНомерДляПечати(ДокументОбъект));
	ДанныеОбъекта.Вставить("ХозОперация"			, ДокументОбъект.ХозОперация);
	ДанныеОбъекта.Вставить("ДокументОснование"		, ДокументОбъект.ДокументОснование);
	ДанныеОбъекта.Вставить("ВалютаДокумента"		, Объект.ВалютаДокумента);
	ДанныеОбъекта.Вставить("КурсДокумента"			, Объект.КурсДокумента);
	ДанныеОбъекта.Вставить("ТипЦен"					, Объект.ТипЦен);
	ДанныеОбъекта.Вставить("Поставщик"				, ДокументОбъект.Контрагент);
	ДанныеОбъекта.Вставить("ПодразделениеКомпании"	, ДокументОбъект.ПодразделениеКомпании);
	ДанныеОбъекта.Вставить("Покупатель"				, ДокументОбъект.Организация);
	ДанныеОбъекта.Вставить("Организация"			, Объект.Организация);
	ДанныеОбъекта.Вставить("ДоговорВзаиморасчетов",
		?(ЗначениеЗаполнено(ДокументОбъект.ДоговорВзаиморасчетов), Объект.ДоговорВзаиморасчетов, "--"));
	ДанныеОбъекта.Вставить("Товары"									, ЗаполнитьНомераСтрокВТаблице(Объект.Ссылка, ТаблицаТоваров));
	ДанныеОбъекта.Вставить("Статус"									, Статус);
	ДанныеОбъекта.Вставить("Ссылка"									, ДокументОбъект.Ссылка);
	ДанныеОбъекта.Вставить("ИдентификаторГосударственногоКонтракта"	, Неопределено);
	
	Если Статус = 1 Тогда
		ДанныеОбъекта.Вставить("НомерИсходногоДокумента",
			?(ЗначениеЗаполнено(ДокументОбъект.НомерИсходногоДокумента), ДокументОбъект.НомерИсходногоДокумента, "--"));
		ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",
			?(ЗначениеЗаполнено(ДокументОбъект.ДатаИсходногоДокумента), ДокументОбъект.ДатаИсходногоДокумента, "--"));
		ДанныеОбъекта.Вставить("ПлатежноРасчетныеДокументы" , Неопределено);
	КонецЕсли;
	
	// свойства
	ИскомыеСвойства = Новый Массив();
	ИскомыеСвойства.Добавить(ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ДатаОтгрузки);
	
	ЗначенияСвойствОбъекта = УправлениеСвойствами.ПолучитьЗначенияСвойств(
		ДокументОбъект, Истина, Истина, ИскомыеСвойства);
		
	ДанныеОбъекта.Вставить("ДатаОтгрузки",
		УправлениеСвойствамиПлатформа.ПолучитьЗначениеСвойства("ДатаОтгрузки", ЗначенияСвойствОбъекта));	

	ДанныеОбъекта.Вставить("Руководитель"     	     , 
		УправлениеПечатьюПлатформа.ДанныеОтветственногоЛицаПодписанты(Объект, "Руководитель")
	);
	ДанныеОбъекта.Вставить("ГлавныйБухгалтер" 		 ,
		УправлениеПечатьюПлатформа.ДанныеОтветственногоЛицаПодписанты(Объект, "ГлавныйБухгалтер")
	);
	ДанныеОбъекта.Вставить("Менеджер"         		 , 
		УправлениеПечатьюПлатформа.ДанныеОтветственногоЛица(Объект, "Менеджер")
	);
	
	ДанныеОбъекта.Вставить("ЭтоПродажа", Ложь);
	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

// Выполняет перенос свойств в реквизиты объекта
// Руководитель, ГлавныйБухгалтер,Отустил, ОтпустилКонтрагент, Получил, ПолучилКонтрагент
//
Процедура ВыполнитьПереносПодписантовИзСвойств() Экспорт 
	
	ПереносимыеСвойства = УправлениеСвойствамиАльфаАвто.ПолучитьСтруктуруСвойствДокументовПоступления();
	ПеренесеныВсеСвойства = Истина;
	
	МассивПереносимыхСвойств = УправлениеСвойствамиАльфаАвто.СтруктураВМассив(ПереносимыеСвойства); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаПоступленияАвтомобилейДополнительныеРеквизиты.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаПоступленияАвтомобилей.ДополнительныеРеквизиты КАК КорректировкаПоступленияАвтомобилейДополнительныеРеквизиты
	|ГДЕ
	|	КорректировкаПоступленияАвтомобилейДополнительныеРеквизиты.Свойство В(&ИзменяемыеРеквизиты)";
	
	Запрос.УстановитьПараметр("ИзменяемыеРеквизиты", МассивПереносимыхСвойств);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();  
	
	Пока Выборка.Следующий() Цикл  
		
		СвойстваКУдалению = Новый Массив();
		
		НачатьТранзакцию(); 
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.КорректировкаПоступления");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();  
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЗначенияСвойств = ДокументОбъект.ДополнительныеРеквизиты.Выгрузить();
			
			Если НЕ ЗначениеЗаполнено(ДокументОбъект.Руководитель) Тогда 
				
				СвойствоРуководитель = ЗначенияСвойств.Найти(ПереносимыеСвойства.Руководитель,"Свойство");
				
				Если СвойствоРуководитель <> Неопределено Тогда
					
					ДокументОбъект.Руководитель = УправлениеСвойствамиАльфаАвто.НайтиИлиСоздатьПодписантаКонтрагента(
						СвойствоРуководитель.Значение,
						Выборка.Ссылка
					);
					
					Если ЗначениеЗаполнено(ДокументОбъект.Руководитель) Тогда
						
						СвойстваКУдалению.Добавить(СвойствоРуководитель.Свойство);
						
					КонецЕсли;
					
				ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.ПодписантОтКонтрагента) Тогда 
					
					ДокументОбъект.Руководитель = ДокументОбъект.ПодписантОтКонтрагента
					
				КонецЕсли;
				
			КонецЕсли;

			Если Не ЗначениеЗаполнено(ДокументОбъект.ГлавныйБухгалтер) Тогда	
				
				СвойствоГлавныйБухгалтер = ЗначенияСвойств.Найти(ПереносимыеСвойства.ГлавныйБухгалтер,"Свойство");
				
				Если СвойствоГлавныйБухгалтер <> Неопределено Тогда
					
					ДокументОбъект.ГлавныйБухгалтер = УправлениеСвойствамиАльфаАвто.НайтиИлиСоздатьПодписантаКонтрагента(
						СвойствоГлавныйБухгалтер.Значение,
						Выборка.Ссылка
					); 
					
					Если ЗначениеЗаполнено(ДокументОбъект.ГлавныйБухгалтер) Тогда
						
						СвойстваКУдалению.Добавить(СвойствоГлавныйБухгалтер.Свойство); 
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;   

			Если Не ЗначениеЗаполнено(ДокументОбъект.Принял) Тогда
				СвойствоДоверенность = ЗначенияСвойств.Найти(ПереносимыеСвойства.Доверенность,"Свойство");
				
				Если СвойствоДоверенность <> Неопределено Тогда

										
					ДокументОбъект.Принял = УправлениеСвойствамиАльфаАвто.НайтиИлиСоздатьПодписантаОрганизации(
						СвойствоДоверенность.Значение.Владелец,
						Выборка.Ссылка,
						СвойствоДоверенность.Значение
					);
						
					Если ЗначениеЗаполнено(ДокументОбъект.Принял)  И значениеЗаполнено(ДокументОбъект.Принял.Основание) Тогда
							
						СвойстваКУдалению.Добавить(СвойствоДоверенность.Свойство);
							
					КонецЕсли; 
					
				КонецЕсли;
					
			КонецЕсли;
		
			Если СвойстваКУдалению.Количество()>0 Тогда  
				
				Для Каждого Элемент Из СвойстваКУдалению Цикл 
					
					Строка = ДокументОбъект.ДополнительныеРеквизиты.Найти(Элемент, "Свойство");
					
					Если Строка <> Неопределено Тогда 
						
						ДокументОбъект.ДополнительныеРеквизиты.Удалить(Строка.НомерСтроки -1); 
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ЗафиксироватьТранзакцию();
		Исключение 
			
			ОтменитьТранзакцию();  
			ПеренесеныВсеСвойства = Ложь;
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка обновления списка подписантов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Предупреждение,
				,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПеренесеныВсеСвойства Тогда
		Запрос = Новый Запрос;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НаборыДополнительныхРеквизитовИСведений.Ссылка КАК Набор
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведений
		|ГДЕ
		|	НаборыДополнительныхРеквизитовИСведений.Ссылка.ИмяПредопределенныхДанных = ""Документ_КорректировкаПоступленияАвтомобилей""";
		
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();	
			
			Пока Выборка.Следующий() Цикл
				

				Попытка	
					НаборОбъект = Выборка.Набор.ПолучитьОбъект();
					
					Для Каждого Элемент Из МассивПереносимыхСвойств Цикл 
					
						Строка = НаборОбъект.ДополнительныеРеквизиты.Найти(Элемент, "Свойство");
					
						Если Строка <> Неопределено Тогда 
						
							НаборОбъект.ДополнительныеРеквизиты.Удалить(Строка.НомерСтроки -1); 
						
						КонецЕсли;
					
					КонецЦикла;
					
					ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НаборОбъект);
				Исключение
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось обработать справочник: ""НаборыДополнительныхРеквизитовИСведений"" по причине:
							|%1'"), 
							ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
						Метаданные.Справочники.НаборыДополнительныхРеквизитовИСведений, "НаборыДополнительныхРеквизитовИСведений", ТекстСообщения);
				КонецПопытки;	
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры	

// Выполяет проверку выполнения отложенного обработчика обновления ВыполнитьПереносПодписантовИзСвойств
//
/// Параметры:
//  Параметры 			- Структура - параметры выполнения операции     
//
// Возвращаемое значение:
// Булево - результат проверки
//
Функция ПроверитьПереносПодписантовИзСвойств(Параметры) Экспорт
	
	ПереносимыеСвойства = УправлениеСвойствамиАльфаАвто.ПолучитьСтруктуруСвойствДокументовРеализации();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаПоступленияАвтомобилейДополнительныеРеквизиты.Свойство КАК Свойство,
	|	КорректировкаПоступленияАвтомобилейДополнительныеРеквизиты.Значение КАК Значение
	|ИЗ
	|	Документ.КорректировкаПоступленияАвтомобилей.ДополнительныеРеквизиты КАК КорректировкаПоступленияАвтомобилейДополнительныеРеквизиты
	|ГДЕ
	|	КорректировкаПоступленияАвтомобилейДополнительныеРеквизиты.Свойство В(&ИзменяемыеРеквизиты)
	|	И КорректировкаПоступленияАвтомобилейДополнительныеРеквизиты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ИзменяемыеРеквизиты", УправлениеСвойствамиАльфаАвто.СтруктураВМассив(ПереносимыеСвойства));
	Запрос.УстановитьПараметр("Ссылка",				 Параметры);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
	    Возврат Ложь;
	Иначе 
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СозданиеНаОсновании

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	ОбъектыВводимыеНаОсновании = Новый Массив();
	ОбъектыВводимыеНаОсновании.Добавить(Документы.КорректировкаПоступленияАвтомобилей); 
	ОбъектыВводимыеНаОсновании.Добавить(Документы.РасходныйСкладскойОрдерНаАвтомобили); 
	ОбъектыВводимыеНаОсновании.Добавить(Документы.Событие);
	ОбъектыВводимыеНаОсновании.Добавить(Документы.СчетФактураПолученный);
	ОбъектыВводимыеНаОсновании.Добавить(Документы.ЧекНаОплату); 
	
	Для Каждого ОбъектВводимыйНаОсновании Из ОбъектыВводимыеНаОсновании Цикл
		ОбъектВводимыйНаОсновании.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет список команд создания на основании.
//
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	 КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Возврат СозданиеНаОснованииАльфаАвто.ДобавитьКомандуСозданияНаОсновании(КомандыСоздатьНаОсновании,
		Метаданные.Документы.КорректировкаПоступленияАвтомобилей);

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// СтандартныеПодсистемы.УправлениеДоступом
// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст = 
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(ПодразделениеКомпании)
		|	И ЗначениеРазрешено(Организация)
		|	И ЗначениеРазрешено(СкладКомпании)
		|	И ЗначениеРазрешено(Контрагент)";
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиОбновления

// Процедура заполнения в ТЧ Автомобили колонки "Количество"
//
Процедура ЗаполнитьКоличествоАвтомобилейВКорректировкахПоступления() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КорректировкаПоступленияАвтомобилейАвтомобили.Ссылка КАК Ссылка,
	               |	КорректировкаПоступленияАвтомобилейАвтомобили.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	Документ.КорректировкаПоступленияАвтомобилей.Автомобили КАК КорректировкаПоступленияАвтомобилейАвтомобили
	               |ГДЕ
	               |	КорректировкаПоступленияАвтомобилейАвтомобили.КоличествоПоДокументуПоступления = 0
	               |	И КорректировкаПоступленияАвтомобилейАвтомобили.Количество = 0
	               |	И КорректировкаПоступленияАвтомобилейАвтомобили.КоличествоРазница = 0
	               |ИТОГИ ПО
	               |	Ссылка";
	
	ВыборкаДокумента = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокумента.Следующий() Цикл
		
		ДокументОбъект = ВыборкаДокумента.Ссылка.ПолучитьОбъект();
		
		ВыборкаСтрокДокумента = ВыборкаДокумента.Выбрать();
		
		Пока ВыборкаСтрокДокумента.Следующий() Цикл
			СтрокаАвтомобиля = ДокументОбъект.Автомобили.Найти(ВыборкаСтрокДокумента.НомерСтроки, "НомерСтроки");
			Если НЕ СтрокаАвтомобиля = Неопределено Тогда
				СтрокаАвтомобиля.КоличествоПоДокументуПоступления = 1;
				СтрокаАвтомобиля.Количество = 1;
			КонецЕсли;
		КонецЦикла;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		Попытка
			ДокументОбъект.Записать();
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'Не удалось перезаполнить количество в ТЧ Автомобилей документа %1'"),
					СокрЛП(Строка(ДокументОбъект.Ссылка))
				)
			);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьКоличествоАвтомобилейВКорректировкахПоступления()

// Заполняет реквизит "НомерИсправления" на основании введенного корректировочного счета-фактуры.
//
Процедура ЗаполнитьНомерИсправленияВКорректировкахПоступленияАвтомобилей() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Исключения = Новый Массив;
	Результат = КорректировкиДляЗаполнения(Исключения);
	
	Пока Не Результат.Пустой Цикл
		
		ВыборкаДокумента = Результат.Выборка;
		
		Пока ВыборкаДокумента.Следующий() Цикл
			
			ДокументОбъект = ВыборкаДокумента.Ссылка.ПолучитьОбъект();
			ДокументОбъект.НомерИсправления = ВыборкаДокумента.НомерИсправления;
			
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			
			Попытка
				ДокументОбъект.Записать();
			Исключение
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(
						НСтр("ru = 'Не удалось заполнить реквизит номер исправления документа %1'"),
						СокрЛП(Строка(ДокументОбъект.Ссылка))
					)
				);
			КонецПопытки;
			
			Исключения.Добавить(ДокументОбъект.Ссылка);
			
		КонецЦикла;
		
		Результат = КорректировкиДляЗаполнения(Исключения);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Проверяет возможность ввода счет-фактуры полученной на основании документа.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект.КорректировкаПоступленияАвтомобилей - документ, для которого выполняется действие.
//  ДополнительныеПараметры - Структура - Набор параметров, использующихся при выполнения операции.
//
// Возвращаемое значение:
//  Булево - Истина, если ввод возможен, Ложь - в противном случае.
//
Функция ВозможенВводСчетФактурыПолученной(ДокументОбъект, ДополнительныеПарметры = Неопределено) Экспорт
	
	Возврат ОбработкаСобытийДокументаСервер.ДокументСодержитСтрокиСНДС(ДокументОбъект, "Автомобили");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьНомераСтрокВТаблице(Сам, Знач ТаблицаИзменений)
	
	СтруктураПоиска = Новый Структура("Номенклатура,ГТД");
	 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураПолученныйТовары.Номенклатура КАК Номенклатура,
		|	СчетФактураПолученныйТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СчетФактураПолученныйТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СчетФактураПолученныйТовары.ГТД КАК ГТД,
		|	СчетФактураПолученныйТовары.ИсходныйНомерСтрокиТовара КАК ИсходныйНомерСтроки
		|ИЗ
		|	Документ.СчетФактураПолученный.Товары КАК СчетФактураПолученныйТовары
		|ГДЕ
		|	СчетФактураПолученныйТовары.Ссылка.ДокументОснование = &ССылка";
	
	Запрос.УстановитьПараметр("Ссылка", Сам);
	
	ДанныеСФ = Запрос.Выполнить().Выгрузить();
	
	Для каждого ТекущаяСтрока Из ТаблицаИзменений Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		
		НайденныеСтроки = ДанныеСФ.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ТекущаяСтрока.ИсходныйНомерСтроки = НайденныеСтроки[0].ИсходныйНомерСтроки; 
		
	КонецЦикла;
	
	Возврат ТаблицаИзменений;
	
КонецФункции

Функция КорректировкиДляЗаполнения(Исключения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура("Выборка, Пустой");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Исключения", Исключения);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2000
	|	СчетФактураПолученный.ДокументОснование КАК Ссылка,
	|	СчетФактураПолученный.НомерИсправления КАК НомерИсправления
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ДокументОснование ССЫЛКА Документ.КорректировкаПоступленияАвтомобилей
	|	И НЕ ВЫРАЗИТЬ(СчетФактураПолученный.ДокументОснование КАК Документ.КорректировкаПоступленияАвтомобилей) В (&Исключения)
	|	И ВЫРАЗИТЬ(СчетФактураПолученный.ДокументОснование КАК Документ.КорректировкаПоступленияАвтомобилей).ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.КорректировкаПоступленияАвтомобилейИсправлениеВПервичныхДокументах)
	|	И ВЫРАЗИТЬ(СчетФактураПолученный.ДокументОснование КАК Документ.КорректировкаПоступленияАвтомобилей).НомерИсправления = 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Результат.Пустой = Истина;
	Иначе
		Результат.Пустой = Ложь;
		Результат.Выборка = РезультатЗапроса.Выбрать();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПроведениеДокумента

Процедура ИнициализироватьДанныеДокументаДляПроведения(Объект, ДополнительныеПараметры) Экспорт
	
	ОбработкаСобытийДокументаСервер.ДанныеДокументаДляПроведения(Объект, ДополнительныеПараметры);
	
	// TODO: Сумму продаж комплектации для ничисления долга
	ДополнительныеПараметры.ДляПроведения.Вставить(
		"СуммаКомплектации",
		ПолучитьЗначениеПараметраСтруктуры(ДополнительныеПараметры, "СуммаКомплектации", Неопределено));
	
	ДополнительныеПараметры.ДляПроведения.Вставить(
		"ВалютаДоговора",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорВзаиморасчетов, "ВалютаВзаиморасчетов"));
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	// получим партию средних остатков
	ПартияОтрицательныхОстатков = Константы.ПартияТоваровОтрицательныхОстатков.Получить();
	
	// получим стратегию списания товаров
	СтратегияСписанияПартийТоваровПоДатам = ПолучитьСтратегиюСписанияПоДатам(
		Объект.СкладКомпании,
		Объект.Организация,
		Объект.Дата);
	
	ТекущеееОснование = Объект.ДокументОснование;
	Пока ТипЗнч(ТекущеееОснование) = Тип("ДокументСсылка.КорректировкаПоступления") Цикл
		ТекущеееОснование = ТекущеееОснование.ДокументОснование;
	КонецЦикла;
	ПартияДокумента = ТекущеееОснование;
	
	ДополнительныеПараметры.ДляПроведения.Вставить("ПартияДокумента", ПартияДокумента);
	ДополнительныеПараметры.ДляПроведения.Вставить("ПартияОтрицательныхОстатков", ПартияОтрицательныхОстатков);
	
	ДанныеДокумента = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = ДанныеДокумента;
	Запрос.УстановитьПараметр("ВалютаРегл", ВалютаРегл);
	Запрос.УстановитьПараметр("ВалютаУпр", ВалютаУпр);
	Запрос.УстановитьПараметр("ВалютаДоговора", ДополнительныеПараметры.ДляПроведения.ВалютаДоговора);
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеПараметры.ДляПроведения.Ссылка);
	Запрос.УстановитьПараметр(
		"МоментВремени",
		Новый Граница(ДополнительныеПараметры.ДляПроведения.Дата, ВидГраницы.Включая));
	
	Запрос.Текст = "ВЫБРАТЬ
	|	КорректировкаПоступленияАвтомобилей.Организация КАК Организация,
	|	КорректировкаПоступленияАвтомобилей.Ссылка КАК Ссылка,
	|	КорректировкаПоступленияАвтомобилей.Дата КАК Дата,
	|	КорректировкаПоступленияАвтомобилей.ХозОперация КАК ХозОперация,
	|	КорректировкаПоступленияАвтомобилей.ДокументОснование КАК ДокументОснование,
	|	КорректировкаПоступленияАвтомобилей.Контрагент КАК Контрагент,
	|	КорректировкаПоступленияАвтомобилей.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	КорректировкаПоступленияАвтомобилей.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов КАК ВалютаДоговора,
	|	КорректировкаПоступленияАвтомобилей.Сделка КАК Сделка,
	|	КорректировкаПоступленияАвтомобилей.СуммаДокумента КАК Сумма,
	|	КорректировкаПоступленияАвтомобилей.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	КорректировкаПоступленияАвтомобилей.ВалютаДокумента КАК ВалютаДокумента,
	|	КорректировкаПоступленияАвтомобилей.КурсДокумента КАК КурсДокумента,
	|	КорректировкаПоступленияАвтомобилей.КурсВалютыУпр КАК КурсВалютыУпр,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.НеЗачитывать) КАК СпособЗачетаАвансов,
	|	&ВалютаРегл КАК ВалютаРегл,
	|	&ВалютаУпр КАК ВалютаУпр
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.КорректировкаПоступленияАвтомобилей КАК КорректировкаПоступленияАвтомобилей
	|ГДЕ
	|	КорректировкаПоступленияАвтомобилей.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(КорректировкаПоступленияАвтомобилейАвтомобили.СуммаВсегоРазница), 0) КАК СуммаКорректировки,
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ СуммаРазницыКорректировки
	|ИЗ
	|	Документ.КорректировкаПоступленияАвтомобилей.Автомобили КАК КорректировкаПоступленияАвтомобилейАвтомобили
	|ГДЕ
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияАвтомобилейАвтомобили.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта В (&ВалютаРегл, &ВалютаУпр, &ВалютаДоговора)) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.ХозОперация КАК ХозОперация,
	|	ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ДанныеДокумента.ВалютаДоговора КАК ВалютаДоговора,
	|	ДанныеДокумента.Сделка КАК Сделка,
	|	ДанныеДокумента.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КурсВалютыВзаиморасчетов = 0
	|			ТОГДА ЕСТЬNULL(КурсыВалютВзаиморасчетов.Курс, 0) / ЕСТЬNULL(КурсыВалютВзаиморасчетов.Кратность, 1)
	|		ИНАЧЕ ДанныеДокумента.КурсВалютыВзаиморасчетов
	|	КОНЕЦ КАК КурсВалютыВзаиморасчетов,
	|	ДанныеДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ДанныеДокумента.КурсДокумента КАК КурсДокумента,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КурсВалютыУпр = 0
	|			ТОГДА ЕСТЬNULL(КурсыВалютУпр.Курс, 0) / ЕСТЬNULL(КурсыВалютУпр.Кратность, 1)
	|		ИНАЧЕ ДанныеДокумента.КурсВалютыУпр
	|	КОНЕЦ КАК КурсВалютыУпр,
	|	ДанныеДокумента.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	ДанныеДокумента.ВалютаРегл КАК ВалютаРегл,
	|	ДанныеДокумента.ВалютаУпр КАК ВалютаУпр,
	|	ЕСТЬNULL(КурсыВалютРегл.Курс, 0) / ЕСТЬNULL(КурсыВалютРегл.Кратность, 1) КАК КурсВалютыРегл,
	|	ЕСТЬNULL(СуммаРазницыКорректировки.СуммаКорректировки, 0) КАК СуммаКорректировки
	|ПОМЕСТИТЬ Задолженность
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУпр
	|		ПО ДанныеДокумента.ВалютаУпр = КурсыВалютУпр.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютВзаиморасчетов
	|		ПО ДанныеДокумента.ВалютаДоговора = КурсыВалютВзаиморасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютРегл
	|		ПО ДанныеДокумента.ВалютаРегл = КурсыВалютРегл.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммаРазницыКорректировки КАК СуммаРазницыКорректировки
	|		ПО ДанныеДокумента.Ссылка = СуммаРазницыКорректировки.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КурсыВалют";
	
	Запрос.Выполнить();
	
	ДополнительныеПараметры.ДляПроведения.Вставить("ДанныеДокумента", ДанныеДокумента);
	
КонецПроцедуры

Процедура СформироватьТаблицуРасчетыСКонтрагентами(ДвиженияДокумента, Отказ) Экспорт
	
	РасчетыСКонтрагентами = ОбработкаСобытийДокументаСервер.ИнициализироватьТаблицуРегистра("РасчетыСКонтрагентами");
	
	Если ДвиженияДокумента.ДляПроведения.ПартияДокумента.ХозОперация =
			Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия
		ИЛИ ДвиженияДокумента.ДляПроведения.ПартияДокумента.ХозОперация =
			Справочники.ХозОперации.ПереходВСобственностьАвтомобилей Тогда
		
		ДвиженияДокумента.ТаблицыДвиженийДокумента.Вставить("РасчетыСКонтрагентами", РасчетыСКонтрагентами);
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДвиженияДокумента.ДляПроведения.ДанныеДокумента;
	Запрос.Текст = "ВЫБРАТЬ
	|	Задолженность.Организация КАК Организация,
	|	Задолженность.Ссылка КАК Ссылка,
	|	Задолженность.Дата КАК Дата,
	|	Задолженность.ХозОперация КАК ХозОперация,
	|	Задолженность.ДокументОснование КАК ДокументОснование,
	|	Задолженность.Контрагент КАК Контрагент,
	|	Задолженность.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Задолженность.ВалютаДоговора КАК ВалютаДоговора,
	|	Задолженность.Сделка КАК Сделка,
	|	Задолженность.СуммаКорректировки КАК Сумма,
	|	Задолженность.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Задолженность.ВалютаДокумента КАК ВалютаДокумента,
	|	Задолженность.КурсДокумента КАК КурсДокумента,
	|	Задолженность.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Задолженность.КурсВалютыРегл КАК КурсВалютыРегл,
	|	Задолженность.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Задолженность.ВалютаРегл КАК ВалютаРегл,
	|	Задолженность.ВалютаУпр КАК ВалютаУпр
	|ИЗ
	|	Задолженность КАК Задолженность
	|ГДЕ
	|	Задолженность.ДоговорВзаиморасчетов.СпособВеденияВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.СпособыВеденияУчетаВзаиморасчетов.ПоРасчетнымДокументам)";
	
	ДанныеОбОплате = Запрос.Выполнить().Выбрать();
	
	Если Не ДанныеОбОплате.Следующий()
		Или ДанныеОбОплате.Сумма = 0 Тогда
		
		ДвиженияДокумента.ТаблицыДвиженийДокумента.Вставить("РасчетыСКонтрагентами", РасчетыСКонтрагентами);
		Возврат;
		
	КонецЕсли;
	
	Если ДанныеОбОплате.Сумма > 0 Тогда
		
		РасчетыСКонтрагентамиСервер.ДобавитьЗаписьРасчетаКонтрагентов(
			ДанныеОбОплате,
			РасчетыСКонтрагентами,
			ДанныеОбОплате.Сумма,
			ВидДвиженияНакопления.Расход,
			Перечисления.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности,
			ДанныеОбОплате.Сделка,,
			Истина);
		
	Иначе
		
		РасчетыСКонтрагентамиСервер.ЗачестьОплатуПоСделке(
			РасчетыСКонтрагентами,
			ДанныеОбОплате,
			Ложь,
			Истина);
		
	КонецЕсли;
	
	СуммаКомплектации = ПолучитьЗначениеПараметраСтруктуры(
		ДвиженияДокумента.ДляПроведения,
		"СуммаКомплектации",
		Неопределено);
	
	Если СуммаКомплектации <> Неопределено Тогда
		
		НоваяЗапись = РасчетыСКонтрагентами.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеОбОплате);
		
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
		НоваяЗапись.Период = ДанныеОбОплате.Дата;
		НоваяЗапись.Регистратор = ДанныеОбОплате.Ссылка;
		НоваяЗапись.ТипРасчета = Перечисления.ТипыРасчетов.Аванс;
		НоваяЗапись.ДокументРасчетов = ДанныеОбОплате.Ссылка;
		
		Если ДанныеОбОплате.ВалютаДоговора = ДанныеОбОплате.ВалютаУпр Тогда
			Сумма = СуммаКомплектации.СуммаУпр;
		ИначеЕсли ДанныеОбОплате.ВалютаДоговора = ДанныеОбОплате.ВалютаРегл Тогда
			Сумма = СуммаКомплектации.Сумма;
		Иначе
			Сумма = Окр(
				РаботаСКурсамиВалютПлатформа.ПересчетПоВалюте(
					СуммаКомплектации.СуммаУпр,
					ДанныеОбОплате.ДанныеОбОплате.ВалютаУпр,
					ДанныеОбОплате.КурсВалютыУпр,
					ДанныеОбОплате.ВалютаДоговора,
					ДанныеОбОплате.КурсВалютыВзаиморасчетов),
				2);
		КонецЕсли;
		
		НоваяЗапись.Сумма = Сумма;
		НоваяЗапись.СуммаУпр = СуммаКомплектации.СуммаУпр;
		НоваяЗапись.СуммаБаз = СуммаКомплектации.Сумма;
			
		НоваяЗапись.Курс = ДанныеОбОплате.КурсДокумента;
		НоваяЗапись.ВалютаПлатежа = ДанныеОбОплате.ВалютаДокумента;
		НоваяЗапись.СпособЗачета = ДанныеОбОплате.СпособЗачетаАвансов;
		НоваяЗапись.ВидОперации = Перечисления.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности;
		НоваяЗапись.Активность = Истина;
		
	КонецЕсли;
	
	РасчетыСКонтрагентамиСервер.ПроверитьРасчетыСКонтрагентами(
		ДвиженияДокумента.ДляПроведения.ДокументОбъект,
		РасчетыСКонтрагентами,
		Отказ);
	
	ДвиженияДокумента.ТаблицыДвиженийДокумента.Вставить("РасчетыСКонтрагентами", РасчетыСКонтрагентами);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
