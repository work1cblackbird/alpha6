
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	КлючФормы = ПоискДокументовПоОснованию(ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Основание", ПараметрКоманды);
	ПараметрыФормы.Вставить("Ключ",      КлючФормы);
	
	ОткрытьФорму("Документ.КорректировкаРеализацииАвтомобилей.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ,,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоискДокументовПоОснованию(Основнование)
	
	Результат = Документы.КорректировкаПоступленияАвтомобилей.ПустаяСсылка();
	
	Если Основнование.Проведен Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПодчиненныеДокументы.Ссылка КАК Документ
		|ИЗ
		|	КритерийОтбора.ПодчиненныеДокументы(&Основание) КАК ПодчиненныеДокументы
		|ГДЕ
		|	ПодчиненныеДокументы.Ссылка ССЫЛКА Документ.КорректировкаРеализацииАвтомобилей";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Основание", Основнование);
		Выборка = Запрос.Выполнить().Выгрузить();
		
		Если Выборка.Количество() > 0 Тогда
			Результат = Выборка[0].Документ;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='На основании документа <%1> уже введен <%2>.'"),Основнование,Выборка[0].Документ);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Выборка[0].Документ);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПоискДокументовПоОснованию()

#КонецОбласти