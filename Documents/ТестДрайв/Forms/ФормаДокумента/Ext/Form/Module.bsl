///////////////////////////////////////////////////////////////////////////////
// Модуль основной формы документа "Тест-драйв"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ИмяЭлементаКоманднойПанели", "ГлобальныеКоманды");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	РаботаСФормой.ИнициализироватьМенюВыбораХозОперации(ЭтотОбъект);
	РаботаСФормой.РасставитьСвязиПараметровВыбораПоОрганизации(ЭтотОбъект, Объект);
	РаботаСФормой.ОграничитьВыборКонтактныхЛиц(Элементы.Контрагент);
	РаботаСФормой.ЗаблокироватьРедактированиеНомераИДатыДокумента(ЭтотОбъект, Объект);
	РаботаСФормой.ОткрытьФормуТолькоДляПросмотра(ЭтотОбъект, Объект);
	
	// пока закомментировал
	//ОбновитьКонтактнуюИнформациюКонтрагента(Ложь);
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормы.ПриСозданииНаСервере_ФормаДокумента(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец АльфаАвто.СобытияОбъектов
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
	КонецЕсли;
	
	ПараметрыДокумента = ОбщиеПараметрыДокументов.СформироватьПредставлениеПараметровДокумента(Объект);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормыКлиент.ПриОткрытии_ФормаДокумента(ЭтотОбъект, Отказ);
	// Конец АльфаАвто.СобытияОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение, ПараметрыДействия=Неопределено)
	
	Если НЕ УправлениеДиалогомДокументаСервер.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ПараметрыДействия) Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ПараметрыДействия = Новый Структура;
	ОбработкаВыбораНаСервере(ВыбранноеЗначение, ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормыКлиент.ОбработкаОповещения_ФормаДокумента(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец АльфаАвто.СобытияОбъектов
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормы.ПриЧтенииНаСервере_ФормаДокумента(ЭтотОбъект, ТекущийОбъект);
	// Конец АльфаАвто.СобытияОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РаботаСФормойКлиент.ЗамерВремениЗапись("ТестДрайв", ПараметрыЗаписи.РежимЗаписи, Ложь);
		
	Отказ = Отказ Или РаботаСФормойКлиент.НачатьПроверкуПодразделенияДокументаИПользователя(
		Объект.ПодразделениеКомпании,
		Новый ОписаниеОповещения(
			"ПроверкаПодразделенияДокументаИПользователяЗавершение",
			ЭтотОбъект,
			Новый Структура("ПараметрыЗаписи", ПараметрыЗаписи)
		)
	);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормы.ПередЗаписьюНаСервере_ФормаДокумента(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	// Конец АльфаАвто.СобытияОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормы.ПослеЗаписиНаСервере_ФормаДокумента(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец АльфаАвто.СобытияОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	РаботаСФормойКлиент.ОповеститьОЗаписиДокумента(Объект.Ссылка);
	РаботаСФормойКлиент.ОбновитьПодчиненныеСчета(Объект.Ссылка, Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Вставить("ПоказыватьПараметрыДокумента", Элементы.ПараметрыДокумента.Видимость);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки.Получить("ПоказыватьПараметрыДокумента") = Ложь Тогда
		Элементы.ПараметрыДокумента.Видимость = Ложь;
		Элементы.НомерДата         .Видимость = Ложь;
	КонецЕсли;
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ДатаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.ДатаПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ДатаПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ДатаНачалаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.ДатаНачалаПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент = Неопределено)
	
	ПараметрыДействия = Новый Структура;
	ДатаНачалаПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ДатаОкончанияПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.ДатаОкончанияПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент = Неопределено)
	
	ПараметрыДействия = Новый Структура;
	ДатаОкончанияПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.КонтрагентПриИзменении(Объект, ПараметрыДействия);
	ЗаметкиПользователяАльфаАвто.ПроверитьЗаметкиПоПредмету(Объект.Контрагент, ПараметрыДействия);
	ОбновитьКонтактнуюИнформациюКонтрагента();
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	КонтрагентПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура АвтомобильПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.АвтомобильПриИзменении(Объект, ПараметрыДействия);
	ЗаметкиПользователяАльфаАвто.ПроверитьЗаметкиПоПредмету(Объект.Автомобиль, ПараметрыДействия);
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобильПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	АвтомобильПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура МаршрутПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.МаршрутПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	МаршрутПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура РезультатТестДрайваПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.РезультатТестДрайваПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатТестДрайваПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	РезультатТестДрайваПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура СостояниеПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.СостояниеПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	СостояниеПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура МенеджерПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ТестДрайв.МенеджерПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МенеджерПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	МенеджерПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныйТелефонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыРедактирования = УправлениеКонтактнойИнформациейАльфаАвтоКлиент.НовыеПараметрыРедактирования();
	ПараметрыРедактирования.ПутьКДанным = "Объект.КонтактныйТелефон";
	УправлениеКонтактнойИнформациейАльфаАвтоКлиент.НачатьРедактированиеКонтактнойИнформации(
		ЭтотОбъект,
		Элемент.ТекстРедактирования,
		ПараметрыРедактирования
	);
	
	Модифицированность = Истина;
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовУправленияОбщегоНазначения

&НаКлиенте
Процедура ПредварительноеАнкетированиеНажатие(Элемент)

	Если ЗначениеЗаполнено(Объект.ПредварительныйОпрос) Тогда
		ПоказатьЗначение(,Объект.ПредварительныйОпрос);
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеАнкет();

	Если НЕ ЗначениеЗаполнено(ПредварительнаяАнкета) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для данной модели автомобиля не назначена предварительная анкета для тест-драйва.'")
		);
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() ИЛИ Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Перед вводом анкетного опроса необходимо записать текущий документ.'")
		);		
		Возврат;
	КонецЕсли;	
	
	НачатьИнтервью(ПредварительнаяАнкета, "ПредварительноеАнкетирование"); 
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьИнтервью(ШаблонАнкеты, Событие)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Респондент", Объект.Контрагент);
	ЗначенияЗаполнения.Вставить("ШаблонАнкеты", ШаблонАнкеты);
	ЗначенияЗаполнения.Вставить("РежимАнкетирования", ПредопределенноеЗначение("Перечисление.РежимыАнкетирования.Интервью"));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ТолькоФормаЗаполнения", Истина);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", ЭтотОбъект, Событие);
	ФормаАнкеты = ОткрытьФорму("Документ.Анкета.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	Если ФормаАнкеты <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ФормаАнкеты, ЗначенияЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИтоговоеАнкетированиеНажатие(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ИтоговыйОпрос) Тогда
		ПоказатьЗначение(,Объект.ИтоговыйОпрос);
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеАнкет();

	Если НЕ ЗначениеЗаполнено(ИтоговаяАнкета) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для данной модели автомобиля не назначена итоговая анкета для тест-драйва.'")
		);
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() ИЛИ Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Перед вводом анкетного опроса необходимо записать текущий документ.'")
		);		
		Возврат;
	КонецЕсли;	
	
	НачатьИнтервью(ИтоговаяАнкета, "ИтоговоеАнкетирование");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИнформацияОбАвтомобилеНажатие(Элемент)
	
	АвтомобилиКлиент.ОтчетИсторияАвтомобиля(ЭтотОбъект, Объект.Автомобиль);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АвтомобильНачалоВыбораНаСервере()
	
	Возврат Документы.ТестДрайв.ПолучитьАвтомобилиДляТестДрайва();
	
КонецФункции

&НаКлиенте
Процедура АвтомобильНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму(
		"Справочник.Автомобили.ФормаВыбора",
		Новый Структура("Ссылка,РежимВыбора", АвтомобильНачалоВыбораНаСервере(), Истина),
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", ЭтотОбъект, "ВыборАвтомобиля"),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
//@skip-warning
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда,
                                                НавигационнаяСсылка = Неопределено,
                                                СтандартнаяОбработка = Неопределено)
	
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
    
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбновитьКонтрагента(Команда)
	
	ДанныеКонтрагента = КонтрагентПоРабочемуЛисту(Объект.ДокументОснование);
	
	Объект.ОбращениеККлиенту = ПолучитьЗначениеПараметраСтруктуры(
		ДанныеКонтрагента,
		"ОбращениеККлиенту",
		Объект.ОбращениеККлиенту);
	
	КонтрагентРЛ = ПолучитьЗначениеПараметраСтруктуры(ДанныеКонтрагента, "Контрагент");
	
	Если ЗначениеЗаполнено(КонтрагентРЛ) Тогда
		Объект.Контрагент = КонтрагентРЛ;
		КонтрагентПриИзменении(Неопределено);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПодборАвтомобиля(Команда)
	
	МассивАвтомобилей = Новый Массив;
	//Для Каждого СтрокаАвтомобиля Из Объект.Автомобили Цикл
	//	МассивАвтомобилей.Добавить(СтрокаАвтомобиля.Автомобиль);	
	//КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимПодбора", Истина);
	ПараметрыФормы.Вставить("Документ", Объект.Ссылка);  
	ПараметрыФормы.Вставить("ПодразделениеКомпании", Объект.ПодразделениеКомпании);
	//ПараметрыФормы.Вставить("Марка", Объект.Марка);
	//ПараметрыФормы.Вставить("Модель", Объект.Модель);
	//ПараметрыФормы.Вставить("ВариантКомплектации", Объект.ВариантКомплектации);
	//ПараметрыФормы.Вставить("ТипДвигателя", Объект.ТипДвигателя);
	//ПараметрыФормы.Вставить("ТипКузова", Объект.ТипКузова);
	//ПараметрыФормы.Вставить("ТипКПП", Объект.ТипКПП);
	//ПараметрыФормы.Вставить("ТипПривода", Объект.ТипПривода);
	//ПараметрыФормы.Вставить("ТипСалона", Объект.ТипСалона);
	//ПараметрыФормы.Вставить("Цвет",      Объект.Цвет);
	//ПараметрыФормы.Вставить("ВидАренды", Объект.ВидАренды);
	ПараметрыФормы.Вставить("АрендаДатаНачала", Объект.ДатаНачала);
	ПараметрыФормы.Вставить("АрендаДатаОкончания", Объект.ДатаОкончания);  
	ПараметрыФормы.Вставить("МассивАвтомобилей", МассивАвтомобилей);  
	
	ОткрытьФорму("Обработка.ПланировщикАренды.Форма",
		ПараметрыФормы, ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ПодборАвтомобиля_Завершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборАвтомобиля_Завершение(ПараметрыПодбора, ДополнительныеПараметры = Неопределено) Экспорт 
	
	Если ТипЗнч(ПараметрыПодбора) = Тип("Структура") 
		И ПараметрыПодбора.Свойство("Автомобиль") Тогда
		ПодборАвтомобиля_ЗавершениеНаСервере(ПараметрыПодбора, ДополнительныеПараметры);
		//Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаАвтомобили;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодборАвтомобиля_ЗавершениеНаСервере(ПараметрыПодбора, ДополнительныеПараметры = Неопределено) 
	
	ДатыИзменились = Ложь;
	
	Если Объект.ДатаНачала <> ПараметрыПодбора.ДатаНачала И ПараметрыПодбора.ДатаНачала <> '00010101' Тогда
		Объект.ДатаНачала = ПараметрыПодбора.ДатаНачала;
		ДатыИзменились = Истина;
	КонецЕсли;
	
	Если Объект.ДатаОкончания <> ПараметрыПодбора.ДатаОкончания И ПараметрыПодбора.ДатаОкончания <> '00010101' Тогда
		Объект.ДатаОкончания = ПараметрыПодбора.ДатаОкончания;	
		ДатыИзменились = Истина;
	КонецЕсли;
	
	Если ДатыИзменились Тогда     
		Документы.ТестДрайв.ДатаОкончанияПриИзменении(Объект);
		//Документы.ДоговорАренды.ДатаОкончанияПриИзменении(Объект);
	КонецЕсли;
	
	//НоваяСтрока = Объект.Автомобили.Добавить();
	//НоваяСтрока.Автомобиль = ПараметрыПодбора.Автомобиль;
	//Документы.ЗаявкаНаАренду.АвтомобилиАвтомобильПриИзменении(Объект, НоваяСтрока);

	Объект.Автомобиль = ПараметрыПодбора.Автомобиль;
	Документы.ТестДрайв.АвтомобильПриИзменении(Объект); 
	
	// Обновляем заголовок надписи итоговой суммы по документу
	ЗащищенныеФункцииСервер.УстановитьЗаголовокНадписиСуммаДокумента(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеАнкет()

	ПредварительнаяАнкета = Неопределено;
	ИтоговаяАнкета = Неопределено;
	
	Если ЗначениеЗаполнено(Объект.Автомобиль) Тогда
		Анкеты = Документы.ТестДрайв.ПолучитьАнкетыТестДрайва(Объект.Дата, Объект.Автомобиль.Модель);	
		Если ТипЗнч(Анкеты)=Тип("Структура") Тогда
			ПредварительнаяАнкета = Анкеты.ПредварительнаяАнкета;
			ИтоговаяАнкета = Анкеты.ИтоговаяАнкета;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСсылкиНаАнкетирование()

	ОбновитьДанныеАнкет();
	
	Элементы.ПредварительноеАнкетирование.Видимость = ЗначениеЗаполнено(ПредварительнаяАнкета);
	Элементы.ИтоговоеАнкетирование.Видимость = ЗначениеЗаполнено(ИтоговаяАнкета); 
	
	Если НЕ ЗначениеЗаполнено(ПредварительнаяАнкета) И НЕ ЗначениеЗаполнено(ИтоговаяАнкета) Тогда
		Возврат;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Объект.ПредварительныйОпрос) Тогда
		Элементы.ПредварительноеАнкетирование.Заголовок = Строка(Объект.ПредварительныйОпрос);			
		Элементы.ПредварительныйОпрос.Видимость = Истина;
	Иначе
		Элементы.ПредварительноеАнкетирование.Заголовок = "Ввести предварительный анкетный опрос";		
		Элементы.ПредварительныйОпрос.Видимость = Ложь;
		Элементы.ПредварительноеАнкетирование.Доступность = Не ЭтотОбъект.ТолькоПросмотр;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ИтоговыйОпрос) Тогда
		Элементы.ИтоговоеАнкетирование.Заголовок = Строка(Объект.ИтоговыйОпрос);			
		Элементы.ИтоговыйОпрос.Видимость = Истина;
	Иначе
		Элементы.ИтоговоеАнкетирование.Заголовок = "Ввести итоговый анкетный опрос";		
		Элементы.ИтоговыйОпрос.Видимость = Ложь; 
		Элементы.ИтоговоеАнкетирование.Доступность = Не ЭтотОбъект.ТолькоПросмотр;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КонтрагентПоРабочемуЛисту(РабочийЛист)
	
	Результат = Новый Структура("ОбращениеККлиенту,Контрагент");
	
	Если НЕ ЗначениеЗаполнено(РабочийЛист) ИЛИ ТипЗнч(РабочийЛист) <> Тип("ДокументСсылка.РабочийЛист") Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РабочийЛист, "ОбращениеККлиенту,Контрагент");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьКонтактнуюИнформациюКонтрагента(ПодставлятьНомерТелефона = Истина)
	
	УправлениеКонтактнойИнформациейАльфаАвто.ОбновитьСписокВыбораКИ(
		Объект.Контрагент,
		ЭтотОбъект,
		"Телефон",
		"КонтактныйТелефон"
	);
	
	Если ПодставлятьНомерТелефона Тогда
		
		Объект.КонтактныйТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
				Объект.Контрагент,
				Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента, ТекущаяДатаСеанса()
		);
		
	КонецЕсли;
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ПроверкаПодразделенияДокументаИПользователяЗавершение(Контекст) Экспорт
	
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область ОбработчикиАльфаАвто

// Ядро
&НаКлиенте
Процедура ПараметрыДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;;
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыДокументаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	
	УправлениеДиалогомДокументаКлиент.РасширенноеРедактированиеПоляКомментарий(ЭтотОбъект, Элемент,
		СтандартнаяОбработка);
	
КонецПроцедуры
// Конец Ядро

#КонецОбласти

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомДокументаСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомДокументаСервер.УправлениеДиалогомНаСервере(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Объект.Автомобиль) Тогда
		Элементы.НадписьИнформацияОбАвтомобиле.Заголовок = Документы.ТестДрайв.СформироватьИнформациюОбАвтомобиле(Объект.Автомобиль, Объект.Дата);
	Иначе
		Элементы.НадписьИнформацияОбАвтомобиле.Заголовок = "<Автомобиль не выбран>";		
	КонецЕсли;
	
	СформироватьСсылкиНаАнкетирование();
	
	Элементы.НадписьИнформацияОбАвтомобиле.Заголовок = Справочники.Автомобили.СформироватьИнформациюАвтомобиля(Объект);
	Элементы.ОбновитьКонтрагента.Доступность = (ЗначениеЗаполнено(Объект.ДокументОснование)
		И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.РабочийЛист"));
	
КонецПроцедуры
	
&НаСервере
Процедура ОбработкаРезультатаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры=Неопределено)
	
	Если НЕ УправлениеДиалогомДокументаСервер.ОбработкаРезультатаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаОповещения(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если НЕ УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РезультатОповещения) Тогда
		Если ДополнительныеПараметры = "ВыборАвтомобиля" Тогда
			Объект.Автомобиль = РезультатОповещения;
			АвтомобильПриИзменении(Неопределено);
			Возврат;
		ИначеЕсли ДополнительныеПараметры = "ПредварительноеАнкетирование" Тогда
			Объект.ПредварительныйОпрос = РезультатОповещения;
		ИначеЕсли ДополнительныеПараметры = "ИтоговоеАнкетирование" Тогда
			Объект.ИтоговыйОпрос = РезультатОповещения;
		КонецЕсли;
		СформироватьСсылкиНаАнкетирование();
		Записать();
		Возврат;
	КонецЕсли;
	
	ОбработкаРезультатаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(ПолучитьЗначениеПараметраСтруктуры(ПолучитьЗначениеПараметраСтруктуры(РезультатОповещения, "ИзмененныеРеквизиты"), "Дата")) Тогда
		ДатаНачалаПриИзменении();
	КонецЕсли;
	
	ОбработкаРезультатаВыполненияДействия(РезультатОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРезультатаВыполненияДействия(ПараметрыДействия)
	
	ОбработкаРеквизитовДокументаКлиент.ПолучитьРазрешенияДляПересчета(ЭтотОбъект, ПараметрыДействия);
	УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаВыполненияДействия(ЭтотОбъект, ПараметрыДействия);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыДокумента

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаЗакрытияПараметровДокумента(РезультатОповещения, ДополнительныеПараметры = Неопределено) Экспорт
	                                                                                                                
	ОбработкаРезультатаЗакрытияПараметровДокументаНаСервере(РезультатОповещения, ДополнительныеПараметры);
	ОбработкаРеквизитовДокументаКлиент.ПолучитьРазрешенияДляПересчета(ЭтотОбъект, РезультатОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаЗакрытияПараметровДокументаНаСервере(РезультатОповещения, ДополнительныеПараметры)
	
	ОбщиеПараметрыДокументов.ПараметрыДокументаПриИзмененииРеквизитов(Объект, 	РезультатОповещения);
	ОбщиеПараметрыДокументов.ПараметрыДокументаПриИзменении(ЭтотОбъект,			РезультатОповещения);
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти