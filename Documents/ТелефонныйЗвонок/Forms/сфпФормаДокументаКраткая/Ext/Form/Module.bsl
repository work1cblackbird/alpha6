
#Область ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("НовыйЗвонок") Тогда	
		Звонок = Параметры.НовыйЗвонок;
	КонецЕсли;
	
	Контакт = Звонок.АбонентКонтакт;
	ПредставлениеКонтакта = Звонок.АбонентПредставление;
	НомерТелефона = Звонок.АбонентКакСвязаться;
	Комментарий = Звонок.Комментарий;
	
	Если Параметры.Свойство("Контакт") Тогда
		Контакт = Параметры.Контакт;
		Если ЗначениеЗаполнено(Контакт) Тогда
			ПредставлениеКонтакта = "" + Контакт;
		КонецЕсли;
		Модифицированность = Истина;
	КонецЕсли;

	Если Параметры.Свойство("НомерТелефона") Тогда
		НомерТелефона = Параметры.НомерТелефона;
	КонецЕсли;
	
	Элементы.ПредставлениеКонтакта.ТолькоПросмотр = ЗначениеЗаполнено(Контакт);
	
	ЭтаФорма.Заголовок = Звонок;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		Если ЗавершениеРаботы Тогда
			ТекстПредупреждения = НСтр("ru = 'Данные были изменены. Все изменения будут потеряны.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
			ОповещениеЗавершения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОповещениеЗавершения, ТекстПредупреждения, РежимДиалогаВопрос.ДаНетОтмена);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть(Неопределено);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ЗначениеЗаполнено(Звонок) Тогда
		Если Модифицированность Тогда
			ЗаписатьИзменения();
		КонецЕсли;	
	КонецЕсли;
	
	Оповестить("ЛентаСобытий_Обновить", Звонок);
	
	ЭтаФорма.Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТелефонныйЗвонок(Команда)
	
	Если ЗначениеЗаполнено(Звонок) Тогда
		Если Модифицированность Тогда
			ЗаписатьИзменения();
		КонецЕсли;	
		
		ПараметрыФормы = Новый Структура("Ключ", Звонок);
		ОткрытьФорму("Документ.ТелефонныйЗвонок.Форма.сфпФормаДокумента", ПараметрыФормы,,,,,, РежимОткрытияОкнаФормы.Независимый);
	КонецЕсли;
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьИзменения()
	
	Если ЗначениеЗаполнено(Звонок) Тогда
		ТекОбъект = Звонок.ПолучитьОбъект();
		
		ЗаписатьОбъект = Ложь;
		
		Если НЕ Элементы.ПредставлениеКонтакта.ТолькоПросмотр И ЗначениеЗаполнено(ПредставлениеКонтакта) Тогда
			Если ТекОбъект.АбонентПредставление <> ПредставлениеКонтакта Тогда
				ТекОбъект.АбонентПредставление = ПредставлениеКонтакта;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Комментарий) Тогда
			Если ТекОбъект.Комментарий <> Комментарий Тогда
				ТекОбъект.Комментарий = Комментарий;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Контакт) Тогда
			Если ТекОбъект.АбонентКонтакт <> Контакт Тогда
				ТекОбъект.АбонентКонтакт = Контакт;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НомерТелефона) Тогда
			Если ТекОбъект.АбонентКакСвязаться <> НомерТелефона Тогда
				ТекОбъект.АбонентКакСвязаться = НомерТелефона;
				ЗаписатьОбъект = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗаписатьОбъект Тогда
			ТекОбъект.Записать();
		КонецЕсли;
	КонецЕсли;

	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеКонтактаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область Инициализация
#КонецОбласти