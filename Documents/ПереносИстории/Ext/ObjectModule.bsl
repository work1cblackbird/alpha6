// Модуль документа Исходный документ

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	ПрефиксацияОбъектов.ПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ПродолжитьВыполнение = ОбработкаСобытийДокументаСервер.ОбработкаЗаполнения(
		ЭтотОбъект,
		ДанныеЗаполнения,
		ТекстЗаполнения,
		СтандартнаяОбработка
	);
	Если НЕ ПродолжитьВыполнение Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ОбработкаСобытийДокументаСервер.ПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ОбработкаСобытийДокументаСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ОбработкаСобытийДокументаСервер.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	// Сформируем короткое описание для отчетов
	ДлинаКраткогоПредставления = 300;
	КраткоеОписание = Описание;
	
	Если СтрДлина(Описание) > ДлинаКраткогоПредставления Тогда
		
		КраткоеОписание = СтрШаблон("%1...", Лев(Описание, ДлинаКраткогоПредставления));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если НЕ ОбработкаСобытийДокументаСервер.ПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ ОбработкаСобытийДокументаСервер.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если НЕ ОбработкаСобытийДокументаСервер.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ИсторияПродаж Тогда
		
		СформироватьИсториюПродаж();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьИсториюПродаж()
	
	Если Продажи.Количество() = 0 Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ВидНоменклатурыУслуга = Перечисления.ВидыНоменклатуры.Услуга;
	ТипСкладыКомпании = Тип("СправочникСсылка.СкладыКомпании");
	
	ВидыНоменклатурыЗаписей = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Продажи.ВыгрузитьКолонку("Номенклатура"), "ВидНоменклатуры");
	
	Для Каждого ТекущаяСтрока Из Продажи Цикл
		
		НоваяЗапись = Движения.Продажи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСтрока);
		
		Если НЕ ЗначениеЗаполнено(НоваяЗапись.Период) Тогда
			НоваяЗапись.Период = Дата;
		КонецЕсли;
		
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ПодразделениеКомпании = ПодразделениеКомпании;

		Если ВидыНоменклатурыЗаписей[ТекущаяСтрока.Номенклатура] = ВидНоменклатурыУслуга Тогда
			
			Если ЗначениеЗаполнено(ТекущаяСтрока.СкладКомпании)
				И ТипЗнч(ТекущаяСтрока.СкладКомпании) = ТипСкладыКомпании Тогда
				
				НоваяЗапись.СкладКомпании = Неопределено;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
