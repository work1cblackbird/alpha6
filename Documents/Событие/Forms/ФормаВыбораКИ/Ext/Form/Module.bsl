
#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("Контрагент") Тогда
		
		СтруктураНайденнойКИ = Новый СписокЗначений;
		
		СписокКонтактов = Новый Массив;
		СписокКонтактов.Добавить(Параметры.Контрагент);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ТипСобытия = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ТипСобытия");
		
		Если ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.Звонок") Тогда
			УправлениеКонтактнойИнформацией.СоздатьВТКонтактнаяИнформация(Запрос.МенеджерВременныхТаблиц, СписокКонтактов, 
					ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон"),, ТекущаяДатаСеанса());
		ИначеЕсли ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ЭлектронноеОбращение") Тогда
			УправлениеКонтактнойИнформацией.СоздатьВТКонтактнаяИнформация(Запрос.МенеджерВременныхТаблиц, СписокКонтактов, 
					ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты"),, ТекущаяДатаСеанса());
		Иначе
			УправлениеКонтактнойИнформацией.СоздатьВТКонтактнаяИнформация(Запрос.МенеджерВременныхТаблиц, СписокКонтактов, 
					ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"),, ТекущаяДатаСеанса());
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактнаяИнформация.Представление КАК КИ,
		|	КонтактнаяИнформация.Объект КАК Контрагент
		|ИЗ
		|	ВТКонтактнаяИнформация КАК КонтактнаяИнформация
		|ИТОГИ ПО
		|	Контрагент ИЕРАРХИЯ";
		
		ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией), "СписокКИ");
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события возникающего на клиенте при выполнении команды "Выбрать".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура ВыбратьКИ(Команда)
	
	Если НЕ Элементы.СписокКИ.ТекущиеДанные = Неопределено Тогда
		Закрыть(Элементы.СписокКИ.ТекущиеДанные.КИ);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // ВыбратьКИ()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настройка условного оформления формы
//
&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Вызываем общий обработчик действия
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформление(ЭтотОбъект);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СписокКИКонтактнаяИнформация");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СписокКИ.КИ");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст",  Новый ПолеКомпоновкиДанных("СписокКИ.Контрагент"));
   	
КонецПроцедуры // УстановитьУсловноеОформление()

#КонецОбласти