///////////////////////////////////////////////////////////////////////////////
// Модуль основной формы документа "Поступление товаров"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	
	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	НалоговыйДокумент = ЗащищенныеФункцииСервер.УстановитьЗаголовокНадписиНалоговыйДокумент(ЭтотОбъект, Объект);
	
	// ПодключаемоеОборудование
	ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	ТипыОборудования.СканерШтрихкода = Истина;
	ТипыОборудования.СчитывательМагнитныхКарт = Истина;
	МенеджерОборудования.ПриСозданииНаСервере(ЭтаФорма, ТипыОборудования);
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ИмяЭлементаКоманднойПанели", "ГлобальныеКоманды");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыЭДОПриСоздании = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаДокумента();
	ПараметрыЭДОПриСоздании.Форма = ЭтотОбъект;
	ПараметрыЭДОПриСоздании.ДокументССылка = Объект.Ссылка;
	ПараметрыЭДОПриСоздании.КонтроллерСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыЭДОПриСоздании.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	ПараметрыЭДОПриСоздании.МестоРазмещенияКоманд = Элементы.ПодменюЭДО;
	
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаДокумента(ПараметрыЭДОПриСоздании);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// УтверждениеДокументов
	УтверждениеДокументовСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	// Конец УтверждениеДокументов
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументовАльфаАвто.ПриСозданииНаСервере_ФормаДокумента(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
	РаботаСФормой.ИнициализироватьМенюВыбораХозОперации(ЭтотОбъект);
	РаботаСФормой.РасставитьСвязиПараметровВыбораПоОрганизации(ЭтотОбъект, Объект);
	РаботаСФормой.ОграничитьВыборКонтактныхЛиц(Элементы.Контрагент);
	РаботаСФормой.НастроитьОтображениеСИспользованиемБазовогоКоличества(Элементы.ТоварыКоличество);
	РаботаСФормой.ЗаблокироватьРедактированиеНомераИДатыДокумента(ЭтотОбъект, Объект);
	
	РазрешитьРедактированиеЦенИСумм = ПраваИНастройкиПользователя.Значение("РедактированиеЦенИСуммВНоменклатурныхТаблицах", Объект);
	РаботаСФормой.РазрешитьРедактированиеЦенИСумм(
		РаботаСФормой.ТиповыеПоляСуммовыхРеквизитов(ЭтотОбъект),
		РазрешитьРедактированиеЦенИСумм
	);
	
	СпособВыбораСкидки = ПраваИНастройкиПользователя.Значение("СпособВыбораСкидки", Объект);
	РаботаСФормой.РазрешитьРедактированиеСкидок(
		РаботаСФормой.ТиповыеПоляСкидочныхРеквизитов(ЭтотОбъект),
		РазрешитьРедактированиеЦенИСумм,
		СпособВыбораСкидки
	);
	
	ОписаниеНовойКолонкиПроизводитель = РаботаСФормой.ОписаниеНовойКолонкиПроизводитель();
	ОписаниеНовойКолонкиПроизводитель.ТабличнаяЧасть = Элементы.Товары;
	ОписаниеНовойКолонкиПроизводитель.ПоставитьПеред = Элементы.ТоварыАртикул;
	НоваяКолонкаПроизводитель = РаботаСФормой.НоваяКолонкаПроизводитель(ЭтотОбъект, ОписаниеНовойКолонкиПроизводитель);
	
	КолонкиКодАртикулИПроизводитель = Новый Структура();
	КолонкиКодАртикулИПроизводитель.Вставить("Код", Элементы.ТоварыКод);
	КолонкиКодАртикулИПроизводитель.Вставить("Артикул", Элементы.ТоварыАртикул);
	КолонкиКодАртикулИПроизводитель.Вставить("Производитель", НоваяКолонкаПроизводитель);
	РаботаСФормой.НастроитьВидимостьКолонокКодАртикулИПроизводитель(КолонкиКодАртикулИПроизводитель);
	РаботаСФормой.ОткрытьФормуТолькоДляПросмотра(ЭтотОбъект, Объект);
	
	ОтобразитьРазницу();
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормы.ПриСозданииНаСервере_ФормаДокумента(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец АльфаАвто.СобытияОбъектов
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		РаботаСФормой.ЗаполнитьСлужебныеРеквизитыТоваров(Объект.Товары);
		РаботаСФормой.УстановитьВидимостьКолонкиХарактеристика(ЭтотОбъект, Объект);
		РаботаСФормой.УстановитьВидимостьКолонкиСодержаниеУслуги(ЭтотОбъект, Объект);
		
		// Маркировка
		МаркировкаТоваровСервер.ПриСозданииЧтенииНаСервере(ЭтотОбъект, Объект);
		// Конец Маркировка
		
		// ПрослеживаемыеТовары
		УчетПрослеживаемыхТоваровСервер.ЗаполнитьСлужебныйРеквизитПрослеживаемогоТовара(Объект);
		// Конец ПрослеживаемыеТовары
		
		РасчетыСКонтрагентамиСервер.ЗаполнитьСлужебныйРеквизитОстатокПоДокументуОплаты(Объект);
		
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
		
	КонецЕсли;
	
	ПараметрыДокумента = ОбщиеПараметрыДокументов.СформироватьПредставлениеПараметровДокумента(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// УтверждениеДокументов
	УтверждениеДокументовКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец УтверждениеДокументов
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормыКлиент.ПриОткрытии_ФормаДокумента(ЭтотОбъект, Отказ);
	// Конец АльфаАвто.СобытияОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ПараметрыДействия = Новый Структура;
	ОбработкаВыбораНаСервере(ВыбранноеЗначение, ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение, ПараметрыДействия = Неопределено)
	
	Если НЕ УправлениеДиалогомДокументаСервер.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ПараметрыДействия) Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормыКлиент.ОбработкаОповещения_ФормаДокумента(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец АльфаАвто.СобытияОбъектов
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		Возврат;
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.КонтроллерСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// Штрихкодирование
	ШтрихкодированиеКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец Штрихкодирование
	
	// УтверждениеДокументов
	УтверждениеДокументовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец УтверждениеДокументов
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументовКлиент.ОбработчикОповещенияФормаДокумента(ИмяСобытия,ЭтотОбъект);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
	РаботаСФормойКлиент.ТребуетсяОбновитьЗаголовокНадписиНалоговыйДокумент(ЭтотОбъект, ИмяСобытия);
	
	РаботаСФормойКлиент.ТребуетсяОбновитьЗаголовокНадписиТаможеннаяДекларация(ЭтотОбъект, ИмяСобытия);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыЭДОПриСоздании = ОбменСКонтрагентами.ПараметрыПриЧтенииНаСервере_ФормаДокумента();
	ПараметрыЭДОПриСоздании.Форма = ЭтотОбъект;
	ПараметрыЭДОПриСоздании.ДокументСсылка = Объект.Ссылка;
	ПараметрыЭДОПриСоздании.МестоРазмещенияКоманд = Элементы.ПодменюЭДО;
	ПараметрыЭДОПриСоздании.КонтроллерСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыЭДОПриСоздании.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	ОбменСКонтрагентами.ПриЧтенииНаСервере_ФормаДокумента(ПараметрыЭДОПриСоздании);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	РаботаСФормой.ЗаполнитьСлужебныеРеквизитыТоваров(Объект.Товары);
	РаботаСФормой.УстановитьВидимостьКолонкиХарактеристика(ЭтотОбъект, Объект);
	РаботаСФормой.УстановитьВидимостьКолонкиСодержаниеУслуги(ЭтотОбъект, Объект);
	
	// Маркировка
	МаркировкаТоваровСервер.ПриСозданииЧтенииНаСервере(ЭтотОбъект, Объект);
	// Конец Маркировка
	
	// ПрослеживаемыеТовары
	УчетПрослеживаемыхТоваровСервер.ЗаполнитьСлужебныйРеквизитПрослеживаемогоТовара(Объект);
	// Конец ПрослеживаемыеТовары
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормы.ПриЧтенииНаСервере_ФормаДокумента(ЭтотОбъект, ТекущийОбъект);
	// Конец АльфаАвто.СобытияОбъектов
	
	РасчетыСКонтрагентамиСервер.ЗаполнитьСлужебныйРеквизитОстатокПоДокументуОплаты(Объект);
	
	НалоговыйДокумент = ЗащищенныеФункцииСервер.УстановитьЗаголовокНадписиНалоговыйДокумент(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РаботаСФормойКлиент.ЗамерВремениЗапись("ПоступлениеТоваров", ПараметрыЗаписи.РежимЗаписи,
		Объект.Товары.Количество() > 50);
		
	Отказ = Отказ Или РаботаСФормойКлиент.НачатьПроверкуПодразделенияДокументаИПользователя(
		Объект.ПодразделениеКомпании,
		Новый ОписаниеОповещения(
			"ПроверкаПодразделенияДокументаИПользователяЗавершение",
			ЭтотОбъект,
			Новый Структура("ПараметрыЗаписи", ПараметрыЗаписи)
		)
	);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормы.ПередЗаписьюНаСервере_ФормаДокумента(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	// Конец АльфаАвто.СобытияОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	Если НЕ ЭтотОбъект.Элементы.Найти("НадписьНалоговыйДокумент") = Неопределено Тогда
		ЭтотОбъект.НалоговыйДокумент = ЗащищенныеФункцииСервер.УстановитьЗаголовокНадписиНалоговыйДокумент(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.КонтроллерСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;

	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	РаботаСФормой.ЗаполнитьСлужебныеРеквизитыТоваров(Объект.Товары);
	РаботаСФормой.УстановитьВидимостьКолонкиХарактеристика(ЭтотОбъект, Объект);
	РаботаСФормой.УстановитьВидимостьКолонкиСодержаниеУслуги(ЭтотОбъект, Объект);
	
	// Маркировка
	МаркировкаТоваровСервер.ЗаполнитьСлужебныйРеквизитКодыМаркировки(Объект);
	// Конец Маркировка
	
	// ПрослеживаемыеТовары
	УчетПрослеживаемыхТоваровСервер.ЗаполнитьСлужебныйРеквизитПрослеживаемогоТовара(Объект);
	// Конец ПрослеживаемыеТовары
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
	// АльфаАвто.СобытияОбъектов
	СобытияОбъектовФормы.ПослеЗаписиНаСервере_ФормаДокумента(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец АльфаАвто.СобытияОбъектов
	
	РасчетыСКонтрагентамиСервер.ЗаполнитьСлужебныйРеквизитОстатокПоДокументуОплаты(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтаФорма, ПараметрыЗаписи);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	РаботаСФормойКлиент.ОповеститьОЗаписиДокумента(Объект.Ссылка);
	
	ВидыСчетов = Новый Массив;
	ВидыСчетов.Добавить("СчетФактураПолученный");
	ВидыСчетов.Добавить("СчетФактураВыданный");
	ВидыСчетов.Добавить("СчетОтПоставщика");
	РаботаСФормойКлиент.ОбновитьПодчиненныеСчета(Объект.Ссылка, ВидыСчетов);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Вставить("ПоказыватьПараметрыДокумента", Элементы.ПараметрыДокумента.Видимость);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки.Получить("ПоказыватьПараметрыДокумента") = Ложь Тогда
		Элементы.ПараметрыДокумента.Видимость = Ложь;
		Элементы.НомерДата         .Видимость = Ложь;
	КонецЕсли;
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

//@skip-warning
&НаКлиенте
Процедура ХозОперацияПриИзменении(Команда)
	
	УправлениеДиалогомДокументаКлиент.ОбработатьВыборХозОперации(Объект, Элементы, Команда.Имя);
	ПараметрыДействия = Новый Структура;
	ХозОперацияПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ХозОперацияПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ПоступлениеТоваров.ХозОперацияПриИзменении(Объект, ПараметрыДействия);
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ДатаПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ПоступлениеТоваров.ДатаПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СкладКомпанииПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	СкладКомпанииПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура СкладКомпанииПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ПоступлениеТоваров.СкладКомпанииПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	КонтрагентПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ПоступлениеТоваров.КонтрагентПриИзменении(Объект, ПараметрыДействия);
	ЗаметкиПользователяАльфаАвто.ПроверитьЗаметкиПоПредмету(Объект.Контрагент, ПараметрыДействия);
	ПриИзмененииКлючевыхРеквизитовСостояниеЭДО();
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаиморасчетовПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ДоговорВзаиморасчетовПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ДоговорВзаиморасчетовПриИзмененииНаСервере(ПараметрыДействия=Неопределено)
	
	Документы.ПоступлениеТоваров.ДоговорВзаиморасчетовПриИзменении(Объект, ПараметрыДействия);
	ПриИзмененииКлючевыхРеквизитовСостояниеЭДО();
	ЗаметкиПользователяАльфаАвто.ПроверитьЗаметкиПоПредмету(Объект.ДоговорВзаиморасчетов, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеЭДОНажатие(Элемент)
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоУниверсальныйДокументПриИзменении(Элемент)
	УправлениеДиалогомНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СпособЗачетаАвансовПриИзменении(Элемент)
	
	СпособЗачетаАвансовПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СпособЗачетаАвансовПриИзмененииНаСервере()
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если Элемент.ТекущийЭлемент.Имя = "ТоварыСпособРаспределенияЗаказов" И ТекущиеДанные <> Неопределено Тогда
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
			
			Возврат;
			
		КонецЕсли;
		
		КонтекстРаспределения = Новый Структура();
		КонтекстРаспределения.Вставить("Проведен",                   Не Объект.Ссылка.Пустая());
		КонтекстРаспределения.Вставить("ДатаДокумента",              Объект.Дата);
		КонтекстРаспределения.Вставить("Контрагент",                 Объект.Контрагент);
		КонтекстРаспределения.Вставить("СпособРаспределенияЗаказов", ТекущиеДанные.СпособРаспределенияЗаказов);
		КонтекстРаспределения.Вставить("Номенклатура",               ТекущиеДанные.Номенклатура);
		КонтекстРаспределения.Вставить("ХарактеристикаНоменклатуры", ТекущиеДанные.ХарактеристикаНоменклатуры);
		КонтекстРаспределения.Вставить("ПодразделениеКомпании",      Объект.ПодразделениеКомпании);
		КонтекстРаспределения.Вставить("Организация",                Объект.Организация);
		КонтекстРаспределения.Вставить("РаспределениеТовара",        Объект.РаспределениеТовара);
		КонтекстРаспределения.Вставить("Товары",                     Объект.Товары);
		КонтекстРаспределения.Вставить("ДокументОснование",          Объект.ДокументОснование);
		
		ОткрытьФорму(
			"Документ.ПоступлениеТоваров.Форма.РаспределениеТовара",
			КонтекстРаспределения,
			ЭтотОбъект,
			,
			,
			,
			Новый ОписаниеОповещения("РаспределениеТовараОповещение", ЭтотОбъект),
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
		);
		
	КонецЕсли;
	
	// Маркировка
	МаркировкаТоваровКлиент.ОткрытьСписокКодовМаркировки(
		ЭтотОбъект,
		ВыбраннаяСтрока,
		Поле,
		СтандартнаяОбработка
	);
	// Конец Маркировка
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторТовара) Или Копирование Тогда
		
		ТекущиеДанные.ИдентификаторТовара = Новый УникальныйИдентификатор;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.СпособРаспределенияЗаказов) Тогда
		
		ТекущиеДанные.СпособРаспределенияЗаказов = СпособРаспределенияЗаказовПоУмолчанию(Объект.ПодразделениеКомпании);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УправлениеДиалогомДокументаКлиент.ТоварыПриОкончанииРедактирования(
		ЭтотОбъект,
		Элемент,
		НоваяСтрока,
		ОтменаРедактирования
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	ТоварыПослеУдаленияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПослеУдаленияНаСервере(ПараметрыДействия = Неопределено)
	
	УправлениеДиалогомДокументаСервер.ТоварыПослеУдаления(ЭтотОбъект, Элементы.Товары, ПараметрыДействия);
	
КонецПроцедуры

#Область ОбработчикиСобытийПолейТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ТоварыНоменклатураПриИзмененииНаСервере();
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		// Маркировка
		МаркировкаТоваровКлиент.НачатьСканированиеМаркировки(
			Объект,
			ТекущиеДанные,
			ЭтотОбъект,
			НЕ ТекущиеДанные.МаркируемыйТовар
		);
		// Конец Маркировка
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Ячейка) Тогда
			
			ЯчейкаПриИзменении();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТоварыНоменклатураПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыНоменклатураПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	УправлениеДиалогомДокументаСервер.НоменклатураПриИзменении(ЭтотОбъект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущаяСтрока", ТекущиеДанные.Номенклатура);
	
	УправлениеДиалогомДокументаКлиент.ТоварыНоменклатураНачалоВыбора(
		ЭтотОбъект,
		Элемент,
		ДанныеВыбора,
		ВыборДобавлением,
		СтандартнаяОбработка,
		ДополнительныеПараметры,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТоварыХарактеристикаНоменклатурыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыХарактеристикаНоменклатурыПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыХарактеристикаНоменклатурыПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	
	ТоварыЕдиницаИзмеренияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыЕдиницаИзмеренияПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыЕдиницаИзмеренияПриИзменении(Объект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТоварыКоличествоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыКоличествоПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыКоличествоПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоБазовоеПриИзменении(Элемент)
	
	ТоварыКоличествоБазовоеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыКоличествоБазовоеПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыКоличествоБазовоеПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТоварыЦенаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыЦенаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыЦенаПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	ТоварыСуммаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыСуммаПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаВсегоПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыСуммаВсегоПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаВсегоПриИзменении(Элемент)
	
	ТоварыСуммаВсегоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТоварыСтавкаНДСПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСтавкаНДСПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыСтавкаНДСПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	ТоварыСуммаНДСПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаНДСПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыСуммаНДСПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПроцентНаценкиПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыПроцентНаценкиПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентНаценкиПриИзменении(Элемент)
	
	ТоварыПроцентНаценкиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаРозничнаяПриИзменении(Элемент)
	
	ТоварыЦенаРозничнаяПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыЦенаРозничнаяПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыЦенаРозничнаяПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаРозничнаяПриИзменении(Элемент)
	
	ТоварыСуммаРозничнаяПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаРозничнаяПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыСуммаРозничнаяПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЯчейкаПриИзменении(Элемент)
	
	ЯчейкаПриИзменении();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыЯчейкаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ТоварыЯчейкаПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийПолейТаблицыФормыЗачетАвансов

&НаСервере
Процедура ЗачетАвансовДокументАвансаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.ЗачетАвансов.НайтиПоИдентификатору(Элементы.ЗачетАвансов.ТекущаяСтрока);
	Документы.ПоступлениеТоваров.ЗачетАвансовДокументАвансаПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗачетАвансовДокументАвансаПриИзменении(Элемент)
	
	ЗачетАвансовДокументАвансаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
//@skip-warning
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда,
                                                НавигационнаяСсылка = Неопределено,
                                                СтандартнаяОбработка = Неопределено)
	
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
    
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПодборНоменклатуры(Команда)
	
	ПараметрыДействия = Новый Структура();
	ПараметрыДействия.Вставить("НеУстанавливатьОтборНаОстаток", Истина);
	УправлениеДиалогомКлиент.ОткрытьПодборНоменклатуры(ЭтотОбъект, Истина,,,, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборПоЗаказам(Команда)
	
	Отборы = Новый Соответствие;
	Отборы.Вставить("Контрагент", Объект.Контрагент);
	Отборы.Вставить("Заказ.ПодразделениеКомпании", Объект.ПодразделениеКомпании);
	Отборы.Вставить("ЗаказСервиса", Ложь);	
	УправлениеДиалогомКлиент.ОткрытьПодборПоЗаказам(ЭтотОбъект, "СКД_ЗаказыПоставщикам", Отборы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРасхождение(Команда)
	
	Элементы.ТоварыПоказатьРасхождение.Пометка = Не Элементы.ТоварыПоказатьРасхождение.Пометка;
	Элементы.ТоварыКоличествоПоНакладной.Видимость = Элементы.ТоварыПоказатьРасхождение.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСопоставленияКодовМаркировкиНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Товары", Объект.Товары);
	ПараметрыФормы.Вставить("КодыМаркировки", Объект.КодыМаркировки);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("Документ", Объект.Ссылка);
	
	ОбработкаОповещения = Новый ОписаниеОповещения("Подключаемый_РезультатСопоставленияКодовМаркировки", ЭтотОбъект);
	
	ОткрытьФорму(
		"Обработка.СопоставлениеКодовМаркировки.Форма",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОбработкаОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПоНакладной(Команда)
	
	Для Каждого ТекСтрока Из Объект.Товары Цикл
		
		ТекСтрока.КоличествоПоНакладной = ТекСтрока.Количество;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНаценкуДляВсейТаблицы(Команда)

	ОписаниеОповещение = Новый ОписаниеОповещения("ПоказатьОкноВводаПроцентаНаценки", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещение, НСтр("ru='Табличная часть ""Товары""'") + " " 
			+ НСтр("ru = 'будет изменена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОкноВводаПроцентаНаценки(Ответ, Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда	
		ОписаниеОповещение = Новый ОписаниеОповещения("ПослеВводаПроцентаНаценки", ЭтотОбъект);
		ПоказатьВводЧисла(ОписаниеОповещение, 0, НСтр("ru = 'Введите процент наценки'"), 6, 2);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаПроцентаНаценки(ПроцентНаценки, Параметры) Экспорт

	УстановитьНаценкуДляВсейТаблицыНаСервере(ПроцентНаценки);

КонецПроцедуры

&НаСервере
Процедура УстановитьНаценкуДляВсейТаблицыНаСервере(ПроцентНаценки, ПараметрыДействия = Неопределено)
	
	Для Каждого ТекСтрока Из Объект.Товары Цикл
	
		ТекСтрока.ПроцентНаценки = ПроцентНаценки;
		Документы.ПоступлениеТоваров.ТоварыПроцентНаценкиПриИзменении(Объект, ТекСтрока, ПараметрыДействия);
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборАвансов(Команда)
	
	УправлениеДиалогомКлиент.ОткрытьПодборАвансов(ЭтотОбъект, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СпособРаспределенияЗаказовПоУмолчанию(ПодразделениеКомпании)
	
	Возврат Перечисления.СпособыРаспределенияЗаказов.СпособРаспределенияЗаказовПоУмолчанию(ПодразделениеКомпании);
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтотОбъект);
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформлениеСодержаниеУслуги(ЭтотОбъект);
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформлениеЗапретРедактированияЯчейкиУслуги(ЭтотОбъект);
	
	// Маркировка
	МаркировкаТоваровСервер.УстановитьУсловноеОформлениеКодыМаркировок(ЭтотОбъект);
	// Конец Маркировка
	
	УстановитьУсловноеОформлениеРаспределение();
	УстановитьУсловноеОформлениеКоличествоПоНакладной();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеРаспределение()
	
	ВариантыРаспределения = Новый Соответствие();
	ВариантыРаспределения.Вставить(0, НСтр("ru = '<Автоматическое>'"));	
	ВариантыРаспределения.Вставить(1, НСтр("ru = '<Выборочное и автоматическое>'"));	
	ВариантыРаспределения.Вставить(2, НСтр("ru = '<Выборочное>'"));	
	
	Для Каждого ВариантРаспределения Из ВариантыРаспределения Цикл
		
		ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементУсловногоОформления
			.Поля
			.Элементы
			.Добавить()
			.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСпособРаспределенияЗаказов.Имя);
		
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СпособРаспределенияЗаказов");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = ВариантРаспределения.Ключ;
		
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт",
			ШрифтыСтиля.РаспределениеТоваровТекст);
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", ВариантРаспределения.Значение);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеКоличествоПоНакладной()
	
	// Товары сумма "Только просмотр", если не заполнено количество по накладной
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСумма.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КоличествоПоНакладной");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// Товары сумма всего "Только просмотр", если не заполнено количество по накладной
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСуммаВсего.Имя);
	
	ГруппаОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КоличествоПоНакладной");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТоварыСуммаВсегоТолькоПросмотр");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРазницу()
	
	// В том случае, если хотя бы в одной строке есть разница по накладной, отразим колонку.
	ЕстьРазница = Ложь;
	
	Для Каждого ТекСтрока Из Объект.Товары Цикл
		
		Если
			ТекСтрока.КоличествоПоНакладной <> ТекСтрока.Количество 
			Или ТекСтрока.Количество * ТекСтрока.Коэффициент <> ТекСтрока.КоличествоБазовое
		Тогда
			
			ЕстьРазница = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;

	Элементы.ТоварыПоказатьРасхождение.Пометка = ЕстьРазница;
	Элементы.ТоварыКоличествоПоНакладной.Видимость = ЕстьРазница;
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ПроверкаПодразделенияДокументаИПользователяЗавершение(Контекст) Экспорт
	
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЗаполнениеОбъектов
&НаКлиенте
Процедура ПослеОбработкиЗаполнения(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ПослеОбработкиЗаполненияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеОбработкиЗаполненияНаСервере()
	
	ЗаполнениеОбъектовАльфаАвто.ПослеОбработкиЗаполнения(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ЗаполнениеОбъектов

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_ДекорацияСостояниеОригиналаНажатие()
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументовКлиент.ОткрытьМенюВыбораСостояния(ЭтотОбъект, Элементы.ДекорацияСостояниеОригинала);
	//Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКлючевыхРеквизитовСостояниеЭДО()
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	ПараметрыПриИзменении = ОбменСКонтрагентами.ПараметрыПриИзмененииКлючевыхРеквизитовЭДО();
	
	ПараметрыПриИзменении.Форма                  = ЭтотОбъект;
	ПараметрыПриИзменении.ДокументСсылка         = Объект.Ссылка;
	ПараметрыПриИзменении.ДокументОбъект         = РеквизитФормыВЗначение("Объект");
	ПараметрыПриИзменении.КонтроллерСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыПриИзменении.ГруппаСостояниеЭДО     = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПриИзмененииКлючевыхРеквизитовЭДО(ПараметрыПриИзменении);
	
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

#КонецОбласти

#Область ОбработчикиАльфаАвто

// Ядро
&НаКлиенте
Процедура ПараметрыДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;;
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыДокументаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	
	УправлениеДиалогомДокументаКлиент.РасширенноеРедактированиеПоляКомментарий(ЭтотОбъект, Элемент,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСуммаДокументаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеДиалогомДокументаКлиент.ПоказатьРасширенныеИтогиОперации(ЭтотОбъект, Элемент);
	
КонецПроцедуры
// Конец Ядро

// СчетаФактуры
&НаКлиенте
Процедура НадписьНалоговыйДокументНажатие(Элемент)
	
	УправлениеДиалогомДокументаКлиент.НадписьНалоговыйДокументНажатие(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьВзаиморасчетыНажатие(Элемент)
	
	УправлениеДиалогомДокументаКлиент.НадписьВзаиморасчетыНажатие(ЭтотОбъект);
	
КонецПроцедуры
// Конец СчетаФактуры

&НаКлиенте
Процедура НадписьТаможеннаяДекларацияНажатие(Элемент)
	
	УправлениеДиалогомДокументаКлиент.НадписьТаможеннаяДекларацияНажатие(ЭтотОбъект);
	
КонецПроцедуры // НадписьТаможеннаяДекларацияНажатие()

#КонецОбласти

&НаКлиенте
Процедура РаспределениеТовараОповещение(РезультатОповещения, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(РезультатОповещения) = Тип("Структура") Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		
		ОтборСтрок = Новый Структура(
			"Номенклатура, ХарактеристикаНоменклатуры",
			ТекущиеДанные.Номенклатура,
			ТекущиеДанные.ХарактеристикаНоменклатуры
		);
		НайденныеСтроки = Объект.РаспределениеТовара.НайтиСтроки(ОтборСтрок);
		
		ТекущиеДанные.СпособРаспределенияЗаказов = РезультатОповещения.СпособРаспределенияЗаказов;
		
		Если ТекущиеДанные.СпособРаспределенияЗаказов <> ПредопределенноеЗначение("Перечисление.СпособыРаспределенияЗаказов.Автоматическое") Тогда
			
			Для Каждого ТекСтрока Из НайденныеСтроки Цикл
				
				Объект.РаспределениеТовара.Удалить(ТекСтрока);
				
			КонецЦикла;
			
			Для Каждого ТекСтрока Из РезультатОповещения.РаспределениеТовара Цикл
				
				НоваяСтрока = Объект.РаспределениеТовара.Добавить();
				НоваяСтрока.Номенклатура               = ТекущиеДанные.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = ТекущиеДанные.ХарактеристикаНоменклатуры;
				НоваяСтрока.ЗаказПоставщика            = ТекСтрока.ЗаказПоставщика;
				НоваяСтрока.ЗаказПокупателя            = ТекСтрока.ЗаказПокупателя;
				НоваяСтрока.Количество                 = ТекСтрока.Количество;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОчисткиЯчейкиХраненияОповещение(РезультатОповещения, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатОповещения = КодВозвратаДиалога.Да Тогда
		
		ВопросОчисткиЯчейкиХраненияЗавершение(ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВопросОчисткиЯчейкиХраненияЗавершение(ДополнительныеПараметры)
	
	Набор = РегистрыСведений.ЯчейкиХраненияПоУмолчанию.СоздатьНаборЗаписей();
	Набор.Отбор.СкладКомпании.Установить(Объект.СкладКомпании);
	Набор.Отбор.Номенклатура.Установить(ДополнительныеПараметры.НоменклатураВЯчейке);
	Набор.Записать();
	
КонецПроцедуры

// Маркировка
&НаКлиенте
Процедура Подключаемый_СканированиеМаркировкиЗавершение(КодМаркировки, ДополнительныеПараметры = Неопределено) Экспорт
	
	МаркировкаТоваровКлиент.ДобавитьКодМаркировки(Объект.КодыМаркировки, КодМаркировки, ДополнительныеПараметры);
	
	// Обновим отображение на форме
	Результат = Новый Структура;
	ОбработкаРезультатаОповещенияНаСервере(Результат, "РазрешенияДляПересчета");
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		СвязанныеРаботыКлиент.НачатьПодборСвязанныхРабот(Объект, ТекущиеДанные, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры
// Конец Маркировка

&НаСервере
Процедура СформироватьПредставлениеСоответствяМаркировок()
	
	Если НЕ Элементы.ПоложениеСопоставленияКодовМаркировки.Видимость Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СопоставлениеЗавершено = (Объект.КодыМаркировки.НайтиСтроки(Новый Структура("Подтверждено", Ложь)).Количество() = 0);
	
	Если СопоставлениеЗавершено Тогда
		Элементы.НадписьСопоставленияКодовМаркировки.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
		Элементы.НадписьСопоставленияКодовМаркировки.Заголовок = НСтр("ru = 'Сопоставление кодов маркировки'");
	Иначе
		Элементы.НадписьСопоставленияКодовМаркировки.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
		Элементы.НадписьСопоставленияКодовМаркировки.Заголовок = НСтр("ru = 'Необходимо сопоставить коды маркировки'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатСопоставленияКодовМаркировки(
	АдресРезультата,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРезультатСопоставления(АдресРезультата);
	
КонецПроцедуры // Подключаемый_РезультатСопоставленияКодовМаркировки()

&НаСервере
Процедура ЗаполнитьРезультатСопоставления(АдресРезультата)
	
	ДанныеСопоставления = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	МассивКодовМаркировки = Новый Массив;
	
	Для Каждого ТекущаяСтрока Из ДанныеСопоставления Цикл
		
		// Найдем код маркировки
		СтрокаМаркировки = Объект
			.КодыМаркировки
			.НайтиСтроки(Новый Структура("КодМаркировки", ТекущаяСтрока.КодМаркировки));
		
		Если СтрокаМаркировки.Количество() > 0 Тогда
			
			// Если это новая строка, то удалим ее
			Если Не ТекущаяСтрока.ВыборСтроки Тогда
				
				Если ТекущаяСтрока.ДобавленаСтрока Тогда
					
					Объект.КодыМаркировки.Удалить(СтрокаМаркировки[0]);
					Продолжить;
					
				КонецЕсли;
				
			КонецЕсли;
			
			МассивКодовМаркировки.Добавить(СтрокаМаркировки[0].КодМаркировки);
			
			СтрокаМаркировки[0].Подтверждено = ТекущаяСтрока.Подтверждено;
			
		Иначе
			
			// Новая Строка
			НоваяСтрока = Объект.КодыМаркировки.Добавить();
			НоваяСтрока.КодМаркировки = ТекущаяСтрока.КодМаркировки;
			НоваяСтрока.Подтверждено = ТекущаяСтрока.Подтверждено;
			МассивКодовМаркировки.Добавить(НоваяСтрока.КодМаркировки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Очистим от удаленных строк от сопоставления
	УдалитьСтрокиКМ = Новый Массив;
	
	Для Каждого ТекущаяСтрока Из Объект.КодыМаркировки Цикл
		
		Если МассивКодовМаркировки.Найти(ТекущаяСтрока.КодМаркировки) = Неопределено Тогда
			
			УдалитьСтрокиКМ.Добавить(ТекущаяСтрока);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ТекущаяСтрока Из УдалитьСтрокиКМ Цикл
		
		Объект.КодыМаркировки.Удалить(ТекущаяСтрока);
		
	КонецЦикла;
	
	Модифицированность = Истина;	
	СформироватьПредставлениеСоответствяМаркировок();
	
КонецПроцедуры

&НаКлиенте
Процедура ЯчейкаПриИзменении()
	
	ПараметрыДействия = Новый Структура;
	
	ТоварыЯчейкаПриИзмененииНаСервере(ПараметрыДействия);
	
	Если ПараметрыДействия.Свойство("ТекстПредупреждения") Тогда
		
		ПоказатьПредупреждение(, ПараметрыДействия.ТекстПредупреждения);
		
	КонецЕсли;
	
	Если ПараметрыДействия.Свойство("ТекстВопроса") Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВопросОчисткиЯчейкиХраненияОповещение", ЭтотОбъект, ПараметрыДействия);
		ПоказатьВопрос(Оповещение, ПараметрыДействия.ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

#Область СвязанныеРаботы

&НаСервере
Функция ПодборСвязанныхРаботЗавершениеНаСервере(СвязанныеПозиции)
	
	ПараметрыДействия = СвязанныеРаботы.ЗаполнитьСвязаннымиРаботами(СвязанныеПозиции, Объект, ЭтотОбъект);
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
	Возврат ПараметрыДействия;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПодборСвязанныхРаботЗавершение(СвязанныеРаботы, Постобработка = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(СвязанныеРаботы) Тогда
		
		ПараметрыДействия = ПодборСвязанныхРаботЗавершениеНаСервере(СвязанныеРаботы);
		
	КонецЕсли;
	
	СвязанныеРаботыКлиент.Постобработка(Постобработка);
	
	// Вызываем обработчик результата выполнения
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры // Подключаемый_ПодборСвязанныхРаботЗавершение()

#КонецОбласти

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	// Вызываем общий обработчик события настройки параметров выбора
	УправлениеДиалогомДокументаСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	
	// Запретим использование розничного склада при поступлении товаров на комиссию
	Если Элементы.Найти("СкладКомпании") <> Неопределено Тогда
		
		Если Объект.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
			УправлениеДиалогомСервер.ОбновитьПараметрВыбора(Элементы.СкладКомпании.ПараметрыВыбора, "Отбор.Розничный", ЛОЖЬ);
		Иначе
			УправлениеДиалогомСервер.УдалитьПараметрВыбора(Элементы.СкладКомпании.ПараметрыВыбора, "Отбор.Розничный");
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеДиалогомДокументаСервер.НастроитьПараметрыВыбораНоменклатурыДляКомиссионныхДокументов(ЭтотОбъект);
	УправлениеДиалогомДокументаСервер.РасставитьСвязиПараметровВыбораДокументовАванса(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомДокументаСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	Элементы.СкладКомпании.Доступность = Объект.ХозОперация <> Справочники.ХозОперации.УслугиСтороннихОрганизаций
			И Объект.ХозОперация <> Справочники.ХозОперации.УслугиПоСубподряду;
	Элементы.ТоварыЗаказНаряд.Видимость = Объект.ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду
		И ТипЗнч(Объект.ДокументОснование) <> Тип("ДокументСсылка.ЗаказНаряд");
	ЗащищенныеФункцииСервер.УстановитьВидимостьКолонкиЦенаРозничная(ЭтотОбъект, Объект.СкладКомпании);
	Элементы.ПоложениеСопоставленияКодовМаркировки.Видимость = Объект.КодыМаркировки.Количество() > 0
		И ПравоДоступа("Просмотр", Метаданные.Обработки.СопоставлениеКодовМаркировки);
	СформироватьПредставлениеСоответствяМаркировок();
	
	// Установим видимость колонок ГТД, ставки НДС и суммы НДС от хоз. операции
	ЭтоНеИмпорт = (НЕ Объект.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровИмпорт);
	Элементы.ТоварыСтавкаНДС.Видимость = ЭтоНеИмпорт;
	Элементы.ТоварыСуммаНДС.Видимость = ЭтоНеИмпорт;
	Элементы.ТоварыГТД.Видимость = ЭтоНеИмпорт;
	Элементы.НадписьТаможеннаяДекларация.Видимость = НЕ ЭтоНеИмпорт;
	УправлениеДиалогомДокументаСервер.УстановитьЗаголовокНадписиТаможеннаяДекларация(ЭтотОбъект, Объект);

	Элементы.ВхДокДата.АвтоОтметкаНезаполненного = Объект.ЭтоУниверсальныйДокумент;
	Элементы.ВхДокНомер.АвтоОтметкаНезаполненного = Объект.ЭтоУниверсальныйДокумент;
	
	РазрешитьРедактированиеЦенИСумм = ПравоПользователя("РедактированиеЦенИСуммВНоменклатурныхТаблицах", Объект);
	РазрешитьИзменятьРозничныеЦены  = ПравоПользователя("ИзменятьРозничныеЦеныВПоступленииТоваров", Объект);
	РедактированиеДоступно 			= Объект.СкладКомпании.Розничный И НЕ ((НЕ РазрешитьРедактированиеЦенИСумм) 
										 ИЛИ (НЕ РазрешитьИзменятьРозничныеЦены) 
										 ИЛИ Объект.СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается);
	Элементы.ТоварыУстановитьНаценкуДляВсейТаблицы.Видимость = РедактированиеДоступно;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры = Неопределено)
	
	Если НЕ УправлениеДиалогомДокументаСервер.ОбработкаРезультатаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоСпискуЗаказанного(РезультатОповещения, ДополнительныеПараметры);
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
	Если ТиПЗнч(ДополнительныеПараметры) = Тип("Строка") И ЛЕВ(ДополнительныеПараметры, 17)="КомандаЗаполнения" Тогда
		
		Для Каждого ТекСтрока Из Объект.Товары Цикл
			
			ТекСтрока.КоличествоПоНакладной = ТекСтрока.Количество;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаОповещения(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если НЕ УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаРезультатаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры);
	ОбработкаРезультатаВыполненияДействия(РезультатОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРезультатаВыполненияДействия(ПараметрыДействия)
	
	ОбработкаРеквизитовДокументаКлиент.ПолучитьРазрешенияДляПересчета(ЭтотОбъект, ПараметрыДействия);
	УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаВыполненияДействия(ЭтотОбъект, ПараметрыДействия);
	
КонецПроцедуры

#Область ПараметрыДокумента

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаЗакрытияПараметровДокумента(РезультатОповещения, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбработкаРезультатаЗакрытияПараметровДокументаНаСервере(РезультатОповещения, ДополнительныеПараметры);
	ОбработкаРеквизитовДокументаКлиент.ПолучитьРазрешенияДляПересчета(ЭтотОбъект, РезультатОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаЗакрытияПараметровДокументаНаСервере(РезультатОповещения, ДополнительныеПараметры)
	
	ОбщиеПараметрыДокументов.ПараметрыДокументаПриИзмененииРеквизитов(Объект, РезультатОповещения);
	ОбщиеПараметрыДокументов.ПараметрыДокументаПриИзменении(ЭтотОбъект, РезультатОповещения);
	
	Если
		ТипЗнч(РезультатОповещения) = Тип("Структура")
		И РезультатОповещения.Свойство("ИзмененныеРеквизиты")
		И ТипЗнч(РезультатОповещения.ИзмененныеРеквизиты) = Тип("Структура")
		И РезультатОповещения.ИзмененныеРеквизиты.Свойство("Организация")
	Тогда
		
		ПриИзмененииКлючевыхРеквизитовСостояниеЭДО();
		
	КонецЕсли;
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьРеквизитыПечатиНажатие(Элемент)

	УправлениеПечатьюКлиентАльфаАвто.ОткрытьФормуРедактированияРеквизитовДоставки(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область Штрихкодирование

&НаКлиенте
Процедура Подключаемый_ШтрихкодированиеОбработкаОповещения(РезультатОповещения,
	ДополнительныеПараметры = Неопределено) Экспорт

	Если РезультатОповещения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если РезультатОповещения.Свойство("Действие") Тогда
		ШтрихкодированиеОбработкаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ШтрихкодированиеОбработкаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры)

	ШтрихкодированиеВызовСервера.ОбработкаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры, , Объект);

КонецПроцедуры

#КонецОбласти

#Область УтверждениеДокументов

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуУтверждения(Команда)
	
	УтверждениеДокументовКлиент.ОбработкаКомандыФормы(ЭтотОбъект, Команда, Объект.Ссылка);
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьОбработкуКомандыУтвержденияНаСервере(ПараметрыОбработки,
		ДополнительныеПараметры) Экспорт
	
	ОбработкаКомандыУтвержденияНаСервере(ПараметрыОбработки, ДополнительныеПараметры);
	Оповестить("ПослеУтвержденияДокументов", Объект.Ссылка, ИмяФормы);
	ОповеститьОбИзменении(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаКомандыУтвержденияНаСервере(ПараметрыОбработки, ДополнительныеПараметры)
	
	УтверждениеДокументовВызовСервера.ОбработкаКомандыФормы(ЭтотОбъект, ПараметрыОбработки.ИмяКоманды, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыУтвержденияДокументов()
	
	ОбновитьКомандыУтвержденияДокументовНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКомандыУтвержденияДокументовНаСервере()
	
	УтверждениеДокументовКлиентСервер.УстановитьДоступностьКнопокУтверждения(ЭтотОбъект, Объект, ТолькоПросмотр);
	УтверждениеДокументовВызовСервера.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, Объект, ТолькоПросмотр);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ОбновитьЗаголовокНадписиТаможеннаяДекларация(РезультатОповещения,
		ДополнительныеПараметры) Экспорт
	
	ОбновитьЗаголовокНадписиТаможеннаяДекларацииСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокНадписиТаможеннаяДекларацииСервере()
	
	УправлениеДиалогомДокументаСервер.УстановитьЗаголовокНадписиТаможеннаяДекларация(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьЗаголовокНадписиНалоговыйДокумент(РезультатОповещения, ДополнительныеПараметры) Экспорт
	
	ОбновитьЗаголовокНадписиНалоговыйДокументНаСервере();
	
КонецПроцедуры

Процедура ОбновитьЗаголовокНадписиНалоговыйДокументНаСервере()
	
	РаботаСФормойВызовСервера.ОбновитьЗаголовокНадписиНалоговыйДокумент(ЭтотОбъект, Объект);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьПоСпискуЗаказанного(АдресРезультата, Событие)
	
	Если Событие <> "ПодборПоСпискуЗаказанного" ИЛИ НЕ ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПодобранныеТовары = ПолучитьИзВременногоХранилища(АдресРезультата);
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПодобранныеТовары.Номенклатура КАК Номенклатура,
	|	ПодобранныеТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПодобранныеТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПодобранныеТовары.Заказ КАК Заказ,
	|	ПодобранныеТовары.Количество * ПодобранныеТовары.Коэффициент КАК Количество
	|ПОМЕСТИТЬ ТоварыКДобавлениюСырые
	|ИЗ
	|	&ПодобранныеТовары КАК ПодобранныеТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКДобавлениюСырые.Номенклатура КАК Номенклатура,
	|	ТоварыКДобавлениюСырые.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТоварыКДобавлениюСырые.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТоварыКДобавлениюСырые.Заказ КАК Заказ,
	|	СУММА(ТоварыКДобавлениюСырые.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыКДобавлению
	|ИЗ
	|	ТоварыКДобавлениюСырые КАК ТоварыКДобавлениюСырые
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКДобавлениюСырые.ХарактеристикаНоменклатуры,
	|	ТоварыКДобавлениюСырые.ЕдиницаИзмерения,
	|	ТоварыКДобавлениюСырые.Заказ,
	|	ТоварыКДобавлениюСырые.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК Заказ,
	|	ЗаказыПоставщикамОстатки.ЗаказаноОстаток КАК Количество,
	|	ЗаказыПоставщикамОстатки.СуммаОстаток КАК Сумма,
	|	ЗаказыПоставщикамОстатки.СуммаУпрОстаток КАК СуммаУпр
	|ПОМЕСТИТЬ ДанныеЗаказов
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам.Остатки(
	|			&Момент,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						ТоварыКДобавлению.Номенклатура КАК Номенклатура
	|					ИЗ
	|						ТоварыКДобавлению КАК ТоварыКДобавлению)
	|				И ХарактеристикаНоменклатуры В
	|					(ВЫБРАТЬ
	|						ТоварыКДобавлению.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|					ИЗ
	|						ТоварыКДобавлению КАК ТоварыКДобавлению)
	|				И ЗаказПоставщику В
	|					(ВЫБРАТЬ
	|						ТоварыКДобавлению.Заказ КАК Заказ
	|					ИЗ
	|						ТоварыКДобавлению КАК ТоварыКДобавлению)) КАК ЗаказыПоставщикамОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКДобавлению.Номенклатура КАК Номенклатура,
	|	ТоварыКДобавлению.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТоварыКДобавлению.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ТоварыКДобавлению.Количество) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ДанныеЗаказов.Количество, 0) = 0
	|				ТОГДА 0
	|			КОГДА ЕСТЬNULL(ДанныеЗаказов.Количество, 0) = ТоварыКДобавлению.Количество
	|				ТОГДА ЕСТЬNULL(ДанныеЗаказов.Сумма, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДанныеЗаказов.Сумма, 0) / ЕСТЬNULL(ДанныеЗаказов.Количество, 0) * ТоварыКДобавлению.Количество
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ДанныеЗаказов.Количество, 0) = 0
	|				ТОГДА 0
	|			КОГДА ЕСТЬNULL(ДанныеЗаказов.Количество, 0) = ТоварыКДобавлению.Количество
	|				ТОГДА ЕСТЬNULL(ДанныеЗаказов.СуммаУпр, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДанныеЗаказов.СуммаУпр, 0) / ЕСТЬNULL(ДанныеЗаказов.Количество, 0) * ТоварыКДобавлению.Количество
	|		КОНЕЦ) КАК СуммаУпр
	|ИЗ
	|	ТоварыКДобавлению КАК ТоварыКДобавлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеЗаказов КАК ДанныеЗаказов
	|		ПО ТоварыКДобавлению.Номенклатура = ДанныеЗаказов.Номенклатура
	|			И ТоварыКДобавлению.ХарактеристикаНоменклатуры = ДанныеЗаказов.ХарактеристикаНоменклатуры
	|			И ТоварыКДобавлению.Заказ = ДанныеЗаказов.Заказ
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКДобавлению.ЕдиницаИзмерения,
	|	ТоварыКДобавлению.Номенклатура,
	|	ТоварыКДобавлению.ХарактеристикаНоменклатуры");
	Запрос.УстановитьПараметр("ПодобранныеТовары", ПодобранныеТовары);
	Запрос.УстановитьПараметр("Момент", ?(Объект.Ссылка.Пустая(), Неопределено, Новый Граница(Объект.Дата, ВидГраницы.Исключая)));
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаРег = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КурсРег = РаботаСКурсамиВалютПлатформа.ПолучитьКурсВалюты(ВалютаРег, Неопределено, Ложь);
	УсловиеПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения");
	ПараметрыДействия = Новый Структура;
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСумма = Выборка.Сумма;
		
		Если Объект.ВалютаДокумента = ВалютаУпр Тогда
			
			НоваяСумма = Выборка.СуммаУпр;
			
		ИначеЕсли Объект.ВалютаДокумента <> ВалютаРег Тогда
			
			НоваяСумма = РаботаСКурсамиВалютПлатформа.ПересчетПоВалюте(
			Выборка.Сумма, ВалютаРег, КурсРег, Объект.ВалютаДокумента, Объект.КурсДокумента, 2);
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(УсловиеПоиска, Выборка);
		НайденныеСтроки = Объект.Товары.НайтиСтроки(УсловиеПоиска);
		
		Если НайденныеСтроки.Количество() Тогда
			
			НайденныеСтроки[0].Количество = НайденныеСтроки[0].Количество + Выборка.Количество;
			Документы.ПоступлениеТоваров.ТоварыКоличествоПриИзменении(Объект, НайденныеСтроки[0], ПараметрыДействия);
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, "Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,Количество");
		Документы.ПоступлениеТоваров.ТоварыНоменклатураПриИзменении(Объект, НоваяСтрока, ПараметрыДействия);
		НоваяСтрока.СуммаВсего = НоваяСумма;
		Документы.ПоступлениеТоваров.ТоварыСуммаВсегоПриИзменении(Объект, НоваяСтрока, ПараметрыДействия);
		УправлениеДиалогомДокументаСервер.НоменклатураПриИзменении(ЭтотОбъект, НоваяСтрока, ПараметрыДействия);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
