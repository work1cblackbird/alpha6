
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ПодключаемоеОборудование
	ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	ТипыОборудования.СканерШтрихкода = Истина;
	ТипыОборудования.СчитывательМагнитныхКарт = Истина;
	МенеджерОборудования.ПриСозданииНаСервере(ЭтаФорма, ТипыОборудования);
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ИмяЭлементаКоманднойПанели", "ГлобальныеКоманды");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	ЗапретРедактированияРеквизитовОбъектовАльфаАвто.ЗаблокироватьФискальныеРеквизиты(ЭтотОбъект);
	
	РаботаСФормой.ИнициализироватьМенюВыбораХозОперации(ЭтотОбъект);
	РаботаСФормой.РасставитьСвязиПараметровВыбораПоОрганизации(ЭтотОбъект, Объект);
	РаботаСФормой.ОграничитьВыборКонтактныхЛиц(Элементы.Контрагент);
	РаботаСФормой.НастроитьОтображениеСИспользованиемБазовогоКоличества(Элементы.ТоварыКоличество);
	РаботаСФормой.ЗаблокироватьРедактированиеНомераИДатыДокумента(ЭтотОбъект, Объект);
	
	РазрешитьРедактированиеЦенИСумм = ПраваИНастройкиПользователя.Значение("РедактированиеЦенИСуммВНоменклатурныхТаблицах", Объект);
	РаботаСФормой.РазрешитьРедактированиеЦенИСумм(
		РаботаСФормой.ТиповыеПоляСуммовыхРеквизитов(ЭтотОбъект),
		РазрешитьРедактированиеЦенИСумм
	);
	
	СпособВыбораСкидки = ПраваИНастройкиПользователя.Значение("СпособВыбораСкидки", Объект);
	РаботаСФормой.РазрешитьРедактированиеСкидок(
		РаботаСФормой.ТиповыеПоляСкидочныхРеквизитов(ЭтотОбъект),
		РазрешитьРедактированиеЦенИСумм,
		СпособВыбораСкидки
	);
	
	ОписаниеНовойКолонкиПроизводитель = РаботаСФормой.ОписаниеНовойКолонкиПроизводитель();
	ОписаниеНовойКолонкиПроизводитель.ТабличнаяЧасть = Элементы.Товары;
	ОписаниеНовойКолонкиПроизводитель.ПоставитьПеред = Элементы.ТоварыАртикул;
	НоваяКолонкаПроизводитель = РаботаСФормой.НоваяКолонкаПроизводитель(ЭтотОбъект, ОписаниеНовойКолонкиПроизводитель);
	
	КолонкиКодАртикулИПроизводитель = Новый Структура();
	КолонкиКодАртикулИПроизводитель.Вставить("Код", Элементы.ТоварыКод);
	КолонкиКодАртикулИПроизводитель.Вставить("Артикул", Элементы.ТоварыАртикул);
	КолонкиКодАртикулИПроизводитель.Вставить("Производитель", НоваяКолонкаПроизводитель);
	РаботаСФормой.НастроитьВидимостьКолонокКодАртикулИПроизводитель(КолонкиКодАртикулИПроизводитель);
	РаботаСФормой.ОткрытьФормуТолькоДляПросмотра(ЭтотОбъект, Объект);
	
	ФормаСобственности = Объект.Контрагент.ФормаСобственности;
	
	ПроверкаСоответствияКодовМаркировкиПроводилась = Ложь;
	ОбновитьКонтактнуюИнформацию(НЕ ЗначениеЗаполнено(Объект.Ссылка));
	СпособВеденияВзаимозачетов = Объект.ДоговорВзаиморасчетов.СпособВеденияВзаиморасчетов;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		РаботаСФормой.ЗаполнитьСлужебныеРеквизитыТоваров(Объект.Товары);
		РаботаСФормой.УстановитьВидимостьКолонкиХарактеристика(ЭтотОбъект, Объект);
		ОбновитьИндикаторыСостоянияОплаты();
		СформироватьСписокВыбораАдресПокупателя();
		
		// Маркировка
		МаркировкаТоваровСервер.ПриСозданииЧтенииНаСервере(ЭтотОбъект, Объект);
		МаркировкаТоваровСервер.ИнициализироватьСлужебныеРеквизиты(ЭтотОбъект);
		// Конец Маркировка
		
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
	КонецЕсли;
	
	ПараметрыДокумента = ОбщиеПараметрыДокументов.СформироватьПредставлениеПараметровДокумента(Объект);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
	ОбновитьИндикаторыСостоянияОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение, ПараметрыДействия=Неопределено)
	
	Если НЕ УправлениеДиалогомДокументаСервер.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ПараметрыДействия) Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ПараметрыДействия = Новый Структура;
	ОбработкаВыбораНаСервере(ВыбранноеЗначение, ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// Штрихкодирование
	ШтрихкодированиеКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец Штрихкодирование
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОбновитьИндикаторыСостоянияОплаты();
	РаботаСФормой.ЗаполнитьСлужебныеРеквизитыТоваров(Объект.Товары);
	РаботаСФормой.УстановитьВидимостьКолонкиХарактеристика(ЭтотОбъект, Объект);
	
	// Маркировка
	МаркировкаТоваровСервер.ПриСозданииЧтенииНаСервере(ЭтотОбъект, Объект);
	МаркировкаТоваровСервер.ИнициализироватьСлужебныеРеквизиты(ЭтотОбъект);
	// Конец Маркировка
	
	ОбновитьРеквизитыРезультатаПроверкиКМ();
	
	НастроитьПараметрыВыбораЭлементовФормы();
	
	СформироватьСписокВыбораАдресПокупателя();
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РаботаСФормойКлиент.ЗамерВремениЗапись("ЧекНаОплату", ПараметрыЗаписи.РежимЗаписи,
		Объект.Товары.Количество() > 50);
		
	Отказ = Отказ Или РаботаСФормойКлиент.НачатьПроверкуПодразделенияДокументаИПользователя(
		Объект.ПодразделениеКомпании,
		Новый ОписаниеОповещения(
			"ПроверкаПодразделенияДокументаИПользователяЗавершение",
			ЭтотОбъект,
			Новый Структура("ПараметрыЗаписи", ПараметрыЗаписи)
		)
	);
	
КонецПроцедуры 

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение
		И ТекущийОбъект.СуммаДокумента > (ТекущийОбъект.Оплаты.Итог("Сумма")-ТекущийОбъект.Оплаты.Итог("Сдача"))
		И ПраваИНастройкиПользователя.Значение("ПогашатьЧекЦеликом") Тогда
		
		Если ТекущийОбъект.Оплаты.Количество()=0 Тогда
			НоваяСтрока = ТекущийОбъект.Оплаты.Добавить();
			НоваяСтрока.ТипОплаты = Перечисления.ТипыОплатыККТ.Наличные;
			НоваяСтрока.Сумма     = ТекущийОбъект.СуммаДокумента - (ТекущийОбъект.Оплаты.Итог("Сумма")-ТекущийОбъект.Оплаты.Итог("Сдача"));
		Иначе
			СтрокаНаличных = ТекущийОбъект.Оплаты.Найти(Перечисления.ТипыОплатыККТ.Наличные, "ТипОплаты");
			
			Если ЗначениеЗаполнено(СтрокаНаличных) Тогда
				СтрокаНаличных.Сумма = СтрокаНаличных.Сумма + 
					(ТекущийОбъект.СуммаДокумента - (ТекущийОбъект.Оплаты.Итог("Сумма")-ТекущийОбъект.Оплаты.Итог("Сдача")));
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	КэшРезультатаПроверкиКодовМаркировки = ПоместитьВоВременноеХранилище(
		Объект.КодыМаркировки.Выгрузить(),
		УникальныйИдентификатор
	);
	
КонецПроцедуры 

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Проверим наличие проверки кодов маркировки
	Если ПроверкаСоответствияКодовМаркировкиПроводилась Тогда
		
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("ЕстьРазличияСостоянийКМ", Истина);
		УспешнаяПроверкаКМ = (Объект.Товары.НайтиСтроки(СтруктураПоиска).Количество() = 0);
		
		// Маркировка
		МаркировкаТоваровСервер.ЗафиксироватьРезультатПроверкиКодовМаркировки(
			ТекущийОбъект,
			ДатаПроверкиКМ,
			АвторПроверкиКМ,
			УспешнаяПроверкаКМ
		);
		// Конец Маркировка
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	ЗапретРедактированияРеквизитовОбъектовАльфаАвто.ЗаблокироватьФискальныеРеквизиты(ЭтотОбъект);
	
	ОбновитьИндикаторыСостоянияОплаты();
	ОбновитьРеквизитыРезультатаПроверкиКМ();
	РаботаСФормой.ЗаполнитьСлужебныеРеквизитыТоваров(Объект.Товары);
	
	// Маркировка
	МаркировкаТоваровСервер.ЗаполнитьСлужебныйРеквизитКодыМаркировки(Объект);
	МаркировкаТоваровСервер.ИнициализироватьСлужебныеРеквизиты(ЭтотОбъект);
	// Конец Маркировка
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	РаботаСФормойКлиент.ОповеститьОЗаписиДокумента(Объект.Ссылка);
	РаботаСФормойКлиент.ОбновитьПодчиненныеСчета(Объект.Ссылка, Неопределено);
	
КонецПроцедуры 

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры 

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Вставить("ПоказыватьПараметрыДокумента", Элементы.ПараметрыДокумента.Видимость);
	
КонецПроцедуры 

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки.Получить("ПоказыватьПараметрыДокумента") = Ложь Тогда
		Элементы.ПараметрыДокумента.Видимость = Ложь;
		Элементы.НомерДата         .Видимость = Ложь;
	КонецЕсли;
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриПовторномОткрытии()
	ПриПовторномОткрытииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриПовторномОткрытииНаСервере()
	ОбновитьИндикаторыСостоянияОплаты();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ДатаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ЧекНаОплату.ДатаПриИзменении(Объект, ПараметрыДействия);
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ДатаПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ХозОперацияПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ЧекНаОплату.ХозОперацияПриИзменении(Объект, ПараметрыДействия);
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ХозОперацияПриИзменении(Команда)
	
	УправлениеДиалогомДокументаКлиент.ОбработатьВыборХозОперации(Объект, Элементы, Команда.Имя);
	ПараметрыДействия = Новый Структура;
	ХозОперацияПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры 

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ЧекНаОплату.КонтрагентПриИзменении(Объект, ПараметрыДействия);
	ФормаСобственности = Объект.Контрагент.ФормаСобственности;

	НастроитьПараметрыВыбораЭлементовФормы(); 
	
	Объект.АдресПокупателя = "";
	СформироватьСписокВыбораАдресПокупателя();
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	КонтрагентПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
    ОбновитьКонтактнуюИнформацию();

КонецПроцедуры

&НаСервере
Процедура КассаККМПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ЧекНаОплату.КассаККМПриИзменении(Объект, ПараметрыДействия);
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КассаККМПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	КассаККМПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ФРПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ФРПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ФРПриИзмененииНаСервере(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура КарточкаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ЧекНаОплату.КарточкаПриИзменении(Объект, ПараметрыДействия);
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КарточкаПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	КарточкаПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ДоговорВзаиморасчетовПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	СпособВеденияВзаимозачетов = Объект.ДоговорВзаиморасчетов.СпособВеденияВзаиморасчетов;
	Документы.ЧекНаОплату.ДоговорВзаиморасчетовПриИзменении(Объект, ПараметрыДействия);
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаиморасчетовПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ДоговорВзаиморасчетовПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура СделкаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	СпособВеденияВзаимозачетов = Объект.ДоговорВзаиморасчетов.СпособВеденияВзаиморасчетов;
	Документы.ЧекНаОплату.СделкаПриИзменении(Объект, ПараметрыДействия);
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СделкаПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ПараметрыДействия.Вставить("ЗаполнитьПоСделке");
	СделкаПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура СпособЗачетаАвансовПриИзмененииНаСервере()
	
	Если РасчетыСКонтрагентамиСервер.ИспользуютсяВзаиморасчетыПоРасчетнымДокументам(Объект.ДоговорВзаиморасчетов)
		И Объект.СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.Автоматически Тогда
		Объект.Сделка = Неопределено;
	КонецЕсли;
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗачетаАвансовПриИзменении(Элемент)
	
	СпособЗачетаАвансовПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СтавкаНДСПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ЧекНаОплату.СтавкаНДСПриИзменении(Объект, ПараметрыДействия);
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	СтавкаНДСПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура СуммаДокументаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ЧекНаОплату.СуммаДокументаПриИзменении(Объект, ПараметрыДействия);
	
	ОбновитьИндикаторыСостоянияОплаты();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	СуммаДокументаПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяПСНПриИзменении(Элемент)
	ИспользуетсяПСНПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИспользуетсяПСНПриИзмененииНаСервере()
	Элементы.Патент.Доступность = ИспользуетсяПСН; 
	Если НЕ ИспользуетсяПСН Тогда
		Объект.Патент = Справочники.Патенты.ПустаяСсылка();	  
	Иначе
		ОбработкаРеквизитовДокументаСервер.УстановитьПатентПоОрганизации(Объект); 
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ТелефонEmailПокупателяПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакСпособаРасчетаПриИзменении(Элемент)
	УправлениеДиалогомНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТелефонEmailПокупателяПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ТелефонEmailПокупателяПриИзмененииНаСервере(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныйРеквизитЧекаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	ОткрытьФорму("РегистрСведений.ФискальныеОперации.ФормаСписка",
		ПараметрыФормы, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовУправленияОбщегоНазначения

&НаКлиенте
Процедура НадписьВзаиморасчетыНажатие(Элемент)
	
	УправлениеДиалогомДокументаКлиент.НадписьВзаиморасчетыНажатие(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ПроверкаСоответствияКодовМаркировкиПроводилась = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	ПроверкаСоответствияКодовМаркировкиПроводилась = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ЗаполнитьПризнакАвтомобиляОтФизЛица();
	РассчитатьИтоговыеСуммы(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ТоварыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// Маркировка
	МаркировкаТоваровКлиент.ОткрытьСписокКодовМаркировки(
		ЭтотОбъект,
		ВыбраннаяСтрока,
		Поле,
		СтандартнаяОбработка,,
		Элементы.ТоварыКоличество.ТолькоПросмотр
	);
	// Конец Маркировка
	
	СкидкиНаценкиКлиент.ОбработкаВыбора(ЭтотОбъект, Поле, ВыбраннаяСтрока, Объект.БлокироватьПерерасчетСкидок);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторТовара) ИЛИ Копирование Тогда
		ТекущиеДанные.ИдентификаторТовара = Новый УникальныйИдентификатор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УправлениеДиалогомДокументаКлиент.ТоварыПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		ТекущаяСтрока.СуммаСкидкиБонусами = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПослеУдаленияНаСервере(ПараметрыДействия = Неопределено)
	
	УправлениеДиалогомДокументаСервер.ТоварыПослеУдаления(ЭтотОбъект, Элементы.Товары, ПараметрыДействия);
	РассчитатьИтоговыеСуммы(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	ТоварыПослеУдаленияНаСервере();
	
КонецПроцедуры

#Область ОбработчикиСобытийПолейТаблицыФормыТовары

&НаСервере
Процедура ТоварыНоменклатураПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыНоменклатураПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
	УправлениеДиалогомДокументаСервер.НоменклатураПриИзменении(ЭтотОбъект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТоварыНоменклатураПриИзмененииНаСервере();
	
КонецПроцедуры 

&НаСервере
Процедура ТоварыХарактеристикаНоменклатурыПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыХарактеристикаНоменклатурыПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры 

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТоварыХарактеристикаНоменклатурыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыЕдиницаИзмеренияПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыЕдиницаИзмеренияПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	
	ТоварыЕдиницаИзмеренияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыКоличествоПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыКоличествоПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТоварыКоличествоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыЦенаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыЦенаПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТоварыЦенаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыСуммаПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	ТоварыСуммаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаВсегоПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыСуммаВсегоПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры 

&НаКлиенте
Процедура ТоварыСуммаВсегоПриИзменении(Элемент)
	
	ТоварыСуммаВсегоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСтавкаНДСПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыСтавкаНДСПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТоварыСтавкаНДСПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаНДСПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыСуммаНДСПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	ТоварыСуммаНДСПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПроцентСкидкиПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыПроцентСкидкиПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	ТекущиеДанные.ЗначениеСкидкиШапкиИзменено = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентСкидкиПриИзменении(Элемент)
	
	ТоварыПроцентСкидкиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаСкидкиПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыСуммаСкидкиПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	ТекущиеДанные.ЗначениеСкидкиШапкиИзменено = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиПриИзменении(Элемент)
	
	ТоварыСуммаСкидкиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПроцентСкидкиСтрокиПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыПроцентСкидкиСтрокиПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
	ТекущиеДанные.ЗначениеСкидкиСтрокиИзменено = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентСкидкиСтрокиПриИзменении(Элемент)
	
	ТоварыПроцентСкидкиСтрокиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСуммаСкидкиСтрокиПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Документы.ЧекНаОплату.ТоварыСуммаСкидкиСтрокиПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
	ТекущиеДанные.ЗначениеСкидкиСтрокиИзменено = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиСтрокиПриИзменении(Элемент)
	
	ТоварыСуммаСкидкиСтрокиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварыСкидкаНаТоварПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	ТекущиеДанные.ЗначениеСкидкиСтрокиИзменено = Ложь;
	ТекущиеДанные.СкидкаСтрокиИзменена = Истина;
	Документы.ЧекНаОплату.ТоварыСкидкаНаТоварПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаНаТоварПриИзменении(Элемент)
	
	ТоварыСкидкаНаТоварПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОплаты

&НаКлиенте
Процедура ОплатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элементы.Оплаты.ТекущиеДанные.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Наличные");
		Элементы.Оплаты.ТекущиеДанные.Сумма     = Объект.СуммаДокумента - (Объект.Оплаты.Итог("Сумма") - Объект.Оплаты.Итог("Сдача"));
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ОплатыПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	СуммаДокументаПриИзмененииНаСервере(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыПриИзменении(Элемент)
	
	ОплатыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОплатыПослеУдаленияНаСервере(ПараметрыДействия = Неопределено)
	
	УстановитьПризнакОтправлятьКомандуНаЭТ();
	СуммаДокументаПриИзмененииНаСервере(ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыПослеУдаления(Элемент)
	
	ОплатыПослеУдаленияНаСервере();
	
КонецПроцедуры

#Область ОбработчикиСобытийПолейТаблицыФормыОплаты

&НаСервере
Процедура ОплатыТипОплатыПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Оплаты.НайтиПоИдентификатору(Элементы.Оплаты.ТекущаяСтрока);
	Документы.ЧекНаОплату.ОплатыТипОплатыПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	УстановитьПризнакОтправлятьКомандуНаЭТ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыТипОплатыПриИзменении(Элемент)
	
	ОплатыТипОплатыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОплатыСуммаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Оплаты.НайтиПоИдентификатору(Элементы.Оплаты.ТекущаяСтрока);
	Документы.Чек.ОплатыСуммаПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыСуммаПриИзменении(Элемент)
	
	ОплатыСуммаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОплатыДоговорВзаиморасчетовПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Оплаты.НайтиПоИдентификатору(Элементы.Оплаты.ТекущаяСтрока);
	Документы.ЧекНаОплату.ОплатыДоговорВзаиморасчетовПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОплатыДоговорВзаиморасчетовПриИзменении(Элемент)
	
	ОплатыДоговорВзаиморасчетовПриИзмененииНаСервере();
	
КонецПроцедуры 

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
//@skip-warning
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда,
                                                НавигационнаяСсылка = Неопределено,
                                                СтандартнаяОбработка = Неопределено)
	
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
    
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПробитьЧек(Команда)
	
	ПараметрыДействия = Новый Структура;
	ПараметрыДействия.Вставить("НеНуженЭТ", Не Объект.ОтправлятьКомандуНаЭТ);
	
	ОткрыватьФормуПроверкиМаркировкиККТ = Объект.КодыМаркировки.Количество() > 0
		И ОткрыватьФормуПроверкиМаркировкиККТ(Объект.ПризнакСпособаРасчета, Объект.ФР);
	ПараметрыДействия.Вставить("ОткрыватьФормуПроверкиМаркировкиККТ", ОткрыватьФормуПроверкиМаркировкиККТ);
	
	СегодняшняяДата = ПолучитьДатуНаНачалоДня();   
	ПараметрыДействия.Вставить("СегодняшняяДата", СегодняшняяДата);
	
	УправлениеДиалогомДокументаКлиент.ПробитьЧек(ЭтотОбъект,ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеККМ(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ФР", Объект.ФР);
	
	ОткрытьФорму(
		"ОбщаяФорма.УправлениеФискальнымУстройством",
		ПараметрыФормы,
		ЭтотОбъект
	);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭТ(Команда)
	
	ОткрытьФорму(
		"ОбщаяФорма.УправлениеЭквайринговымТерминалом",
		,
		ЭтотОбъект,
		,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОплату(Команда)
	
	ПараметрыОплаты = Новый Структура();

	Если Команда.Имя="ОплатаНаличные" Тогда
		ПараметрыОплаты.Вставить("ТипОплаты", ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Наличные"));
	ИначеЕсли Команда.Имя="ОплатаПлатежнаяКарта" Тогда   
		ПараметрыОплаты.Вставить("ТипОплаты", ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Электронно"));
		ОсновнойТипПлатежнойКарты(ПараметрыОплаты);
	КонецЕсли; 

	НайденныеСтроки = Объект.Оплаты.НайтиСтроки(ПараметрыОплаты);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.Оплаты.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		Если НЕ ТолькоПросмотр Тогда
			Элементы.Оплаты.ИзменитьСтроку();
		КонецЕсли;
	Иначе
		Элементы.Оплаты.ДобавитьСтроку();
		ЗаполнитьЗначенияСвойств(Элементы.Оплаты.ТекущиеДанные, ПараметрыОплаты);
	КонецЕсли;
	
	Элементы.Оплаты.ТекущийЭлемент = Элементы.Оплаты.ПодчиненныеЭлементы.ОплатыСумма;

	ОбновитьИндикаторыСостоянияОплаты();
	УстановитьПризнакОтправлятьКомандуНаЭТ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборНоменклатуры(Команда)
	
	ПараметрыДействия = Новый Структура();
	ПараметрыДействия.Вставить("НеУстанавливатьОтборНаОстаток", Истина);
	УправлениеДиалогомКлиент.ОткрытьПодборНоменклатуры(ЭтотОбъект,,,,, ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьНаСервере()
	
	Документы.ЧекНаОплату.ПроизвестиРаспределениеСуммыОплаты(Объект.СуммаДокумента, Объект.Товары);

КонецПроцедуры

&НаКлиенте
Процедура Распределить(Команда)
	
	РаспределитьНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСтатусыКодовМаркировки(Команда)
	
	ИмяДействия = "ОтправкаЗапросаНаПолучениеТекущихСтатусовМаркировки";
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Действие", ИмяДействия);
	ДополнительныеПараметры.Вставить("КодыМаркировки", ЗаполнитьКодыМаркировкиДляПроверки());
	ДополнительныеПараметры.Вставить("Организация", Объект.Организация);
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", Объект.ПодразделениеКомпании);
	ДополнительныеПараметры.Вставить("НеТребуетсяКлючСессии", Ложь);
	ДополнительныеПараметры.Вставить(
		"РазрешенныеСостояния",
		МаркировкаТоваровКлиент.РазрешенныеСостояния(
			Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ЧекНаОплатуВозврат")));
	ОбработчикОповещения = Новый ОписаниеОповещения("ПолучитьЗапросЗавершение", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("ОбработкаРезультата", ОбработчикОповещения);
	
	МаркировкаТоваровКлиент.ОтправитьПолучитьЗапрос(ЭтотОбъект, Объект, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДатуНаНачалоДня()
	
	Возврат НачалоДня(ТекущаяДатаСеанса());
	
КонецФункции

// Маркировка
&НаКлиенте
Процедура ПолучитьЗапросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаСоответствияКодовМаркировкиПроводилась = Истина;
	
	ДатаПроверкиКМ = ОбщегоНазначенияКлиент.ДатаСеанса();
	АвторПроверкиКМ = ПользователиКлиент.ТекущийПользователь();
	
	ЗаполнитьРеквизитыРезультатаПроверкиКМ(Результат.Данные.КодыМаркировки, Истина);
	
КонецПроцедуры
// Конец Маркировка

//@skip-warning
&НаКлиенте
Процедура ПроверкаПодразделенияДокументаИПользователяЗавершение(Контекст) Экспорт
	
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

#Область ОбработчикиБиблиотек

// ЗаполнениеОбъектов
&НаКлиенте
Процедура ПослеОбработкиЗаполнения(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ОчисткаТабличнойЧасти")
		И ПолучитьЗначениеПараметраСтруктуры(ДополнительныеПараметры, "ИмяТабличнойЧасти") = "Оплаты" Тогда
		УправлениеДиалогомКлиентСервер.УстановитьПризнакНеобходимостиЭТ(Объект);
	КонецЕсли;
	
	ПослеОбработкиЗаполненияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеОбработкиЗаполненияНаСервере()
	
	ЗаполнениеОбъектовАльфаАвто.ПослеОбработкиЗаполнения(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ЗаполнениеОбъектов

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
//@skip-warning
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти

#Область ОбработчикиАльфаАвто

// Ядро
&НаКлиенте
Процедура ПараметрыДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;;
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыДокументаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	
	УправлениеДиалогомДокументаКлиент.РасширенноеРедактированиеПоляКомментарий(ЭтотОбъект, Элемент,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСуммаДокументаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеДиалогомДокументаКлиент.ПоказатьРасширенныеИтогиОперации(ЭтотОбъект, Элемент);
	
КонецПроцедуры
// Конец Ядро

#КонецОбласти

&НаСервере
Процедура РассчитатьИтоговыеСуммы(ПараметрыДействия = Неопределено)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		Возврат;
	КонецЕсли;
	
	ИтогиОперации = Документы.ЧекНаОплату.РассчитатьИтогиОперации(Объект);
	Если НЕ Объект.СуммаДокумента=ИтогиОперации.СуммаДокумента Тогда
		Объект.СуммаДокумента = ИтогиОперации.СуммаДокумента;
		СуммаДокументаПриИзмененииНаСервере(ПараметрыДействия);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИндикаторыСостоянияОплаты()
	
	СуммаОплачено = Объект.Оплаты.Итог("Сумма");
	СуммаСдача    = Объект.Оплаты.Итог("Сдача");
	СуммаДоплатить = Объект.СуммаДокумента - (СуммаОплачено - СуммаСдача);
	СуммаПереплата = (СуммаОплачено - СуммаСдача) - Объект.СуммаДокумента;
	
	Если СуммаОплачено > 0 Тогда
		Если СуммаДоплатить > 0 Тогда
			Элементы.НадписьСуммаСдача    .Видимость = ЛОЖЬ;
			Элементы.НадписьСуммаДоплатить.Видимость = ИСТИНА;
			Элементы.НадписьСуммаПереплата.Видимость = ЛОЖЬ;
		ИначеЕсли СуммаПереплата > 0 Тогда
			Элементы.НадписьСуммаСдача    .Видимость = ЛОЖЬ;
			Элементы.НадписьСуммаДоплатить.Видимость = ЛОЖЬ;
			Элементы.НадписьСуммаПереплата.Видимость = ИСТИНА;
		Иначе
			Элементы.НадписьСуммаСдача    .Видимость = ИСТИНА;
			Элементы.НадписьСуммаДоплатить.Видимость = ЛОЖЬ;
			Элементы.НадписьСуммаПереплата.Видимость = ЛОЖЬ;
		КонецЕсли;
	Иначе
		Элементы.НадписьСуммаСдача    .Видимость = ИСТИНА;
		Элементы.НадписьСуммаДоплатить.Видимость = ЛОЖЬ;
		Элементы.НадписьСуммаПереплата.Видимость = ЛОЖЬ;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОсновнойТипПлатежнойКарты(ПараметрыОплаты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТипыПлатежныхКарт.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТипыПлатежныхКарт КАК ТипыПлатежныхКарт
		|ГДЕ
		|	ТипыПлатежныхКарт.Основной = ИСТИНА";  
	
	РезультатЗапроса = Запрос.Выполнить();  
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();  
		Выборка.Следующий();
		ПараметрыОплаты.Вставить("ТипПлатежнойКарты", Выборка.Ссылка);
		
	КонецЕсли;

КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьПризнакАвтомобиляОтФизЛица()
	
	Для Каждого ТекущаяСтрока Из Объект.Товары Цикл
		ТекущаяСтрока.ИндикаторСебестоимостиАвтомобиля = БиблиотекаКартинок.Информация;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьТоварыНаСервере(ПараметрыДействия = Неопределено)
	
	Объект.Товары.Очистить();
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Документы.ЧекНаОплату.ЗаполнитьТовары(ДокументОбъект, Ложь, ПараметрыДействия);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ЗавершениеЗаполненияТоваров(ПерезаполнитьТовары, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ПерезаполнитьТовары Тогда
		ПерезаполнитьТоварыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомДокументаСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомДокументаСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	
	Элементы.Карточка.ТолькоПросмотр = НЕ (ПраваИНастройкиПользователя.Значение("РазрешитьРучнойВыборКарточки", Объект));
	Элементы.ФормаПробитьЧек.Доступность = НЕ ЗначениеЗаполнено(Объект.ДатаФР) И НЕ ТолькоПросмотр;
	
	ВыделениеМежценовойРазницыОтдельнойСтрокой = ПраваИНастройкиПользователя.Значение("ВыделениеМежценовойРазницыОтдельнойСтрокой", Объект);
	Элементы.ТоварыСебестоимостьАвтомобиля.Видимость = ВыделениеМежценовойРазницыОтдельнойСтрокой;
	ЗаполнитьПризнакАвтомобиляОтФизЛица();
	
	Если НЕ (Объект.ХозОперация = Справочники.ХозОперации.ЧекНаОплату
		ИЛИ Объект.ХозОперация = Справочники.ХозОперации.ЧекНаОплатуВозврат) Тогда
		Элементы.ТоварыГруппаКодыМаркировки.Видимость = Ложь;
	КонецЕсли;
	
	ФормаСобственности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ФормаСобственности");
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"АдресПокупателя",
		"ТолькоПросмотр",
		НЕ (ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо
		ИЛИ ФормаСобственности = Перечисления.ФормыСобственности.ИндивидуальныйПредприниматель)
	);
	
	// Установим видимость картинки с проверкой кодов маркировки через ККТ
	Элементы.ТоварыКартинкаПроверкиМаркировкиККТ.Видимость = 
		Объект.КодыМаркировки.Количество() > 0
		И ЗначениеЗаполнено(Объект.ФР)
		И ОборудованиеЧекопечатающиеУстройстваВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(Объект.ФР);
	
	СпособВыбораСкидки = ПравоПользователя("СпособВыбораСкидки", Объект);
	ДоступныСкидки = СпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.РучныеСкидки
		ИЛИ СпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.АвтоматическиеИРучныеСкидки
		ИЛИ СпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов;
	ДоступноРедактированиеСкидок = СпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТоварыПроцентСкидки",
		"ТолькоПросмотр",
		Объект.БлокироватьПерерасчетСкидок ИЛИ НЕ ДоступноРедактированиеСкидок
	);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТоварыСуммаСкидки",
		"ТолькоПросмотр",
		Объект.БлокироватьПерерасчетСкидок ИЛИ НЕ ДоступноРедактированиеСкидок
	);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТоварыСкидкаНаТовар",
		"ТолькоПросмотр",
		Объект.БлокироватьПерерасчетСкидок ИЛИ НЕ ДоступныСкидки
	);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТоварыПроцентСкидкиСтроки",
		"ТолькоПросмотр",
		Объект.БлокироватьПерерасчетСкидок ИЛИ НЕ ДоступноРедактированиеСкидок
	);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТоварыСуммаСкидкиСтроки",
		"ТолькоПросмотр",
		Объект.БлокироватьПерерасчетСкидок ИЛИ НЕ ДоступноРедактированиеСкидок
	);
	
	УправлениеДиалогомДокументаСервер.УстановитьИнформациюПечатиЧека(
		ЭтотОбъект,
		Объект.ФР,
		Объект.ТелефонEmailПокупателя,
		НЕ ЗначениеЗаполнено(Объект.ДатаФР)
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ОтправлятьКомандуНаЭТ",
		"ТолькоПросмотр",
		Не ПраваИНастройкиПользователя.Значение("РазрешитьРучнуюАвторизациюБезналичныхПлатежей")
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпособЗачетаАвансов",
		"Видимость",
		(Объект.ДоговорВзаиморасчетов.СпособВеденияВзаиморасчетов = Перечисления.СпособыВеденияУчетаВзаиморасчетов.ПоСделкам)
		ИЛИ РасчетыСКонтрагентамиСервер.УчетПоСпособуЗачетаОплаты(Объект.ВерсияОбъекта)
	);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтотОбъект);
	
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформлениеРучногоСписанияХарактеристик(ЭтотОбъект);
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформлениеСуммыВсего(ЭтотОбъект);
	УправлениеДиалогомДокументаСервер.УстановитьУсловноеОформлениеГТД(ЭтотОбъект);
	
	// Маркировка
	МаркировкаТоваровСервер.УстановитьУсловноеОформлениеКодыМаркировок(ЭтотОбъект);
	// Конец Маркировка
	
	СкидкиНаценкиСервер.УстановитьУсловноеОформление(
		ЭтотОбъект,
		СкидкиНаценкиСервер.ПараметрыУстановкиУсловногоОформления(ЭтотОбъект)
	);
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТоварыИндикаторСебестоимостиАвтомобиля");
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СебестоимостьАвтомобиля");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	// При установки способа расчетов с контрагентом
	Если РасчетыСКонтрагентамиСервер.УчетПоСпособуЗачетаОплаты(Объект.ВерсияОбъекта) Тогда
		ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Сделка");
		
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СпособЗачетаАвансов");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СпособыЗачетаАвансов.Автоматически;
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СпособВеденияВзаимозачетов");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СпособыВеденияУчетаВзаиморасчетов.ПоРасчетнымДокументам;
		
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры=Неопределено)
	
	Если НЕ УправлениеДиалогомДокументаСервер.ОбработкаРезультатаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = "РедактированиеКодовМаркировкиСтрокиТовара" Тогда
		ПроверкаСоответствияКодовМаркировкиПроводилась = Истина;
		Для Каждого Строка Из Объект.Товары Цикл
			ПроверкаСоответствияКодовМаркировкиПроводилась = ПроверкаСоответствияКодовМаркировкиПроводилась
				И Строка.ПроверкаСоответствияКодовМаркировкиПроводилась;
		КонецЦикла;
	КонецЕсли;
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	ОбновитьИндикаторыСостоянияОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаОповещения(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если НЕ УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаРезультатаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры);
	ОбработкаРезультатаВыполненияДействия(РезультатОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРезультатаВыполненияДействия(ПараметрыДействия)
	
	КоллекцияОпераций = Новый Структура();
	Если ТипЗнч(ПараметрыДействия) = Тип("Структура") И ПараметрыДействия.Свойство("ЗаполнитьПоСделке") Тогда
		Если ЗначениеЗаполнено(Объект.Сделка) Тогда
			ОбработчикОповещения = Новый ОписаниеОповещения("Подключаемый_ЗавершениеЗаполненияТоваров", ЭтотОбъект);
			
			ПоследовательныеОперацииКлиентСервер.ДобавитьВопросДаНет(
				КоллекцияОпераций,
				"ТребуетсяЗаполнитьПоСделке", 
				НСтр("ru='Перезаполнить таблицу <Товары> по сделке?'"),
				, ,
				ОбработчикОповещения
			);
			
			ПараметрыДействия.Вставить("ТребуетсяЗаполнитьПоСделке", Ложь);
			ПараметрыДействия.Вставить("ТребуетсяПересчетЦен",       Ложь);
			ПараметрыДействия.Вставить("ТребуетсяУстановкаЦен",      Ложь);
		КонецЕсли;
		ПараметрыДействия.Удалить("ЗаполнитьПоСделке");
	КонецЕсли;
	
	ОбработкаРеквизитовДокументаКлиент.ПолучитьРазрешенияДляПересчета(ЭтотОбъект, ПараметрыДействия, КоллекцияОпераций);
	УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаВыполненияДействия(ЭтотОбъект, ПараметрыДействия);
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция ОткрыватьФормуПроверкиМаркировкиККТ(ПризнакСпособаРасчета, ФР)
	
	ДоступныеПризнакиРасчета = Новый Массив();
	ДоступныеПризнакиРасчета.Добавить(Перечисления.ПризнакиСпособаРасчета.ПередачаБезОплаты);
	ДоступныеПризнакиРасчета.Добавить(Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой);
	ДоступныеПризнакиРасчета.Добавить(Перечисления.ПризнакиСпособаРасчета.ПередачаСЧастичнойОплатой);
	
	Возврат ДоступныеПризнакиРасчета.Найти(ПризнакСпособаРасчета) <> Неопределено
		И ОборудованиеЧекопечатающиеУстройстваВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ФР);
	
КонецФункции

&НаКлиенте
Функция ЗаполнитьКодыМаркировкиДляПроверки()
	
	Результат = Новый Массив;
	Для Каждого Строка Из Объект.Товары Цикл
		ПараметрыОтбора = Новый Структура("ИдентификаторТовара", Строка.ИдентификаторТовара);
		НайденныеСтроки = Объект.КодыМаркировки.НайтиСтроки(ПараметрыОтбора);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			Результат.Добавить(НайденнаяСтрока.КодМаркировки);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьРеквизитыРезультатаПроверкиКМ()
	
	Если НЕ ЭтоАдресВременногоХранилища(КэшРезультатаПроверкиКодовМаркировки) Тогда
		Возврат;
	КонецЕсли;
	
	КэшКодыМаркировки = ПолучитьИзВременногоХранилища(КэшРезультатаПроверкиКодовМаркировки);
	ЗаполнитьРеквизитыРезультатаПроверкиКМ(КэшКодыМаркировки);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыРезультатаПроверкиКМ(КодыМаркировки, ПроведенаПроверкаВсехКодов = Ложь)
	
	Для Каждого Строка Из КодыМаркировки Цикл
		ПараметрыОтбора = Новый Структура("КодМаркировки", Строка.КодМаркировки);
		НайденныеСтроки = Объект.КодыМаркировки.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		СвойстваНеЗаполнять = "КодМаркировки";
		Если ЕстьРеквизит(Строка, "КодРезультатаПроверки") Тогда
			ПоляНеЗаполнять = Новый Массив;
			ПоляНеЗаполнять.Добавить("КодРезультатаПроверки");
			ПоляНеЗаполнять.Добавить("КодМаркировкиПроверен");
			ПоляНеЗаполнять.Добавить("КодОбработкиЗапроса");
			ПоляНеЗаполнять.Добавить("ПредставлениеРезультатаПроверки");
			ПоляНеЗаполнять.Добавить("РезультатПроверки");
			ПоляНеЗаполнять.Добавить("РезультатПроверкиОИСМ");
			ПоляНеЗаполнять.Добавить("СтатусТовара");
			ПоляНеЗаполнять.Добавить("ТекстОшибки");
			ПоляНеЗаполнять.Добавить("РезультатПроверкиРазрешительногоРежима");
			ПоляНеЗаполнять.Добавить("ТекстОшибкиПроверки");
			ПоляНеЗаполнять.Добавить("ЗначениеОтраслевогоРеквизита");
			СвойстваНеЗаполнять = СтрШаблон("%1,%2", СвойстваНеЗаполнять, СтрСоединить(ПоляНеЗаполнять, ","));
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(
			НайденныеСтроки[0],
			Строка, ,
			СвойстваНеЗаполнять);
		Если ПроведенаПроверкаВсехКодов Тогда
			НайденныеСтроки[0].ПроверкаСоответствияКодовМаркировкиПроводилась = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из Объект.Товары Цикл
		ПараметрыОтбора = Новый Структура("ИдентификаторТовара", Строка.ИдентификаторТовара);
		КодыМаркировкиСтроки = Объект.КодыМаркировки.НайтиСтроки(ПараметрыОтбора);
		ПроверкаПроводилась = Истина;
		Для Каждого Код Из КодыМаркировкиСтроки Цикл
			Строка.ЕстьРазличияСостоянийКМ = Строка.ЕстьРазличияСостоянийКМ ИЛИ НЕ Код.Соответствует;
			ПроверкаПроводилась = ПроверкаПроводилась И Код.ПроверкаСоответствияКодовМаркировкиПроводилась;
		КонецЦикла;
		Строка.ПроверкаСоответствияКодовМаркировкиПроводилась = ПроверкаПроводилась;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокВыбораАдресПокупателя()
	
	Элементы.АдресПокупателя.СписокВыбора.Очистить();
	СписокАдресов = УправлениеДиалогомДокументаСервер.АдресаКонтрагента(
		Объект.Контрагент,
		Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
	Для Каждого Адрес Из СписокАдресов Цикл
		Элементы.АдресПокупателя.СписокВыбора.Добавить(Адрес);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСтатусыКодовМаркировкиККТ(Команда)
	
	Если Объект.Ссылка.Пустая() ИЛИ Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Выполнение действия возможно только после записи данных.
			|Данные будут записаны.'"
		);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыполнитьПодключаемуюКомандуПодтверждениеЗаписи",
			ЭтотОбъект
		);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
		
	КонецЕсли;
	
	ЗапроситьСтатусыКодовМаркировкиККТПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодключаемуюКомандуПодтверждениеЗаписи(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		
		ОчиститьСообщения();
		
		Попытка
			Записать();
		Исключение
			ПричинаОшибки(ЭтотОбъект, РезультатВопроса, ДополнительныеПараметры);
		КонецПопытки;
		
		Если Объект.Ссылка.Пустая() Или Модифицированность Тогда
			Возврат; // Запись не удалась.
		КонецЕсли;
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗапроситьСтатусыКодовМаркировкиККТПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСтатусыКодовМаркировкиККТПродолжение()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Документ", Объект.Ссылка);
	
	Обработчик = Новый ОписаниеОповещения(
		"ЗапроситьСтатусыКодовМаркировкиККТОкончание",
		ЭтотОбъект
	);
		
	ОткрытьФорму(
		"ОбщаяФорма.ФормаСпискаМаркировокПроверкаККТ",
		ПараметрыОткрытия,
		ЭтотОбъект,
		, , ,
		Обработчик,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСтатусыКодовМаркировкиККТОкончание(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Прочитать();
	ОбработкаРезультатаВыполненияДействия(ДополнительныеПараметры);
	
КонецПроцедуры

#Область ПараметрыДокумента

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаЗакрытияПараметровДокумента(РезультатОповещения, ДополнительныеПараметры = Неопределено) Экспорт
	                                                                                                                
	ОбработкаРезультатаЗакрытияПараметровДокументаНаСервере(РезультатОповещения, ДополнительныеПараметры);
	ОбработкаРеквизитовДокументаКлиент.ПолучитьРазрешенияДляПересчета(ЭтотОбъект, РезультатОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаЗакрытияПараметровДокументаНаСервере(РезультатОповещения, ДополнительныеПараметры)
	
	ОбщиеПараметрыДокументов.ПараметрыДокументаПриИзмененииРеквизитов(Объект, 	РезультатОповещения);
	ОбщиеПараметрыДокументов.ПараметрыДокументаПриИзменении(ЭтотОбъект,			РезультатОповещения);
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область Штрихкодирование

&НаКлиенте
Процедура Подключаемый_ШтрихкодированиеОбработкаОповещения(РезультатОповещения,
	ДополнительныеПараметры = Неопределено) Экспорт

	Если РезультатОповещения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если РезультатОповещения.Свойство("Действие") Тогда
		ШтрихкодированиеОбработкаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ШтрихкодированиеОбработкаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры)

	ШтрихкодированиеВызовСервера.ОбработкаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры, , Объект);

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ОбновитьКонтактнуюИнформацию(ОбновлятьКИ = Истина)
	
	Документы.ЧекНаОплату.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, ОбновлятьКИ);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакОтправлятьКомандуНаЭТ()
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование)
		И ЕстьРеквизит(Объект.ДокументОснование, "ОплатаОнлайн")
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ОплатаОнлайн") Тогда
		Объект.ОтправлятьКомандуНаЭТ = Ложь;
		Возврат;
	КонецЕсли;
	
	УправлениеДиалогомКлиентСервер.УстановитьПризнакНеобходимостиЭТ(Объект);
	
КонецПроцедуры

#КонецОбласти