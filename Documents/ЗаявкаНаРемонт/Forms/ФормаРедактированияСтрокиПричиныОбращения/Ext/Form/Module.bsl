#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПричинаОбращения            = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ПричинаОбращения", 
									Справочники.ПричиныОбращений.ПустаяСсылка());
	ПричинаОбращенияСодержание  = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ПричинаОбращенияСодержание", "");
	ТипПричиныОбращения         = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ТипПричиныОбращения", 
									Справочники.ТипыПричинОбращения.ПустаяСсылка());
	ВидРемонтаПричиныОбращения  = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ВидРемонтаПричиныОбращения", 
									Справочники.ВидыРемонта.ПустаяСсылка());
	НормаВремени                = ПолучитьЗначениеПараметраСтруктуры(Параметры, "НормаВремени", 0) / 3600;
	ДляПросмотра                = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ТолькоПросмотр", Ложь);
	
	БлокироватьЭлементыФормы(ДляПросмотра);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПричинаОбращенияПриИзменении(Элемент)
	ПричинаОбращенияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПричинаОбращенияПриИзмененииНаСервере()
	
	ПричинаОбращенияСодержание = ?(НЕ ПустаяСтрока(ПричинаОбращения.ПричинаОбращения), 
									ПричинаОбращения.ПричинаОбращения, Строка(ПричинаОбращения));
	ТипПричиныОбращения        = ПричинаОбращения.ТипПричиныОбращения;
	ВидРемонтаПричиныОбращения = ПричинаОбращения.ВидРемонта;
	НормаВремени               = ПричинаОбращения.ВремяВыполнения;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВДокумент(Команда)
	
	// Выполним проверки
	Если НЕ ЗначениеЗаполнено(ПричинаОбращения) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Причина обращения должна быть заполнена.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура("ПричинаОбращения,ТипПричиныОбращения,ВидРемонтаПричиныОбращения,ПричинаОбращенияСодержание,НормаВремени",
										ПричинаОбращения,
										ТипПричиныОбращения,
										ВидРемонтаПричиныОбращения,
										?(НЕ ПустаяСтрока(ПричинаОбращенияСодержание), ПричинаОбращенияСодержание, Строка(ПричинаОбращения)),
										НормаВремени * 3600);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура БлокироватьЭлементыФормы(Блокировать)
	
	Если НЕ Блокировать Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ВидРемонтаПричиныОбращения.ТолькоПросмотр = Истина;
	Элементы.ПричинаОбращения.ТолькоПросмотр = Истина;
	Элементы.ПричинаОбращенияКомментарий.ТолькоПросмотр = Истина;
	Элементы.Продолжительность.ТолькоПросмотр = Истина;
	Элементы.ТипПричиныОбращения.ТолькоПросмотр = Истина;
	Элементы.ФормаВДокумент.Доступность = Ложь;
КонецПроцедуры

#КонецОбласти