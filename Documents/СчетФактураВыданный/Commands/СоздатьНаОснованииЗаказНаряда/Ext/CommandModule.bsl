
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ЗаказНарядЗакрыт(ПараметрКоманды) Тогда
		ОткрытьФормуСчетФактурыВыданный(ПараметрКоманды, ПараметрыВыполненияКоманды);
	Иначе
		АвтосервисКлиент.ВопросСозданияСчетФактурыЗаказНаряда(ПараметрыВыполненияКоманды.Источник, ПараметрКоманды);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаказНарядЗакрыт(ЗаказНаряд)
	
	СостояниеЗаказНаряда = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказНаряд, "Состояние");
	
	Возврат (СостояниеЗаказНаряда = Справочники.ВидыСостоянийЗаказНарядов.Закрыт);
	
КонецФункции // ЗаказНарядЗакрыт()

&НаКлиенте
Процедура ОткрытьФормуСчетФактурыВыданный(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Основание", ПараметрКоманды);
	
	ОткрытьФорму(
		"Документ.СчетФактураВыданный.ФормаОбъекта",
		ПараметрыОткрытия,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность);
	
КонецПроцедуры // ОткрытьФормуСчетФактурыВыданный()

#КонецОбласти
