#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыОтбора = Новый Структура();
	
	ОтборМодель              = Неопределено;
	ОтборВариантКомплектации = Неопределено;
	
	// Определим местов вызова команды
	Если ЕстьРеквизитНаКлиенте(ПараметрыВыполненияКоманды.Источник, "Объект") Тогда
		ОтборМодель              = ПараметрыВыполненияКоманды.Источник.Объект.Модель;
		ОтборВариантКомплектации = ПараметрыВыполненияКоманды.Источник.Объект.ВариантКомплектации;
	ИначеЕсли ЕстьРеквизитНаКлиенте(ПараметрыВыполненияКоманды.Источник, "Список") Тогда
		ОтборМодель              = ПараметрыВыполненияКоманды.Источник.ОтборМодель;
		ОтборВариантКомплектации = ПараметрыВыполненияКоманды.Источник.ОтборКомплектация;
	КонецЕсли;
	
	// Установим отбор
	Если ЗначениеЗаполнено(ОтборМодель) Тогда
		ПараметрыОтбора.Вставить("Модель", ОтборМодель);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборВариантКомплектации) Тогда
		ПараметрыОтбора.Вставить("ВариантКомплектации", ОтборВариантКомплектации);
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЗаказНаАвтомобиль.ФормаСписка", Новый Структура("Отбор", ПараметрыОтбора), ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти