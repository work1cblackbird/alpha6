
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	СписокАвтомобилей = ФормированиеСпискаАвтомобилейБезЗаказа(ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ДокументОснование", ПараметрКоманды);
	ПараметрыФормы.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
	
	Если СписокАвтомобилей.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'Документ ""%1"" не содержит автомобилей без заказа.'"),ПараметрКоманды),
			ПараметрКоманды
		);
		Возврат;
	ИначеЕсли СписокАвтомобилей.Количество() = 1 Тогда
		ОбработкаВыбораАвтомобиля(СписокАвтомобилей[0], ПараметрыФормы);
	ИначеЕсли СписокАвтомобилей.Количество() > 0 Тогда
		ОбработкаОповещения = Новый ОписаниеОповещения("ОбработкаВыбораАвтомобиля", ЭтотОбъект, ПараметрыФормы);
		СписокАвтомобилей.ПоказатьВыборЭлемента(ОбработкаОповещения, "Выберите автомобиль для заказа");
	КонецЕсли;
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаВыбораАвтомобиля(ВыбранныйАвтомобиль, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйАвтомобиль = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Основание", Новый Структура("ДокументОснование,Автомобиль", ДополнительныеПараметры.ДокументОснование, ВыбранныйАвтомобиль.Значение));
	
	ПараметрыВыполненияКоманды = ПолучитьЗначениеПараметраСтруктуры(ДополнительныеПараметры, "ПараметрыВыполненияКоманды");
	
	ОткрытьФорму("Документ.ЗаказНаАвтомобиль.ФормаОбъекта", ПараметрыФормы,  ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры // ОбработкаВыбораАвтомобиля()

&НаСервере
Функция ФормированиеСпискаАвтомобилейБезЗаказа(ДокументОснование)
	
	СписокАвтомобилей = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказыАвтомобилейОстатки.Автомобиль,
	|	ЗаказыАвтомобилейОстатки.КоличествоОстаток,
	|	ЗаказыАвтомобилейОстатки.РезервОстаток
	|ИЗ
	|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(
	|			&МоментВремени,
	|			Автомобиль В (&СписокАвтомобилей)
	|				И Заказ = &Заказ) КАК ЗаказыАвтомобилейОстатки";
	Запрос.УстановитьПараметр("МоментВремени", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("СписокАвтомобилей", ДокументОснование.Автомобили.Выгрузить(,"Автомобиль"));
	Запрос.УстановитьПараметр("Заказ", ДокументОснование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.КоличествоОстаток = 0 Тогда
			СписокАвтомобилей.Добавить(Выборка.Автомобиль);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокАвтомобилей;
	
КонецФункции // ФормированиеСпискаАвтомобилейБезЗаказа()

#КонецОбласти