
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Основание = ПоследнийДокументВЦепочке(ПараметрКоманды);
	
	Если Основание <> ПараметрКоманды Тогда
		
		// Оповести пользователя об необходимости изменить документ основание
		СоставВопроса = Новый Массив;
		СоставВопроса.Добавить(НСтр("ru = 'На основании уже введены другие заказы на автомобиль.'"));
		СоставВопроса.Добавить(СтрШаблон(НСтр("ru = 'Необходимо изменить документ-основание на [%1].'"), Основание));
		
		ТекстВопроса = СтрСоединить(СоставВопроса, Символы.ПС);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(Истина, "Изменить и продолжить");
		Кнопки.Добавить(Ложь, "Отмена");
		
		ОбработчикОповещения = Новый ОписаниеОповещения("РеакцияНаВопрос", ЭтотОбъект, Основание);
		
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, Кнопки);
		
		Возврат;
		
	КонецЕсли;
	
	РеакцияНаВопрос(Истина, Основание);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РеакцияНаВопрос(Заполнять, Основание) Экспорт
	
	Если Заполнять Тогда
		
		ПараметрыОткрытия = Новый Структура("Основание", Основание);
		ОткрытьФорму("Документ.ЗаказНаАвтомобиль.ФормаОбъекта", ПараметрыОткрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоследнийДокументВЦепочке(ЗаказНаАвтомобиль)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказНаАвтомобиль.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказНаАвтомобиль КАК ЗаказНаАвтомобиль
	|ГДЕ
	|	ЗаказНаАвтомобиль.ДокументОснование = &Основание
	|	И ЗаказНаАвтомобиль.Проведен = ИСТИНА
	|	И ЗаказНаАвтомобиль.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Основание", ЗаказНаАвтомобиль);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат ЗаказНаАвтомобиль;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат ПоследнийДокументВЦепочке(Выборка.Ссылка);
	
КонецФункции

#КонецОбласти