
#Область ОбработчикиСобытийЭлементовТаблицыФормыМодели

&НаКлиенте
Процедура МоделиПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущаяСтрока = Неопределено ИЛИ Элемент.ТекущаяСтрока = ТекущаяМодель Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ВариантыКомплектации.Очистить();
	ТекущаяМодель = Элемент.ТекущаяСтрока;
	МоделиПриАктивизацииСтрокиНаСервере(Элемент.ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура МоделиПриАктивизацииСтрокиНаСервере(Модель)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИСТИНА КАК Пометка,
		|	ВариантыКомплектации.Ссылка КАК ВариантКомплектации
		|ИЗ
		|	Справочник.ВариантыКомплектации КАК ВариантыКомплектации
		|ГДЕ
		|	ВариантыКомплектации.Владелец В ИЕРАРХИИ(&Модель)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка"
	);
	Запрос.УстановитьПараметр("Модель", Модель);
	ВариантыКомплектации.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВариантыКомплектацииВыделение(Команда)
	
	Если Команда.Имя = "ВыделитьВсе" Тогда
		
		Пометка = Истина;
		
	ИначеЕсли Команда.Имя = "ОтменитьВсе" Тогда
		
		Пометка = Ложь;
		
	Иначе
		
		Пометка = Неопределено;
		
	КонецЕсли;
	
	Для каждого СтрокаВариантовКомплектации Из ВариантыКомплектации Цикл
		
		Если Пометка = Неопределено Тогда
			
			СтрокаВариантовКомплектации.Пометка = НЕ СтрокаВариантовКомплектации.Пометка;
			
		Иначе
			
			СтрокаВариантовКомплектации.Пометка = Пометка;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ФильтрСтрокиСПометкой = Новый Структура("Пометка", Истина);
	ВыбранныеСтроки = ВариантыКомплектации.НайтиСтроки(ФильтрСтрокиСПометкой);
	
	Если ВыбранныеСтроки.Количество() = 0 Тогда
		
		ПоказатьПредупреждение(
			,
			НСтр("ru = 'Для заполнения документа опциями выберете хотя бы один вариант комплектации.'")
		);
		Возврат;
		
	КонецЕсли;
	
	ВыбранныеКомплектации = Новый Массив();
	
	Для Каждого Строка Из ВыбранныеСтроки Цикл
		
		ВыбранныеКомплектации.Добавить(Строка.ВариантКомплектации);
		
	КонецЦикла;
	
	ОпцииВариантовКомплектации = ОпцииВариантовКомплектации(ВыбранныеКомплектации);
	
	Если ОпцииВариантовКомплектации.Количество() = 0 Тогда
		
		ОпцииВариантовКомплектации = Неопределено;
		
	КонецЕсли;
	
	Закрыть(ОпцииВариантовКомплектации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ОпцииВариантовКомплектации(Комплектации)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОпцииВариантовКомплектации.ВариантКомплектации.Владелец КАК Модель,
		|	ОпцииВариантовКомплектации.ВариантКомплектации КАК ВариантКомплектации,
		|	ОпцииВариантовКомплектации.Опция КАК Опция
		|ИЗ
		|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
		|ГДЕ
		|	ОпцииВариантовКомплектации.ВариантКомплектации В(&ВариантыКомплектации)
		|	И ОпцииВариантовКомплектации.ВидОпции = ЗНАЧЕНИЕ(Перечисление.ВидыОпцийАвтомобилей.ДополнительнаяОпция)"
	);
	Запрос.УстановитьПараметр("ВариантыКомплектации", Комплектации);
	УстановитьПривилегированныйРежим(Истина);
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	
КонецФункции

#КонецОбласти