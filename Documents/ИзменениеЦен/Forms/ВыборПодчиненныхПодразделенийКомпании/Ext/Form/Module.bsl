
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеДерева();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДеревоПодразделенийКомпании(Параметры.ПодразделениеКомпании, Параметры.ВыбранныеПодразделения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщегоНазначенияАвтосалонКлиент.РазвернутьДерево(ЭтотОбъект, "ПодразделенияКомпании", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СнятьПометки(Команда)
	
	ОбщегоНазначенияАвтосалонКлиентСервер.УстановитьЗначениеКолонкиВДереве(ПодразделенияКомпании, Ложь, "Выбран");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Команда)
	
	ОбщегоНазначенияАвтосалонКлиентСервер.УстановитьЗначениеКолонкиВДереве(ПодразделенияКомпании, Истина, "Выбран");
	
	ПодразделенияКомпании.ПолучитьЭлементы()[0].Выбран = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВДокумент(Команда)
	
	Результат = ПланированиеРесурсовКлиентСервер.НайтиВДеревеОбъектов(
		ПодразделенияКомпании,
		Новый Структура("Выбран", Истина));
		
	ВозвращаемоеЗначение = Новый Массив;
	Для Каждого Элемент Из Результат Цикл
		ВозвращаемоеЗначение.Добавить(Элемент.ПодразделениеКомпании);
	КонецЦикла;
	
	Закрыть(Новый ФиксированныйМассив(ВозвращаемоеЗначение));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоПодразделенийКомпании(ОсновноеПодразделение, ВыбранныеПодразделенияКомпании)
	
	// получим подчиненные подразделения
	ЭлементыДерева = ПодразделенияКомпании.ПолучитьЭлементы();
	Строка = ЭлементыДерева.Добавить();
	Строка.ПодразделениеКомпании = ОсновноеПодразделение;
	Строка.ЭтоОсновноеПодразделение = Истина;
	
	ТекРодитель = Строка;
	
	Выборка = Справочники.ПодразделенияКомпании.ВыбратьИерархически(ОсновноеПодразделение);
	Пока Выборка.Следующий() Цикл
		Пока Выборка.Родитель <> ТекРодитель.ПодразделениеКомпании Цикл
			ТекРодитель = ТекРодитель.ПолучитьРодителя();
		КонецЦикла;
		
		ТекРодитель = ТекРодитель.ПолучитьЭлементы().Добавить();
		ТекРодитель.ПодразделениеКомпании = Выборка.Ссылка;
		ТекРодитель.Выбран = (ВыбранныеПодразделенияКомпании.Найти(Выборка.Ссылка) <> Неопределено);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеДерева()
	
	// настроим строку корневого подразделения
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодразделенияКомпании.ЭтоОсновноеПодразделение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодразделенияКомпанииВыбран.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодразделенияКомпанииПодразделениеКомпании.Имя);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,, Истина));
	
КонецПроцедуры

#КонецОбласти