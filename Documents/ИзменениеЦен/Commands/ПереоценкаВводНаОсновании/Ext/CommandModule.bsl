
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	СписокВыбораТипЦен = ПолучитьСписокВыбораТиповЦен(ПараметрКоманды);
	ДополнительныеПараметры = Новый Структура("ПараметрКоманды,ПараметрыВыполненияКоманды", ПараметрКоманды, ПараметрыВыполненияКоманды);
	Если СписокВыбораТипЦен.Количество() = 1 Тогда
		Подключаемый_ОбработкаРезультатаВыбораТипЦен(СписокВыбораТипЦен[0], ДополнительныеПараметры);
	ИначеЕсли СписокВыбораТипЦен.Количество() > 0 Тогда
		ОбработчикОповещения = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаВыбораТипЦен", ЭтотОбъект, ДополнительныеПараметры);
		СписокВыбораТипЦен.ПоказатьВыборЭлемента(ОбработчикОповещения, НСтр("ru = 'Выберите тип цен'"));
	Иначе
		Подключаемый_ОбработкаРезультатаВыбораТипЦен(ПредопределенноеЗначение("Справочник.ТипыЦен.ПустаяСсылка"), ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция получения типов цен документа
//
&НаСервере
Функция ПолучитьСписокВыбораТиповЦен(Документ)
	
	СписокТиповЦен = Новый СписокЗначений;
	
	Для Каждого ТекущаяСтрока Из Документ.ПараметрыРасчета Цикл
		Если НЕ ТекущаяСтрока.ТипЦен.Рассчитывается Тогда
			СписокТиповЦен.Добавить(ТекущаяСтрока.ТипЦен);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокТиповЦен;
	
КонецФункции // ПолучитьСписокВыбораТиповЦен()

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаВыбораТипЦен(РезультатОповещения, ДополнительныеПараметры) Экспорт
	
	Если РезультатОповещения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПараметрыВыполненияКоманды = ДополнительныеПараметры.ПараметрыВыполненияКоманды;
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Ссылка", ДополнительныеПараметры.ПараметрКоманды);
	Если ТипЗнч(РезультатОповещения) = Тип("ЭлементСпискаЗначений") Тогда
		ДанныеЗаполнения.Вставить("ТипЦен", РезультатОповещения.Значение);
	КонецЕсли;
	ПараметрыФормы = Новый Структура("Основание", ДанныеЗаполнения);
	ОткрытьФорму("Документ.Переоценка.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти