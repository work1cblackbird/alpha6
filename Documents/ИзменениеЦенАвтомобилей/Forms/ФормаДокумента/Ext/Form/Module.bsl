///////////////////////////////////////////////////////////////////////////////
// Модуль основной формы документа "Изменение цен автомобилей"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ИмяЭлементаКоманднойПанели", "ГлобальныеКоманды");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// УтверждениеДокументов
	УтверждениеДокументовСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	// Конец УтверждениеДокументов
	
	РаботаСФормой.ИнициализироватьМенюВыбораХозОперации(ЭтотОбъект);
	РаботаСФормой.РасставитьСвязиПараметровВыбораПоОрганизации(ЭтотОбъект, Объект);
	РаботаСФормой.ЗаблокироватьРедактированиеНомераИДатыДокумента(ЭтотОбъект, Объект);
	
	РазрешитьРедактированиеЦенИСумм = ПраваИНастройкиПользователя.Значение("РедактированиеЦенИСуммВНоменклатурныхТаблицах", Объект);
	РаботаСФормой.РазрешитьРедактированиеЦенИСумм(
		РаботаСФормой.ТиповыеПоляСуммовыхРеквизитов(ЭтотОбъект),
		РазрешитьРедактированиеЦенИСумм
	);
	РаботаСФормой.ОткрытьФормуТолькоДляПросмотра(ЭтотОбъект, Объект);
	
	УстановитьКолонкиРасчетныхЦен();
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
	КонецЕсли;
	
	ПараметрыДокумента = ОбщиеПараметрыДокументов.СформироватьПредставлениеПараметровДокумента(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// УтверждениеДокументов
	УтверждениеДокументовКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец УтверждениеДокументов
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	УправлениеДиалогомАльфаАвтоКлиент.ПриОткрытии(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ПараметрыДействия = Новый Структура;
	ОбработкаВыбораНаСервере(ВыбранноеЗначение, ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение, ПараметрыДействия=Неопределено)
	
	Если НЕ УправлениеДиалогомДокументаСервер.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ПараметрыДействия) Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// УтверждениеДокументов
	УтверждениеДокументовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец УтверждениеДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РаботаСФормойКлиент.ЗамерВремениЗапись("ИзменениеЦенАвтомобилей", ПараметрыЗаписи.РежимЗаписи,
		Объект.Автомобили.Количество() > 50);
		
	Отказ = Отказ Или РаботаСФормойКлиент.НачатьПроверкуПодразделенияДокументаИПользователя(
		Объект.ПодразделениеКомпании,
		Новый ОписаниеОповещения(
			"ПроверкаПодразделенияДокументаИПользователяЗавершение",
			ЭтотОбъект,
			Новый Структура("ПараметрыЗаписи", ПараметрыЗаписи)
		)
	);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	НастроитьПараметрыВыбораЭлементовФормы();
	
	Для Каждого ТекСтрока Из Объект.Автомобили Цикл
		ПересчитатьРассчетныеЦеныНаСервере(ТекСтрока);
	КонецЦикла;
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	РаботаСФормойКлиент.ОповеститьОЗаписиДокумента(Объект.Ссылка);
	РаботаСФормойКлиент.ОбновитьПодчиненныеСчета(Объект.Ссылка, Неопределено);
	
	Оповестить("ЗаписанДокументИзменениеЦенАвтомобилей");
	
КонецПроцедуры 

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Вставить("ПоказыватьПараметрыДокумента", Элементы.ПараметрыДокумента.Видимость);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки.Получить("ПоказыватьПараметрыДокумента") = Ложь Тогда
		Элементы.ПараметрыДокумента.Видимость = Ложь;
		Элементы.НомерДата         .Видимость = Ложь;
	КонецЕсли;
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ДатаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ИзменениеЦенАвтомобилей.ДатаПриИзменении(Объект, ПараметрыДействия);
	
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ДатаПриИзмененииНаСервере(ПараметрыДействия);
	
	Если Объект.Дата <> Объект.ДатаНачалаДействия Тогда
		ОповещениеВопроса = Новый ОписаниеОповещения("ИзменитьДатуНачалаДействия", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеВопроса, НСтр("ru = 'Дата документа изменена. Сменить дату начала действия цен автомобилей?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры 

&НаСервере
Процедура ХозОперацияПриИзмененииНаСервере(ПараметрыДействия = Неопределено)

	Документы.ИзменениеЦенАвтомобилей.ХозОперацияПриИзменении(Объект, ПараметрыДействия);
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ХозОперацияПриИзменении(Команда)
	
	УправлениеДиалогомДокументаКлиент.ОбработатьВыборХозОперации(Объект, Элементы, Команда.Имя);
	
	ПараметрыДействия = Новый Структура;
	ХозОперацияПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры 

&НаСервере
Процедура РасчетЦенОтПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ЗаполнитьБазовыеЦены();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура РасчетЦенОтПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	РасчетЦенОтПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);

КонецПроцедуры

&НаСервере
Процедура ТипЦенПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Документы.ИзменениеЦенАвтомобилей.ТипЦенПриИзменении(Объект, ПараметрыДействия);
	ЗаполнитьБазовыеЦены();
	УстановитьКолонкиРасчетныхЦен();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ТипЦенПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ТипЦенПриИзмененииНаСервере(ПараметрыДействия);
	УстановитьКолонкиРасчетныхЦен();
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОкруглятьДоПриИзменении(Элемент)
	
	ПересчитатьЦену(Ложь);
	
КонецПроцедуры 

&НаКлиенте
Процедура БазовыйТипЦенПриИзменении(Элемент)
	
	ЗаполнитьБазовыеЦены();
	
КонецПроцедуры 

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ПараметрыДействия = Новый Структура;
	ДокументОснованиеПриИзмененииНаСервере(ПараметрыДействия);
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	ЗаполнитьБазовыеЦены();
	
КонецПроцедуры

&НаСервере
Процедура ДокументОснованиеПриИзмененииНаСервере(ПараметрыДействия)
	
	ОбработкаРеквизитовДокументаСервер.ДокументОснованиеПриИзменении(Объект, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура СкладКомпанииПриИзменении(Элемент)
	
	ЗаполнитьБазовыеЦены();
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАвтомобили

&НаСервере
Процедура АвтомобилиПриИзмененииНаСервере()
	
	ТекущиеДанные = Объект.Автомобили.НайтиПоИдентификатору(Элементы.Автомобили.ТекущаяСтрока);
	ПересчитатьРассчетныеЦеныНаСервере(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиПриИзменении(Элемент)
	
	Если Элемент.ТекущаяСтрока <> Неопределено Тогда
		АвтомобилиПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	УправлениеДиалогомАльфаАвтоКлиент.
		АвтомобилиПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа, Параметр);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиПередНачаломИзменения(Элемент, Отказ)
	
	УправлениеДиалогомАльфаАвтоКлиент.АвтомобилиПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УправлениеДиалогомАльфаАвтоКлиент.АвтомобилиПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиПослеУдаления(Элемент)
	
	АвтомобилиПослеУдаленияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура АвтомобилиПослеУдаленияНаСервере(ПараметрыДействия = Неопределено)
		
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	УправлениеДиалогомАльфаАвтоКлиент.
		АвтомобилиПроверкаПеретаскивания(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле);
	
КонецПроцедуры

#Область ОбработчикиСобытийПолейТаблицыФормыАвтомобили

&НаКлиенте
Процедура АвтомобилиАвтомобильНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Список = Новый СписокЗначений;
	Список.Добавить("Автомобили");
	Список.Добавить("Модели");
	ВыборАвтомобиля = Новый ОписаниеОповещения("Подключаемый_ВыполнитьПослеВыбора", ЭтотОбъект);
	ПоказатьВыборИзСписка(ВыборАвтомобиля, Список, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиАвтомобильПриИзменении(Элемент)
	
	АвтомобилиАвтомобильПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура АвтомобилиАвтомобильПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Автомобили.НайтиПоИдентификатору(Элементы.Автомобили.ТекущаяСтрока);
	Документы.ИзменениеЦенАвтомобилей.АвтомобилиАвтомобильПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиЦенаПриИзменении(Элемент)
	
	АвтомобилиЦенаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура АвтомобилиЦенаПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Автомобили.НайтиПоИдентификатору(Элементы.Автомобили.ТекущаяСтрока);
	Документы.ИзменениеЦенАвтомобилей.АвтомобилиЦенаПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиВариантКомплектацииПриИзменении(Элемент)
	
	АвтомобилиВариантКомплектацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура АвтомобилиВариантКомплектацииПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Автомобили.НайтиПоИдентификатору(Элементы.Автомобили.ТекущаяСтрока);
	Документы.ИзменениеЦенАвтомобилей.АвтомобилиВариантКомплектацииПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиПроцентНаценкиПриИзменении(Элемент)
	
	АвтомобилиПроцентНаценкиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура АвтомобилиПроцентНаценкиПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Автомобили.НайтиПоИдентификатору(Элементы.Автомобили.ТекущаяСтрока);
	Документы.ИзменениеЦенАвтомобилей.АвтомобилиПроцентНаценкиПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобилиСуммаНаценкиПриИзменении(Элемент)
	
	АвтомобилиСуммаНаценкиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура АвтомобилиСуммаНаценкиПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	ТекущиеДанные = Объект.Автомобили.НайтиПоИдентификатору(Элементы.Автомобили.ТекущаяСтрока);
	Документы.ИзменениеЦенАвтомобилей.АвтомобилиСуммаНаценкиПриИзменении(Объект, ТекущиеДанные, ПараметрыДействия);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
//@skip-warning
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда,
                                                НавигационнаяСсылка = Неопределено,
                                                СтандартнаяОбработка = Неопределено)
	
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
    
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПересчитатьЦенуВДокументе(Команда)
	
	ПересчитатьЦену(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПересчитатьЦену(ОбновитьПроцентНаценки)
	
	Для Каждого СтрокаТЧ Из Объект.Автомобили Цикл
		Если ОбновитьПроцентНаценки Тогда
			СтрокаТЧ.ПроцентНаценки = Объект.ПроцентНаценки;
		КонецЕсли;
		
		Если СтрокаТЧ.ЦенаБазовая > 0 Тогда		
			СтрокаТЧ.Цена = СтрокаТЧ.ЦенаБазовая+СтрокаТЧ.ЦенаБазовая*СтрокаТЧ.ПроцентНаценки/100;
		КонецЕсли;
		
		СтрокаТЧ.СуммаНаценки = СтрокаТЧ.Цена - СтрокаТЧ.ЦенаБазовая;
		
		НоваяЦена = СтрокаТЧ.Цена;
		ДельтаОкругления    = ?(Объект.ОкруглятьДо = 0, 0, НоваяЦена/Объект.ОкруглятьДо);
		ДельтаОкругленияЦел = Цел(ДельтаОкругления);
		Если ДельтаОкругления <> ДельтаОкругленияЦел Тогда
			НоваяЦена = (ДельтаОкругленияЦел + 1)*Объект.ОкруглятьДо;
		КонецЕсли;
		
		Если НоваяЦена <> СтрокаТЧ.Цена Тогда
			СтрокаТЧ.Цена = НоваяЦена;
		КонецЕсли;
		// Пересчет расчетных цен документа
		ПересчитатьРассчетныеЦеныНаКлиенте(СтрокаТЧ);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьБазовыеЦены()
	
	Для Каждого Строка Из Объект.Автомобили Цикл
		Документы.ИзменениеЦенАвтомобилей.ЗаполнитьБазовуюЦену(Объект, Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьКоэффициентСкидкиНаценки(СтрокаТаблицы, БазовыйКоэффициент, ПроцентСкидкиНаценки)
	//КоэффициентСкидкиНаценки
	Если ПроцентСкидкиНаценки = 0 Тогда
		СтрокаТаблицы.Коэффициент = БазовыйКоэффициент;
	Иначе
		СтрокаТаблицы.Коэффициент = БазовыйКоэффициент+(БазовыйКоэффициент*(ПроцентСкидкиНаценки/100));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьТаблицуРасчетныхЦен(Запрос, ТаблицаНаценокРасчетныхЦен, БазовыйТипЦен, Знач БазовыйКоэффициент = 1)
	
	Запрос.УстановитьПараметр("БазовыйТипЦен", БазовыйТипЦен);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаНаценокРасчетныхЦен.Добавить();
		НоваяСтрока.Имя       = "Цена" + СтрЗаменить(Выборка.ТипЦен.УникальныйИдентификатор(),"-","_");
		НоваяСтрока.Заголовок = Выборка.Наименование;
		
		УстановитьКоэффициентСкидкиНаценки(НоваяСтрока, БазовыйКоэффициент, Выборка.Процент);
		ЗаполнитьТаблицуРасчетныхЦен(Запрос, ТаблицаНаценокРасчетныхЦен, Выборка.ТипЦен, НоваяСтрока.Коэффициент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКолонкиРасчетныхЦен()
	
	МассивНовыхЦенКолонки  	  = Новый Массив;
	МассивСтарыхЦенКолонки    = Новый Массив;
	МассивСтарыхЦенРеквизиты  = Новый Массив;
	// Заполним старые цены
	Для Каждого ТекСтрока Из ТаблицаНаценокРасчетныхЦен Цикл
		МассивСтарыхЦенКолонки.Добавить("Автомобили" + ТекСтрока.Имя);
		МассивСтарыхЦенРеквизиты.Добавить("Объект.Автомобили."+ТекСтрока.Имя);
	КонецЦикла;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТипыЦен.Ссылка КАК ТипЦен,
	|	ТипыЦен.Наименование КАК Наименование,
	|	ТипыЦен.ПроцентСкидкиНаценки КАК Процент
	|ИЗ
	|	Справочник.ТипыЦен КАК ТипыЦен
	|ГДЕ
	|	ТипыЦен.БазовыйТипЦен = &БазовыйТипЦен И 
	|	ТипыЦен.Рассчитывается И 
	|	ТипыЦен.ДляАвтомобилей = ИСТИНА");
	
	ТаблицаНаценокРасчетныхЦен.Очистить();
	ЗаполнитьТаблицуРасчетныхЦен(Запрос, ТаблицаНаценокРасчетныхЦен, Объект.ТипЦен);
	// Заполним новые цены
	Для Каждого ТекСтрока Из ТаблицаНаценокРасчетныхЦен Цикл
		НовыйРеквизит = Новый РеквизитФормы(ТекСтрока.Имя, Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)), "Объект.Автомобили", ТекСтрока.Заголовок);
		МассивНовыхЦенКолонки.Добавить(НовыйРеквизит);
	КонецЦикла;
	
	Если МассивНовыхЦенКолонки.Количество()>0 ИЛИ МассивСтарыхЦенКолонки.Количество()>0 Тогда
		// Удалим старые колонки
		Для Каждого ТекСтрока Из МассивСтарыхЦенКолонки Цикл
			Если Элементы.Найти(ТекСтрока) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Элементы.Удалить(Элементы[ТекСтрока]);
		КонецЦикла;
		
		ЭтаФорма.ИзменитьРеквизиты(МассивНовыхЦенКолонки, МассивСтарыхЦенРеквизиты);
		// Создадим новые колонки
		Для Каждого ТекСтрока Из МассивНовыхЦенКолонки Цикл
			НоваяКолонка = Элементы.Добавить("Автомобили"+ТекСтрока.Имя, Тип("ПолеФормы"), Элементы.Автомобили);
			НоваяКолонка.ПутьКДанным = "Объект.Автомобили."+ТекСтрока.Имя;
		КонецЦикла;
	КонецЕсли;
	// Пересчет расчетных цен документа
	Для Каждого ТекСтрока Из Объект.Автомобили Цикл
		ПересчитатьРассчетныеЦеныНаСервере(ТекСтрока);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьРассчетныеЦеныНаКлиенте(Строка)
	
	Для Каждого СтрокаНаценки Из ТаблицаНаценокРасчетныхЦен Цикл
		Строка[СтрокаНаценки.Имя] = Строка.Цена * СтрокаНаценки.Коэффициент;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьРассчетныеЦеныНаСервере(Строка)
	
	Для Каждого СтрокаНаценки Из ТаблицаНаценокРасчетныхЦен Цикл
		Строка[СтрокаНаценки.Имя] = Строка.Цена * СтрокаНаценки.Коэффициент;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДатуНачалаДействия(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ДатаНачалаДействия = Объект.Дата;
	КонецЕсли; 
	
КонецПроцедуры 

//@skip-warning
&НаКлиенте
Процедура ПроверкаПодразделенияДокументаИПользователяЗавершение(Контекст) Экспорт
	
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЗаполнениеОбъектов
&НаКлиенте
Процедура ПослеОбработкиЗаполнения(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ПослеОбработкиЗаполненияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеОбработкиЗаполненияНаСервере()
	
	ЗаполнениеОбъектовАльфаАвто.ПослеОбработкиЗаполнения(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ЗаполнениеОбъектов

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область ОбработчикиАльфаАвто

// Ядро
&НаКлиенте
Процедура ПараметрыДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;;
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыДокументаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеДиалогомДокументаКлиент.НастроитьПараметрыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	
	УправлениеДиалогомДокументаКлиент.РасширенноеРедактированиеПоляКомментарий(ЭтотОбъект, Элемент,
		СтандартнаяОбработка);
	
КонецПроцедуры
// Конец Ядро

#КонецОбласти

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомДокументаСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	// Вызываем общий обработчик действия
	УправлениеДиалогомДокументаСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	// установим видимость базы
	Элементы.СкладКомпании.Видимость     = (Объект.РасчетЦенОт = 2) ИЛИ (Объект.РасчетЦенОт = 3);
	Элементы.ДокументОснование.Видимость = (Объект.РасчетЦенОт = 1);
	Элементы.БазовыйТипЦен.Видимость     = (Объект.РасчетЦенОт = 0);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры=Неопределено)
	
	Если НЕ УправлениеДиалогомДокументаСервер.ОбработкаРезультатаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = "РазрешенияДляПересчета" Тогда
		// Пересчет расчетных цен документа
		Для Каждого ТекСтрока Из Объект.Автомобили Цикл
			ПересчитатьРассчетныеЦеныНаСервере(ТекСтрока);
		КонецЦикла;
	КонецЕсли;
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаОповещения(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если НЕ УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаОповещения(ЭтотОбъект, РезультатОповещения, ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаРезультатаОповещенияНаСервере(РезультатОповещения, ДополнительныеПараметры);
	
	Если ПолучитьЗначениеПараметраСтруктуры(РезультатОповещения, "ИзмененныеРеквизиты") <> Неопределено Тогда
		// Проверим - изменился ли реквизит "Дата"
		ИзмененнаяДата = ПолучитьЗначениеПараметраСтруктуры(РезультатОповещения.ИзмененныеРеквизиты, "Дата", Неопределено);
		
		Если ИзмененнаяДата <> Неопределено И ИзмененнаяДата <> Объект.ДатаНачалаДействия Тогда
			ОповещениеВопроса = Новый ОписаниеОповещения("ИзменитьДатуНачалаДействия", ЭтотОбъект);
			ПоказатьВопрос(ОповещениеВопроса, НСтр("ru = 'Дата документа изменена. Сменить дату начала действия цен автомобилей?'"), РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	ИначеЕсли ТипЗнч(РезультатОповещения) = Тип("СправочникСсылка.Автомобили") ИЛИ ТипЗнч(РезультатОповещения) = Тип("СправочникСсылка.Модели") Тогда
		Элементы.Автомобили.ТекущиеДанные.Автомобиль = РезультатОповещения;
		АвтомобилиАвтомобильПриИзменении(Элементы.АвтомобилиАвтомобиль);
	КонецЕсли;
	// Вызываем обработчик результата выполнения
	ОбработкаРезультатаВыполненияДействия(РезультатОповещения);
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ВыполнитьПослеВыбора(Результат, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Если Результат.Значение = "Автомобили" Тогда
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			ОткрытьФорму("Справочник.Автомобили.ФормаВыбора",
						ПараметрыОткрытия,
						ЭтаФорма,,,,
						Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", ЭтаФорма, ДополнительныеПараметры),
						РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Результат.Значение = "Модели" Тогда
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			ПараметрыОткрытия.Вставить("НазначениеМодели", ПредопределенноеЗначение("Перечисление.НазначениеМоделиАвтомобиля.ПродаетсяВАвтосалоне"));
			ОткрытьФорму("Справочник.Модели.ФормаВыбора",
						ПараметрыОткрытия,
						ЭтаФорма,,,,
						Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", ЭтаФорма, ДополнительныеПараметры),
						РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаРезультатаВыполненияДействия(ПараметрыДействия)
	
	ОбработкаРеквизитовДокументаКлиент.ПолучитьРазрешенияДляПересчета(ЭтотОбъект, ПараметрыДействия);
	УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаВыполненияДействия(ЭтотОбъект, ПараметрыДействия);
	
КонецПроцедуры 

#КонецОбласти

#Область ПараметрыДокумента

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаЗакрытияПараметровДокумента(РезультатОповещения, ДополнительныеПараметры = Неопределено) Экспорт
	                                                                                                                
	ОбработкаРезультатаЗакрытияПараметровДокументаНаСервере(РезультатОповещения, ДополнительныеПараметры);
	ОбработкаРеквизитовДокументаКлиент.ПолучитьРазрешенияДляПересчета(ЭтотОбъект, РезультатОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаЗакрытияПараметровДокументаНаСервере(РезультатОповещения, ДополнительныеПараметры)
	
	ОбщиеПараметрыДокументов.ПараметрыДокументаПриИзмененииРеквизитов(Объект, 	РезультатОповещения);
	ОбщиеПараметрыДокументов.ПараметрыДокументаПриИзменении(ЭтотОбъект,			РезультатОповещения);
	
	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область УтверждениеДокументов

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуУтверждения(Команда)
	
	УтверждениеДокументовКлиент.ОбработкаКомандыФормы(ЭтотОбъект, Команда, Объект.Ссылка);
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьОбработкуКомандыУтвержденияНаСервере(ПараметрыОбработки,
		ДополнительныеПараметры) Экспорт
	
	ОбработкаКомандыУтвержденияНаСервере(ПараметрыОбработки, ДополнительныеПараметры);
	Оповестить("ПослеУтвержденияДокументов", Объект.Ссылка, ИмяФормы);
	ОповеститьОбИзменении(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаКомандыУтвержденияНаСервере(ПараметрыОбработки, ДополнительныеПараметры)
	
	УтверждениеДокументовВызовСервера.ОбработкаКомандыФормы(ЭтотОбъект, ПараметрыОбработки.ИмяКоманды, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыУтвержденияДокументов()
	
	ОбновитьКомандыУтвержденияДокументовНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКомандыУтвержденияДокументовНаСервере()
	
	УтверждениеДокументовКлиентСервер.УстановитьДоступностьКнопокУтверждения(ЭтотОбъект, Объект, ТолькоПросмотр);
	УтверждениеДокументовВызовСервера.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, Объект, ТолькоПросмотр);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти