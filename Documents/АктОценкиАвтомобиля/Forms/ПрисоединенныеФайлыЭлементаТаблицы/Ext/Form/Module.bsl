
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Файлы) Тогда
		
		ВызватьИсключение НСтр("ru = 'Нет присоединенных файлов для отображения'");
		
	КонецЕсли;
	
	КэшАдресовВоВременномХранилище = Новый ФиксированноеСоответствие(Новый Соответствие());
	ОчередьФалов = Новый ФиксированныйМассив(Параметры.Файлы);
	ИндексТекущегоФайла = 0;
	ПоказатьКартинку(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СледующийНажатие(Элемент)
	
	НовыйИндексТекущегоФайла = ИндексСледующейКартинки();
	
	Если ИндексТекущегоФайла <> НовыйИндексТекущегоФайла Тогда
		
		ИндексТекущегоФайла = НовыйИндексТекущегоФайла;
		ПоказатьКартинку(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийНажатие(Элемент)
	
	НовыйИндексТекущегоФайла = ИндексПредыдущейКартинки();
	
	Если ИндексТекущегоФайла <> НовыйИндексТекущегоФайла Тогда
		
		ИндексТекущегоФайла = НовыйИндексТекущегоФайла;
		ПоказатьКартинку(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьТекущийФайл(Команда)
	
	ТекущийФайл = ОчередьФалов.Получить(ИндексТекущегоФайла);
	УдалитьТекущийФайлНаСервере();
	Оповестить("УдалениеПрисоединенногоФайла", ТекущийФайл, ЭтотОбъект);
	
	Если ОчередьФалов.Количество() = 0 Тогда
		
		Закрыть();
		
	Иначе
		
		ПоказатьКартинку(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьКартинку(Форма)
	
	ТекущийФайл = Форма.ОчередьФалов.Получить(Форма.ИндексТекущегоФайла);
	АдресКартинкиВКэше = Форма.КэшАдресовВоВременномХранилище.Получить(ТекущийФайл);
	
	Если АдресКартинкиВКэше <> Неопределено Тогда
		
		Форма.АдресКартинки = АдресКартинкиВКэше;
		
	КонецЕсли;
	
	НовыйАдресКартинки = РаботаСФайламиАльфаАвтоВызовСервера.НавигационнаяСсылкаКартинки(
		ТекущийФайл,
		Форма.УникальныйИдентификатор
	);
	ДобавитьАдресВКэш(Форма, НовыйАдресКартинки, ТекущийФайл);
	Форма.АдресКартинки = НовыйАдресКартинки;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьАдресВКэш(Форма, НовыйАдресКартинки, ТекущийФайл)
	
	КэшАдресовВоВременномХранилищеВр = Новый Соответствие(Форма.КэшАдресовВоВременномХранилище);
	КэшАдресовВоВременномХранилищеВр.Вставить(ТекущийФайл, НовыйАдресКартинки);
	Форма.КэшАдресовВоВременномХранилище = Новый ФиксированноеСоответствие(КэшАдресовВоВременномХранилищеВр);
	
КонецПроцедуры

&НаКлиенте
Функция ИндексСледующейКартинки()
	
	Если ИндексТекущегоФайла = ОчередьФалов.ВГраница() Тогда
		
		Возврат 0;
		
	КонецЕсли;
	
	Возврат ИндексТекущегоФайла + 1;
	
КонецФункции

&НаКлиенте
Функция ИндексПредыдущейКартинки()
	
	Если ИндексТекущегоФайла = 0 Тогда
		
		Возврат ОчередьФалов.ВГраница();
		
	КонецЕсли;
	
	Возврат ИндексТекущегоФайла - 1;
	
КонецФункции

&НаСервере
Процедура УдалитьТекущийФайлНаСервере()
	
	ТекущийФайл = ОчередьФалов.Получить(ИндексТекущегоФайла);
	
	Попытка
		
		ЗаблокироватьДанныеДляРедактирования(ТекущийФайл,, УникальныйИдентификатор);
		ТекущийФайл.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
		УбратьФайлИзОчереди();
		
	Исключение
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Удаление присоединенного файла'"),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.АктОценкиАвтомобиля,
			ТекущийФайл,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
		);
		ВызватьИсключение НСтр("ru = 'Не удалось удалить файл. Подробнее в журнале регистрации'");
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура УбратьФайлИзОчереди()
	
	ОчередьФаловИзменяемая = Новый Массив(ОчередьФалов);
	ОчередьФаловИзменяемая.Удалить(ИндексТекущегоФайла);
	ОчередьФалов = Новый ФиксированныйМассив(ОчередьФаловИзменяемая);
	
	Если ОчередьФаловИзменяемая.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ИндексТекущегоФайла > ОчередьФаловИзменяемая.ВГраница() Тогда
		
		ИндексТекущегоФайла = ОчередьФаловИзменяемая.ВГраница();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти