#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Хранилище") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ДанныеДокумента = ПолучитьИзВременногоХранилища(Параметры.Хранилище);
	
	ЗаполнитьДеревоЗаказНаряда(ДанныеДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщегоНазначенияАвтосалонКлиент.РазвернутьДерево(ЭтотОбъект, "ДеревоЗаказНаряда", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоЗаказНаряда

&НаКлиенте
Процедура ДеревоЗаказНарядаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если Строка = Неопределено Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	Приемник = ДеревоЗаказНаряда.НайтиПоИдентификатору(Строка);
	
	Если НЕ Приемник.ЭтоГруппа Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	Для Каждого Значение Из ПараметрыПеретаскивания.Значение Цикл
		
		Источник = ДеревоЗаказНаряда.НайтиПоИдентификатору(Значение);
		
		Если Источник.ЭтоГруппа Тогда
			ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			Возврат;
		КонецЕсли;
		
		Если Приемник.Представление = НСтр("ru = 'Нераспределенные товары и автоработы'") Тогда
			ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗаказНарядаПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗаказНарядаОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	ОбщегоНазначенияАвтосалонКлиент.РазвернутьДерево(ЭтотОбъект, "ДеревоЗаказНаряда", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПродолжитьЗапись(Команда)
	
	Если ВсеРаспределено() Тогда
		ПараметрыЗакрытия = ПараметрыЗакрытия();
		Закрыть(ПараметрыЗакрытия);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Остались нераспределенные автоработы/товары по причинам обращения.'")
		);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыЗакрытия()
	
	Дерево = РеквизитФормыВЗначение("ДеревоЗаказНаряда");
	ТаблицаРезультат = Новый ТаблицаЗначений;
	Для Каждого Колонка Из Дерево.Колонки Цикл
		ТаблицаРезультат.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	
	Для Каждого Строка Из Дерево.Строки Цикл
		Для Каждого ПодчиненнаяСтрока Из Строка.Строки Цикл
			ПодчиненнаяСтрока.ИдентификаторПричиныОбращения = Строка.ИдентификаторПричиныОбращения;
			ЗаполнитьЗначенияСвойств(ТаблицаРезультат.Добавить(), ПодчиненнаяСтрока);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаРезультат);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДеревоЗаказНаряда(ДанныеДокумента)
	
	ЭлементыДерева = ДеревоЗаказНаряда.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	Для Каждого ПричинаОбращения Из ДанныеДокумента.ПричиныОбращения Цикл
		
		НоваяСтрока = ЭлементыДерева.Добавить();
		
		НоваяСтрока.Представление   = ПричинаОбращения.ПричинаОбращения;
		НоваяСтрока.ЭтоГруппа         = Истина;
		НоваяСтрока.ИдентификаторПричиныОбращения = ПричинаОбращения.ИдентификаторПричиныОбращения;
		
	КонецЦикла;
	
	НоваяСтрока = ЭлементыДерева.Добавить();
	НоваяСтрока.Представление = НСтр("ru = 'Нераспределенные товары и автоработы'");
	НоваяСтрока.ЭтоГруппа = Истина;
	ЭлементыСтроки = НоваяСтрока.ПолучитьЭлементы();
	
	Для Каждого Товар Из ДанныеДокумента.ТаблицаТоваров Цикл
		НоваяСтрока = Элементыстроки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Товар);
		НоваяСтрока.Представление = Товар.Номенклатура;
	КонецЦикла;
	
	Для Каждого Авторабота Из ДанныеДокумента.ТаблицаАвторабот Цикл
		НоваяСтрока = ЭлементыСтроки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Авторабота, , "Представление");
		НоваяСтрока.ЭтоАвторабота = Истина;
		НоваяСтрока.Представление = Авторабота.Авторабота;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВсеРаспределено()
	
	Для Каждого Строка Из ДеревоЗаказНаряда.ПолучитьЭлементы() Цикл
		
		Если Не Строка.Представление = НСтр("ru = 'Нераспределенные товары и автоработы'") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка.ПолучитьЭлементы().Количество() > 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти