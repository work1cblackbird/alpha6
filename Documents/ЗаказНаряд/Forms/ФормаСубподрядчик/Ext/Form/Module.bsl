
#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Получим данные с документа
	Контрагент            = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Контрагент");
	ДоговорВзаиморасчетов = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ДоговорВзаиморасчетов");
	Авторабота            = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Авторабота", Неопределено);
	ЗаказНаряд            = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Документ");
	Организация           = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Организация");
	ПодразделениеКомпании = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ПодразделениеКомпании");
	ДатаНачала            = КонецДня(ТекущаяДатаСеанса());
	ТолькоПросмотр        = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ТолькоПросмотр", Ложь);
	
	// Если не передана авторабота, то запретим открытие формы
	Если Авторабота = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Авторабота) Тогда
		Элементы.НадписьАвторабота.Заголовок = Элементы.НадписьАвторабота.Заголовок + "<Авторабота не выбрана>";
	Иначе
		Элементы.НадписьАвторабота.Заголовок = Элементы.НадписьАвторабота.Заголовок + "<"+СокрЛП(Авторабота)+">";
	КонецЕсли;
	
	УправлениеДиалогомСервер();
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события возникающего на клиенте при изменении данных реквизита "Контрагент" в контексте сервера.
//
&НаСервере
Процедура КонтрагентПриИзмененииНаСервере(ПараметрыДействия = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		ДоговорВзаиморасчетов=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
	Иначе
		Если ЗначениеЗаполнено(ДоговорВзаиморасчетов) Тогда
			Если ДоговорВзаиморасчетов.Владелец <> Контрагент Тогда
				ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
			КонецЕсли; 
		КонецЕсли; 
		ДопПараметры=Новый Структура;
		ВидДоговора=Перечисления.ВидыДоговоров.Покупка;
		ДопПараметры.Вставить("ВидДоговора",ВидДоговора);
		ДопПараметры.Вставить("ХозОперация",Справочники.ХозОперации.УслугиПоСубподряду);
		ДоговорВзаиморасчетов = ОбработкаРеквизитовДокументаСервер.ПолучитьДоговорВзаиморасчетов(Контрагент, ВидДоговора, ЗаказНаряд, ДопПараметры, Истина);
	КонецЕсли;
	
	// Проверим наличие заметок по выбранному объекту.
	ЗаметкиПользователяАльфаАвто.ПроверитьЗаметкиПоПредмету(Контрагент, ПараметрыДействия);
	
	// Обновляем отображение элементов формы
	УправлениеДиалогомСервер();
	
КонецПроцедуры // КонтрагентПриИзмененииНаСервере()

// Обработчик события возникающего на клиенте при изменении данных реквизита "Контрагент".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
		// Определим структуру параметров обработки текущего события
	ПараметрыДействия = Новый Структура;
	
	// Обработаем событие в контексте сервера
	КонтрагентПриИзмененииНаСервере(ПараметрыДействия);
	
	// Обработаем изменения требующие ответа от пользователя
	ОбработкаРезультатаВыполненияДействия(ПараметрыДействия);
	
КонецПроцедуры // КонтрагентПриИзменении()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события нажатия кнопки "Применить".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура Применить(Команда)
	
	КонтрагентСубподряда=Новый Структура();
	КонтрагентСубподряда.Вставить("Контрагент",Контрагент);
	КонтрагентСубподряда.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
	Закрыть(КонтрагентСубподряда);
	
КонецПроцедуры // Применить()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеДиалогомСервер()
	
	Элементы.ДоговорВзаиморасчетов.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Контрагент);
	
КонецПроцедуры // УправлениеДиалогомСервер()

// Отображает результат выполнения действия.
//
// Параметры:
//  ПараметрыДействия - Структура - Набор параметров, использующихся при выполнения операции.
//
&НаКлиенте
Процедура ОбработкаРезультатаВыполненияДействия(ПараметрыДействия)
	
	// Вызываем общий обработчик результата выполнения действия
	УправлениеДиалогомДокументаКлиент.ОбработкаРезультатаВыполненияДействия(ЭтотОбъект, ПараметрыДействия);
	
КонецПроцедуры // ОбработкаРезультатаВыполненияДействия()

#КонецОбласти

