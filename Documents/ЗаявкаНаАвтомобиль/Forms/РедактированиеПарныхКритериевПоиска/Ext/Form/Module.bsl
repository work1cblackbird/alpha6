
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ГруппаКритериев = Параметры.ГруппаКритериев;
	ЭтотОбъект[ГруппаКритериев + "Начало"] = Параметры.Начало;
	ЭтотОбъект[ГруппаКритериев + "Конец"] = Параметры.Конец;
	НастроитьВидимостьГрупп();
	
	ФорматГодаВыпуска = АвтомобилиСервер.ПолучитьФорматПредставленияГодаВыпускаАвтомобиля(Неопределено);
	Элементы.ГодВыпускаНачало.ФорматРедактирования = ФорматГодаВыпуска;
	Элементы.ГодВыпускаКонец.ФорматРедактирования = ФорматГодаВыпуска;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Завершить(Команда)
	
	Закрыть(Новый Структура(
		"ГруппаКритериев,Начало,Конец",
		ГруппаКритериев,
		ЭтотОбъект[ГруппаКритериев + "Начало"],
		ЭтотОбъект[ГруппаКритериев + "Конец"]
	));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьВидимостьГрупп()
	
	ГруппыДляОбработки = СтрРазделить("ОбъемДвигателя,ГодВыпуска,Пробег,Цена", ",");
	
	Для Каждого ГруппаДляОбработки Из ГруппыДляОбработки Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			ГруппаДляОбработки,
			"Видимость",
			ГруппаДляОбработки = ГруппаКритериев
		);
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти