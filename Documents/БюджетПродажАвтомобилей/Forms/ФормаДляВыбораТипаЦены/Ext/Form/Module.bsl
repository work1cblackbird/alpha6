
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипЦен = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ТипЦен", Справочники.ТипыЦен.ПустаяСсылка());
	Курс   = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Курс", 1);
	Дата   = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Дата", ТекущаяДатаСеанса());
	
	ИспользоватьРасчетныйТипЦен = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ИспользоватьРасчетныйТипЦен", Истина);
	
	Если НЕ ИспользоватьРасчетныйТипЦен Тогда
		УправлениеДиалогомСервер.ОбновитьПараметрВыбора(Элементы.ТипЦен.ПараметрыВыбора, "Отбор.Рассчитывается", Ложь);
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтотОбъект.ВладелецФормы = Неопределено Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Запрещен самостоятельный вызов.'"),,
			НСтр("ru = 'Данная форма используется другими объектами конфигурации.'"),
			БиблиотекаКартинок.Предупреждение32,
			СтатусОповещенияПользователя.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ТолькоПросмотр = ВладелецФормы.ТолькоПросмотр;
	
	ТипЦенПриИзменении(Элементы.ТипЦен);
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ТипЦенПриИзмененииНаСервере()
	
	Если НЕ ИспользоватьРасчетныйТипЦен И ТипЦен.Рассчитывается Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Использование расчетных типов цен запрещено.'"));
		ТипЦен = ТипЦенКопия;
		Возврат;
	КонецЕсли;
	
	ТипЦенКопия = ТипЦен;
	
	ВалютаЦены = ТипЦен.ВалютаЦены;
	
	Если ЗначениеЗаполнено(ВалютаЦены) Тогда
		Если ТипЦен.ВВалютеУчета Тогда
			Элементы.НадписьОтношениеВалют.Видимость = Ложь;
			Элементы.Курс.Видимость       = Ложь;
			Элементы.НадписьВВалютеУчетаНоменклатуры.Видимость = Истина;
		Иначе
			Элементы.НадписьОтношениеВалют.Видимость = Истина;
			Элементы.Курс.Видимость       = Истина;
			Элементы.НадписьВВалютеУчетаНоменклатуры.Видимость = Ложь;
			ВалютаУпрУчета    = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			СтруктураКурсаУпр = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаУпрУчета, Дата);
			
			НоваяВалюта    = ВалютаЦены;
			СтруктураКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(НоваяВалюта, Дата);
			
			Курс = ( СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность) ) / ( СтруктураКурсаУпр.Курс / ?(СтруктураКурсаУпр.Кратность = 0, 1, СтруктураКурсаУпр.Кратность) );
			
			Если НоваяВалюта = ВалютаУпрУчета Тогда
				Элементы.Курс.Доступность = Ложь;
			Иначе
				Элементы.Курс.Доступность = Истина;
			КонецЕсли;
			
			Элементы.НадписьОтношениеВалют.Заголовок = "" + НоваяВалюта + "/" + ВалютаУпрУчета;
		КонецЕсли;
	Иначе
		Элементы.НадписьОтношениеВалют.Видимость = Ложь;
		Элементы.Курс.Видимость       = Ложь;
		Элементы.НадписьВВалютеУчетаНоменклатуры.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЦенПриИзменении(Элемент)
	
	ТипЦенПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура();
	Результат.Вставить("КурсДокумента", Курс);
	Результат.Вставить("ТипЦен",        ТипЦен);
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

