#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда	

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область ОбновлениеВерсииИБ

// Определяет настройки начального заполнения элементов
//
// Параметры:
//  Настройки - Структура - настройки заполнения
//   * ПриНачальномЗаполненииЭлемента - Булево - Если Истина, то для каждого элемента будет
//      вызвана процедура индивидуального заполнения ПриНачальномЗаполненииЭлемента.
//
Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
	Настройки.ПриНачальномЗаполненииЭлемента = Ложь;
	
КонецПроцедуры

// Вызывается при начальном заполнении справочника Организации.
//
// Параметры:
//  КодыЯзыков - Массив - список языков конфигурации. Актуально для мультиязычных конфигураций.
//  Элементы   - ТаблицаЗначений - данные заполнения. Состав колонок соответствует набору реквизитов
//                                 справочника.
//  ТабличныеЧасти - Структура - описание табличных частей объекта, где:
//   * Ключ - Строка - имя табличной части;
//   * Значение - ТаблицаЗначений - табличная часть в виде таблицы значений, структуру которой
//                                  необходимо скопировать перед заполнением.
//                                  Например:
//                                    Элемент.Ключи = ТабличныеЧасти.Ключи.Скопировать();
//                                    ЭлементТЧ = Элемент.Ключи.Добавить();
//                                    ЭлементТЧ.ИмяКлюча = "Первичный";
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	Для Каждого Характеристика Из СвойстваИХарактеристикиАвтомобилейОсновные() Цикл
		
		Элемент = Элементы.Добавить();
		Элемент.ИмяПредопределенныхДанных = Характеристика.ИмяПредопределенныхДанных;
		Элемент.Код = Характеристика.Код;
		Элемент.Наименование = Характеристика.Наименование;
		Элемент.ИспользуетсяДляОценки = Истина;
		Элемент.ДобавлятьПоУмолчаниюВАктОсмотра = Истина;
		Элемент.Родитель = ОсновныеХарактеристики;
		
	КонецЦикла;
	
	Для Каждого Характеристика Из СвойстваИХарактеристикиАвтомобилейДляОценки() Цикл
		
		Элемент = Элементы.Добавить();
		Элемент.ИмяПредопределенныхДанных = Характеристика.ИмяПредопределенныхДанных;
		Элемент.Код = Характеристика.Код;
		Элемент.Наименование = Характеристика.Наименование;
		Элемент.ИспользуетсяДляОценки = Истина;
		Элемент.ДобавлятьПоУмолчаниюВАктОсмотра = Истина;
		Элемент.Родитель = Оценка;
		
	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьСвойствоПривода() Экспорт
	
	УдаляемыйПривод = УдалитьПривод.ПолучитьОбъект();
	
	Если УдаляемыйПривод.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	НовыйПривод = Привод.ПолучитьОбъект();
	Если Не НовыйПривод.ИспользуетсяДляОценки Тогда
		НовыйПривод.ИспользуетсяДляОценки = УдаляемыйПривод.ИспользуетсяДляОценки;
	КонецЕсли;
	Если Не НовыйПривод.ДобавлятьПоУмолчаниюВАктОсмотра Тогда
		НовыйПривод.ДобавлятьПоУмолчаниюВАктОсмотра = УдаляемыйПривод.ДобавлятьПоУмолчаниюВАктОсмотра;
	КонецЕсли;
	НовыйПривод.ОбменДанными.Загрузка = Истина;
	
	Попытка
		НовыйПривод.Записать();
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Запись нового свойства автомобиля ""Привод""'"),
			УровеньЖурналаРегистрации.Ошибка,
			,
			Привод,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	УдаляемыйПривод.ПометкаУдаления = Истина;
	УдаляемыйПривод.ИспользуетсяДляОценки = Ложь;
	УдаляемыйПривод.ДобавлятьПоУмолчаниюВАктОсмотра = Ложь;
	УдаляемыйПривод.ОбменДанными.Загрузка = Истина;
	
	Попытка
		УдаляемыйПривод.Записать();
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Запись удаляемого свойства автомобиля ""Привод""'"),
			УровеньЖурналаРегистрации.Ошибка,
			,
			Привод,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыОбработкиРеквизитовОбъекта

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Массив - Возвращаемый массив содержит строковые идентификаторы реквизитов.
//
Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	// Обязательные реквизиты объекта
	ОбязательныеРеквизиты = Новый Массив();
	ОбязательныеРеквизиты.Добавить("Наименование");
	
	// Возвращаем сформированные параметры проверки
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет уникальности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Структура - Возвращаемая структура содержит строковые идентификаторы реквизитов.
//  Для описания проверки табличных частей используется
//  вложенная структура.
//
Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	// Структура уникальных реквизитов
	УникальныеРеквизиты = Новый Структура();
	УникальныеРеквизиты.Вставить("Код");
	
	// Возвращаем сформированные параметры проверки
	Возврат УникальныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СвойстваИХарактеристикиАвтомобилейОсновные()
	
	СвойстваИХарактеристикиАвтомобилей = Новый Массив();
	СвойстваИХарактеристикиАвтомобилей.Добавить(ABS);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ESP);
	СвойстваИХарактеристикиАвтомобилей.Добавить(АдаптивныйКруизКонтроль);
	СвойстваИХарактеристикиАвтомобилей.Добавить(АнтиоткатнаяСистема);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Аудио);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ДатчикДождя);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ДатчикСвета);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Климат);
	СвойстваИХарактеристикиАвтомобилей.Добавить(КруизКонтроль);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Ксенон);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ЛевыйРуль);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ЛитыеДиски);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Люк);
	СвойстваИХарактеристикиАвтомобилей.Добавить(МаксимальноРазрешеннаяМасса);
	СвойстваИХарактеристикиАвтомобилей.Добавить(МассаБезНагрузки);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Навигация);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ОбогревЗеркал);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ОмывательФар);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Парктроник);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ПодогревРуля);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ПодогревСидений);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ПодушкиБезопасности);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Привод);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ПротивопробуксовочнаяСистема);
	СвойстваИХарактеристикиАвтомобилей.Добавить(РегулировкаРуля);
	СвойстваИХарактеристикиАвтомобилей.Добавить(РегулировкаСиденияВодителя);
	СвойстваИХарактеристикиАвтомобилей.Добавить(РегулировкаСиденияПассажира);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Тонировка);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ЧислоМест);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ЭлектроприводЗеркал);
	Возврат СвойстваИХарактеристикиАвтомобилей;
	
КонецФункции

Функция СвойстваИХарактеристикиАвтомобилейДляОценки()
	
	СвойстваИХарактеристикиАвтомобилей = Новый Массив();
	СвойстваИХарактеристикиАвтомобилей.Добавить(ДополнительныйИмобилайзер);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ЗаводскаяГарантия);
	СвойстваИХарактеристикиАвтомобилей.Добавить(КоличествоСобственников);
	СвойстваИХарактеристикиАвтомобилей.Добавить(КузовнойРемонтЗамена);
	СвойстваИХарактеристикиАвтомобилей.Добавить(КузовнойРемонтОкраска);
	СвойстваИХарактеристикиАвтомобилей.Добавить(КузовнойРемонт);
	СвойстваИХарактеристикиАвтомобилей.Добавить(Кондиционер);
	СвойстваИХарактеристикиАвтомобилей.Добавить(СрабатываниеПодушекБезопасности);
	СвойстваИХарактеристикиАвтомобилей.Добавить(РемонтРамыКузова);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ОригиналПТС);
	СвойстваИХарактеристикиАвтомобилей.Добавить(ПробегЗаГод);
	СвойстваИХарактеристикиАвтомобилей.Добавить(СервиснаяКнижкаСОтметками);
	СвойстваИХарактеристикиАвтомобилей.Добавить(СезонностьШин);
	СвойстваИХарактеристикиАвтомобилей.Добавить(МодельПриобретаемогоАвтомобиля);
	Возврат СвойстваИХарактеристикиАвтомобилей;
	
КонецФункции

#КонецОбласти

#КонецЕсли
