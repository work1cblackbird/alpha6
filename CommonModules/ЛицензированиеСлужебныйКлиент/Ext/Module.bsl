// Пример модуля для встраивания в конфигурации на базе БСП.

// Общий модуль "Лицензирование служебный клиент"

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ СИСТЕМЫ

// Обработчик перед началом работы системы.
//
// Параметры:
//  Параметры - Структура - Дополнительные параметры.
//
Процедура ПередНачаломРаботыСистемы(Параметры) Экспорт
	
	ПараметрыРаботы = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если ПараметрыРаботы.СостояниеСистемыЗащиты.Ошибка Тогда
		
		Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения("ИнтерактивнаяОбработкаНастройкиСистемыЗащиты", ЭтотОбъект, ПараметрыРаботы.СостояниеСистемыЗащиты);
		
	КонецЕсли;
	
КонецПроцедуры // ПередНачаломРаботыСистемы()

// Обработчик при завершении работы системы.
//
Процедура ПриЗавершенииРаботыСистемы() Экспорт
	
	ОписаниеОшибки = "";
	
	ЛицензированиеСервер.ЗавершениеРаботыСистемыЛицензирования(ОписаниеОшибки);
	
КонецПроцедуры // ПриЗавершенииРаботыСистемы()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Обработчик настройки системы защиты.
//
// Параметры:
//  Параметры              - Структура - Дополнительные параметры.
//  СостояниеСистемыЗащиты - Массив    - Результат запуска.
//
Процедура ИнтерактивнаяОбработкаНастройкиСистемыЗащиты(Параметры, СостояниеСистемыЗащиты) Экспорт
	
	ПараметрыФормы = Новый Структура("РезультатыЗапуска,ПрограммноеОткрытиеФормы", СостояниеСистемыЗащиты.РезультатыЗапуска, ИСТИНА);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИнтерактивнаяОбработкаНастройкиСистемыЗащитыОбработкаРезультата", ЭтотОбъект, Параметры);
	
	ОткрытьФорму("Обработка.УправлениеЛицензированием.Форма.СостояниеСистемыЗащиты", ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры // ИнтерактивнаяОбработкаНастройкиСистемыЗащиты()

// Обработчик события возникающего при получении результата работы подчиненной формы.
//
// Параметры:
//  РезультатЗакрытия  - Произвольный - Результат выполнения операции в подчиненной форме.
//  Параметры - Произвольный - Дополнительные параметры вызова обработчика.
//
Процедура ИнтерактивнаяОбработкаНастройкиСистемыЗащитыОбработкаРезультата(РезультатЗакрытия, Параметры=Неопределено) Экспорт
	
	Параметры.Отказ = (РезультатЗакрытия=Неопределено) ИЛИ РезультатЗакрытия;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, ЛОЖЬ);
	
КонецПроцедуры // ИнтерактивнаяОбработкаНастройкиСистемыЗащитыОбработкаРезультата()

#КонецОбласти
