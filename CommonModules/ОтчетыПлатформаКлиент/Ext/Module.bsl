
#Область ПрограммныйИнтерфейс

// Получение элементов для дерева.
//
// Параметры:
//  ТекущиеДанные    - ДанныеФормыЭлементДерева - Текущие данные таблицы элементов.
//  ТаблицаЭлементов - ДанныеФормыДерево        - Таблица элементов.
// 
// Возвращаемое значение:
//  ДанныеФормыЭлементДерева - Полученный элемент дерева.
//
Функция ПолучитьЭлементыДляДерева(ТекущиеДанные, ТаблицаЭлементов) Экспорт
	
	Если ТекущиеДанные = Неопределено Тогда
		Результат = ТаблицаЭлементов.ПолучитьЭлементы();
	ИначеЕсли ТекущиеДанные.Папка Тогда
		Результат = ТекущиеДанные.ПолучитьЭлементы();
	ИначеЕсли (НЕ ТекущиеДанные.ПолучитьРодителя() = Неопределено) Тогда
		РодительСтроки = ТекущиеДанные.ПолучитьРодителя();
		Результат = РодительСтроки.ПолучитьЭлементы();
	Иначе
		Результат = ТаблицаЭлементов.ПолучитьЭлементы();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Открытие формы отчета.
//
// Параметры:
//  КлючОтчета                  - Строка                                    - Имя отчета.
//  КлючВарианта                - Строка                                    - Ключ варианта отчета.
//  НастройкиВарианта           - НастройкиКомпоновкиДанных                 - Настройка варианта отчета.
//  ИсточникДоступныхНастроек   - ИсточникДоступныхНастроекКомпоновкиДанных - Источник настроек отчета.
//  КлючНазначенияИспользования - Строка                                    - Ключ назначения использования.
//  Отбор                       - Структура                                 - Отбор отчета.
//  НачалоПериода               - Дата                                      - Дата начала формирования отчета.
//  КонецПериода                - Дата                                      - Дата окончания формирования отчета.
//  СформироватьПриОткрытии     - Булево                                    - Признак вывода отчета при открытии формы.
//
Процедура ОткрытьОтчет(
	КлючОтчета,
	КлючВарианта = Неопределено,
	НастройкиВарианта = Неопределено,
	ИсточникДоступныхНастроек = Неопределено,
	КлючНазначенияИспользования = Неопределено,
	Отбор = Неопределено,
	НачалоПериода = Неопределено,
	КонецПериода = Неопределено,
	СформироватьПриОткрытии = Истина) Экспорт
	
	Если Отбор = Неопределено Тогда
		Отбор = Новый Структура;
	КонецЕсли;
	
	// Начало периода
	Если НЕ НачалоПериода = Неопределено Тогда
		Отбор.Вставить("НачалоПериода", НачалоДня(НачалоПериода));
	КонецЕсли;
	
	// Конец периода
	Если НЕ КонецПериода = Неопределено Тогда
		
		ЗначениеПериода = ?(КонецПериода = Дата(1, 1, 1), КонецДня(ОбщегоНазначенияКлиент.ДатаСеанса()), КонецДня(КонецПериода));
		Отбор.Вставить("КонецПериода", ЗначениеПериода);
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                     НастройкиВарианта);
	СтруктураПараметров.Вставить("КлючВарианта",                КлючВарианта);
	СтруктураПараметров.Вставить("КлючНазначенияИспользования", КлючНазначенияИспользования);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии",     СформироватьПриОткрытии);
	СтруктураПараметров.Вставить("ИсточникДоступныхНастроек",   ИсточникДоступныхНастроек);
	СтруктураПараметров.Вставить("Отбор",                       Отбор);
	
	ОткрытьФорму(КлючОтчета + ".Форма", СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти