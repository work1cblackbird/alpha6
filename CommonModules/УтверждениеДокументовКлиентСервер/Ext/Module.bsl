////////////////////////////////////////////////////////////////////////////////
// Общий модуль "Утверждение документов (вызов сервера)"
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Устанавливает доступность кнопок в подменю утверждения документов
//
// Параметры:
//  Форма              - УправляемаяФорма - Форма, в которой возникло событие.
//  Документ     - ДокументСсылка, ДанныеФормыСтруктура - Документ, для которого настраиваются доступность команд утверждения
//  ИзменениеЗапрещено - Булево           - Признак доступности форму для изменений.
//
Процедура УстановитьДоступностьКнопокУтверждения(Форма, Знач Документ, ИзменениеЗапрещено = Ложь) Экспорт
	
	ГруппаКнопок = Форма.Элементы.Найти("ПодменюИзменениеСтатусаУтвержденияДокументов");
	ОсновнаяГруппа = Форма.Элементы.Найти("ПодменюУтверждениеДокументов");
	
	Если ГруппаКнопок = Неопределено ИЛИ ОсновнаяГруппа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Документ = Неопределено Тогда
		ОсновнаяГруппа.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДанныеФормыСтруктура") Тогда
		
		// для группировки строк
		Если НЕ Документ.Свойство("Ссылка") Тогда
			ОсновнаяГруппа.Доступность = Ложь;
			Возврат;
		КонецЕсли;
		
		Документ = Документ.Ссылка;
		
	КонецЕсли;
	
	ОсновнаяГруппа.Доступность = Истина;
	
	ПолноеИмяОбъекта = РаботаСФормойКлиентСервер.ПолноеИмяОбъекта(Форма);
	Если СтрНайти(ПолноеИмяОбъекта, "ЖурналДокументов.") Тогда
		ПолноеИмяОбъекта = "";
	КонецЕсли;
	
	СтатусыДокумента = УтверждениеДокументовВызовСервера.СтатусыДокумента(Документ, ПолноеИмяОбъекта,
		НЕ ИзменениеЗапрещено И ТипЗнч(Документ) <> Тип("СтрокаГруппировкиДинамическогоСписка"));
	
	Для Каждого Кнопка Из ГруппаКнопок.ПодчиненныеЭлементы Цикл
		
		ИмяСтатуса = Сред(Кнопка.Имя, 28);
		СтатусДоступен = СтатусыДокумента.Возможные.НайтиПоЗначению(ИмяСтатуса) <> Неопределено
			И СтатусыДокумента.Доступные.НайтиПоЗначению(СтатусыДокумента.Все.Получить(ИмяСтатуса)) <> Неопределено;
		
		Если Кнопка.Доступность <> СтатусДоступен Тогда
			
			Кнопка.Доступность = СтатусДоступен;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти