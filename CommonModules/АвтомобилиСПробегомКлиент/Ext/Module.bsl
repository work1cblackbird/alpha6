
#Область ПрограммныйИнтерфейс

// Обновление отчета по автомобилям с пробегом.
//
// Параметры:
//  Форма - Форма - форма для вывода отчета;
//  Документ - ДокументСсылка.ЗаказНаАвтомобиль, ДокументСсылка.ПоступлениеАвтомобилей - ссылка на заказ на автомобиль;
//  ОтборСвязанныеДокументы - Массив - массив отборов отчета;
//  Колонка - Строка - имя реквизита заказа.
// 
Процедура НачатьОбновлениеИнформацииОРаспределении(
	Форма,
	Документ,
	ОтборСвязанныеДокументы = Неопределено,
	Колонка = "Источник") Экспорт
	
	ДлительныеОперацииАвтосервисКлиентСервер.ВключитьРежимОжиданияПоляТабличногоДокумента(
		Форма.Элементы.РаспределенияДенежныхСредств
	);
	
	ПараметрыПроцедуры = Новый Структура(
		"Документ,ОтборСвязанныеДокументы,Колонка",
		Документ,
		ОтборСвязанныеДокументы,
		Колонка
	);
	
	Ответ = АвтомобилиСПробегомВызовСервера.НачатьОбновлениеИнформацииОРаспределении(
		Форма.УникальныйИдентификатор,
		ПараметрыПроцедуры
	);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	Ручка = Новый ОписаниеОповещения("ОбновлениеИнформацииОРаспределенииЗавершение", Форма);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Ответ, Ручка, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти