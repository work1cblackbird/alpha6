
#Область ПрограммныйИнтерфейс

// Автомобили, которыми владел клиент с временными интервалами.
// Если дата окончания пустая, значит этим автомобилем владеет сейчас.
//
// Параметры:
//  Клиент - СправочникСсылка.Контрагенты - 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - с колонками:
//   * Автомобиль - СправочникСсылка.Автомобили;
//   * ДатаНачала - Дата;
//   * ДатаОкончания - Дата;
//
Функция ИсторияАвтомобилей(Клиент) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Автомобили.Период КАК Период,
		|	Автомобили.Автомобиль КАК Автомобиль
		|ПОМЕСТИТЬ ДатыВладенияАвтомобилем
		|ИЗ
		|	РегистрСведений.Автомобили КАК Автомобили
		|ГДЕ
		|	Автомобили.ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)
		|	И Автомобили.Значение = &Клиент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ДатыВладенияАвтомобилем.Период) КАК Период,
		|	ДатыВладенияАвтомобилем.Автомобиль КАК Автомобиль
		|ПОМЕСТИТЬ НачалоВладениемАвтомобиля
		|ИЗ
		|	ДатыВладенияАвтомобилем КАК ДатыВладенияАвтомобилем
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыВладенияАвтомобилем.Автомобиль
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ДатыВладенияАвтомобилем.Период) КАК Период,
		|	ДатыВладенияАвтомобилем.Автомобиль КАК Автомобиль
		|ПОМЕСТИТЬ ПоследниееВладениеАвтомобилем
		|ИЗ
		|	ДатыВладенияАвтомобилем КАК ДатыВладенияАвтомобилем
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыВладенияАвтомобилем.Автомобиль
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПоследниееВладениеАвтомобилем.Автомобиль КАК Автомобиль,
		|	МИНИМУМ(ЕСТЬNULL(Автомобили.Период, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))) КАК ОкончаниеПериода
		|ПОМЕСТИТЬ ОкончаниеВладения
		|ИЗ
		|	ПоследниееВладениеАвтомобилем КАК ПоследниееВладениеАвтомобилем
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили КАК Автомобили
		|		ПО ПоследниееВладениеАвтомобилем.Период < Автомобили.Период
		|			И ПоследниееВладениеАвтомобилем.Автомобиль = Автомобили.Автомобиль
		|			И (Автомобили.ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин))
		|			И (Автомобили.Значение <> &Клиент)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоследниееВладениеАвтомобилем.Автомобиль
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачалоВладениемАвтомобиля.Автомобиль КАК Автомобиль,
		|	НачалоВладениемАвтомобиля.Период КАК ДатаНачала,
		|	ОкончаниеВладения.ОкончаниеПериода КАК ДатаОкончания
		|ИЗ
		|	НачалоВладениемАвтомобиля КАК НачалоВладениемАвтомобиля
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОкончаниеВладения КАК ОкончаниеВладения
		|		ПО НачалоВладениемАвтомобиля.Автомобиль = ОкончаниеВладения.Автомобиль
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаНачала УБЫВ"
	);
	Запрос.УстановитьПараметр("Клиент", Клиент);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти