
#Область ПрограммныйИнтерфейс

// Формирует данные номенклатуры для таблицы "ПодобранныеПозиции".
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура, для которой формируются данные
//
// Возвращаемое значение:
//  Структура - структура с полями:
//   * ВладелецХарактеристики - СправочникСсылка.Номенклатура,
//								СправочникСсылка.ТипыНоменклатуры, НЕОПРЕДЕЛЕНО - владелец характеристики.
//
Функция ДанныеПодобраннойНоменклатуры(Номенклатура) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		
		Возврат Новый Структура("ВладелецХарактеристики");
		
	КонецЕсли;
	
	Возврат СостояниеОбеспеченияВызовСервера.ДанныеПодобраннойНоменклатуры(Номенклатура);
	
КонецФункции // ДанныеПодобраннойНоменклатуры()

// Открывает обработку Состояние обеспечения с переданными параметрами.
//
// Параметры:
//  Форма				- ФормаКлиентскогоПриложения	- Форма владелец подбора.
//  ПараметрыОткрытия	- Структура						- структура параметров:
//    Организация					- СправочникСсылка.Организации - Организация, по которой осуществляется отбор.
//    ПодразделениеКомпании			- СправочникСсылка.ПодразделенияКомпании - Подразделение, по которому
//																				осуществляется отбор.
//    Менеджер						- СправочникСсылка.Сотрудники - Сотрудник, по которому осуществляется отбор.
//    Клиент						- СправочникСсылка.Контрагенты - Контрагент, по которому осуществляется отбор.
//    Номенклатура					- СправочникСсылка.Номенклатура - Номенклатура, по которой осуществляется отбор.
//    ЗаказПокупателя				- ДокументСсылка.ЗаказПокупателя - Заказ, по которому осуществляется отбор.
//    ДокументОснование				- ДокументСсылка.ЗаявкаНаРемонт, ДокументСсылка.ОтчетКомиссионера,
//									  ДокументСсылка.ЗаказВнутренний, ДокументСсылка.ЗаказНаряд,
//									  ДокументСсылка.СчетНаОплату, ДокументСсылка.Событие,
//									  ДокументСсылка.ПоступлениеТоваров,
//									  ДокументСсылка.ЗаказПокупателя - Документ-основание,по которому осуществляется отбор.
//    СостояниеГотовКОтгрузке		- Булево - выводит заказы с состоянием "ГотовКОтгрузке";
//    СостояниеОжидаетПоставки		- Булево - выводит заказы с состоянием "ОжидаетПоставки";
//    СостояниеТребуетсяОбеспечение	- Булево - выводит заказы с состоянием "ТребуетсяОбеспечение".
//
Процедура ОткрытьСостояниеОбеспечения(Форма, ПараметрыОткрытия) Экспорт
	
	ОткрытьФорму("Обработка.СостояниеОбеспечения.Форма",
		ПараметрыОткрытия,
		Форма,
		, , , ,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры // ОткрытьСостояниеОбеспечения()

#КонецОбласти