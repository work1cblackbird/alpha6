
#Область СлужебныйПрограммныйИнтерфейс

// Процедура - Дополнить объект общими реквизитами
//
// Параметры:
//  МетаданныеОбъект - Метаданные - Описание объекта.
//  Реквизиты - см. РеквизитыОбъектаНазначения
//  Префикс - Строка - Дополнительный индикатор объекта.
//  СведенияОСпискеРеквизитов - Структура:
//   * СписокРеквизитов - Массив
//   * СписокСодержитДанные - Булево
//  СведенияОИсключаемыхРеквизитах - Структура:
//   * СписокРеквизитов - Массив
//   * СписокСодержитДанные - Булево
//
Процедура ДополнитьОбъектОбщимиРеквизитами(
		МетаданныеОбъект,
		Реквизиты,
		Префикс,
		СведенияОСпискеРеквизитов,
		СведенияОИсключаемыхРеквизитах) Экспорт
	
	Для Каждого Реквизит Из Метаданные.ОбщиеРеквизиты Цикл
		ЗначениеСостава = Реквизит.Состав.Найти(МетаданныеОбъект);
		Если ЗначениеСостава = Неопределено
			ИЛИ НЕ ЗначениеСостава.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать
			ИЛИ Реквизит.Тип.СодержитТип(Тип("ХранилищеЗначения")) Тогда
			Продолжить;
		КонецЕсли;
		ДобавитьРеквизитПоМетаданнымОбъекта(
			Реквизиты,
			Реквизит,
			Префикс,
			СведенияОСпискеРеквизитов,
			СведенияОИсключаемыхРеквизитах);
	КонецЦикла;
	
КонецПроцедуры // ДополнитьОбъектОбщимиРеквизитами()

// Проверка того, что передаваемое имя реквизита является общим реквизитом.
//
// Параметры:
//  ИмяПараметра - Строка - Имя проверяемого реквизита.
//  РеквизитНеНайден - Булево - Признак того, что рекизит ранее был найден.
//
Процедура ПроверитьОбщийРеквизит(ИмяПараметра, РеквизитНеНайден) Экспорт
	
	Если Не РеквизитНеНайден Тогда
		Возврат;
	КонецЕсли;
	
	ОбщийРеквизит = Метаданные.ОбщиеРеквизиты.Найти(ИмяПараметра);
	Если ОбщийРеквизит <> Неопределено Тогда
		РеквизитНеНайден = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ПроверитьОбщийРеквизит()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Описание
// 
// Параметры:
//  Реквизиты - см. РеквизитыОбъектаНазначения
//  Реквизит - ОбъектМетаданных
//  Префикс - Строка
//  СведенияОСпискеРеквизитов - Структура:
//   * СписокРеквизитов - Массив
//   * СписокСодержитДанные - Булево
//  СведенияОИсключаемыхРеквизитах - Структура:
//   * СписокРеквизитов - Массив
//   * СписокСодержитДанные - Булево
//
Процедура ДобавитьРеквизитПоМетаданнымОбъекта(Реквизиты, Реквизит, Префикс, СведенияОСпискеРеквизитов, СведенияОИсключаемыхРеквизитах)
	
	ИмяРеквизита = Неопределено;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		Если ТипЗнч(Реквизит) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения") Тогда
			ЗначенияСвойство = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизит, "ИдентификаторДляФормул, ТипЗначения, ФорматСвойства");
			ИмяРеквизита   = "~Свойство." + ЗначенияСвойство.ИдентификаторДляФормул;
			Представление = Строка(Реквизит);
			Тип           = ЗначенияСвойство.ТипЗначения;
			Формат        = ЗначенияСвойство.ФорматСвойства;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		Если ТипЗнч(Реквизит) = Тип("СправочникСсылка.ВидыКонтактнойИнформации") Тогда
			ИдентификаторДляФормул = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизит, "ИдентификаторДляФормул");
			ИмяРеквизита  =  "~КИ." + ИдентификаторДляФормул;
			Представление = Строка(Реквизит);
			Тип           = Новый ОписаниеТипов("Строка");
			Формат        = "";
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяРеквизита = Неопределено Тогда
		ИмяРеквизита  = Реквизит.Имя;
		Представление = Реквизит.Представление();
		Тип           = Реквизит.Тип;
		Формат        = Реквизит.Формат;
	КонецЕсли;
	
	Если СведенияОСпискеРеквизитов.СписокСодержитДанные
		И СведенияОСпискеРеквизитов.СписокРеквизитов.Найти(ВРег(СокрЛП(ИмяРеквизита))) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СведенияОИсключаемыхРеквизитах.СписокСодержитДанные
		И СведенияОИсключаемыхРеквизитах.СписокРеквизитов.Найти(ВРег(СокрЛП(ИмяРеквизита))) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовыйСтрока = Реквизиты.Добавить();
	НовыйСтрока.Имя           = Префикс + ИмяРеквизита;
	НовыйСтрока.Представление = Представление;
	НовыйСтрока.Тип           = Тип;
	НовыйСтрока.Формат        = Формат;
	
КонецПроцедуры

#КонецОбласти
