
#Область ПрограммныйИнтерфейс


// Проверяет наличие изменений по макету "Компонента печати штрихкодов" и актуализирует соответствующую компоненту.
//
Процедура ОбновитьВнешниеКомпонентыИзМакета() Экспорт
	
	ИспользуемыеКомпоненты = ВнешниеКомпонентыСервер.ИспользуемыеКомпоненты("ДляЗагрузки");
	
	МодульРаботаСФайламиСлужебныйКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ГенерацияШтрихкода");
	ОписаниеКомпоненты = МодульРаботаСФайламиСлужебныйКлиентСервер.ОписаниеКомпоненты();
	ИдентификаторКомпонентыШрихкода = ОписаниеКомпоненты.ИмяОбъекта;
	
	КомпонентаИзСправочника = ИспользуемыеКомпоненты.Найти(ИдентификаторКомпонентыШрихкода, "Идентификатор");
	Если КомпонентаИзСправочника = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КомпонентаПоследнейВерсииИзМакета = СтандартныеПодсистемыСервер.КомпонентаПоследнейВерсии(
		ОписаниеКомпоненты.ИмяОбъекта, ОписаниеКомпоненты.ПолноеИмяМакета);
	
	МестоположениеМакетаРазделенное = СтрРазделить(КомпонентаПоследнейВерсииИзМакета.Местоположение, ".");
	ДвоичныеДанные =
		ПолучитьОбщийМакет(МестоположениеМакетаРазделенное.Получить(МестоположениеМакетаРазделенное.ВГраница()));
	
	ИнформацияОКомпоненте = ВнешниеКомпонентыСлужебный.ИнформацияОКомпонентеИзФайла(ДвоичныеДанные);
	
	Если Не ИнформацияОКомпоненте.Разобрано ИЛИ ОбщегоНазначенияКлиентСервер.СравнитьВерсии(
			ИнформацияОКомпоненте.Реквизиты.Версия, КомпонентаИзСправочника.Версия) < 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыКомпоненты = ВнешниеКомпонентыСлужебный.ПараметрыЗагрузки();
	ПараметрыКомпоненты.Идентификатор = ОписаниеКомпоненты.ИмяОбъекта;
	ПараметрыКомпоненты.Наименование = КомпонентаИзСправочника.Наименование;
	ПараметрыКомпоненты.Версия = ИнформацияОКомпоненте.Реквизиты.Версия;
	ПараметрыКомпоненты.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Добавлена автоматически %1.'"), ТекущаяДатаСеанса());
	ПараметрыКомпоненты.ОбновлятьСПортала1СИТС = КомпонентаИзСправочника.ОбновлятьАвтоматически;
	ПараметрыКомпоненты.Данные = ДвоичныеДанные;
	
	ВнешниеКомпонентыСлужебный.ЗагрузитьКомпонентуИзДвоичныхДанных(ПараметрыКомпоненты, Ложь);
	
КонецПроцедуры // ОбновитьВнешниеКомпонентыИзМакета()

#КонецОбласти
