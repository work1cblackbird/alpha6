
#Область ПрограммныйИнтерфейс

// Выполнить команду.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
//  Команда - КомандаФормы - 
Процедура ОтправитьИлиОбновитьУчастника(Форма, Команда) Экспорт
	
	Если Форма.Модифицированность Тогда
		
		ВызватьИсключение НСтр("ru = 'Для выполнения операции объект должен быть записан'");
		
	КонецЕсли;
	
	ТипУчастника = УчастникиПЭПВызовСервера.ОпределитьТипУчастника(Форма.Объект.Ссылка);
	
	ПараметрыОткрытия = Новый Структура();
	
	Если ТипУчастника.ЭтоЮридическоеЛицо Тогда
		
		ПараметрыОткрытия.Вставить("ЮридическоеЛицо", Форма.Объект.Ссылка);
		ИмяФормы = "Обработка.ЛичныйКабинетПЭП.Форма.ЮридическоеЛицо";
		
	ИначеЕсли ТипУчастника.ЭтоКонтактноеЛицо Тогда
		
		ПараметрыОткрытия.Вставить("КонтактноеЛицо", Форма.Объект.Ссылка);
		ИмяФормы = "Обработка.ЛичныйКабинетПЭП.Форма.КонтактноеЛицо";
		
	ИначеЕсли ТипУчастника.ЭтоФизическоеЛицо Тогда
		
		ПараметрыОткрытия.Вставить("ФизическоеЛицо", Форма.Объект.Ссылка);
		ИмяФормы = "Обработка.ЛичныйКабинетПЭП.Форма.ФизическоеЛицо";
		
	КонецЕсли;
	
	ДополнительныеПараменты = Новый Структура("ТипУчастника", ТипУчастника);
	ОбратныйВызов = Новый ОписаниеОповещения("ОтправитьДанныеНаСервер", УчастникиПЭПКлиент,
		ДополнительныеПараменты);
	ОткрытьФорму(ИмяФормы, ПараметрыОткрытия, Форма, Форма,,, ОбратныйВызов);
	
КонецПроцедуры

// Отправить данные на сервер.
// 
// Параметры:
//  ДанныеУчастника - Произвольный - описание полей участника, может зависить от участника.
//  ДополнительныеПараменты - Структура - с полями:
//  * ТипУчастника - см. УчастникиПЭПВызовСервера.ОпределитьТипУчастника 
//  * ЭтоОбновление - Булево - признак, что участник уже зарегистрирован на сервере.
Процедура ОтправитьДанныеНаСервер(ДанныеУчастника, ДополнительныеПараменты) Экспорт
	
	Если ДанныеУчастника = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЭтоОбновление = УчастникиПЭПВызовСервера.ОтправитьИлиОбновитьУчастника(ДанныеУчастника,
		ДополнительныеПараменты.ТипУчастника);
	
	Если ЭтоОбновление Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Данные участника обновлены'"));
		
	Иначе
		
		ПоказатьПредупреждение(, НСтр("ru = 'Участник зарегестрирован на сервере'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
