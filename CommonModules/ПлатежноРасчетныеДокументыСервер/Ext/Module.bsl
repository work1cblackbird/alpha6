////////////////////////////////////////////////////////////////////////////////
// Работа с объектами механизма "Платежно расчетные документы"
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует таблицу платежно расчетных документов для указанного документа.
//
// Параметры:
//  Документ  - ДокументСсылка - Документ для которого необходимо получить платежно расчетные документы.
//  Договор  - СправочникСсылка.ДоговорыВзаиморасчетов - Договор используется для поиска платежей
//                  в документе "Выписка".
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица с документами вида "Номер" от "Дата".
//                       Одна строка соответствует одному документу.
//
Функция ПолучитьПлатежноРасчетныеДокументы(Документ, Договор=Неопределено) Экспорт
	
	// Подготовим таблицу
	Результат = ПустаяТаблица();
	
	Если НЕ ЗначениеЗаполнено(Документ) Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Договор) Тогда
		
		Если ЕстьРеквизит(Документ, "ДоговорВзаиморасчетов") Тогда
			
			Договор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ДоговорВзаиморасчетов");
			
		Иначе
			
			Возврат Результат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДатаРеализации = Неопределено;
	Если РасчетыСКонтрагентамиСервер.ИспользуютсяВзаиморасчетыПоРасчетнымДокументам(Договор) Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	РасчетыСКонтрагентами.ДокументРасчетов КАК Сделка,
		|	РасчетыСКонтрагентами.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	РасчетыСКонтрагентами.ТипРасчета КАК ТипРасчета,
		|	РасчетыСКонтрагентами.Период КАК Период
		|ПОМЕСТИТЬ ЗакрытыеСделки
		|ИЗ
		|	РегистрНакопления.РасчетыСКонтрагентами КАК РасчетыСКонтрагентами
		|ГДЕ
		|	РасчетыСКонтрагентами.Регистратор = &Основание
		|{ГДЕ
		|	РасчетыСКонтрагентами.Период КАК ПериодОтбор}
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗакрытыеСделки.Сделка КАК Платеж,
		|	&Основание КАК Сделка,
		|	ЗакрытыеСделки.Сделка.Дата КАК Период
		|ПОМЕСТИТЬ Платежи
		|ИЗ
		|	ЗакрытыеСделки КАК ЗакрытыеСделки
		|ГДЕ
		|	ЗакрытыеСделки.ТипРасчета = ЗНАЧЕНИЕ(перечисление.ТипыРасчетов.Аванс)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыпискаСостав.Ссылка КАК Выписка,
		|	ВыпискаСостав.Сделка КАК Сделка,
		|	ВыпискаСостав.ВхДокНомер КАК Номер,
		|	ВыпискаСостав.ВхДокДата КАК Дата
		|ПОМЕСТИТЬ Выписки
		|ИЗ
		|	Документ.Выписка.Состав КАК ВыпискаСостав
		|ГДЕ
		|	ВыпискаСостав.ДоговорВзаиморасчетов = &Договор
		|	И ВыпискаСостав.Ссылка В
		|			(ВЫБРАТЬ
		|				Платежи.Платеж
		|			ИЗ
		|				Платежи КАК Платежи)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Платежи.Платеж КАК Платеж,
		|	ВЫБОР
		|		КОГДА Платежи.Платеж ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ТОГДА ""ПКО""
		|		КОГДА Платежи.Платеж ССЫЛКА Документ.ЧекНаОплату
		|			ТОГДА ""Чек""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Префикс,
		|	ВЫБОР
		|		КОГДА Платежи.Платеж ССЫЛКА Документ.Выписка
		|			ТОГДА ЕСТЬNULL(Выписки.Номер, Платежи.Платеж.ВхДокНомер)
		|		ИНАЧЕ Платежи.Платеж.Номер
		|	КОНЕЦ КАК Номер,
		|	ВЫБОР
		|		КОГДА Платежи.Платеж ССЫЛКА Документ.Выписка
		|			ТОГДА ЕСТЬNULL(Выписки.Дата, Платежи.Платеж.ВхДокДата)
		|		ИНАЧЕ Платежи.Платеж.Дата
		|	КОНЕЦ КАК Дата,
		|	Платежи.Платеж.ХозОперация КАК ХозОперация,
		|	Платежи.Платеж.Номер КАК НомерДополнительный,
		|	Платежи.Платеж.Дата КАК ДатаДополнительная,
		|	Платежи.Период КАК Период
		|ИЗ
		|	Платежи КАК Платежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Выписки КАК Выписки
		|		ПО Платежи.Платеж = Выписки.Выписка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Платежи.Период";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВзаиморасчетыКомпании.Сделка КАК Сделка,
		|	ВзаиморасчетыКомпании.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		|ПОМЕСТИТЬ ЗакрытыеСделки
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
		|ГДЕ
		|	ВзаиморасчетыКомпании.Регистратор = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВзаиморасчетыКомпании.Регистратор КАК Платеж,
		|	ВзаиморасчетыКомпании.Сделка КАК Сделка,
		|	ВзаиморасчетыКомпании.Период КАК Период
		|ПОМЕСТИТЬ Платежи
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
		|ГДЕ
		|	(ВзаиморасчетыКомпании.Сделка, ВзаиморасчетыКомпании.ДоговорВзаиморасчетов) В
		|			(ВЫБРАТЬ
		|				ЗакрытыеСделки.Сделка,
		|				ЗакрытыеСделки.ДоговорВзаиморасчетов
		|			ИЗ
		|				ЗакрытыеСделки КАК ЗакрытыеСделки)
		|	И (ВзаиморасчетыКомпании.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ИЛИ ВзаиморасчетыКомпании.Регистратор ССЫЛКА Документ.Выписка
		|			ИЛИ ВзаиморасчетыКомпании.Регистратор ССЫЛКА Документ.ЧекНаОплату)
		|{ГДЕ
		|	ВзаиморасчетыКомпании.Период КАК ПериодОтбор}
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыпискаСостав.Ссылка КАК Выписка,
		|	ВыпискаСостав.Сделка КАК Сделка,
		|	ВыпискаСостав.ВхДокНомер КАК Номер,
		|	ВыпискаСостав.ВхДокДата КАК Дата
		|ПОМЕСТИТЬ Выписки
		|ИЗ
		|	Документ.Выписка.Состав КАК ВыпискаСостав
		|ГДЕ
		|	ВыпискаСостав.ДоговорВзаиморасчетов = &Договор
		|	И ВыпискаСостав.Ссылка В
		|			(ВЫБРАТЬ
		|				Платежи.Платеж
		|			ИЗ
		|				Платежи КАК Платежи)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Платежи.Платеж КАК Платеж,
		|	ВЫБОР
		|		КОГДА Платежи.Платеж ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ТОГДА ""ПКО""
		|		КОГДА Платежи.Платеж ССЫЛКА Документ.ЧекНаОплату
		|			ТОГДА ""Чек""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Префикс,
		|	ВЫБОР
		|		КОГДА Платежи.Платеж ССЫЛКА Документ.Выписка
		|			ТОГДА ЕСТЬNULL(Платежи.Платеж.ВхДокНомер, """")
		|		ИНАЧЕ Платежи.Платеж.Номер
		|	КОНЕЦ КАК Номер,
		|	ВЫБОР
		|		КОГДА Платежи.Платеж ССЫЛКА Документ.Выписка
		|			ТОГДА ЕСТЬNULL(Платежи.Платеж.ВхДокДата, ДАТАВРЕМЯ(1, 1, 1))
		|		ИНАЧЕ Платежи.Платеж.Дата
		|	КОНЕЦ КАК Дата,
		|	Платежи.Платеж.ХозОперация КАК ХозОперация,
		|	Платежи.Платеж.Номер КАК НомерДополнительный,
		|	Платежи.Платеж.Дата КАК ДатаДополнительная,
		|	Платежи.Период КАК Период
		|ИЗ
		|	Платежи КАК Платежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Выписки КАК Выписки
		|		ПО Платежи.Платеж = Выписки.Выписка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Платежи.Период";
		
		ДатаРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Дата");
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			ДатыЗаказНаряда = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Дата,ДатаЗакрытия");
			ДатаРеализации = ?(ЗначениеЗаполнено(ДатыЗаказНаряда.ДатаЗакрытия), ДатыЗаказНаряда.ДатаЗакрытия, ДатыЗаказНаряда.Дата);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПостроительЗапроса = Новый ПостроительЗапроса(ТекстЗапроса);
	
	ПостроительЗапроса.ЗаполнитьНастройки();
	ПостроительЗапроса.Параметры.Вставить("Основание", Документ);
	ПостроительЗапроса.Параметры.Вставить("Договор", Договор);
	ТипДокумента = ТипЗнч(Документ);
	
	Если ЗначениеЗаполнено(ДатаРеализации) Тогда
		
		Отбор = ПостроительЗапроса.Отбор.Добавить("ПериодОтбор");
		Отбор.ВидСравнения = ВидСравнения.Меньше;
		Отбор.Значение = ДатаРеализации;
		Отбор.Использование = Истина;
		
	КонецЕсли;
	
	ПостроительЗапроса.Выполнить();
	
	Если ПостроительЗапроса.Результат.Пустой() Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	Выборка = ПостроительЗапроса.Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Результат.Добавить();
		
		Если ЗначениеЗаполнено(Выборка.Номер) Тогда
			
			НомерБезНулей = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(Выборка.Номер, "0");
			НоваяСтрока.Номер = СокрЛП(Выборка.Префикс + " " + НомерБезНулей);
			НоваяСтрока.Дата = Выборка.Дата;
			
		ИначеЕсли ТипЗнч(Выборка.Платеж) = Тип("ДокументСсылка.Выписка") Тогда
			
			Если Выборка.ХозОперация <> Справочники.ХозОперации.БанковскаяВыписка Тогда
				
				НомерБезНулей = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(Выборка.НомерДополнительный, "0");
				НоваяСтрока.Номер = СокрЛП(Выборка.Префикс + " " + НомерБезНулей);
				НоваяСтрока.Дата = Выборка.ДатаДополнительная;
				
			Иначе
				
				НоваяСтрока.Номер = НСтр("ru = 'в выписке не указан'");
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьПлатежноРасчетныеДокументы()

//Формирует номер платежного документа взависимости от значения константы ПолныйНомерДокументаВПечатныхФормах
//
// Параметры:
//  НомерИсходный  - Строка - номер платежно-расчетного документа.
//  Префикс		   - Строка - наименование платежно-расчетного документа".
//
// Возвращаемое значение:
//   Строка   - преобразованный номер платежного документа
//
Функция СформироватьНомер(НомерИсходный, Префикс) Экспорт
	
	СокращенныйНомер = НЕ Константы.ПолныйНомерДокументаВПечатныхФормах.Получить();
	
	Если СокращенныйНомер Тогда
		Номер = СокрЛП(НомерИсходный);
			
		Номер =   Префикс + ПрефиксацияОбъектов.ПолучитьНомерНаПечать(Номер,Истина,Истина,Истина);
	Иначе
		Номер = Префикс + НомерИсходный;	
	КонецЕсли;	
	
	Возврат Номер;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует пустую таблицу "Платежно расчетных документов".
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Пустая таблица с колонками "Номер" и "Дата".
//
Функция ПустаяТаблица()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Номер", ОбщегоНазначения.ОписаниеТипаСтрока(30));
	Таблица.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	Возврат Таблица;
	
КонецФункции // ПустаяТаблица()

#КонецОбласти