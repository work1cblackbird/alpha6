// Общий модуль "Заметки пользователя (платформа,сервер)"

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ФОРМЫ

// Настраивает параметры действия для вывода оповещения о наличии заметок по текущему измененному реквизиту.
// 
// Параметры:
//  Предмет            - СправочникСсылка - Реквизит, по которому производится поиск заметок.
//  ПараметрыДействия  - Структура        - Набор параметров, в которые будет записано оповещение.
//
Процедура ПроверитьЗаметкиПоПредмету(Предмет, ПараметрыДействия=Неопределено) Экспорт
	
	// Инициализируем параметры действия.
	Если НЕ ЗначениеЗаполнено(ПараметрыДействия) Тогда
		ПараметрыДействия = Новый Структура;
	КонецЕсли;
	
	// Проверим заполнение предмета.
	Если НЕ ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем количество заметок по предмету.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заметки.Ссылка) КАК КоличествоЗаметок
	|ИЗ
	|	Справочник.Заметки КАК Заметки
	|ГДЕ
	|	Заметки.Предмет = &Предмет
	|	И НЕ Заметки.ПометкаУдаления
	|	И Заметки.НапоминатьПриВыборе";
	
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Проверяем наличие заметок по предмету.
	Если Выборка.Следующий() И Выборка.КоличествоЗаметок > 0 Тогда
				
		Если НЕ ПараметрыДействия.Свойство("ВыводОповещения") Тогда
			
			// Передадим в параметры действия настройки для вывода оповещения пользователя.
			ПараметрыДействия.Вставить("ВыводОповещения", Новый Структура("Использование, Заголовок, Текст, Ссылка, Картинка"));
			
			ПараметрыДействия.ВыводОповещения.Использование = Истина;
			ПараметрыДействия.ВыводОповещения.Заголовок     = ""; //СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Заметки по %1'"), Строка(Предмет));
			ПараметрыДействия.ВыводОповещения.Текст         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для элемента %1 зарегистрированы заметки в количестве %2 шт.'"), Строка(Предмет), Выборка.КоличествоЗаметок);
			
			ПараметрыКоманды = Новый Структура;
			ПараметрыКоманды.Вставить("Предмет", Предмет);
			
			Ссылка = ПолучитьНавигационнуюСсылку(Метаданные.ОбщиеКоманды.МоиЗаметкиПоПредмету, "ЗаметкиПоПредмету", ПараметрыКоманды);
			
			ПараметрыДействия.ВыводОповещения.Ссылка        = Ссылка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаметкиПоПредмету()

// Добавление элементов на форму справочника "Заметки"
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма, для которой выполняется событие.
//
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	ЭлементСодержание = Форма.Элементы.Найти("Содержание");
	НовыйЭлемент = Форма.Элементы.Вставить("НапоминатьПриВыборе", Тип("ПолеФормы"), Форма, ЭлементСодержание);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	НовыйЭлемент.ПутьКДанным = "Объект.НапоминатьПриВыборе";
	
КонецПроцедуры

#КонецОбласти