
#Область ПрограммныйИнтерфейс

// Открыть подбор скидок и маркетинговых программ
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект документ для которого усуществляется подбор
//  Форма  - УправляемаяФорма - Форма владелец подбора
//  ОписаниеОповещения - ОписаниеОповещения - Обработчик оповещения о закрытии формы
//
Процедура ОткрытьПодборСкидокИМаркетинговыхПрограмм(Объект, Форма, ОписаниеОповещения=Неопределено) Экспорт
	ПараметрыОткрытия = НоВый Структура;
	
	Если Объект.Ссылка.Пустая() Тогда
		ПараметрыОткрытия.Вставить("ДатаСреза", НачалоДня(ТекущаяДата()));
	Иначе
		ПараметрыОткрытия.Вставить("ДатаСреза", НачалоДня(Объект.Дата));
	КонецЕсли;
	
	ПараметрыОткрытия.Вставить("ПодразделениеКомпании", Объект.ПодразделениеКомпании);
	ПараметрыОткрытия.Вставить("Модель", Объект.Модель);
	ПараметрыОткрытия.Вставить("Комплектация", Объект.ВариантКомплектации);
	ПараметрыОткрытия.Вставить("Валюта", Объект.ВалютаДокумента);
	ПараметрыОткрытия.Вставить("Курс", Объект.КурсДокумента);
	
	ВыбранныеСкидки = Новый Массив;
	Для Каждого Строка Из Объект.СкидкиИМаркетинговыеПрограммы Цикл
		ВыбранныеСкидки.Добавить(Новый Структура("Скидка,Значение", Строка.Скидка, Строка.Значение));
	КонецЦикла;
	ПараметрыОткрытия.Вставить("ВыбранныеСкидки", ВыбранныеСкидки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", Форма, "ВыбранныеСкидки");
	ОткрытьФорму(
		"ОбщаяФорма.ИзменениеСкидокИМаркетинговыхПрограмм",
		ПараметрыОткрытия,
		Форма,
		Форма,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
КонецПроцедуры

// Открыть форму просмотра текущих скидок документа
//
// Параметры:
//  Источник - Строка, ДокументСсылка.ЗаказНаАвтомобиль - Ссылка на документ заказа или
//                                                        ссылка на таблицу во временном хранилище
//  Форма - УправляемаяФорма - Форма владелец
//
Процедура ПросмотрСкидокИМаркетинговыхПрограмм(Источник, Форма) Экспорт
	
	ОткрытьФорму(
		"ОбщаяФорма.ПросмотрСкидокИМаркетинговыхПрограмм",
		Новый Структура("Источник", Источник),
		Форма,
		Форма,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

#КонецОбласти