// Общий модуль "Расширенная информация (клиент/сервер)"


#Область СлужебныеПроцедурыИФункции

// Функция ищет строки в реквизите формы типа "ДеревоЗначений".
//
// Параметры:
//	ЭлементыСтрокДерева		- КоллекцияСтрокДереваЗначений	- Строки дерева значений.
//	СтруктураПоиска			- Структура						- Структура поиска.
//	Рекурсивно				- Булево						- Флаг рекурсии.
//	МассивНайденныхСтрок	- Массив						- Массив найденных строк.
//
// Возвращаемое значение:
//	Массив	- Массив найденных строк.
//
Функция НайтиВЭлементахДерева(ЭлементыСтрокДерева, СтруктураПоиска, Рекурсивно = Ложь, МассивНайденныхСтрок = Неопределено) Экспорт
	Если МассивНайденныхСтрок = Неопределено Тогда
		Массив = Новый Массив();
	Иначе
		Массив = МассивНайденныхСтрок;
	КонецЕсли;
	Для Каждого СтрокаДерева Из ЭлементыСтрокДерева Цикл
		бНайдено = Истина;
		Для Каждого СтруктураПоискаКлючЗначение Из СтруктураПоиска Цикл
			Если СтрокаДерева[СтруктураПоискаКлючЗначение.Ключ] <> СтруктураПоискаКлючЗначение.Значение Тогда
				бНайдено = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если бНайдено Тогда
			Массив.Добавить(СтрокаДерева);
		КонецЕсли;
		Если Рекурсивно Тогда
			ПодчиненныеСтроки = СтрокаДерева.ПолучитьЭлементы();
			Если ПодчиненныеСтроки.Количество() > 0 Тогда
				НайтиВЭлементахДерева(ПодчиненныеСтроки, СтруктураПоиска, Рекурсивно, Массив);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Массив;
КонецФункции // НайтиВЭлементахДерева()

// Функция возвращает пустое содержание.
//
// Параметры:
//	СсылкаНаОбъект	- Произвольный	- Ссылка на объект.
//
// Возвращаемое значение:
//	Строка	- Пустое содержание.
//
Функция НастройкиПолейРасширеннойИнформацииПолучитьПустоеСодержание(СсылкаНаОбъект = Неопределено) Экспорт
	ГиперссылкаНастройки = "<FONT size=""1""><br><hr noshade size=""1""></FONT><DIV align=""right""><FONT size=""2""><A style=""COLOR: #000000; font-weight: bold""   href=""НастройкаПечати"">Печать</A>&nbsp;&nbsp;&nbsp;
	|<A style=""COLOR: #000000; font-weight: bold;"" href=""НастройкаПолейОтображения"">Настройка</A></FONT></DIV>";
	Описание =
	"<html>
	|<head>
	|<meta http-equiv=Content-Type content=" + """" + "text/html; charset=utf-8"">
	|<title></title>
	|</head>
	|<body>
	|" + ГиперссылкаНастройки +"
	|</body>
	|</html>";
	Возврат Описание;
КонецФункции // НастройкиПолейРасширеннойИнформацииПолучитьПустоеСодержание()

#КонецОбласти