/////////////////////////////////////////////////////////////////////////////////////////
// Редактирование истории: механизм редактирования истории реквизитов объекта
//
/////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Общий обработчик события возникающего после начала транзакции записи, но до начала записи объекта.
//
// Параметры:
//  Объект - СправочникОбъект - Объект, для которого выполняется обработка события.
//
Процедура ПередЗаписью(Объект) Экспорт
	
	Если Объект.ИсторияКПП.Количество() = 1 Тогда
		// Если запись в истории КПП одна, то считается, что изменений нет
		// и значение КПП нужно определять из данных объекта.
		Объект.ИсторияКПП.Очистить();
	ИначеЕсли Объект.ИсторияКПП.Количество() > 1 Тогда
		// Первая запись в истории должна иметь пустую дату
		Объект.ИсторияКПП.Сортировать("Период");
		Объект.ИсторияКПП[0].Период = Дата('00010101');
		
		// Последняя запись в истории всегда должна соответствовать КПП в объекте
		ОбщегоНазначения
			.МенеджерОбъектаПоСсылке(Объект.Ссылка)
			.УстановитьАктуальноеЗначениеИсторииКПП(Объект.КПП, Объект.ИсторияКПП);
	КонецЕсли;
	
	Если Объект.ИсторияНаименований.Количество() = 1 Тогда
		// Если запись в истории наименований одна, то считается, что изменений нет
		// и значение наименования нужно определять из данных объекта.
		Объект.ИсторияНаименований.Очистить();
	ИначеЕсли Объект.ИсторияНаименований.Количество() > 1 Тогда
		// Первая запись в истории должна иметь пустую дату
		Объект.ИсторияНаименований.Сортировать("Период");
		Объект.ИсторияНаименований[0].Период = '00010101';
		
		// Последняя запись в истории всегда должна соответствовать наименованию в объекте
		ОбщегоНазначения
			.МенеджерОбъектаПоСсылке(Объект.Ссылка)
			.УстановитьАктуальноеЗначениеИсторииНаименований(Объект.НаименованиеПолное, Объект.ИсторияНаименований);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
