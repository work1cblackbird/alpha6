
#Область ПрограммныйИнтерфейс

// См. МашиночитаемыеДоверенностиПереопределяемый.ПриИзмененииДанныеФизЛица
//
Процедура ПриИзмененииДанныеФизЛица(ФизЛицо, Сведения) Экспорт
	
	СведенияОФизЛице = МашиночитаемыеДоверенности.НовыеДанныеФизЛица();
	
	ДанныеСотрудника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФизЛицо,
		"Наименование,ИНН,СтраховойНомерПФР,ДатаРождения,Пол,Фамилия,Имя,Отчество");
	
	СведенияОФизЛице.ДатаРождения      = ДанныеСотрудника.ДатаРождения;
	СведенияОФизЛице.ИНН               = ДанныеСотрудника.ИНН;
	СведенияОФизЛице.Пол               = Строка(ДанныеСотрудника.Пол);
	СведенияОФизЛице.СтраховойНомерПФР = ДанныеСотрудника.СтраховойНомерПФР;
	СведенияОФизЛице.ФИО               = ДанныеСотрудника.Наименование;
	СведенияОФизЛице.Фамилия           = ДанныеСотрудника.Фамилия;
	СведенияОФизЛице.Имя               = ДанныеСотрудника.Имя;
	СведенияОФизЛице.Отчество          = ДанныеСотрудника.Отчество;
	
	ЗаполнитьЗначенияСвойств(Сведения, СведенияОФизЛице);
	
	ПаспортСотрудника = ПаспортСотрудника(ФизЛицо);
	
	Если ПаспортСотрудника <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(Сведения, ПаспортСотрудника);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПаспортСотрудника(Сотрудник)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПодтверждающиеДокументы.Серия КАК Серия,
		|	ПодтверждающиеДокументы.ДатаВыдачи КАК ДатаВыдачи,
		|	ПодтверждающиеДокументы.КемВыдан КАК КемВыдан,
		|	ПодтверждающиеДокументы.Номер КАК Номер,
		|	ПодтверждающиеДокументы.КодПодразделения КАК КодПодразделения
		|ИЗ
		|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		|ГДЕ
		|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента = &ВидДокумента
		|	И ПодтверждающиеДокументы.Владелец = &Сотрудник"
	);
	Запрос.УстановитьПараметр("ВидДокумента", Перечисления.ВидыДокументов.Паспорт);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0];
	
КонецФункции

#КонецОбласти