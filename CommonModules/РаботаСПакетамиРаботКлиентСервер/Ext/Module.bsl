

#Область ПрограммныйИнтерфейс

// Создание нового пакета работ заказ-наряда.
//
// Параметры:
//  НомерПакета                   - Число  - Порядковый номер пакета работ.
//  ПредставлениеПричиныОбращения - Строка - Представление причины обращения для пакета работ.
// 
// Возвращаемое значение:
//  Структура - Параметры нового пакета работ.
//
Функция СоздатьНовыйПакетРабот(НомерПакета, ПредставлениеПричиныОбращения = "") Экспорт
	
	Возврат Новый Структура("ПакетРабот,Представление,НомерПакета,НомерКартинкиСостоянияПакетаРабот,СтатусПакета",
		Новый УникальныйИдентификатор, СформироватьПредставлениеНовогоПакета(НомерПакета, ПредставлениеПричиныОбращения),
		НомерПакета, 3, ПредопределенноеЗначение("Справочник.ВидыСостоянийПакетовРабот.НеСтартовал"));
	
КонецФункции

// Получение максимального значения числовой колонки таблицы.
//
// Параметры:
//  Таблица    - ТаблицаЗначений - Таблица, с числовой колонкой.
//  ИмяКолонки - Строка          - Имя числовой колонки.
// 
// Возвращаемое значение:
//  Число - Максимальное значение числовой колонки.
//
Функция МаксимальноеЗначениеЧисловойКолонки(Таблица, ИмяКолонки = "НомерПакета") Экспорт
	
	МаксимальноеЗначение = Неопределено;
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Если МаксимальноеЗначение = Неопределено ИЛИ МаксимальноеЗначение < СтрокаТаблицы[ИмяКолонки] Тогда
			МаксимальноеЗначение = СтрокаТаблицы[ИмяКолонки];
		КонецЕсли;
	КонецЦикла;
	
	Возврат МаксимальноеЗначение;
	
КонецФункции

// Проверка пустого пакета работ.
//
// Параметры:
//  Значение - Строка - Идентификатор пакета работ.
// 
// Возвращаемое значение:
//  Булево - Если пакет работ пустой, то Истина. Иначе Ложь.
//
Функция ЭтоПустойПакетРабот(Значение) Экспорт
	
	Возврат (Значение = "00000000-0000-0000-0000-000000000000");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьПредставлениеНовогоПакета(НомерПакета, ПредставлениеПричиныОбращения)
	
	Возврат СтрШаблон("Пакет №%1 %2", НомерПакета, ?(ПустаяСтрока(ПредставлениеПричиныОбращения), "", "("+ ПредставлениеПричиныОбращения +")"));
	
КонецФункции

#КонецОбласти
