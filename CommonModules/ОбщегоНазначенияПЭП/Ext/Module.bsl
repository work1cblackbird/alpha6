
#Область ПрограммныйИнтерфейс

// Текущий авторизованный пользователь
// 
// Возвращаемое значение:
//  Произвольный - текущий авторизованный пользователь
Функция ТекущийПользователь() Экспорт
	
	Возврат ОбщегоНазначенияПЭППереопределяемый.ТекущийПользователь();
	
КонецФункции

// Сотрудник привязанный к текущему пользователю
// 
// Возвращаемое значение:
//  Произвольный - текущий авторизованный пользователь
Функция ТекущийСотрудник() Экспорт
	
	Возврат ОбщегоНазначенияПЭППереопределяемый.ТекущийСотрудник();
	
КонецФункции

// Определяет модуль интеграции. Позволяет переопределить модуль для тестов или использования нескольки контуров.
// 
// Возвращаемое значение:
//  Произвольный
Функция МодульИнтеграции() Экспорт
	
	Если Метаданные.ОбщиеМодули.Найти("ТестыПЭП_Интеграция") <> Неопределено Тогда
		// АПК:488-выкл ВычислитьВБезопасномРежиме не используется, чтобы избежать вызова ОбщийМодуль рекурсивно.
		УстановитьБезопасныйРежим(Истина);
		Возврат Вычислить("ТестыПЭП_Интеграция");
		// АПК:488-вкл
	КонецЕсли;
	
	Возврат ИнтеграцияПЭП;
	
КонецФункции

// Уровни системного журнала.
// 
// Возвращаемое значение:
//  Структура -  Уровни системного журнала
Функция УровниСистемногоЖурнала() Экспорт
	
	Значения = СтрРазделить("Emergency,Alert,Critical,Error,Warning,Notice,Informational,Debug", ",");
	Результат = Новый Структура();
	
	Для Каждого Значение Из Значения Цикл
		
		Результат.Вставить(Значение, Значение);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Уровни системного журнала в списке выбора.
// 
// Возвращаемое значение:
//  СписокЗначений из Строка -  Уровни системного журнала в списке выбора
Функция УровниСистемногоЖурналаВСпискеВыбора() Экспорт
	
	Результат = Новый СписокЗначений();
	
	Для Каждого КлючЗначение Из УровниСистемногоЖурнала() Цикл
		
		Результат.Добавить(КлючЗначение.Ключ, КлючЗначение.Значение);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Коллекция команд ПЭП.
// 
// Возвращаемое значение:
//  ТаблицаЗначений -  Коллекция команд:
// * ИмяКоманды - Строка -
// * Действие - Строка -
// * Заголовок - Строка -
Функция КоллекцияКоманд() Экспорт
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	
	Результат = Новый ТаблицаЗначений();
	Результат.Колонки.Добавить("ИмяКоманды", ТипСтрока);
	Результат.Колонки.Добавить("Действие", ТипСтрока);
	Результат.Колонки.Добавить("Заголовок", ТипСтрока);
	Возврат Результат;
	
КонецФункции

// Создает стандартное место размещения команд.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
// 
// Возвращаемое значение:
//  ГруппаФормы - 
Функция МестоРазмещенияКоманд(Форма) Экспорт
	
	Результат = Форма.Элементы.Найти("КомандыПЭП");
		
	Если Результат = Неопределено Тогда
		
		Результат = Форма.Элементы.Добавить("КомандыПЭП", Тип("ГруппаФормы"),
			Форма.Элементы.ФормаКоманднаяПанель);
		Результат.Вид = ВидГруппыФормы.Подменю;
		Результат.Картинка = БиблиотекаКартинок.ЛоготипПЭП;
		Результат.Заголовок = НСтр("ru ='ПЭП'");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
