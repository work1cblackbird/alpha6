#Область ПрограммныйИнтерфейс

#Область МодульОбъекта

// Переопределяемая процедура обработки заполнения.
// Вызывается из модуля объекта "КассовыйЧекКоррекции"
// 
// Параметры:
//  Объект - ДокументОбъект.КассовыйЧекКоррекции - Заполняемый документ
//  ДанныеЗаполнения -Произвольный - Данные заполнения, переданные при программном и интерфейсном вызоде процедуры
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Объект, ДанныеЗаполнения);
	КонецЕсли;
	
	// +Рарус
	// Произведем инициализацию реквизитов, которые устанавливаются один раз при создании объекта.
	Если Объект.ЭтоНовый() Тогда
	Если (НЕ ЗначениеЗаполнено(Объект.Ответственный)) Тогда
			Объект.Ответственный = Пользователи.ТекущийПользователь();
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = ПараметрыСеанса.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ПодразделениеКомпании) Тогда
		Объект.ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.КассаККМ) Тогда
		Объект.КассаККМ = Справочники.КассыККМ.ПолучитьОсновнуюКассуККМ();
	КонецЕсли;
	// -Рарус
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая перед записью.
// Вызывается из модуля объекта "КассовыйЧекКоррекции"
//
// Параметры:
//  Объект			 - ДокументОбъект.КассовыйЧекКоррекции	 - Заполняемый документ
//  Отказ			 - Булево								 - Признак отказа от записи.
//  РежимЗаписи		 - РежимЗаписиДокумента					 - Текущий режим записи документа.
//  РежимПроведения	 - РежимПроведенияДокумента				 - Текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	// +Рарус
	Если НЕ ЗначениеЗаполнено(Объект.ПодразделениеКомпании) Тогда
		ОбщегоНазначения.СообщитьПользователю(Нстр("ru = 'Поле ""Подразделение"" не заполнено'"), Объект.Ссылка,,, Отказ);
	КонецЕсли;
	// -Рарус
	
КонецПроцедуры

// Переопределяемая процедура обработки проведения.
// Вызывается из модуля объекта "КассовыйЧекКоррекции"
// 
// Параметры:
//  Объект			 - ДокументОбъект.КассовыйЧекКоррекции	 - заполняемый документ
//  Отказ			 - Булево								 - Признак отказа от записи.
//  РежимПроведения	 - РежимПроведенияДокумента				 - Текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая при копировании.
// Вызывается из модуля объекта "КассовыйЧекКоррекции"
// 
// Параметры:
//  Объект - ДокументОбъект.КассовыйЧекКоррекции - заполняемый документ
//  ОбъектКопирования - ДокументОбъект.КассовыйЧекКоррекции - копируемый документ
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	Объект.НомерЧекаККМ   = 0;
	Объект.ПробитЧек      = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ФормаДокумента

#Область ФормаДокументаОбработчикиСобытийФормы

// Переопределяемая процедура, вызываемая при создании.
// Вызывается из модуля формы документа "КассовыйЧекКоррекции"
// 
// Параметры:
//  Объект					 - ДокументОбъект.КассовыйЧекКоррекции	 - заполняемый документ
//  Форма					 - УправляемаяФорма						 - Форма документа КассовыйЧекКоррекции
//  Отказ					 - Булево								 - Признак отказа от создания формы.
//  СтандартнаяОбработка	 - Булево								 - Признак выполнения стандартной (системной) обработки события.
//
Процедура ПриСозданииНаСервере(Объект, Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	// +Рарус
	// Стандартные подсистемы - Версионирование объектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(Форма);
	ПодключаемыеКоманды.ПриСозданииНаСервере(Форма,Неопределено);
	
	// ПодключаемоеОборудование
	ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	МенеджерОборудования.ПриСозданииНаСервере(Форма, ТипыОборудования);
	// Конец ПодключаемоеОборудование
	// -Рарус
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая при чтении.
// Вызывается из модуля формы документа "КассовыйЧекКоррекции"
// 
// Параметры:
//  Объект			 - ДокументОбъект.КассовыйЧекКоррекции	 - Заполняемый документ.
//  Форма			 - УправляемаяФорма						 - Форма документа КассовыйЧекКоррекции.
//  ТекущийОбъект	 - ДокументОбъект.КассовыйЧекКоррекции	 - Объект, который будет прочитан.
//
Процедура ПриЧтенииНаСервере(Объект, Форма, ТекущийОбъект) Экспорт
	
	// +Рарус
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(Форма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(Форма, ТекущийОбъект);
	// -Рарус
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая перед записью на сервере.
// Вызывается из модуля формы документа "КассовыйЧекКоррекции"
// 
// Параметры:
//  Форма			 - УправляемаяФорма						 - Форма документа КассовыйЧекКоррекции.
//  Отказ			 - Булево								 - Признак отказа от записи.
//  ТекущийОбъект	 - ДокументОбъект.КассовыйЧекКоррекции	 - Объект, который будет прочитан.
//  ПараметрыЗаписи	 - Структура							 - Структура, содержащая параметры записи.
//
Процедура ПередЗаписьюНаСервере(Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ФормаДокументаОбработчикиСобытийЭлементовШапки

// Переопределяемая процедура, вызываемая при изменении кассы ККМ.
// Вызывается из модуля формы документа "КассовыйЧекКоррекции"
// 
// Параметры:
//  Объект - ДокументОбъект.КассовыйЧекКоррекции - заполняемый документ.
//
Процедура КассаККМПриИзменении(Объект) Экспорт
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая при изменении основания для коррекции.
// Вызывается из модуля формы документа "КассовыйЧекКоррекции"
// 
// Параметры:
//  Объект - ДокументОбъект.КассовыйЧекКоррекции - заполняемый документ.
//
Процедура ОснованиеДляКоррекцииПриИзменении(Объект) Экспорт
	
	// +Рарус
	Если ТипЗнч(Объект.ОснованиеДляКоррекции) = Тип("ДокументСсылка.ЧекНаОплату") Тогда
		Объект.НомерДокументаОснования = Формат(Объект.ОснованиеДляКоррекции.НомерЧека, "ЧГ=0");
	Иначе
		Объект.НомерДокументаОснования = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ОснованиеДляКоррекции) 
		И НЕ ТипЗнч(Объект.ОснованиеДляКоррекции) = Тип("Строка") Тогда
		
		Объект.ДатаДокументаОснования = Объект.ОснованиеДляКоррекции.Дата;
		Объект.Организация = Объект.ОснованиеДляКоррекции.Организация;
		Объект.ПодразделениеКомпании = Объект.ОснованиеДляКоррекции.ПодразделениеКомпании;
		Объект.КассаККМ    = Объект.ОснованиеДляКоррекции.КассаККМ;
		Если НЕ ЗначениеЗаполнено(Объект.НомерДокументаОснования) Тогда
			Объект.НомерДокументаОснования = Объект.ОснованиеДляКоррекции.Номер;
		КонецЕсли;
	КонецЕсли;
	// -Рарус
	
КонецПроцедуры

#КонецОбласти

#Область ФормаДокументаСлужебныеПроцедурыИФункции

// Переопределяемая процедура устанавливает правила работы формы документа.
//
// Параметры:
//  Объект - ДокументОбъект.КассовыйЧекКоррекции - заполняемый документ.
//  Форма  - УправляемаяФорма					 - Форма документа КассовыйЧекКоррекции.
//
Процедура УстановитьДоступностьЭлементовИФормы(Объект, Форма) Экспорт
	
КонецПроцедуры

// Переопределяемая процедура проверяет возможность печати чека, при этом подготавливает информацию для печтати. 
//
// Параметры:
//  Объект - ДокументОбъект.КассовыйЧекКоррекции - заполняемый документ.
//  Отказ - Булево - признак отказа от печати.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - устройство фискализирующее чек
//  ИспользоватьКассуБезПодключенияОборудования - Булево - признак использования кассы.
//
Процедура ПодготовитьИПроверитьПечатьЧека(Объект,
										  Отказ, 
										  ИдентификаторУстройства, 
										  ИспользоватьКассуБезПодключенияОборудования)	Экспорт
	
	// +Рарус
	Если НЕ ЗначениеЗаполнено(Объект.ПодключаемоеОборудование) Тогда
		ОбщегоНазначения.СообщитьПользователю (НСтр("ru = 'Не указано подключаемое оборудование.'"), Объект.Ссылка, "ПодключаемоеОборудование",, Отказ);
	КонецЕсли;
	
	// Выполним предварительные проверки перед печатью
	Если НЕ Объект.СуммаДокумента > 0 Тогда
		ОбщегоНазначения.СообщитьПользователю (НСтр("ru = 'Не указана сумма к оплате'"), Объект.Ссылка, "СуммаДокумента",,Отказ);
	КонецЕсли;

	// Определим структуру параметров обработки текущего события	
	ИдентификаторУстройства = Объект.ПодключаемоеОборудование;
	ИспользоватьКассуБезПодключенияОборудования = НЕ ИдентификаторУстройства.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ;
	// -Рарус	
	
КонецПроцедуры

// Переопределяемая процедура получает данные для отправки по кассиру (ответственному)
//
// Параметры:
//  Объект - ДокументОбъект.КассовыйЧекКоррекции - заполняемый документ.
//
// Возвращаемое значение:
//  Структура - Данные кассира для отправки.
//
Функция ПолучитьРеквизитыКассира(Объект) Экспорт
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Наименование", "Кассир");
	СтруктураРеквизитов.Вставить("ИНН", Неопределено);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Переопределяемая процедура получает доступные системы налогообложения для организации.
//
// Параметры:
//  Организация - ОпределяемыйТип.ОрганизацияБПО -  Организация по которой получаются системы налогообложения.
//  МассивСистемНалогообложения - Перечисление.ТипыСистемНалогообложенияККТ - список систем налогообложения.
//  СтандартнаяОбработка	 - Булево - Признак выполнения стандартной (системной) обработки события.
//
Процедура ПолучитьДоступныеСистемыНалогообложения(Организация, МассивСистемНалогообложения, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти
