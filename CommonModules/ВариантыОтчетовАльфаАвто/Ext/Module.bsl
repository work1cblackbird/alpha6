
#Область ПрограммныйИнтерфейс

// см. ВариантыОтчетовПереопределяемый.ОпределитьРазделыСВариантамиОтчетов
Процедура ОпределитьРазделыСВариантамиОтчетов(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.Снабжение);
	Разделы.Добавить(Метаданные.Подсистемы.Склад);
	Разделы.Добавить(Метаданные.Подсистемы.Продажи);
	Разделы.Добавить(Метаданные.Подсистемы.Розница);
	Разделы.Добавить(Метаданные.Подсистемы.Маркетинг);
	Разделы.Добавить(Метаданные.Подсистемы.Финансы);
	Разделы.Добавить(Метаданные.Подсистемы.Предприятие);
	Разделы.Добавить(Метаданные.Подсистемы.Автосервис);
	Разделы.Добавить(Метаданные.Подсистемы.АвтосалонЗаказы);
	Разделы.Добавить(Метаданные.Подсистемы.АвтосалонПродажи);
	Разделы.Добавить(Метаданные.Подсистемы.АвтосалонСклад);
	Разделы.Добавить(Метаданные.Подсистемы.АвтосалонСнабжение);
	Разделы.Добавить(Метаданные.Подсистемы.АвтомобилиСПробегом);
	Разделы.Добавить(Метаданные.Подсистемы.АрендаАвтомобилей);
	
КонецПроцедуры

// Возвращает текст запроса получения вариантов отчетов.
//
// Параметры:
//  ЕстьОтборПоТипуДоступа - булево, отбор по типу доступа
//
// Возвращаемое значение:
//   Строка - текст запроса.
//
Функция ТекстЗапросаОтчетовСУточненнымиОтборами(ЕстьОтборПоТипуДоступа = Ложь) Экспорт
	
	СуффиксТекущегоЯзыка = ОбщегоНазначения.СуффиксЯзыкаТекущегоПользователя();
	
	Если СуффиксТекущегоЯзыка  <> Неопределено Тогда
		ТекстЗапросаПредварительный = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВариантыОтчетов.Ссылка КАК Ссылка,
		|	ВариантыОтчетов.Родитель КАК Родитель,
		|	ВЫБОР
		|		КОГДА &ЕстьПравоЧтенияАвторов
		|			ТОГДА ВариантыОтчетов.Автор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Автор,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ВариантыОтчетов.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Внутренний)
		|			ТОГДА ВариантыОтчетов.Отчет.Синоним
		|		КОГДА ВариантыОтчетов.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный)
		|			ТОГДА """"
		|		ИНАЧЕ ПОДСТРОКА(ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК СТРОКА(150)), 14, 137)
		|	КОНЕЦ КАК НаименованиеОтчета,
		|	ВариантыОтчетов.ТипДоступа КАК ТипДоступа,
		|	ВариантыОтчетов.ОбъектДоступа КАК ОбъектДоступа,
		|	ВариантыОтчетов.ВидПериода КАК ВидПериода,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.Автор.Наименование КАК СТРОКА(1000)) КАК АвторПредставление,
		|	ЛОЖЬ КАК ТолькоДляАвтора,
		|	ВЫБОР
		|		КОГДА &ПолучатьИтоговуюТаблицу
		|			ТОГДА ВариантыОтчетов.Отчет
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Отчет,
		|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
		|	ВариантыОтчетов.ТипОтчета КАК ТипОтчета,
		|	ВариантыОтчетов.Пользовательский КАК Пользовательский,
		|	ВариантыОтчетов.ПредопределенныйВариант КАК ПредопределенныйВариант,
		|	ВариантыОтчетов.Назначение КАК Назначение,
		|	ВЫБОР
		|		КОГДА ВариантыОтчетов.Пользовательский
		|			ТОГДА ВариантыОтчетов.ИнтерактивнаяПометкаУдаления
		|		КОГДА ВариантыОтчетов.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Расширение)
		|			ТОГДА ДоступныеВариантыРасширений.Вариант ЕСТЬ NULL
		|		ИНАЧЕ ЕСТЬNULL(ВариантыКонфигурации.ПометкаУдаления, ВариантыОтчетов.ПометкаУдаления)
		|	КОНЕЦ КАК ПометкаУдаления,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА (ВариантыОтчетов.Пользовательский
		|				ИЛИ ВариантыОтчетов.ПредопределенныйВариант В (
		|						НЕОПРЕДЕЛЕНО,
		|						ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетов.ПустаяСсылка),
		|						ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетовРасширений.ПустаяСсылка)))
		|			ТОГДА ВариантыОтчетов.Наименование
		|		ИНАЧЕ ВариантыОтчетов.Наименование
		|		КОНЕЦ КАК ТекущиеНаименованиеВарианта,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.НаименованияПолей, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.НаименованияПолей, ВариантыРасширений.НаименованияПолей) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПолей КАК СТРОКА(1000))
		|	КОНЕЦ КАК НаименованияПолей,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.НаименованияПараметровИОтборов, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.НаименованияПараметровИОтборов, ВариантыРасширений.НаименованияПараметровИОтборов) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПараметровИОтборов КАК СТРОКА(1000))
		|	КОНЕЦ КАК НаименованияПараметровИОтборов,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.КлючевыеСлова, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.КлючевыеСлова, ВариантыРасширений.КлючевыеСлова) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.КлючевыеСлова КАК СТРОКА(1000))
		|	КОНЕЦ КАК КлючевыеСлова,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) <> """"
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.Описание, ВариантыРасширений.Описание) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
		|	КОНЕЦ КАК ТекущиеОписаниеВарианта
		|ПОМЕСТИТЬ ВариантыОтчетов
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредопределенныеВариантыОтчетовВерсийРасширений КАК ДоступныеВариантыРасширений
		|			ПО ВариантыОтчетов.ПредопределенныйВариант = ДоступныеВариантыРасширений.Вариант
		|			И ДоступныеВариантыРасширений.ВерсияРасширений = &ВерсияРасширений
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ВариантыКонфигурации
		|			ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыКонфигурации.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений КАК ВариантыРасширений
		|			ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыРасширений.Ссылка
		|ГДЕ
		|	(НЕ &ЕстьОтборПоТипамОтчетов
		|		ИЛИ ВариантыОтчетов.ТипОтчета В (&ТипыОтчетов))
		|	И ВариантыОтчетов.Отчет В (&ОтчетыПользователя)
		|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
		|	И ВариантыОтчетов.Контекст = &Контекст #УсловиеПоТипуДоступа#
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|";
	Иначе
		ТекстЗапросаПредварительный = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВариантыОтчетов.Ссылка КАК Ссылка,
		|	ВариантыОтчетов.Родитель КАК Родитель,
		|	ВЫБОР
		|		КОГДА &ЕстьПравоЧтенияАвторов
		|			ТОГДА ВариантыОтчетов.Автор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Автор,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ВариантыОтчетов.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Внутренний)
		|			ТОГДА ВариантыОтчетов.Отчет.Синоним
		|		КОГДА ВариантыОтчетов.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный)
		|			ТОГДА """"
		|		ИНАЧЕ ПОДСТРОКА(ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК СТРОКА(150)), 14, 137)
		|	КОНЕЦ КАК НаименованиеОтчета,
		|	ВариантыОтчетов.ТипДоступа КАК ТипДоступа,
		|	ВариантыОтчетов.ОбъектДоступа КАК ОбъектДоступа,
		|	ВариантыОтчетов.ВидПериода КАК ВидПериода,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.Автор.Наименование КАК СТРОКА(1000)) КАК АвторПредставление,
		|	ЛОЖЬ КАК ТолькоДляАвтора,
		|	ВЫБОР
		|		КОГДА &ПолучатьИтоговуюТаблицу
		|			ТОГДА ВариантыОтчетов.Отчет
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Отчет,
		|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
		|	ВариантыОтчетов.ТипОтчета КАК ТипОтчета,
		|	ВариантыОтчетов.Пользовательский КАК Пользовательский,
		|	ВариантыОтчетов.ПредопределенныйВариант КАК ПредопределенныйВариант,
		|	ВариантыОтчетов.Назначение КАК Назначение,
		|	ВЫБОР
		|		КОГДА ВариантыОтчетов.Пользовательский
		|			ТОГДА ВариантыОтчетов.ИнтерактивнаяПометкаУдаления
		|		КОГДА ВариантыОтчетов.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Расширение)
		|			ТОГДА ДоступныеВариантыРасширений.Вариант ЕСТЬ NULL
		|		ИНАЧЕ ЕСТЬNULL(ВариантыКонфигурации.ПометкаУдаления, ВариантыОтчетов.ПометкаУдаления)
		|	КОНЕЦ КАК ПометкаУдаления,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА &ЭтоОсновнойЯзык
		|			И (ВариантыОтчетов.Пользовательский
		|				ИЛИ ВариантыОтчетов.ПредопределенныйВариант В (
		|						НЕОПРЕДЕЛЕНО,
		|						ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетов.ПустаяСсылка),
		|						ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетовРасширений.ПустаяСсылка)))
		|			ТОГДА ВариантыОтчетов.Наименование
		|		КОГДА НЕ &ЭтоОсновнойЯзык
		|			И (ВариантыОтчетов.Пользовательский
		|				ИЛИ ВариантыОтчетов.ПредопределенныйВариант В (
		|						НЕОПРЕДЕЛЕНО,
		|						ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетов.ПустаяСсылка),
		|						ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетовРасширений.ПустаяСсылка)))
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияВариантов.Наименование, ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
		|		КОГДА &ЭтоОсновнойЯзык
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ВариантыКонфигурации.Наименование, ВариантыРасширений.Наименование), ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ПредставленияИзКонфигурации.Наименование, ПредставленияИзРасширений.Наименование), ПредставленияВариантов.Наименование) КАК СТРОКА(1000))
		|	КОНЕЦ КАК ТекущиеНаименованиеВарианта,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА &ЭтоОсновнойЯзык
		|				И ПОДСТРОКА(ВариантыОтчетов.НаименованияПолей, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.НаименованияПолей, ВариантыРасширений.НаименованияПолей) КАК СТРОКА(1000))
		|		КОГДА НЕ &ЭтоОсновнойЯзык
		|				И ПОДСТРОКА(ВариантыОтчетов.НаименованияПолей, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияИзКонфигурации.НаименованияПолей, ПредставленияИзРасширений.НаименованияПолей) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПолей КАК СТРОКА(1000))
		|	КОНЕЦ КАК НаименованияПолей,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА &ЭтоОсновнойЯзык
		|				И ПОДСТРОКА(ВариантыОтчетов.НаименованияПараметровИОтборов, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.НаименованияПараметровИОтборов, ВариантыРасширений.НаименованияПараметровИОтборов) КАК СТРОКА(1000))
		|		КОГДА НЕ &ЭтоОсновнойЯзык
		|				И ПОДСТРОКА(ВариантыОтчетов.НаименованияПараметровИОтборов, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияИзКонфигурации.НаименованияПараметровИОтборов, ПредставленияИзРасширений.НаименованияПараметровИОтборов) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПараметровИОтборов КАК СТРОКА(1000))
		|	КОНЕЦ КАК НаименованияПараметровИОтборов,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА &ЭтоОсновнойЯзык
		|				И ПОДСТРОКА(ВариантыОтчетов.КлючевыеСлова, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.КлючевыеСлова, ВариантыРасширений.КлючевыеСлова) КАК СТРОКА(1000))
		|		КОГДА НЕ &ЭтоОсновнойЯзык
		|				И ПОДСТРОКА(ВариантыОтчетов.КлючевыеСлова, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияИзКонфигурации.КлючевыеСлова, ПредставленияИзРасширений.КлючевыеСлова) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.КлючевыеСлова КАК СТРОКА(1000))
		|	КОНЕЦ КАК КлючевыеСлова,
		|	ВЫБОР
		|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) <> """"
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
		|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.Описание, ВариантыРасширений.Описание) КАК СТРОКА(1000))
		|		КОГДА НЕ &ЭтоОсновнойЯзык И ПОДСТРОКА(ПредставленияВариантов.Описание, 1, 1) <> """"
		|			ТОГДА ВЫРАЗИТЬ(ПредставленияВариантов.Описание КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ПредставленияИзКонфигурации.Описание, ПредставленияИзРасширений.Описание), ВариантыОтчетов.Описание) КАК СТРОКА(1000))
		|	КОНЕЦ КАК ТекущиеОписаниеВарианта
		|ПОМЕСТИТЬ ВариантыОтчетов
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредопределенныеВариантыОтчетовВерсийРасширений КАК ДоступныеВариантыРасширений
		|			ПО ВариантыОтчетов.ПредопределенныйВариант = ДоступныеВариантыРасширений.Вариант
		|			И ДоступныеВариантыРасширений.ВерсияРасширений = &ВерсияРасширений
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ВариантыКонфигурации
		|			ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыКонфигурации.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений КАК ВариантыРасширений
		|			ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыРасширений.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Представления КАК ПредставленияВариантов
		|			ПО ВариантыОтчетов.Ссылка = ПредставленияВариантов.Ссылка
		|			И ПредставленияВариантов.КодЯзыка = &КодЯзыка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Представления КАК ПредставленияИзКонфигурации
		|			ПО ВариантыОтчетов.ПредопределенныйВариант = ПредставленияИзКонфигурации.Ссылка
		|			И ПредставленияИзКонфигурации.КодЯзыка = &КодЯзыка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений.Представления КАК ПредставленияИзРасширений
		|			ПО ВариантыОтчетов.ПредопределенныйВариант = ПредставленияИзРасширений.Ссылка
		|			И ПредставленияИзРасширений.КодЯзыка = &КодЯзыка
		|ГДЕ
		|	(НЕ &ЕстьОтборПоТипамОтчетов
		|		ИЛИ ВариантыОтчетов.ТипОтчета В (&ТипыОтчетов))
		|	И ВариантыОтчетов.Отчет В (&ОтчетыПользователя)
		|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
		|	И ВариантыОтчетов.Контекст = &Контекст #УсловиеПоТипуДоступа#
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|";
	КонецЕсли;
	
	ТекстЗапросаОбщий = "ВЫБРАТЬ
	|	ПредставленияПодсистем.Ссылка КАК Ссылка,
	|	ПредставленияПодсистем.Представление КАК Представление
	|ПОМЕСТИТЬ ПредставленияПодсистем
	|ИЗ
	|	&ПредставленияПодсистем КАК ПредставленияПодсистем
	|ГДЕ
	|	НЕ &ЭтоОсновнойЯзык
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.Пользовательский
	|			ИЛИ ЕСТЬNULL(РазмещениеОтчетовКонфигурации.Подсистема, РазмещениеОтчетовРасширений.Подсистема) ЕСТЬ NULL
	|		ТОГДА РазмещениеОтчетов.Подсистема
	|		ИНАЧЕ ЕСТЬNULL(РазмещениеОтчетовКонфигурации.Подсистема, РазмещениеОтчетовРасширений.Подсистема)
	|	КОНЕЦ КАК Подсистема
	|ПОМЕСТИТЬ РазмещениеВариантовОтчетов
	|ИЗ
	|	ВариантыОтчетов КАК ВариантыОтчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредопределенныеВариантыОтчетовВерсийРасширений КАК ДоступныеВариантыРасширений
	|			ПО ДоступныеВариантыРасширений.ВерсияРасширений = &ВерсияРасширений
	|			И ДоступныеВариантыРасширений.Вариант = ВариантыОтчетов.ПредопределенныйВариант
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Размещение КАК РазмещениеОтчетов
	|			ПО РазмещениеОтчетов.Ссылка = ВариантыОтчетов.Ссылка
	|			И (НЕ &ЕстьОтборПоПодсистемам
	|				ИЛИ РазмещениеОтчетов.Подсистема В (&ПодсистемыОтчетов))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Размещение КАК РазмещениеОтчетовКонфигурации
	|			ПО РазмещениеОтчетовКонфигурации.Ссылка = ВариантыОтчетов.ПредопределенныйВариант
	|			И (НЕ &ЕстьОтборПоПодсистемам
	|				ИЛИ РазмещениеОтчетовКонфигурации.Подсистема В (&ПодсистемыОтчетов))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений.Размещение КАК РазмещениеОтчетовРасширений
	|			ПО РазмещениеОтчетовРасширений.Ссылка = ВариантыОтчетов.ПредопределенныйВариант
	|			И (НЕ &ЕстьОтборПоПодсистемам
	|				ИЛИ РазмещениеОтчетовРасширений.Подсистема В (&ПодсистемыОтчетов))
	|ГДЕ
	|	(&НетОтбораПоПометкеУдаления
	|		ИЛИ НЕ ВариантыОтчетов.ПометкаУдаления
	|		ИЛИ НЕ ДоступныеВариантыРасширений.Вариант ЕСТЬ NULL)
	|	И ЕСТЬNULL(РазмещениеОтчетов.Использование, ИСТИНА) 
	|	И НЕ ЕСТЬNULL(РазмещениеОтчетов.Подсистема,
	|		ЕСТЬNULL(РазмещениеОтчетовКонфигурации.Подсистема, РазмещениеОтчетовРасширений.Подсистема)) ЕСТЬ NULL
	|	И (&ПоказыватьЛичныеВариантыОтчетовДругихАвторов
	|		ИЛИ ВариантыОтчетов.Автор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|		ИЛИ ВариантыОтчетов.Автор = &ТекущийПользователь)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РазмещениеВариантовОтчетов.Ссылка КАК Ссылка,
	|	РазмещениеВариантовОтчетов.Подсистема,
	|	ДоступныеВариантыОтчетов.Видимость
	|ПОМЕСТИТЬ НастройкиВариантовОтчетов
	|ИЗ
	|	РазмещениеВариантовОтчетов КАК РазмещениеВариантовОтчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставыГруппПользователей КАК СоставыГруппПользователей
	|			ПО СоставыГруппПользователей.Пользователь = &ТекущийПользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК ДоступныеВариантыОтчетов
	|			ПО ДоступныеВариантыОтчетов.Вариант = РазмещениеВариантовОтчетов.Ссылка
	|			И ДоступныеВариантыОтчетов.Подсистема В (
	|				РазмещениеВариантовОтчетов.Подсистема,
	|				ЗНАЧЕНИЕ(Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК НедоступныеВариантыОтчетов
	|			ПО НедоступныеВариантыОтчетов.Вариант = РазмещениеВариантовОтчетов.Ссылка
	|			И НедоступныеВариантыОтчетов.Подсистема = РазмещениеВариантовОтчетов.Подсистема
	|			И (НедоступныеВариантыОтчетов.Пользователь В (&ТекущийПользователь, НЕОПРЕДЕЛЕНО)
	|				ИЛИ ТИПЗНАЧЕНИЯ(&ТекущийПользователь) = ТИП(Справочник.Пользователи)
	|					И НедоступныеВариантыОтчетов.Пользователь = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|				ИЛИ ТИПЗНАЧЕНИЯ(&ТекущийПользователь) = ТИП(Справочник.ВнешниеПользователи)
	|					И НедоступныеВариантыОтчетов.Пользователь = ЗНАЧЕНИЕ(Справочник.ГруппыВнешнихПользователей.ВсеВнешниеПользователи))
	|			И НЕ НедоступныеВариантыОтчетов.Видимость
	|ГДЕ
	|	&ЕстьОтборПоВидимости
	|	И ДоступныеВариантыОтчетов.Пользователь В (СоставыГруппПользователей.ГруппаПользователей, НЕОПРЕДЕЛЕНО)
	|	И ДоступныеВариантыОтчетов.Подсистема В (&ПодсистемыОтчетов)
	|	И ДоступныеВариантыОтчетов.Видимость
	|	И НедоступныеВариантыОтчетов.Вариант ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РазмещениеВариантовОтчетов.Ссылка КАК Ссылка,
	|	РазмещениеВариантовОтчетов.Подсистема,
	|	ЕСТЬNULL(ИдентификаторыМетаданных.Синоним, ЕСТЬNULL(ИдентификаторыРасширений.Синоним, ЕСТЬNULL(ПредставленияПодсистем.Представление, """"))) КАК НаименованиеПодсистемы
	|ПОМЕСТИТЬ РазмещениеСУчетомНастроек
	|ИЗ
	|	РазмещениеВариантовОтчетов КАК РазмещениеВариантовОтчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыМетаданных
	|			ПО ИдентификаторыМетаданных.Ссылка = РазмещениеВариантовОтчетов.Подсистема
	|			И &ЭтоОсновнойЯзык
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовРасширений КАК ИдентификаторыРасширений
	|			ПО ИдентификаторыРасширений.Ссылка = РазмещениеВариантовОтчетов.Подсистема
	|			И &ЭтоОсновнойЯзык
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредставленияПодсистем КАК ПредставленияПодсистем
	|			ПО ПредставленияПодсистем.Ссылка = РазмещениеВариантовОтчетов.Подсистема
	|			И НЕ &ЭтоОсновнойЯзык
	|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиВариантовОтчетов КАК НастройкиВариантовОтчетов
	|			ПО НастройкиВариантовОтчетов.Ссылка = РазмещениеВариантовОтчетов.Ссылка
	|			И НастройкиВариантовОтчетов.Подсистема В (
	|				РазмещениеВариантовОтчетов.Подсистема,
	|				ЗНАЧЕНИЕ(Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка))
	|ГДЕ
	|	НЕ &ЕстьОтборПоВидимости
	|		ИЛИ НастройкиВариантовОтчетов.Видимость = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка КАК Ссылка,
	|	ВариантыОтчетов.Родитель КАК Родитель,
	|	ВариантыОтчетов.ТекущиеНаименованиеВарианта КАК НаименованиеВарианта,
	|	ВариантыОтчетов.НаименованиеОтчета КАК НаименованиеОтчета,
	|	ВариантыОтчетов.ТолькоДляАвтора КАК ТолькоДляАвтора,
	|	ВЫБОР
	|		КОГДА &ЕстьПравоЧтенияАвторов
	|			ТОГДА ВариантыОтчетов.Автор
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Автор,
	|	ВЫБОР
	|		КОГДА &ЕстьПравоЧтенияАвторов
	|			ТОГДА ЕСТЬNULL(ВариантыОтчетов.Автор.Наименование, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК АвторПредставление,
	|	ВариантыОтчетов.Отчет КАК Отчет,
	|	ВЫБОР
	|		КОГДА &ПолучатьИтоговуюТаблицу
	|			ТОГДА ВариантыОтчетов.Отчет.Имя
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИмяОтчета,
	|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
	|	ВариантыОтчетов.ТипОтчета КАК ТипОтчета,
	|	ВариантыОтчетов.Пользовательский КАК Пользовательский,
	|	ВариантыОтчетов.ПредопределенныйВариант КАК ПредопределенныйВариант,
	|	ВариантыОтчетов.Назначение КАК Назначение,
	|	ВариантыОтчетов.НаименованияПараметровИОтборов КАК НаименованияПараметровИОтборов,
	|	ВариантыОтчетов.НаименованияПолей КАК НаименованияПолей,
	|	ВариантыОтчетов.КлючевыеСлова КАК КлючевыеСлова,
	|	ВариантыОтчетов.ТекущиеОписаниеВарианта КАК Описание,
	|	Размещение.Подсистема КАК Подсистема,
	|	Размещение.НаименованиеПодсистемы КАК НаименованиеПодсистемы,
	|	НЕОПРЕДЕЛЕНО КАК КлючПользовательскойНастройки,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеПользовательскойНастройки,
	|	ВариантыОтчетов.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	ВариантыОтчетов КАК ВариантыОтчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазмещениеСУчетомНастроек КАК Размещение
	|			ПО ВариантыОтчетов.Ссылка = Размещение.Ссылка
	|ГДЕ
	|	(&НетОтбораПоПометкеУдаления
	|		ИЛИ НЕ ВариантыОтчетов.ПометкаУдаления)
	|	И НЕ &ПоискТолькоВариантовБезПодсистем
	|	И &ВариантыИПодсистемыПоСтрокеПоиска
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВариантыОтчетов.Ссылка,
	|	ВариантыОтчетов.Родитель,
	|	ВариантыОтчетов.ТекущиеНаименованиеВарианта,
	|	ВариантыОтчетов.НаименованиеОтчета,
	|	ВариантыОтчетов.ТолькоДляАвтора,
	|	ВЫБОР
	|		КОГДА &ЕстьПравоЧтенияАвторов
	|			ТОГДА ВариантыОтчетов.Автор
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ЕстьПравоЧтенияАвторов
	|			ТОГДА ЕСТЬNULL(ВариантыОтчетов.Автор.Наименование, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВариантыОтчетов.Отчет,
	|	ВЫБОР
	|		КОГДА &ПолучатьИтоговуюТаблицу
	|			ТОГДА ВариантыОтчетов.Отчет.Имя
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.ТипОтчета,
	|	ВариантыОтчетов.Пользовательский,
	|	ВариантыОтчетов.ПредопределенныйВариант,
	|	ВариантыОтчетов.Назначение,
	|	ВариантыОтчетов.НаименованияПараметровИОтборов,
	|	ВариантыОтчетов.НаименованияПолей,
	|	ВариантыОтчетов.КлючевыеСлова,
	|	ВариантыОтчетов.ТекущиеОписаниеВарианта,
	|	Значение(Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка),
	|	Размещение.НаименованиеПодсистемы,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВариантыОтчетов.ПометкаУдаления
	|ИЗ
	|	ВариантыОтчетов КАК ВариантыОтчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РазмещениеСУчетомНастроек КАК Размещение
	|		ПО ВариантыОтчетов.Ссылка = Размещение.Ссылка
	|ГДЕ
	|	(&НетОтбораПоПометкеУдаления
	|			ИЛИ НЕ ВариантыОтчетов.ПометкаУдаления)
	|	И &ВариантыИПодсистемыПоСтрокеПоиска
	|	И Размещение.Подсистема ЕСТЬ NULL
	|	И &ИскатьВариантыБезПодсистем
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Пользовательские.Вариант,
	|	ВариантыОтчетов.Родитель,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	Пользовательские.КлючПользовательскойНастройки,
	|	Пользовательские.Наименование,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ВариантыОтчетов КАК ВариантыОтчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПользовательскиеНастройкиОтчетов КАК Пользовательские
	|			ПО ВариантыОтчетов.Ссылка = Пользовательские.Вариант
	|ГДЕ
	|	(&НетОтбораПоПометкеУдаления
	|		ИЛИ ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ)
	|	И (&НетОтбораПоПометкеУдаления
	|		ИЛИ Пользовательские.ПометкаУдаления = ЛОЖЬ)
	|	И Пользовательские.Пользователь = &ТекущийПользователь
	|	И &ПользовательскиеНастройкиПоСтрокеПоиска";
	
	УсловиеПоТипуДоступа = "";
	Если ЕстьОтборПоТипуДоступа Тогда
		УсловиеПоТипуДоступа = "
		|	И НЕ (НЕ ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.БезОграничения)
		|				И НЕ ВариантыОтчетов.Автор = &ТекущийПользователь
		|				И НЕ (ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПустаяСсылка)
		|					И (ВариантыОтчетов.Автор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) ИЛИ ВариантыОтчетов.Автор = НЕОПРЕДЕЛЕНО))
		|				И НЕ (ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПользователю)
		|						И ВариантыОтчетов.ОбъектДоступа = &ТекущийПользователь)
		|				И НЕ (ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПодразделениюКомпании)
		|						И ВариантыОтчетов.ОбъектДоступа = &ПодразделениеКомпании)
		|				И НЕ (ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоГруппеПользователей)
		|						И ВариантыОтчетов.ОбъектДоступа В
		|							(ВЫБРАТЬ
		|								ГруппыПользователейСостав.Ссылка
		|							ИЗ
		|								Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
		|							ГДЕ
		|								ГруппыПользователейСостав.Пользователь = &ТекущийПользователь)))";
	КонецЕсли;
	ТекстЗапросаПредварительный = СтрЗаменить(ТекстЗапросаПредварительный, "#УсловиеПоТипуДоступа#", УсловиеПоТипуДоступа);
	
	Возврат ТекстЗапросаПредварительный + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапросаОбщий;
	
КонецФункции

// Возвращает текст запроса получения вариантов отчетов.
//
// Параметры:
//  ОтборПоТипуДоступа - булево, отбор по типу доступа.
//  ЕстьОтборПоПрототипуКлюча - булево, отбор по прототипу ключа.
//
// Возвращаемое значение:
//   Строка - текст запроса.
//
Функция ТекстЗапросаОтчетовСПростымиОтборами(ОтборПоТипуДоступа = Ложь, ЕстьОтборПоПрототипуКлюча = Ложь) Экспорт
	
	СуффиксТекущегоЯзыка = ОбщегоНазначения.СуффиксЯзыкаТекущегоПользователя();
	
	Если СуффиксТекущегоЯзыка <> Неопределено Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВариантыОтчетов.Ссылка КАК Ссылка,
		|	ВариантыОтчетов.Родитель КАК Родитель,
		|	ВариантыОтчетов.ВидПериода КАК ВидПериода,
		|	ВариантыОтчетов.ТипДоступа КАК ТипДоступа,
		|	ВариантыОтчетов.ОбъектДоступа КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА НЕ ПользовательскиеВариантыОтчетов.Ссылка ЕСТЬ NULL
		|			ТОГДА ВариантыОтчетов.Наименование
		|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ВариантыКонфигурации.Наименование, ВариантыРасширений.Наименование), ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
		|	КОНЕЦ КАК Наименование,
		|	ЛОЖЬ КАК ТолькоДляАвтора,
		|	ВЫБОР
		|		КОГДА &ЕстьПравоЧтенияАвторов
		|			ТОГДА ВариантыОтчетов.Автор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Автор,
		|	ВЫБОР
		|		КОГДА &ЕстьПравоЧтенияАвторов
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(ВариантыОтчетов.Автор)
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК АвторПредставление,
		|	ВариантыОтчетов.Отчет КАК Отчет,
		|	&ИмяОтчета КАК ИмяОтчета,
		|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
		|	ВариантыОтчетов.ТипОтчета КАК ТипОтчета,
		|	ВариантыОтчетов.Пользовательский КАК Пользовательский,
		|	ВариантыОтчетов.ПредопределенныйВариант КАК ПредопределенныйВариант,
		|	ВариантыОтчетов.Назначение КАК Назначение,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПараметровИОтборов КАК СТРОКА(1000)) КАК НаименованияПараметровИОтборов,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПолей КАК СТРОКА(1000)) КАК НаименованияПолей,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.КлючевыеСлова КАК СТРОКА(1000)) КАК КлючевыеСлова,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) <> """"
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.Описание, ВариантыРасширений.Описание) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
		|	КОНЕЦ КАК Описание,
		|	НЕОПРЕДЕЛЕНО КАК Подсистема,
		|	"""" КАК НаименованиеПодсистемы,
		|	НЕОПРЕДЕЛЕНО КАК КлючПользовательскойНастройки,
		|	НЕОПРЕДЕЛЕНО КАК ПредставлениеПользовательскойНастройки,
		|	ВариантыОтчетов.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ВариантыКонфигурации
		|		ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыКонфигурации.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений КАК ВариантыРасширений
		|		ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыРасширений.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставыГруппПользователей КАК СоставыГруппПользователей
		|		ПО СоставыГруппПользователей.Пользователь = &ТекущийПользователь
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК ДоступныеВариантыОтчетов
		|		ПО ДоступныеВариантыОтчетов.Вариант = ВариантыОтчетов.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов КАК ПользовательскиеВариантыОтчетов
		|		ПО ПользовательскиеВариантыОтчетов.Ссылка = ВариантыОтчетов.Ссылка
		|		И (ПользовательскиеВариантыОтчетов.Пользовательский
		|			ИЛИ ПользовательскиеВариантыОтчетов.ПредопределенныйВариант В (
		|				НЕОПРЕДЕЛЕНО,
		|				ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетов.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетовРасширений.ПустаяСсылка)))
		|ГДЕ
		|	ВариантыОтчетов.Отчет В (&ОтчетыПользователя)
		|	И (НЕ &ЕстьОтборПоТипамОтчетов
		|		ИЛИ ВариантыОтчетов.ТипОтчета В (&ТипыОтчетов))
		|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
		|	И (НЕ ВариантыОтчетов.Пользовательский
		|		ИЛИ &НетОтбораПоПометкеУдаления
		|		ИЛИ НЕ ВариантыОтчетов.ИнтерактивнаяПометкаУдаления)
		|	И (ВариантыОтчетов.Пользовательский
		|		ИЛИ &НетОтбораПоПометкеУдаления
		|		ИЛИ НЕ ВариантыОтчетов.ПометкаУдаления)
		|	#УсловиеПоТипуДоступа#
		|	И (ИСТИНА ИЛИ (ЕСТЬNULL(ДоступныеВариантыОтчетов.Пользователь, НЕОПРЕДЕЛЕНО) В (СоставыГруппПользователей.ГруппаПользователей, НЕОПРЕДЕЛЕНО)
		|	И ЕСТЬNULL(ДоступныеВариантыОтчетов.Видимость, ИСТИНА)))
		|	И (&ЕстьОтборПоКонтексту
		|			И (ПользовательскиеВариантыОтчетов.Ссылка ЕСТЬ NULL
		|			ИЛИ НЕ ПользовательскиеВариантыОтчетов.Ссылка ЕСТЬ NULL
		|				И ВариантыОтчетов.Контекст = &Контекст)
		|		ИЛИ НЕ &ЕстьОтборПоКонтексту)";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВариантыОтчетов.Ссылка КАК Ссылка,
		|	ВариантыОтчетов.Родитель КАК Родитель,
		|	ВариантыОтчетов.ВидПериода КАК ВидПериода,
		|	ВариантыОтчетов.ТипДоступа КАК ТипДоступа,
		|	ВариантыОтчетов.ОбъектДоступа КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА &НеПолучатьОписания
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА &ЭтоОсновнойЯзык
		|			И НЕ ПользовательскиеВариантыОтчетов.Ссылка ЕСТЬ NULL
		|			ТОГДА ВариантыОтчетов.Наименование
		|		КОГДА НЕ &ЭтоОсновнойЯзык
		|			И НЕ ПользовательскиеВариантыОтчетов.Ссылка ЕСТЬ NULL
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияВариантов.Наименование, ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
		|		КОГДА &ЭтоОсновнойЯзык
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ВариантыКонфигурации.Наименование, ВариантыРасширений.Наименование), ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ПредставленияИзКонфигурации.Наименование, ПредставленияИзРасширений.Наименование), ПредставленияВариантов.Наименование) КАК СТРОКА(1000))
		|	КОНЕЦ КАК Наименование,
		|	ЛОЖЬ КАК ТолькоДляАвтора,
		|	ВЫБОР
		|		КОГДА &ЕстьПравоЧтенияАвторов
		|			ТОГДА ВариантыОтчетов.Автор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Автор,
		|	ВЫБОР
		|		КОГДА &ЕстьПравоЧтенияАвторов
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(ВариантыОтчетов.Автор)
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК АвторПредставление,
		|	ВариантыОтчетов.Отчет КАК Отчет,
		|	&ИмяОтчета КАК ИмяОтчета,
		|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
		|	ВариантыОтчетов.ТипОтчета КАК ТипОтчета,
		|	ВариантыОтчетов.Пользовательский КАК Пользовательский,
		|	ВариантыОтчетов.ПредопределенныйВариант КАК ПредопределенныйВариант,
		|	ВариантыОтчетов.Назначение КАК Назначение,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПараметровИОтборов КАК СТРОКА(1000)) КАК НаименованияПараметровИОтборов,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПолей КАК СТРОКА(1000)) КАК НаименованияПолей,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.КлючевыеСлова КАК СТРОКА(1000)) КАК КлючевыеСлова,
		|	ВЫБОР
		|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) <> """"
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
		|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.Описание, ВариантыРасширений.Описание) КАК СТРОКА(1000))
		|		КОГДА НЕ &ЭтоОсновнойЯзык И ПОДСТРОКА(ПредставленияВариантов.Описание, 1, 1) <> """"
		|			ТОГДА ВЫРАЗИТЬ(ПредставленияВариантов.Описание КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ПредставленияИзКонфигурации.Описание, ПредставленияИзРасширений.Описание), ВариантыОтчетов.Описание) КАК СТРОКА(1000))
		|	КОНЕЦ КАК Описание,
		|	НЕОПРЕДЕЛЕНО КАК Подсистема,
		|	"""" КАК НаименованиеПодсистемы,
		|	НЕОПРЕДЕЛЕНО КАК КлючПользовательскойНастройки,
		|	НЕОПРЕДЕЛЕНО КАК ПредставлениеПользовательскойНастройки,
		|	ВариантыОтчетов.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ВариантыКонфигурации
		|		ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыКонфигурации.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений КАК ВариантыРасширений
		|		ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыРасширений.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Представления КАК ПредставленияВариантов
		|		ПО ВариантыОтчетов.Ссылка = ПредставленияВариантов.Ссылка
		|		И (ПредставленияВариантов.КодЯзыка = &КодЯзыка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Представления КАК ПредставленияИзКонфигурации
		|		ПО ВариантыОтчетов.ПредопределенныйВариант = ПредставленияИзКонфигурации.Ссылка
		|		И (ПредставленияИзКонфигурации.КодЯзыка = &КодЯзыка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений.Представления КАК ПредставленияИзРасширений
		|		ПО ВариантыОтчетов.ПредопределенныйВариант = ПредставленияИзРасширений.Ссылка
		|		И (ПредставленияИзРасширений.КодЯзыка = &КодЯзыка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставыГруппПользователей КАК СоставыГруппПользователей
		|		ПО СоставыГруппПользователей.Пользователь = &ТекущийПользователь
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК ДоступныеВариантыОтчетов
		|		ПО ДоступныеВариантыОтчетов.Вариант = ВариантыОтчетов.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов КАК ПользовательскиеВариантыОтчетов
		|		ПО ПользовательскиеВариантыОтчетов.Ссылка = ВариантыОтчетов.Ссылка
		|		И (ПользовательскиеВариантыОтчетов.Пользовательский
		|			ИЛИ ПользовательскиеВариантыОтчетов.ПредопределенныйВариант В (
		|				НЕОПРЕДЕЛЕНО,
		|				ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетов.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетовРасширений.ПустаяСсылка)))
		|ГДЕ
		|	ВариантыОтчетов.Отчет В (&ОтчетыПользователя)
		|	И (НЕ &ЕстьОтборПоТипамОтчетов
		|		ИЛИ ВариантыОтчетов.ТипОтчета В (&ТипыОтчетов))
		|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
		|	И (НЕ ВариантыОтчетов.Пользовательский
		|		ИЛИ &НетОтбораПоПометкеУдаления
		|		ИЛИ НЕ ВариантыОтчетов.ИнтерактивнаяПометкаУдаления)
		|	И (ВариантыОтчетов.Пользовательский
		|		ИЛИ &НетОтбораПоПометкеУдаления
		|		ИЛИ НЕ ВариантыОтчетов.ПометкаУдаления)
		|	#УсловиеПоТипуДоступа#
		|	И (ИСТИНА ИЛИ (ЕСТЬNULL(ДоступныеВариантыОтчетов.Пользователь, НЕОПРЕДЕЛЕНО) В (СоставыГруппПользователей.ГруппаПользователей, НЕОПРЕДЕЛЕНО)
		|	И ЕСТЬNULL(ДоступныеВариантыОтчетов.Видимость, ИСТИНА)))
		|	И (&ЕстьОтборПоКонтексту
		|			И (ПользовательскиеВариантыОтчетов.Ссылка ЕСТЬ NULL
		|			ИЛИ НЕ ПользовательскиеВариантыОтчетов.Ссылка ЕСТЬ NULL
		|				И ВариантыОтчетов.Контекст = &Контекст)
		|		ИЛИ НЕ &ЕстьОтборПоКонтексту)";
	КонецЕсли;
	
	Если ОтборПоТипуДоступа И ЕстьОтборПоПрототипуКлюча Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#УсловиеПоТипуДоступа#", "
		|	И НЕ(НЕ ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.БезОграничения)
		|				И НЕ ВариантыОтчетов.Автор = &ТекущийПользователь
		|				И НЕ (ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПустаяСсылка)
		|					И (ВариантыОтчетов.Автор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) ИЛИ ВариантыОтчетов.Автор = НЕОПРЕДЕЛЕНО))
		|				И НЕ ВариантыОтчетов.КлючВарианта = &ПрототипКлюч
		|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПользователю)
		|						И ВариантыОтчетов.ОбъектДоступа = &ТекущийПользователь)
		|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПодразделениюКомпании)
		|						И ВариантыОтчетов.ОбъектДоступа = &ПодразделениеКомпании)
		|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоГруппеПользователей)
		|						И ВариантыОтчетов.ОбъектДоступа В
		|							(ВЫБРАТЬ
		|								ГруппыПользователейСостав.Ссылка
		|							ИЗ
		|								Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
		|							ГДЕ
		|								ГруппыПользователейСостав.Пользователь = &ТекущийПользователь)))");
	ИначеЕсли ОтборПоТипуДоступа Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#УсловиеПоТипуДоступа#", "
		|	И НЕ(НЕ ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.БезОграничения)
		|				И НЕ ВариантыОтчетов.Автор = &ТекущийПользователь
		|				И НЕ (ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПустаяСсылка)
		|					И (ВариантыОтчетов.Автор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) ИЛИ ВариантыОтчетов.Автор = НЕОПРЕДЕЛЕНО))
		|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПользователю)
		|						И ВариантыОтчетов.ОбъектДоступа = &ТекущийПользователь)
		|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПодразделениюКомпании)
		|						И ВариантыОтчетов.ОбъектДоступа = &ПодразделениеКомпании)
		|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоГруппеПользователей)
		|						И ВариантыОтчетов.ОбъектДоступа В
		|							(ВЫБРАТЬ
		|								ГруппыПользователейСостав.Ссылка
		|							ИЗ
		|								Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
		|							ГДЕ
		|								ГруппыПользователейСостав.Пользователь = &ТекущийПользователь)))");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#УсловиеПоТипуДоступа#", "");
	КонецЕсли;
	
	ИмяОтчета = "ВЫБОР
	|		КОГДА &ПолучатьИтоговуюТаблицу И ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовМетаданных)
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовМетаданных).Имя
	|		КОГДА &ПолучатьИтоговуюТаблицу И ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовРасширений)
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовРасширений).Имя
	|		КОГДА &ПолучатьИтоговуюТаблицу И ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(СТРОКА)
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК СТРОКА(150))
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда 
		
		ИмяОтчета = "ВЫБОР
		|		КОГДА &ПолучатьИтоговуюТаблицу И ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовМетаданных)
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовМетаданных).Имя
		|		КОГДА &ПолучатьИтоговуюТаблицу И ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовРасширений)
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовРасширений).Имя
		|		КОГДА &ПолучатьИтоговуюТаблицу И ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ДополнительныеОтчетыИОбработки)
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ДополнительныеОтчетыИОбработки).ИмяОбъекта
		|		КОГДА &ПолучатьИтоговуюТаблицу И ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(СТРОКА)
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК СТРОКА(150))
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ";
		
	КонецЕсли;
	
	Возврат СтрЗаменить(ТекстЗапроса, "&ИмяОтчета", ИмяОтчета);
	
КонецФункции

// Возвращает сведения о доступных вариантах отчетов.
//
// Параметры:
//   ПараметрыЗаполнения - Структура
//   АдресРезультата - УникальныйИдентификатор
//                   - Строка
//
Процедура НайтиВариантыОтчетовДляВывода(ПараметрыЗаполнения, АдресРезультата) Экспорт
	
	#Область ЗапросДоступныхВариантовОтчетов
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Подсистемы.Ссылка КАК Подсистема,
	|	Подсистемы.РазделСсылка КАК РазделСсылка,
	|	Подсистемы.Представление КАК Представление,
	|	Подсистемы.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ втПодсистемы
	|ИЗ
	|	&ТаблицаПодсистем КАК Подсистемы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка КАК Ссылка,
	|	ПредопределенныеРазмещение.Подсистема КАК Подсистема,
	|	ПредопределенныеРазмещение.Важный КАК Важный,
	|	ПредопределенныеРазмещение.СмТакже КАК СмТакже,
	|	ВЫБОР
	|		КОГДА &ЭтоОсновнойЯзык
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.Наименование, ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
	|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияИзКонфигурации.Наименование, ПредставленияВариантов.Наименование) КАК СТРОКА(1000))
	|	КОНЕЦ КАК Наименование,
	|	ВЫБОР
	|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) <> """"
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
	|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыКонфигурации.Описание, ВариантыОтчетов.Описание) КАК СТРОКА(1000))
	|		КОГДА НЕ &ЭтоОсновнойЯзык И ПОДСТРОКА(ПредставленияВариантов.Описание, 1, 1) <> """"
	|			ТОГДА ВЫРАЗИТЬ(ПредставленияВариантов.Описание КАК СТРОКА(1000))
	|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияИзКонфигурации.Описание, ВариантыОтчетов.Описание) КАК СТРОКА(1000))
	|	КОНЕЦ КАК Описание,
	|	ВариантыОтчетов.Отчет КАК Отчет,
	|	ВариантыОтчетов.ТипОтчета КАК ТипОтчета,
	|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
	|	ВариантыОтчетов.Автор КАК Автор,
	|	ВариантыОтчетов.Родитель КАК Родитель,
	|	ВЫБОР
	|		КОГДА НЕ &ОтображатьВсеВариантыОтчетов
	|			ИЛИ ВариантыОтчетов.Назначение В (&НазначенияВариантовОтчетов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВидимостьПоНазначению,
	|	ВариантыОтчетов.ПредопределенныйВариант.КлючЗамеров КАК КлючЗамеров
	|ПОМЕСТИТЬ втПредопределенные
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Размещение КАК ПредопределенныеРазмещение
	|			ПО (ВариантыОтчетов.Ссылка В (&ВариантыНайденныеПоиском)
	|				ИЛИ (&НетОтбораПоПодсистемамИОтчетам
	|				ИЛИ ПредопределенныеРазмещение.Подсистема В (&ПодсистемыНайденныеПоиском)))
	|			И ВариантыОтчетов.ПредопределенныйВариант = ПредопределенныеРазмещение.Ссылка
	|			И (ПредопределенныеРазмещение.Подсистема В (&МассивПодсистем))
	|			И (ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ)
	|			И (&НетОтбораПоПодсистемамИОтчетам
	|				ИЛИ ВариантыОтчетов.Отчет В (&ОтчетыПользователя))
	|			И (НЕ ПредопределенныеРазмещение.Ссылка В (&ОтключенныеВариантыПрограммы))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ВариантыКонфигурации
	|			ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыКонфигурации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Представления КАК ПредставленияИзКонфигурации
	|			ПО ВариантыОтчетов.ПредопределенныйВариант = ПредставленияИзКонфигурации.Ссылка
	|			И (ПредставленияИзКонфигурации.КодЯзыка = &КодЯзыка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Представления КАК ПредставленияВариантов
	|			ПО ВариантыОтчетов.Ссылка = ПредставленияВариантов.Ссылка
	|			И (ПредставленияВариантов.КодЯзыка = &КодЯзыка)
	|ГДЕ
	|	НЕ ВариантыОтчетов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВариантыОтчетов.Ссылка,
	|	ПредопределенныеРазмещение.Подсистема,
	|	ПредопределенныеРазмещение.Важный,
	|	ПредопределенныеРазмещение.СмТакже,
	|	ВЫБОР
	|		КОГДА &ЭтоОсновнойЯзык
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыРасширений.Наименование, ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
	|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияИзРасширений.Наименование, ПредставленияВариантов.Наименование) КАК СТРОКА(1000))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) <> """"
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
	|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВариантыРасширений.Описание, ВариантыОтчетов.Описание) КАК СТРОКА(1000))
	|		КОГДА НЕ &ЭтоОсновнойЯзык И ПОДСТРОКА(ПредставленияВариантов.Описание, 1, 1) <> """"
	|			ТОГДА ВЫРАЗИТЬ(ПредставленияВариантов.Описание КАК СТРОКА(1000))
	|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияИзРасширений.Описание, ВариантыОтчетов.Описание) КАК СТРОКА(1000))
	|	КОНЕЦ,
	|	ВариантыОтчетов.Отчет,
	|	ВариантыОтчетов.ТипОтчета,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.Автор,
	|	ВариантыОтчетов.Родитель,
	|	ВЫБОР
	|		КОГДА НЕ &ОтображатьВсеВариантыОтчетов
	|			ИЛИ ВариантыОтчетов.Назначение В (&НазначенияВариантовОтчетов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВариантыОтчетов.ПредопределенныйВариант.КлючЗамеров
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений.Размещение КАК ПредопределенныеРазмещение
	|			ПО (ВариантыОтчетов.Ссылка В (&ВариантыНайденныеПоиском)
	|				ИЛИ (&НетОтбораПоПодсистемамИОтчетам
	|				ИЛИ ПредопределенныеРазмещение.Подсистема В (&ПодсистемыНайденныеПоиском)))
	|			И ВариантыОтчетов.ПредопределенныйВариант = ПредопределенныеРазмещение.Ссылка
	|			И (ПредопределенныеРазмещение.Подсистема В (&МассивПодсистем))
	|			И (&НетОтбораПоПодсистемамИОтчетам
	|				ИЛИ ВариантыОтчетов.Отчет В (&ОтчетыПользователя))
	|			И (НЕ ПредопределенныеРазмещение.Ссылка В (&ОтключенныеВариантыПрограммы))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредопределенныеВариантыОтчетовВерсийРасширений КАК ДоступныеВариантыРасширений
	|			ПО ДоступныеВариантыРасширений.ВерсияРасширений = &ВерсияРасширений
	|			И ДоступныеВариантыРасширений.Вариант = ВариантыОтчетов.ПредопределенныйВариант
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений КАК ВариантыРасширений
	|			ПО ВариантыОтчетов.ПредопределенныйВариант = ВариантыРасширений.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений.Представления КАК ПредставленияИзРасширений
	|			ПО ВариантыОтчетов.ПредопределенныйВариант = ПредставленияИзРасширений.Ссылка
	|			И (ПредставленияИзРасширений.КодЯзыка = &КодЯзыка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Представления КАК ПредставленияВариантов
	|			ПО ВариантыОтчетов.Ссылка = ПредставленияВариантов.Ссылка
	|			И (ПредставленияВариантов.КодЯзыка = &КодЯзыка)
	|ГДЕ
	|	НЕ ВариантыОтчетов.ПометкаУдаления
	|	ИЛИ НЕ ДоступныеВариантыРасширений.Вариант ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РазмещениеОтчетов.Ссылка КАК Ссылка,
	|	РазмещениеОтчетов.Подсистема КАК Подсистема,
	|	РазмещениеОтчетов.Использование КАК Использование,
	|	РазмещениеОтчетов.Важный КАК Важный,
	|	РазмещениеОтчетов.СмТакже КАК СмТакже,
	|	ВЫБОР
	|		КОГДА &ЭтоОсновнойЯзык
	|			И (ВариантыОтчетов.Пользовательский
	|				ИЛИ ВариантыОтчетов.ПредопределенныйВариант В (
	|					НЕОПРЕДЕЛЕНО,
	|					ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетов.ПустаяСсылка),
	|					ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетовРасширений.ПустаяСсылка)))
	|			ТОГДА ВариантыОтчетов.Наименование
	|		КОГДА НЕ &ЭтоОсновнойЯзык
	|			И (ВариантыОтчетов.Пользовательский
	|				ИЛИ ВариантыОтчетов.ПредопределенныйВариант В (
	|					НЕОПРЕДЕЛЕНО,
	|					ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетов.ПустаяСсылка),
	|					ЗНАЧЕНИЕ(Справочник.ПредопределенныеВариантыОтчетовРасширений.ПустаяСсылка)))
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ПредставленияОтчетов.Наименование, ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
	|		КОГДА &ЭтоОсновнойЯзык
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ОтчетыКонфигурации.Наименование, ОтчетыРасширений.Наименование), ВариантыОтчетов.Наименование) КАК СТРОКА(1000))
	|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ПредставленияОтчетовКонфигурации.Наименование, ПредставленияОтчетовРасширений.Наименование), ПредставленияОтчетов.Наименование) КАК СТРОКА(1000))
	|	КОНЕЦ КАК Наименование,
	|	ВЫБОР
	|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) <> """"
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
	|		КОГДА &ЭтоОсновнойЯзык И ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ОтчетыКонфигурации.Описание, ОтчетыРасширений.Описание) КАК СТРОКА(1000))
	|		КОГДА НЕ &ЭтоОсновнойЯзык И ПОДСТРОКА(ПредставленияОтчетов.Описание, 1, 1) <> """"
	|			ТОГДА ВЫРАЗИТЬ(ПредставленияОтчетов.Описание КАК СТРОКА(1000))
	|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ЕСТЬNULL(ПредставленияОтчетовКонфигурации.Описание, ПредставленияОтчетовРасширений.Описание), ВариантыОтчетов.Описание) КАК СТРОКА(1000))
	|	КОНЕЦ КАК Описание,
	|	ВариантыОтчетов.Отчет,
	|	ВариантыОтчетов.ТипОтчета,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.Автор,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.Родитель = ЗНАЧЕНИЕ(Справочник.ВариантыОтчетов.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВариантыОтчетов.ПустаяСсылка)
	|		КОГДА ВариантыОтчетов.Родитель.Родитель = ЗНАЧЕНИЕ(Справочник.ВариантыОтчетов.ПустаяСсылка)
	|			ТОГДА ВариантыОтчетов.Родитель
	|		ИНАЧЕ ВариантыОтчетов.Родитель.Родитель
	|	КОНЕЦ КАК Родитель,
	|	ВЫБОР
	|		КОГДА НЕ &ОтображатьВсеВариантыОтчетов
	|			ИЛИ ВариантыОтчетов.Назначение В (&НазначенияВариантовОтчетов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВидимостьПоНазначению,
	|	ВариантыОтчетов.ПредопределенныйВариант.КлючЗамеров КАК КлючЗамеров
	|ПОМЕСТИТЬ втВарианты
	|ИЗ
	|	Справочник.ВариантыОтчетов.Размещение КАК РазмещениеОтчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|			ПО ВариантыОтчетов.Ссылка = РазмещениеОтчетов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Представления КАК ПредставленияОтчетов
	|			ПО ПредставленияОтчетов.Ссылка = ВариантыОтчетов.Ссылка
	|			И ПредставленияОтчетов.КодЯзыка = &КодЯзыка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ОтчетыКонфигурации
	|			ПО ОтчетыКонфигурации.Ссылка = ВариантыОтчетов.ПредопределенныйВариант
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Представления КАК ПредставленияОтчетовКонфигурации
	|			ПО ПредставленияОтчетовКонфигурации.Ссылка = ВариантыОтчетов.ПредопределенныйВариант
	|			И ПредставленияОтчетовКонфигурации.КодЯзыка = &КодЯзыка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений КАК ОтчетыРасширений
	|			ПО ОтчетыРасширений.Ссылка = ВариантыОтчетов.ПредопределенныйВариант
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетовРасширений.Представления КАК ПредставленияОтчетовРасширений
	|			ПО ПредставленияОтчетовРасширений.Ссылка = ВариантыОтчетов.ПредопределенныйВариант
	|			И ПредставленияОтчетовРасширений.КодЯзыка = &КодЯзыка
	|ГДЕ
	|	(РазмещениеОтчетов.Ссылка В (&ВариантыНайденныеПоиском)
	|		ИЛИ (&НетОтбораПоПодсистемамИОтчетам
	|			ИЛИ РазмещениеОтчетов.Подсистема В (&ПодсистемыНайденныеПоиском)))
	|	И РазмещениеОтчетов.Подсистема В (&МассивПодсистем)
	|	И (РазмещениеОтчетов.Ссылка.Автор = &ТекущийПользователь
	|			ИЛИ РазмещениеОтчетов.Ссылка.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.БезОграничения)
	|			ИЛИ (РазмещениеОтчетов.Ссылка.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПустаяСсылка)
	|				И (РазмещениеОтчетов.Ссылка.Автор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) ИЛИ РазмещениеОтчетов.Ссылка.Автор = НЕОПРЕДЕЛЕНО))
	|			ИЛИ (РазмещениеОтчетов.Ссылка.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПользователю) И РазмещениеОтчетов.Ссылка.ОбъектДоступа = &ТекущийПользователь)
	|			ИЛИ (РазмещениеОтчетов.Ссылка.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПодразделениюКомпании) И РазмещениеОтчетов.Ссылка.ОбъектДоступа = &ПодразделениеКомпании)
	|			ИЛИ (РазмещениеОтчетов.Ссылка.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоГруппеПользователей) И РазмещениеОтчетов.Ссылка.ОбъектДоступа В 
	|		(ВЫБРАТЬ
	|			ГруппыПользователейСостав.Ссылка
	|		ИЗ
	|			Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|		ГДЕ
	|			ГруппыПользователейСостав.Пользователь = &ТекущийПользователь))
	|		ИЛИ (РазмещениеОтчетов.Ссылка.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПустаяСсылка) И РазмещениеОтчетов.Ссылка.Автор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)))
	|	И (&НетОтбораПоПодсистемамИОтчетам
	|		ИЛИ ВариантыОтчетов.Отчет В (&ОтчетыПользователя))
	|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
	|	И (НЕ ВариантыОтчетов.Пользовательский
	|		ИЛИ НЕ ВариантыОтчетов.ИнтерактивнаяПометкаУдаления)
	|	И (ВариантыОтчетов.Пользовательский
	|		ИЛИ НЕ ВариантыОтчетов.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(втВарианты.Ссылка, втПредопределенные.Ссылка) КАК Ссылка,
	|	ЕСТЬNULL(втВарианты.Подсистема, втПредопределенные.Подсистема) КАК Подсистема,
	|	ЕСТЬNULL(втВарианты.Важный, втПредопределенные.Важный) КАК Важный,
	|	ЕСТЬNULL(втВарианты.СмТакже, втПредопределенные.СмТакже) КАК СмТакже,
	|	ЕСТЬNULL(втВарианты.Наименование, втПредопределенные.Наименование) КАК Наименование,
	|	ЕСТЬNULL(втВарианты.Описание, втПредопределенные.Описание) КАК Описание,
	|	ЕСТЬNULL(втВарианты.Автор, втПредопределенные.Автор) КАК Автор,
	|	ЕСТЬNULL(втВарианты.Отчет, втПредопределенные.Отчет) КАК Отчет,
	|	ЕСТЬNULL(втВарианты.ТипОтчета, втПредопределенные.ТипОтчета) КАК ТипОтчета,
	|	ЕСТЬNULL(втВарианты.КлючВарианта, втПредопределенные.КлючВарианта) КАК КлючВарианта,
	|	ЕСТЬNULL(втВарианты.Родитель, втПредопределенные.Родитель) КАК Родитель,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втВарианты.Родитель, втПредопределенные.Родитель) = ЗНАЧЕНИЕ(Справочник.ВариантыОтчетов.ПустаяСсылка)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВерхнийУровень,
	|	ЕСТЬNULL(втВарианты.ВидимостьПоНазначению, втПредопределенные.ВидимостьПоНазначению) КАК ВидимостьПоНазначению,
	|	ЕСТЬNULL(втВарианты.КлючЗамеров, втПредопределенные.КлючЗамеров) КАК КлючЗамеров
	|ПОМЕСТИТЬ втВсеВарианты
	|ИЗ
	|	втПредопределенные КАК втПредопределенные
	|		ПОЛНОЕ СОЕДИНЕНИЕ втВарианты КАК втВарианты
	|			ПО втПредопределенные.Ссылка = втВарианты.Ссылка
	|			И втПредопределенные.Подсистема = втВарианты.Подсистема
	|ГДЕ
	|	ЕСТЬNULL(втВарианты.Использование, ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втВсеВарианты.Ссылка КАК Ссылка,
	|	втВсеВарианты.Подсистема КАК Подсистема,
	|	втПодсистемы.Представление КАК ПодсистемаПредставление,
	|	ЕСТЬNULL(втПодсистемы.Приоритет, """") КАК ПодсистемаПриоритет,
	|	втПодсистемы.РазделСсылка КАК РазделСсылка,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.Подсистема = втПодсистемы.РазделСсылка
	|			И втВсеВарианты.СмТакже = ЛОЖЬ
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БезГруппы,
	|	втВсеВарианты.Важный КАК Важный,
	|	втВсеВарианты.СмТакже КАК СмТакже,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Дополнительный,
	|	МАКСИМУМ(ЕСТЬNULL(НедоступныеВариантыОтчетов.Видимость, ЕСТЬNULL(ДоступныеВариантыОтчетов.Видимость, ЛОЖЬ))) КАК Видимость,
	|	МАКСИМУМ(ЕСТЬNULL(НедоступныеВариантыОтчетов.БыстрыйДоступ, ЕСТЬNULL(ДоступныеВариантыОтчетов.БыстрыйДоступ, ЛОЖЬ))) КАК БыстрыйДоступ,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Внутренний)
	|				ИЛИ втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Расширение)
	|			ТОГДА втВсеВарианты.Отчет.Имя
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный)
	|			ТОГДА втВсеВарианты.Отчет.Наименование
	|		ИНАЧЕ ПОДСТРОКА(ВЫРАЗИТЬ(втВсеВарианты.Отчет КАК СТРОКА(150)), 14, 137)
	|	КОНЕЦ КАК ИмяОтчета,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Внутренний)
	|				ИЛИ втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Расширение)
	|			ТОГДА втВсеВарианты.Отчет.Синоним
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный)
	|			ТОГДА втВсеВарианты.Отчет.Наименование
	|		ИНАЧЕ ПОДСТРОКА(ВЫРАЗИТЬ(втВсеВарианты.Отчет КАК СТРОКА(150)), 14, 137)
	|	КОНЕЦ КАК НаименованиеОтчета,
	|	втВсеВарианты.Наименование КАК НаименованиеВарианта,
	|	ЕСТЬNULL(втВсеВарианты.Наименование, """") КАК Наименование,
	|	втВсеВарианты.Описание КАК Описание,
	|	втВсеВарианты.Автор КАК Автор,
	|	втВсеВарианты.Отчет КАК Отчет,
	|	втВсеВарианты.ТипОтчета КАК ТипОтчета,
	|	втВсеВарианты.КлючВарианта КАК КлючВарианта,
	|	втВсеВарианты.Родитель КАК Родитель,
	|	втВсеВарианты.ВерхнийУровень КАК ВерхнийУровень,
	|	втВсеВарианты.ВидимостьПоНазначению,
	|	втВсеВарианты.КлючЗамеров КАК КлючЗамеров
	|ИЗ
	|	втВсеВарианты КАК втВсеВарианты
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПодсистемы КАК втПодсистемы
	|			ПО втВсеВарианты.Подсистема = втПодсистемы.Подсистема
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставыГруппПользователей КАК СоставыГруппПользователей
	|			ПО СоставыГруппПользователей.Пользователь = &ТекущийПользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК ДоступныеВариантыОтчетов
	|			ПО ДоступныеВариантыОтчетов.Вариант = втВсеВарианты.Ссылка
	|			И ДоступныеВариантыОтчетов.Подсистема В (
	|				втВсеВарианты.Подсистема,
	|				ЗНАЧЕНИЕ(Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка))
	|			И ДоступныеВариантыОтчетов.Пользователь В (СоставыГруппПользователей.ГруппаПользователей, НЕОПРЕДЕЛЕНО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК НедоступныеВариантыОтчетов
	|			ПО НедоступныеВариантыОтчетов.Вариант = втВсеВарианты.Ссылка
	|			И НедоступныеВариантыОтчетов.Подсистема = втВсеВарианты.Подсистема
	|			И (НедоступныеВариантыОтчетов.Пользователь В (&ТекущийПользователь, НЕОПРЕДЕЛЕНО)
	|				ИЛИ ТИПЗНАЧЕНИЯ(&ТекущийПользователь) = ТИП(Справочник.Пользователи)
	|					И НедоступныеВариантыОтчетов.Пользователь = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|				ИЛИ ТИПЗНАЧЕНИЯ(&ТекущийПользователь) = ТИП(Справочник.ВнешниеПользователи)
	|					И НедоступныеВариантыОтчетов.Пользователь = ЗНАЧЕНИЕ(Справочник.ГруппыВнешнихПользователей.ВсеВнешниеПользователи))
	|			И НЕ НедоступныеВариантыОтчетов.Видимость
	|ГДЕ
	|	(&НетОтбораПоВидимости
	|		ИЛИ ДоступныеВариантыОтчетов.Видимость = ИСТИНА
	|			И НедоступныеВариантыОтчетов.Вариант ЕСТЬ NULL)
	|
	|СГРУППИРОВАТЬ ПО
	|	втВсеВарианты.Ссылка,
	|	втВсеВарианты.Подсистема,
	|	втПодсистемы.Представление,
	|	ЕСТЬNULL(втПодсистемы.Приоритет, """"),
	|	втПодсистемы.РазделСсылка,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.Подсистема = втПодсистемы.РазделСсылка
	|			И втВсеВарианты.СмТакже = ЛОЖЬ
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	втВсеВарианты.Важный,
	|	втВсеВарианты.СмТакже,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Внутренний)
	|				ИЛИ втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Расширение)
	|			ТОГДА втВсеВарианты.Отчет.Имя
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный)
	|			ТОГДА втВсеВарианты.Отчет.Наименование
	|		ИНАЧЕ ПОДСТРОКА(ВЫРАЗИТЬ(втВсеВарианты.Отчет КАК СТРОКА(150)), 14, 137)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Внутренний)
	|				ИЛИ втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Расширение)
	|			ТОГДА втВсеВарианты.Отчет.Синоним
	|		КОГДА втВсеВарианты.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный)
	|			ТОГДА втВсеВарианты.Отчет.Наименование
	|		ИНАЧЕ ПОДСТРОКА(ВЫРАЗИТЬ(втВсеВарианты.Отчет КАК СТРОКА(150)), 14, 137)
	|	КОНЕЦ,
	|	втВсеВарианты.Наименование,
	|	ЕСТЬNULL(втВсеВарианты.Наименование, """"),
	|	втВсеВарианты.Описание,
	|	втВсеВарианты.Автор,
	|	втВсеВарианты.Отчет,
	|	втВсеВарианты.ТипОтчета,
	|	втВсеВарианты.КлючВарианта,
	|	втВсеВарианты.Родитель,
	|	втВсеВарианты.ВерхнийУровень,
	|	втВсеВарианты.ВидимостьПоНазначению,
	|	втВсеВарианты.КлючЗамеров
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодсистемаПриоритет,
	|	ИмяОтчета,
	|	Наименование");
	
	#КонецОбласти
	
	ПоискПоСтроке = ЗначениеЗаполнено(ПараметрыЗаполнения.СтрокаПоиска);
	ТолькоТекущийРаздел = ПараметрыЗаполнения.РежимНастройки Или Не ПоискПоСтроке Или ПараметрыЗаполнения.ИскатьВоВсехРазделах = 0;
	ТаблицаПодсистем = ПараметрыЗаполнения.ПодсистемыПрограммы; // ТаблицаЗначений
	ТаблицаПодсистем.Индексы.Добавить("Ссылка");
	МассивПодсистем = ТаблицаПодсистем.ВыгрузитьКолонку("Ссылка");
	
	ПараметрыПоиска = Новый Структура;
	Если ПоискПоСтроке Тогда
		ПараметрыПоиска.Вставить("СтрокаПоиска", ПараметрыЗаполнения.СтрокаПоиска);
	КонецЕсли;
	Если ТолькоТекущийРаздел Тогда
		ПараметрыПоиска.Вставить("Подсистемы", МассивПодсистем);
	КонецЕсли;
	ПараметрыПоиска.Вставить(
		"ПоТипуДоступа",
		ПолучитьЗначениеПараметраСтруктуры(ПараметрыЗаполнения, "ОтборПоТипуДоступа", Ложь));
	
	РезультатПоиска = ВариантыОтчетов.НайтиВариантыОтчетов(ПараметрыПоиска, Ложь, Истина);
	
	// АльфаАвто
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПараметрыСеанса.ПодразделениеКомпании);
	// Конец АльфаАвто
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
	Запрос.УстановитьПараметр("МассивПодсистем", МассивПодсистем);
	Запрос.УстановитьПараметр("ТаблицаПодсистем", ТаблицаПодсистем);
	Запрос.УстановитьПараметр("РазделСсылка", ПараметрыЗаполнения.ТекущийРазделСсылка);
	Запрос.УстановитьПараметр("ЭтоОсновнойЯзык", ОбщегоНазначения.ЭтоОсновнойЯзык());
	Запрос.УстановитьПараметр("КодЯзыка", ТекущийЯзык().КодЯзыка);
	Запрос.УстановитьПараметр("ВариантыНайденныеПоиском", РезультатПоиска.Ссылки);
	Запрос.УстановитьПараметр("ПодсистемыНайденныеПоиском", РезультатПоиска.Подсистемы);
	Запрос.УстановитьПараметр("ОтчетыПользователя", ПараметрыПоиска.ОтчетыПользователя);
	Запрос.УстановитьПараметр("ОтключенныеВариантыПрограммы", ПараметрыПоиска.ОтключенныеВариантыПрограммы);
	Запрос.УстановитьПараметр("НетОтбораПоПодсистемамИОтчетам", Не ПоискПоСтроке И ПараметрыПоиска.Подсистемы.Количество() = 0);
	Запрос.УстановитьПараметр("НетОтбораПоВидимости", ПараметрыЗаполнения.РежимНастройки Или ПоискПоСтроке);
	Запрос.УстановитьПараметр("ВерсияРасширений", ПараметрыСеанса.ВерсияРасширений);
	Запрос.УстановитьПараметр("ОтображатьВсеВариантыОтчетов", ПараметрыЗаполнения.ОтображатьВсеВариантыОтчетов);
	Запрос.УстановитьПараметр("НазначенияВариантовОтчетов", ПараметрыЗаполнения.НазначенияВариантовОтчетов);
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	ЗаполнитьИменаОтчетов(ТаблицаРезультат);
	
	// АльфаАвто
	ТаблицаРезультат.Сортировать("ПодсистемаПриоритет, ИмяОтчета, Наименование");
	// Конец АльфаАвто
	
	ТаблицаРезультат.Колонки.Добавить("ВыводитсяВместеСОсновным", Новый ОписаниеТипов("Булево"));
	ТаблицаРезультат.Колонки.Добавить("КоличествоПодчиненных", Новый ОписаниеТипов("Число"));
	ТаблицаРезультат.Индексы.Добавить("Ссылка");
	
	Если ПоискПоСтроке Тогда
		// Удаление записей о вариантах, связанных с подсистемами, если запись не фигурирует в связи.
		Для Каждого КлючИЗначение Из РезультатПоиска.ВариантыСвязанныеСПодсистемами Цикл
			ВариантСсылка = КлючИЗначение.Ключ;
			СвязанныеПодсистемы = КлючИЗначение.Значение;
			Найденные = ТаблицаРезультат.НайтиСтроки(Новый Структура("Ссылка", ВариантСсылка));
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				Если СвязанныеПодсистемы.Найти(СтрокаТаблицы.Подсистема) = Неопределено Тогда
					ТаблицаРезультат.Удалить(СтрокаТаблицы);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		// Удаление записей о родителях, связанных с вариантами, если родитель пытается вывестись один (без вариантов).
		Для Каждого РодительСсылка Из РезультатПоиска.РодителиСвязанныеСВариантами Цикл
			МестаВывода = ТаблицаРезультат.НайтиСтроки(Новый Структура("Ссылка", РодительСсылка));
			Для Каждого СтрокаТаблицы Из МестаВывода Цикл
				Найденные = ТаблицаРезультат.НайтиСтроки(Новый Структура("Подсистема, Родитель", СтрокаТаблицы.Подсистема, РодительСсылка));
				Если Найденные.Количество() = 0 Тогда
					ТаблицаРезультат.Удалить(СтрокаТаблицы);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если ТолькоТекущийРаздел Тогда
		ДругиеРазделы = Новый Массив;
	Иначе
		КопияТаблицы = ТаблицаРезультат.Скопировать();
		КопияТаблицы.Свернуть("РазделСсылка");
		ДругиеРазделы = КопияТаблицы.ВыгрузитьКолонку("РазделСсылка");
		Индекс = ДругиеРазделы.Найти(ПараметрыЗаполнения.ТекущийРазделСсылка);
		Если Индекс <> Неопределено Тогда
			ДругиеРазделы.Удалить(Индекс);
		КонецЕсли;
	КонецЕсли;
	
	МассивСлов = ?(ПоискПоСтроке, 
		ВариантыОтчетовКлиентСервер.РазложитьСтрокуПоискаВМассивСлов(ВРег(СокрЛП(ПараметрыЗаполнения.СтрокаПоиска))),
		Неопределено);
	
	Результат = ВариантыОтчетов.ВариантыОтчетовДляВывода();
	Результат.ТолькоТекущийРаздел = ТолькоТекущийРаздел;
	Результат.ТаблицаПодсистем = ТаблицаПодсистем;
	Результат.ДругиеРазделы = ДругиеРазделы;
	Результат.Варианты = ТаблицаРезультат;
	Результат.ИспользоватьПодсветку = ПоискПоСтроке;
	Результат.РезультатПоиска = РезультатПоиска;
	Результат.МассивСлов = МассивСлов;
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
КонецПроцедуры

// Удаление личных вариантов отчетов при удалении пользователя. 
// 
// Параметры: 
//  ПользовательОбъект - СправочникОбъект.Пользователи - пользователь, личные варианты отчетов, которого удаляем.
//  Отказ - булево, признак отмены удаления
//
Процедура ПриУдаленииПользователя(ПользовательОбъект, Отказ) Экспорт
	Если ПользовательОбъект.ЭтоНовый()
		Или ПользовательОбъект.ОбменДанными.Загрузка
		Или Отказ
		Или Не ПользовательОбъект.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	// Установить пометку удаления личных вариантов пользователя.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВариантыОтчетов.Ссылка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Автор = &ПользовательСсылка
	|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|	И ВариантыОтчетов.ТипДоступа=ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПользователю)";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПользовательСсылка", ПользовательОбъект.Ссылка);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.ДополнительныеСвойства.Вставить("ИндексироватьСхему", Ложь);
		ВариантОбъект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
КонецПроцедуры

// Заполнение вариантов выбора для элементов периода
//  
// Параметры: 
//  Форма - форма отчета
//
Процедура ЗаполнитьСпискиВыбораПериода(Форма) Экспорт
	
	СписокВыбора = Форма.Элементы.ВидПериода.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить("Произвольный", "Произвольный");
	СписокВыбора.Добавить("День",    "День");
	СписокВыбора.Добавить("Неделя",  "Неделя");
	СписокВыбора.Добавить("Месяц",   "Месяц");
	СписокВыбора.Добавить("Квартал", "Квартал");
	СписокВыбора.Добавить("Год",     "Год");
	
	ЭлементыУправления = Новый Массив();
	ЭлементыУправления.Добавить("НачалоПериода");
	ЭлементыУправления.Добавить("КонецПериода");
	
	Для Каждого Элемент Из ЭлементыУправления Цикл
		
		СписокВыбора = Форма.Элементы[Элемент].СписокВыбора;
		СписокВыбора.Очистить();
		СписокВыбора.Добавить("НачалоПрошлогоГода", НСтр("ru = 'Начало прошлого года'"));
		СписокВыбора.Добавить("НачалоПрошлогоДня", НСтр("ru = 'Начало вчерашнего дня'"));
		СписокВыбора.Добавить("НачалоПрошлогоКвартала", НСтр("ru = 'Начало прошлого квартала'"));
		СписокВыбора.Добавить("НачалоПрошлогоМесяца", НСтр("ru = 'Начало прошлого месяца'"));
		СписокВыбора.Добавить("НачалоПрошлогоПолугодия", НСтр("ru = 'Начало прошлого полугодия'"));
		СписокВыбора.Добавить("НачалоПрошлойДекады", НСтр("ru = 'Начало прошлой декады'"));
		СписокВыбора.Добавить("НачалоПрошлойНедели", НСтр("ru = 'Начало прошлой недели'"));
		СписокВыбора.Добавить("НачалоСледующегоГода", НСтр("ru = 'Начало следующего года'"));
		СписокВыбора.Добавить("НачалоСледующегоДня", НСтр("ru = 'Начало завтрашнего дня'"));
		СписокВыбора.Добавить("НачалоСледующегоКвартала", НСтр("ru = 'Начало следующего квартала'"));
		СписокВыбора.Добавить("НачалоСледующегоМесяца", НСтр("ru = 'Начало следующего месяца'"));
		СписокВыбора.Добавить("НачалоСледующегоПолугодия", НСтр("ru = 'Начало следующего полугодия'"));
		СписокВыбора.Добавить("НачалоСледующейДекады", НСтр("ru = 'Начало следующей декады'"));
		СписокВыбора.Добавить("НачалоСледующейНедели", НСтр("ru = 'Начало следующей недели'"));
		СписокВыбора.Добавить("НачалоЭтогоГода", НСтр("ru = 'Начало этого года'"));
		СписокВыбора.Добавить("НачалоЭтогоДня", НСтр("ru = 'Начало этого дня'"));
		СписокВыбора.Добавить("НачалоЭтогоКвартала", НСтр("ru = 'Начало этого квартала'"));
		СписокВыбора.Добавить("НачалоЭтогоМесяца", НСтр("ru = 'Начало этого месяца'"));
		СписокВыбора.Добавить("НачалоЭтогоПолугодия", НСтр("ru = 'Начало этого полугодия'"));
		СписокВыбора.Добавить("НачалоЭтойДекады", НСтр("ru = 'Начало этой декады'"));
		СписокВыбора.Добавить("НачалоЭтойНедели", НСтр("ru = 'Начало этой недели'"));
		СписокВыбора.Добавить("ПроизвольнаяДата", НСтр("ru = 'Произвольная дата'"));
		
	КонецЦикла;
	
КонецПроцедуры

// Возврашает доступные варианты отчетов.
// 
// Параметры:
//  ОтчетСсылка - ОтчетСсылка - ссылка на отчет
//
// Возвращаемое значение:
//  ТаблицаЗначений:
//    * Ссылка - СправочникСсылка.ВариантыОтчетов
//    * Отчет - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//            - СправочникСсылка.ИдентификаторыОбъектовРасширений
//            - СправочникСсылка.ДополнительныеОтчетыИОбработки
//            - Строка
//    * КлючВарианта - Строка
//    * Наименование - Строка
//    * Описание - Строка
//    * Автор - СправочникСсылка.ВнешниеПользователи
//            - СправочникСсылка.Пользователи
//    * Пользовательский - Булево
//    * ТипОтчета - ПеречислениеСсылка.ТипыОтчетов
//    * ИмяОтчета - Строка
//
Функция ДоступныеВариантыОтчетов(ОтчетСсылка) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка,
	|	ВариантыОтчетов.Отчет,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.ПредопределенныйВариант.Описание КАК СТРОКА(1000))
	|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
	|	КОНЕЦ КАК Описание,
	|	ВариантыОтчетов.Автор,
	|	ВариантыОтчетов.Пользовательский,
	|	ВариантыОтчетов.ТипОтчета,
	|	&ИмяОтчета КАК ИмяОтчета
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Отчет = &Отчет
	|	И НЕ ВариантыОтчетов.ПометкаУдаления
	|	И НЕ(НЕ ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.БезОграничения)
	|				И НЕ ВариантыОтчетов.Автор = &ТекущийПользователь
	|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПользователю)
	|						И ВариантыОтчетов.ОбъектДоступа = &ТекущийПользователь)
	|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоПодразделениюКомпании)
	|						И ВариантыОтчетов.ОбъектДоступа = &ПодразделениеКомпании)
	|				И НЕ(ВариантыОтчетов.ТипДоступа = ЗНАЧЕНИЕ(Перечисление.ТипыДоступаКВариантуОтчета.ПоГруппеПользователей)
	|						И ВариантыОтчетов.ОбъектДоступа В
	|							(ВЫБРАТЬ
	|								ГруппыПользователейСостав.Ссылка
	|							ИЗ
	|								Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|							ГДЕ
	|								ГруппыПользователейСостав.Пользователь = &ТекущийПользователь)))
	|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
	|	И ВариантыОтчетов.КлючВарианта <> """"
	|	И ВариантыОтчетов.Контекст = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
	
	ИмяОтчета = "ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовМетаданных)
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовМетаданных).Имя
	|		КОГДА ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовРасширений)
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовРасширений).Имя
	|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК СТРОКА(150))
	|	КОНЕЦ";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда 
		
		ИмяОтчета = "ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовМетаданных)
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовМетаданных).Имя
		|		КОГДА ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовРасширений)
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовРасширений).Имя
		|		КОГДА ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ДополнительныеОтчетыИОбработки)
		|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ДополнительныеОтчетыИОбработки).ИмяОбъекта
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК СТРОКА(150))
		|	КОНЕЦ";
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяОтчета", ИмяОтчета);
	
	Запрос.УстановитьПараметр("Отчет", ОтчетСсылка);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
	Запрос.УстановитьПараметр("ОтключенныеВариантыПрограммы", ВариантыОтчетовПовтИсп.ОтключенныеВариантыПрограммы());
	Запрос.УстановитьПараметр("ПодразделениеКомпании",        ПараметрыСеанса.ПодразделениеКомпании);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Добавляем элементы на панель отчетов
// 
// Параметры:
//  Форма - форма отчета
//
Процедура ДополнитьЭлементамиПанельОтчетов(Форма) Экспорт
	
	// Выведем текущий раздел на форму
	ГруппаВерхняяПанель = Форма.Элементы.Найти("ВерхняяПанель");
	ЭлементПоиска = Форма.Элементы.Найти("СтрокаПоиска");
	НовыйЭлемент = Форма.Элементы.Вставить("ТекущийРазделСсылка", Тип("ПолеФормы"), ГруппаВерхняяПанель, ЭлементПоиска);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "ТекущийРазделСсылка";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Раздел'");
	НовыйЭлемент.КнопкаСоздания           = Ложь;
	НовыйЭлемент.КнопкаВыбора             = Ложь;
	НовыйЭлемент.КнопкаВыпадающегоСписка  = Истина;
	НовыйЭлемент.КнопкаОткрытия           = Ложь;
	НовыйЭлемент.КнопкаРегулирования      = Ложь;
	НовыйЭлемент.КнопкаОчистки            = Ложь;
	НовыйЭлемент.РастягиватьПоВертикали   = Ложь;
	НовыйЭлемент.РедактированиеТекста     = Ложь;
	НовыйЭлемент.РежимВыбораИзСписка      = Истина;
	НовыйЭлемент.ИсторияВыбораПриВводе    = ИсторияВыбораПриВводе.НеИспользовать;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ТекущийРазделСсылкаПриИзменении");
	
КонецПроцедуры

// Создание варианта отчета
// 
// Параметры:
//  Форма - форма отчета
//
Процедура ПриСозданииНаСервереВариантыОтчетов(Форма) Экспорт
	
	ДобавитьНаФормуОтраслевыеРеквизиты(Форма);
	
	Если Форма.Объект.ВидПериода = "Произвольный" Тогда
		Форма.НачалоПериода = Форма.Объект.НачалоПериода;
		Форма.КонецПериода  = Форма.Объект.КонецПериода;
	КонецЕсли;
	
	ДобавитьНаФормуОтраслевыеЭлементы(Форма);
	
	ВариантыОтчетовАльфаАвто.ЗаполнитьСпискиВыбораПериода(Форма);
	Произвольный = (Форма.Объект.ВидПериода = "Произвольный");
	Форма.Элементы.НачалоПериода.Доступность =  Произвольный;
	Форма.Элементы.КонецПериода.Доступность =  Произвольный;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает имена отчетов по метаданным.
//
// Параметры:
//   ТаблицаРезультат - ТаблицаЗначений - коллекция найденных вариантов отчетов, где:
//       * ИмяОтчета - Строка - имя отчета, как оно указано в метаданных.
//       * Наименование - Строка - наименование варианта отчета.
//       * Отчет - СправочникСсылка.ИдентификаторыОбъектовРасширений
//               - СправочникСсылка.ДополнительныеОтчетыИОбработки
//               - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//               - Строка - ссылка на идентификатор метаданных отчета.
//        * ТипОтчета - ПеречислениеСсылка.ТипыОтчетов - категория отчета.
//
Процедура ЗаполнитьИменаОтчетов(ТаблицаРезультат)
	
	ИдентификаторыОтчетов = Новый Массив;
	Для Каждого СтрокаОтчета Из ТаблицаРезультат Цикл
		Если Не СтрокаОтчета.Дополнительный Тогда
			ИдентификаторыОтчетов.Добавить(СтрокаОтчета.Отчет);
		КонецЕсли;
	КонецЦикла;
	ОбъектыОтчетов = ОбщегоНазначения.ОбъектыМетаданныхПоИдентификаторам(ИдентификаторыОтчетов, Ложь);
	НесуществующиеИНедоступные = Новый Массив;
	
	Для Каждого ОтчетДляВывода Из ТаблицаРезультат Цикл
		Если ОтчетДляВывода.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний
			Или ОтчетДляВывода.ТипОтчета = Перечисления.ТипыОтчетов.Расширение Тогда
			МетаданныеОтчета = ОбъектыОтчетов[ОтчетДляВывода.Отчет]; // ОбъектМетаданныхОтчет
			Если МетаданныеОтчета = Неопределено Или МетаданныеОтчета = Null Тогда
				НесуществующиеИНедоступные.Добавить(ОтчетДляВывода);
			ИначеЕсли ОтчетДляВывода.ИмяОтчета <> МетаданныеОтчета.Имя Тогда
				ОтчетДляВывода.ИмяОтчета = МетаданныеОтчета.Имя;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого НедоступныйОтчет Из НесуществующиеИНедоступные Цикл
		ТаблицаРезультат.Удалить(НедоступныйОтчет);
	КонецЦикла;
	
	ОтчетыНеименованные = ТаблицаРезультат.НайтиСтроки(Новый Структура("Наименование", "")); // Массив из СтрокаТаблицыЗначений
	Для Каждого ОписаниеОтчета Из ОтчетыНеименованные Цикл
		Если ЗначениеЗаполнено(ОписаниеОтчета.ИмяОтчета) Тогда 
			ОписаниеОтчета.Наименование = ОписаниеОтчета.ИмяОтчета;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьНаФормуОтраслевыеРеквизиты(Форма)
	
	ДобавляемыеРеквизиты = Новый Массив;
	ТипСтрока = Новый ОписаниеТипов("Строка");
	
	РеквизитНачалоПериода = Новый РеквизитФормы(
		"НачалоПериода",
		ТипСтрока,
		,
		НСтр("ru = 'Начало периода'")
	);
	ДобавляемыеРеквизиты.Добавить(РеквизитНачалоПериода);
	
	РеквизитКонецПериода = Новый РеквизитФормы(
		"КонецПериода",
		ТипСтрока,
		,
		НСтр("ru = 'Конец периода'")
	);
	ДобавляемыеРеквизиты.Добавить(РеквизитКонецПериода);
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
КонецПроцедуры

Процедура ДобавитьНаФормуОтраслевыеЭлементы(Форма)
	
	ЭлементАвтор = Форма.Элементы.Найти("Автор");
	
	// Вид периода
	НовыйЭлемент = Форма.Элементы.Вставить("ВидПериода", Тип("ПолеФормы"), Форма, ЭлементАвтор);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.ВидПериода";
	НовыйЭлемент.КнопкаВыпадающегоСписка  = Истина;;
	НовыйЭлемент.РедактированиеТекста     = Ложь;
	НовыйЭлемент.ИсторияВыбораПриВводе    = ИсторияВыбораПриВводе.НеИспользовать;
	НовыйЭлемент.Подсказка =
		НСтр("ru = 'Интервал периода, который будет установлен, при открытии отчета с данной настройкой'");
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ВидПериодаПриИзменении");
	
	// Начало периода
	НовыйЭлемент = Форма.Элементы.Вставить("НачалоПериода", Тип("ПолеФормы"), Форма, ЭлементАвтор);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "НачалоПериода";
	НовыйЭлемент.РежимВыбораИзСписка  = Истина;
	НовыйЭлемент.ИсторияВыбораПриВводе    = ИсторияВыбораПриВводе.НеИспользовать;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_НачалоПериодаПриИзменении");
	
	// Конец периода
	НовыйЭлемент = Форма.Элементы.Вставить("КонецПериода", Тип("ПолеФормы"), Форма, ЭлементАвтор);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "КонецПериода";
	НовыйЭлемент.РежимВыбораИзСписка  = Истина;
	НовыйЭлемент.ИсторияВыбораПриВводе    = ИсторияВыбораПриВводе.НеИспользовать;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_КонецПериодаПриИзменении");
	
	// Тип доступа
	НовыйЭлемент = Форма.Элементы.Вставить("ТипДоступа", Тип("ПолеФормы"), Форма, ЭлементАвтор);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.ТипДоступа";
	НовыйЭлемент.ИсторияВыбораПриВводе    = ИсторияВыбораПриВводе.НеИспользовать;
	НовыйЭлемент.Подсказка =
		НСтр("ru = 'Способ доступности настройки пользователям'");
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ТипДоступаПриИзменении");
	
	// Объект доступа
	НовыйЭлемент = Форма.Элементы.Вставить("ОбъектДоступа", Тип("ПолеФормы"), Форма, ЭлементАвтор);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.ОбъектДоступа";
	НовыйЭлемент.КнопкаСоздания           = Ложь;
	НовыйЭлемент.КнопкаВыбора             = Истина;
	НовыйЭлемент.КнопкаОткрытия           = Истина;
	НовыйЭлемент.КнопкаРегулирования      = Ложь;
	НовыйЭлемент.КнопкаОчистки            = Ложь;
	НовыйЭлемент.ВыбиратьТип              = Ложь;
	НовыйЭлемент.ИсторияВыбораПриВводе    = ИсторияВыбораПриВводе.НеИспользовать;
	НовыйЭлемент.Подсказка =
		НСтр("ru = 'Значение отбора настройки'");
	
	// Переместим поле "Автор" ниже
	ГруппаТехническаяИнформация = Форма.Элементы.Найти("ТехническаяИнформация");
	Форма.Элементы.Переместить(ЭлементАвтор, Форма, ГруппаТехническаяИнформация);
	
	Форма.Элементы.Пользователи.Видимость = Ложь;
	
КонецПроцедуры

Процедура ВариантыОтчетовОбработкаПолученияФормыОбработкаПолученияФормы(Источник, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	Если ВидФормы = "Форма" Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = Метаданные.ОбщиеФормы.ФормаОтчета;
	ИначеЕсли ВидФормы = "ФормаНастроек" Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = Метаданные.ОбщиеФормы.ФормаНастроекОтчета;
	ИначеЕсли ВидФормы = "ФормаВарианта" Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = Метаданные.ОбщиеФормы.ФормаВариантаОтчета;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьВариантВРегистрВариантовОтчета(Параметры) Экспорт
	
	Данные = Новый Структура("Вариант, Пользователь, Подсистема, Видимость, БыстрыйДоступ");
	Данные.Подсистема 		= Справочники.ИдентификаторыОбъектовМетаданных.ПустаяСсылка();
	Данные.Видимость 		= Истина;
	Данные.БыстрыйДоступ 	= Ложь;
	
	ОтчетДокументыПроведенныеЗаднимЧислом 	= Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоНаименованию(
												"Отчет по документам, проведенным задним числом (Отчет)");
	Данные.Вариант 							= Справочники.ВариантыОтчетов.НайтиПоРеквизиту("Отчет", ОтчетДокументыПроведенныеЗаднимЧислом);
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ВариантыОтчетов");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Данные.Вариант);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиВариантовОтчетов");
		ЭлементБлокировки.УстановитьЗначение("Вариант", Данные.Вариант);
		
		Блокировка.Заблокировать();
		
		Записи = РегистрыСведений.НастройкиВариантовОтчетов.СоздатьНаборЗаписей();
		Записи.Отбор.Вариант.Установить(Данные.Вариант);
		Записи.Отбор.Пользователь.Установить(Данные.Пользователь);
		Записи.Отбор.Подсистема.Установить(Данные.Подсистема);
		
		Запись = Записи.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Данные);
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Записи);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;

КонецПроцедуры

#КонецОбласти