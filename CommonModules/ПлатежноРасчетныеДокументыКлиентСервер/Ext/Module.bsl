////////////////////////////////////////////////////////////////////////////////
// Работа с объектами механизма "Платежно расчетные документы"
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Преобразовывает записи из таблицы в строку.
//
// Параметры:
//  Таблица  - ТаблицаЗначений, ДанныеФормыКоллекция - Таблица с записями "ПлатежноРасчетныеДокументы".
//  ПредставлениеПустого  - Строка - Значение возвращаемое для пустой таблицы.
//
// Возвращаемое значение:
//   Строка   - Строка представления платежно расчетных документов.
//
Функция ВСтроку(Таблица, ПредставлениеПустого = "") Экспорт
	
	Если НЕ ЗначениеЗаполнено(Таблица) Тогда
		Возврат ПредставлениеПустого;
	КонецЕсли;
	
	СоставСтроки = Новый Массив;
	
	Для Каждого Строка Из Таблица Цикл
		
		СоставСтроки.Добавить(ПредставлениеДокумента(Строка.Номер, Строка.Дата));
		
	КонецЦикла;
	
	Возврат СтрСоединить(СоставСтроки, "; ");
	
КонецФункции // ВСтроку()

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Разделяет строку представления платежно расчетного документа на "Номер" и "Дату".
//
// Параметры:
//  Представление  - Строка - Представление платежно расчетного документа.
//
// Возвращаемое значение:
//   Структура   - Структура с ключами "номер" и "дата".
//
Функция РазбитьПредставление(Представление) Экспорт
	
	Результат = Новый Структура("Номер,Дата", "", Дата(1, 1, 1));
	
	Если ПустаяСтрока(Представление) Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	ПредставлениеСРазделителями = СтрЗаменить(Представление, " от ", "@");
	НомерИДата = СтрРазделить(ПредставлениеСРазделителями, "@", Ложь);
	
	КоличествоМакс = 2;
	Если НомерИДата.Количество() < КоличествоМакс Тогда
		
		Результат.Номер = СокрЛП(НомерИДата[0]);
		Возврат Результат;
		
	КонецЕсли;
	
	// Попробуем преобразовать дату.
	ДатаИзПредставления = СокрЛП(НомерИДата[1]);
	Если СтрНайти(ДатаИзПредставления, ".") > 0 Тогда
		
		ДатаРазделенная = СтрРазделить(ДатаИзПредставления, ".");
		
	ИначеЕсли СтрНайти(ДатаИзПредставления, ",") > 0 Тогда
		
		ДатаРазделенная = СтрРазделить(ДатаИзПредставления, ",");
		
	Иначе
		
		Результат.Номер = Представление;
		Возврат Результат;
		
	КонецЕсли;
	
	Попытка
		Результат.Дата = Дата(ДатаРазделенная[2] + ДатаРазделенная[1] + ДатаРазделенная[0]);
		Результат.Номер = СокрЛП(НомерИДата[0]);
	Исключение
		Результат.Номер = Представление;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // РазбитьПредставление()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеДокумента(Номер, Дата)
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		
		Возврат Номер;
		
	КонецЕсли;
	
	Возврат Номер + " от " + Формат(Дата, "ДЛФ=D");
	
КонецФункции

#КонецОбласти