

#Область ПрограммныйИнтерфейс

// Процедура для проверки ставки НДС в документе
//
// Параметры:
//  Объект - ДокументОбъект - Объект, для которого выполняется обработка события.
//  Отказ  - Булево         - Признак отказа от совершения операции.
//
Процедура ПроверкаКорректностиВводаСтавкиНДС(Объект, Отказ) Экспорт
	
	// При восстановлении последовательностей проверять не будем.
	Если ПолучитьЗначениеПараметраСтруктуры(Объект.ДополнительныеСвойства, "ЭтоВосстановлениеПоследовательностей", Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	// Для данного документа нужна проверка?
	ПроверятьДокумент = ПроверкаСтавкиНДСДляДокумента(Объект);
	
	// Проверять данный документ не будем.
	Если ПроверятьДокумент = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВыдаватьОшибку = (ПроверятьДокумент = 1);
	
	КонтрольКорректностиСтавокНДС = ПраваИНастройкиПользователя.Значение("КонтрольКорректностиСтавокНДС", Объект);
	Если (ТипЗнч(Объект) = Тип("ДокументОбъект.ЗаказНаряд") ИЛИ ТипЗнч(Объект) = Тип("ДокументСсылка.ЗаказНаряд")) 
		И Объект.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
		ДатаПроверки = ?(НЕ ЗначениеЗаполнено(Объект.ДатаЗакрытия), ТекущаяДатаСеанса(), Объект.ДатаЗакрытия);
	Иначе
		ДатаПроверки = Объект.Дата;
	КонецЕсли;
	ДатаПроверкиНДС = Дата("20190101");				
	АктуальнаяСтавкаНДС = ?(ДатаПроверки < ДатаПроверкиНДС, 18, 20);
	
	НЕКорректныСтавкиНДС = Ложь;
	
	// Проверим ставку НДС в ТЧ документа.
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "Товары",                 АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "Автоработы",             АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "Автомобили",             АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "Опции",                  АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "Состав",                 АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "ТоварыЗаменители",       АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "ОприходованныеЦенности", АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "Возвраты",               АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	ПроверкаСтавкиНДСТабличнойЧасти(Объект, "Активы",                 АктуальнаяСтавкаНДС, НЕКорректныСтавкиНДС);
	
	// Проверим ставку НДС по реквизиту.
	Если ЕстьРеквизит(Объект, "СтавкаНДС") Тогда
		
		СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтавкаНДС, "Ставка");
		Если (СтавкаНДС = 18 ИЛИ СтавкаНДС = 20) И НЕ СтавкаНДС = АктуальнаяСтавкаНДС Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'Значение ставки НДС указано некорректно. Актуальная ставка НДС равна %1'"),
					АктуальнаяСтавкаНДС),
				Объект
			);
			НЕКорректныСтавкиНДС = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьРеквизит(Объект, "СтавкаНДСНаАвтомобиль") Тогда
		
		СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтавкаНДСНаАвтомобиль, "Ставка");
		Если (СтавкаНДС = 18 ИЛИ СтавкаНДС = 20) И НЕ СтавкаНДС = АктуальнаяСтавкаНДС Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'Значение ставки НДС указано некорректно. Актуальная ставка НДС равна %1'"),
					АктуальнаяСтавкаНДС),
				Объект
			);
			НЕКорректныСтавкиНДС = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьРеквизит(Объект, "СтавкаНДСАвтомобилей") Тогда
		
		СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтавкаНДСАвтомобилей, "Ставка");
		Если (СтавкаНДС = 18 ИЛИ СтавкаНДС = 20) И НЕ СтавкаНДС = АктуальнаяСтавкаНДС Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'Значение ставки НДС указано некорректно. Актуальная ставка НДС равна %1'"),
					АктуальнаяСтавкаНДС),
				Объект
			);
			НЕКорректныСтавкиНДС = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Отказ = Отказ ИЛИ (НЕКорректныСтавкиНДС И ВыдаватьОшибку И КонтрольКорректностиСтавокНДС);
	
КонецПроцедуры // ПроверкиКорректностьСтавкиНДС

// Возвращает расчетную ставку НДС по переданной ставке.
// Если для переданной ставки нет расчетной, то возвращает переданную в параметре ставку без изменения.
//
// Параметры:
//  СтавкаНДС - СправочникСсылка.СтавкиНДС - обычная ставка НДС для которой нужно получить расчетную.
//
// Возвращаемое значение:
//  СправочникСсылка.СтавкиНДС - расчетная ставка для переданной.
//
Функция РасчетнаяСтавкаНДСПоОбычной(СтавкаНДС) Экспорт
	
	Если ТипЗнч(СтавкаНДС) <> Тип("СправочникСсылка.СтавкиНДС") Тогда
		Возврат СтавкаНДС;
	КонецЕсли;
	
	РасчетнаяСтавка = РаботаСоСтавкамиНДСПовтИсп.СтавкаНДСПоЗначению(СтавкаНДС.Ставка, Истина);
	
	Если РасчетнаяСтавка = Справочники.СтавкиНДС.ПустаяСсылка() Тогда
		Возврат СтавкаНДС;
	Иначе
		Возврат РасчетнаяСтавка;
	КонецЕсли;
	
КонецФункции 

// возвращает ставку НДС взависимости от настроек учетной политики
//
// Параметры:
//	Номенклатура - СправочникСсылка.Номенклатура,СправочникСсылка.Автоработы
//	ПараметрыНДС - Структура - параметры НДС организации
//
// Возвращаемое значение:
//  СправочникСсылка.СтавкиНДС - ставка НДС номенклатуры.
//
Функция СтавкаНДСНоменклатуры(Параметры, Номенклатура=Неопределено) Экспорт
	
	ОсновнаяСтавка20 = РаботаСоСтавкамиНДСПовтИсп.СтавкаНДСПоЗначению(20, Ложь);
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда 
		СтавкаНДСНоменклатуры = ОсновнаяСтавка20;
	ИначеЕсли ТипЗнч(Номенклатура)= Тип("СправочникСсылка.Номенклатура") Тогда
		СтавкаНДСНоменклатуры = Номенклатура.СтавкаНДС; 
	ИначеЕсли ТипЗнч(Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
		СтавкаНДСНоменклатуры = Номенклатура.Номенклатура.СтавкаНДС;
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Опции") Тогда
		СтавкаНДСНоменклатуры = Номенклатура.СтавкаНДС;
	Иначе 
		СтавкаНДСНоменклатуры = ОсновнаяСтавка20;
	КонецЕсли;
	
	Если СтавкаНДСНоменклатуры = Справочники.СтавкиНДС.БезНДС ИЛИ СтавкаНДСНоменклатуры.Ставка = 0 Тогда 
		Возврат СтавкаНДСНоменклатуры;	
	ИначеЕсли Параметры.Свойство("НалогообложениеНДСПриУСН") Тогда 
		Возврат Параметры.ОсновнаяСтавка; 
	Иначе 
		Возврат СтавкаНДСНоменклатуры;
	КонецЕсли;

КонецФункции

//Функция возвращает признак плательщика НДС при УСН 
//
// Параметры:
//	Значение - ПеречислениеСсылка.НалогообложениеНДСприУСН - значение учетной политики организации
//
// Возвращаемое значение:
//  Булево - 
//
Функция УСНПлательщикНДС(Значение) Экспорт
	
	Возврат  Значение = Перечисления.НалогообложениеНДСПриУСН.ПродажаОблагаетсяНДСПоСтавке5 ИЛИ 
		Значение = Перечисления.НалогообложениеНДСПриУСН.ПродажаОблагаетсяНДСПоСтавке7 ИЛИ 
		Значение = ПЕречисления.НалогообложениеНДСПриУСН.ПрименяютсяОбщиеСтавкиНДС;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаСтавкиНДСТабличнойЧасти(Объект, ИмяТабличнойЧасти, СтавкаНДС, Отказ)
	
	НомерСтрок = Новый Массив;
	
	Если ЕстьРеквизит(Объект, "СтавкаНДС", ИмяТабличнойЧасти) Тогда
		
		// Заполним соответствие ставок НДС и их значений.
		МассивСтавокНДС  = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Объект[ИмяТабличнойЧасти].ВыгрузитьКолонку("СтавкаНДС"));
		СписокСтавокНДС  = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивСтавокНДС, "Ставка");
		
		// Проверим ставку в ТЧ документа с актуальной
		Для Каждого ТекущаяСтрока Из Объект[ИмяТабличнойЧасти] Цикл
			
			ТекущаяСтавкаНДС = СписокСтавокНДС.Получить(ТекущаяСтрока.СтавкаНДС);
			
			Если (ТекущаяСтавкаНДС = 18 ИЛИ ТекущаяСтавкаНДС = 20)
				И НЕ ТекущаяСтавкаНДС = СтавкаНДС Тогда
				НомерСтрок.Добавить(ТекущаяСтрока.НомерСтроки);
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		// Такой ТЧ у документа нет.
		Возврат;
	КонецЕсли;
	
	Если НомерСтрок.Количество() > 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
			НСтр("ru = 'Таблица ""%1"". Значение ставки НДС указано некорректно в строках: %2. Актуальная ставка НДС равна %3.'"),
				ИмяТабличнойЧасти,
				СтрСоединить(НомерСтрок, ","),
				СтавкаНДС
			)
			,
			Объект,,,
			Отказ
		);
	КонецЕсли;
	
КонецПроцедуры // ПроверкаСтавкиНДСТабличнойЧасти()

// Функция для проверки ставки НДС в документах
//
// Параметры:
//  ЭтотОбъект - ДокументОбъект, документ, для которого проверяется корректность ставки НДС
//
// Возвращаемое значение: Число,
//  0 - не контролировать,
//  1 - контроль ставки НДС по праву,
//  2 - только предупреждать
//
Функция ПроверкаСтавкиНДСДляДокумента(ЭтотОбъект)
	
	ДокументСсылка = ЭтотОбъект.Ссылка;
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	МассивДокументовПроверять = Новый Массив;
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ВводОстатковАвтомобилей"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ВводОстатковТоваров"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ВводОстатковТоваровВПроизводстве"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ЗаказНаАвтомобиль"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ЗакрытиеСмены"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ЗаменаВЗаказеПокупателя"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ЗаменаВЗаказеПоставщику"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ЗаявкаНаХранениеШин"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.КорректировкаЗаказаПокупателя"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.КорректировкаЗаказаПоставщику"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ОбслуживаниеАктива"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ОптовыйЗаказКлиентаНаАвтомобили"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ПоступлениеАвтомобилей"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ПоступлениеДопРасходов"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.ПоступлениеТоваров"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.РабочийЛист"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.РеализацияАвтомобилей"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.РеализацияАктивов"));
	МассивДокументовПроверять.Добавить(Тип("ДокументСсылка.РеализацияТоваров"));
	
	МассивДокументовПредупреждать = Новый Массив;
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.АвансовыйОтчет"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.АктРазногласий"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ВозвратОтПокупателя"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ВозвратОтПокупателяАвтомобилей"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ВозвратПоставщику"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ВозвратПоставщикуАвтомобилей"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.Выписка"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.КорректировкаПоступления"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.КорректировкаРеализацииАвтомобилей"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераЗаАвтомобили"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ОтчетКомитенту"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ОтчетКомитентуЗаАвтомобили"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ПеремещениеАвтомобилей"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.ЧекНаОплату"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.СчетНаОплату"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.СчетНаОплатуЗаАвтомобили"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.СчетОтПоставщика"));
	МассивДокументовПредупреждать.Добавить(Тип("ДокументСсылка.СчетОтПоставщикаЗаАвтомобили"));
	
	
	Если НЕ МассивДокументовПроверять.Найти(ТипДокумента) = Неопределено Тогда
		// Проверка согдасно праву.
		Возврат 1;
	ИначеЕсли НЕ МассивДокументовПредупреждать.Найти(ТипДокумента) = Неопределено Тогда
		// Предупреждаем только по праву.
		Возврат 2;
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		// Для Заказ-наряда проверяем только когда переводим в состояние "Закрыт", иначе - предупреждение.
		Возврат ?(ЭтотОбъект.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт, 1, 2);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		// До 01.01.2019 только предупреждаем, после - запрещаем по праву.
		Возврат ?(ЭтотОбъект.Дата < Дата("20190101"), 2, 1);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.Чек") Тогда
		Если ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ЧекОтложенный Тогда
			Возврат 0;
		ИначеЕсли ЭтотОбъект.ХозОперация = Справочники.ХозОперации.Чек Тогда
			Возврат 1;
		Иначе
			Возврат 2;
		КонецЕсли;
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		Если ЭтотОбъект.ХозОперация = Справочники.ХозОперации.СчетФактураВыданный Тогда
			Возврат 1;
		Иначе
			Возврат 2;
		КонецЕсли;
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		Если ЭтотОбъект.ХозОперация = Справочники.ХозОперации.СчетФактураПолученный Тогда
			Возврат 1;
		Иначе
			Возврат 2;
		КонецЕсли;
	КонецЕсли;
	
	// Проверка не нужна
	Возврат 0;
	
КонецФункции // ПроверкаСтавкиНДСДляДокумента()

#КонецОбласти