
#Область ПрограммныйИнтерфейс

// Добавление элементов на форму
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма, для которой выполняется событие.
//
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	// Добавить реквизит "Имя файла"
	ДобавляемыеРеквизиты = Новый Массив;
	ТипСтрока = Новый ОписаниеТипов("Строка");
	
	РеквизитНачалоПериода = Новый РеквизитФормы(
		"ИмяФайла",
		ТипСтрока,
		,
		НСтр("ru = 'Имя файла'")
	);
	ДобавляемыеРеквизиты.Добавить(РеквизитНачалоПериода);
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// Выведем новый реквизит на форму
	ГруппаФормы = Форма.Элементы.Найти("СтраницаСохранениеВПапку");
	НовыйЭлемент = Форма.Элементы.Добавить("ИмяФайла", Тип("ПолеФормы"), ГруппаФормы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "ИмяФайла";
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	НовыйЭлемент.ПодсказкаВвода = НСтр("ru = 'Введите имя файла для сохранения'");
	ПодсказкаПоля = Новый Массив;
	ПодсказкаПоля.Добавить(НСтр("ru = 'При создании имени файла запрещены некоторые служебные символы:'"));
	ПодсказкаПоля.Добавить(НСтр("ru = '\ / : * ? """" < > | + % ! @ - в любом месте имени файла;'"));
	ПодсказкаПоля.Добавить(НСтр("ru = 'и пробел - в конце имени файла'"));
	НовыйЭлемент.Подсказка =Новый ФорматированнаяСтрока(ПодсказкаПоля);
	НовыйЭлемент.ЦветТекстаЗаголовка = ЦветаСтиля.ТекстИнформационнойНадписи;
	НовыйЭлемент.Ширина = 40;
	НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
	НовыйЭлемент.УстановитьДействие("ОкончаниеВводаТекста", "Подключаемый_ИмяФайлаОкончаниеВводаТекста");
	
	ЭлементПапкаДляСохраненияФайлов = Форма.Элементы.Найти("ПапкаДляСохраненияФайлов");
	ЭлементПапкаДляСохраненияФайлов.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	ЭлементПапкаДляСохраненияФайлов.Заголовок = НСтр("ru = 'Сохранить в'");
	ЭлементПапкаДляСохраненияФайлов.ПодсказкаВвода = НСтр("ru = 'Введите путь к файлу для сохранения'");
	ЭлементПапкаДляСохраненияФайлов.ЦветТекстаЗаголовка = ЦветаСтиля.ТекстИнформационнойНадписи;
	
КонецПроцедуры // ПриСозданииНаСервере

#КонецОбласти