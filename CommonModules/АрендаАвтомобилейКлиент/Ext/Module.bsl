// Общий модуль "Аренда клиент"

#Область ПрограммныйИнтерфейс

// Используется для редактирования периода аренды в формах документов аренды.
//
// Параметры:
//  Объект				 - ДанныеФормыСтруктура	 -  Объект, для которого нужно изменить период.
//  ОписаниеОповещения	 - 	ОписаниеОповещения -  Описание оповещения при завершении редактирования периода.
//
Процедура ВыборПериода(Объект, ОписаниеОповещения) Экспорт 
	
	ДополнительныеПараметры = Новый Структура("Объект, ОписаниеОповещения", Объект, ОписаниеОповещения);
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период.ДатаНачала = Объект.ДатаНачала;
	Диалог.Период.ДатаОкончания = Объект.ДатаОкончания;
	
	Диалог.Показать(Новый ОписаниеОповещения("ВыборПериода_Завершение", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

// Используется при завершении работы диалога выбора периода.
//
// Параметры:
//  Период					 - СтандартныйПериод - Выбранный в диалоге пользователем период.
//  ДополнительныеПараметры	 - Структура - Содержит дополнительные параметры, в частности,
//  									   изменяемый объект и описание оповещения для возврата в форму документа.
//
Процедура ВыборПериода_Завершение(Период, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Период <> Неопределено Тогда
		
		Объект = ДополнительныеПараметры.Объект;
		
		Период.ДатаНачала    = НачалоЧаса(Период.ДатаНачала);
		Период.ДатаОкончания = КонецЧаса (Период.ДатаОкончания) + 1;
		
		Объект.ДатаНачала    = НачалоДня(Период.ДатаНачала)    + (Объект.ДатаНачала    - НачалоДня(Объект.ДатаНачала));
		Объект.ДатаОкончания = НачалоДня(Период.ДатаОкончания) + (Объект.ДатаОкончания - НачалоДня(Объект.ДатаОкончания));
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Используется для подбора услуг в документ из табличной части в виде аренды.
// Механизм работает аналогично подбору номенклатуры.
//
// Параметры:
//  Форма	 - УправляемаяФорма - Форма документа, в котором выполняется команда.
//  Объект	 - Объект - Объект формы, для которого необходимо выполнить обработку команды.
//
Процедура ПодборУслугВидаАренды(Форма, Объект = Неопределено) Экспорт
	
	Если Объект = Неопределено Тогда
		ПолучитьОсновнойОбъектФормы(Форма, Объект);
	КонецЕсли;
	
	ОткрытьФорму(
		"Справочник.ВидыАренды.Форма.ФормаПодбораУслуг",
		Новый Структура("Документ", Объект),
		Форма,,,,
		Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", Форма, "ПодборНоменклатуры"),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ВыдачаПриемкаАвтомобиля

// Обработка команды ВыдатьАвтомобиль формы договора аренды
//
// Параметры:
//  Форма		 - УправляемаяФорма	 - Форма, в которой возникло событие.
//  ТекСтрока	 - СтрокаТабличнойЧасти	 - Строка, при изменении поля которой возникло событие.
//
Процедура ВыдатьАвтомобиль(Форма, ТекСтрока)  Экспорт 
	
	Если ТекСтрока.СтатусАвтомобиля = ПредопределенноеЗначение("Перечисление.АрендаСостоянияАвтомобилей.НаСтоянке") Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВыдатьПринятьАвтомобиль_Завершение", Форма);
		
		ПараметрыВопроса = Новый Структура("Форма, Автомобиль, Оповещение", Форма, ТекСтрока.Автомобиль, Оповещение);
		ПоказатьВопрос(Новый ОписаниеОповещения("ВыдатьАвтомобиль_Продолжение", ЭтотОбъект, ПараметрыВопроса),
			НСТР("ru = 'Выдать автомобиль клиенту?'"), РежимДиалогаВопрос.ДаНет);
			
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Выдать можно только находящийся на стоянке автомобиль'"));	
	КонецЕсли;
	
КонецПроцедуры

// Используется при ответе на вопрос из процедуры ВыдатьАвтомобиль. 
// Если для вида аренды настроено использование шаблона выдачи, то открывает форму дефектовочной ведомости,
// в противном случае производится запись выдачи в регистр сведений АрендаСостоянияАвтомобилей
//
// Параметры:
//  Ответ		 - КодВозвратаДиалога - Ответ пользователя на вопрос
//  ДопПараметры - Структура - Содержит дополнительные параметры, в частности,
//  							изменяемый объект, автомобиль и описание оповещения для возврата в форму документа.
//
Процедура ВыдатьАвтомобиль_Продолжение(Ответ, ДопПараметры = Неопределено)  Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПараметрыДействия = Новый Структура;
		АрендаАвтомобилейВызовСервера.ВыдатьАвтомобиль(ДопПараметры.Форма.Объект, ДопПараметры.Автомобиль, ПараметрыДействия);
		
		Если ПараметрыДействия.Свойство("ПараметрыФормы") Тогда
			
			ОткрытьФорму("Документ.ДефектовочнаяВедомость.ФормаОбъекта", ПараметрыДействия.ПараметрыФормы, ДопПараметры.Форма,,,,
				ДопПараметры.Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			ВыполнитьОбработкуОповещения(ДопПараметры.Оповещение, Истина);	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработка команды ПринятьАвтомобиль формы договора аренды
//
// Параметры:
//  Форма		 - УправляемаяФорма	 - Форма, в которой возникло событие.
//  ТекСтрока	 - СтрокаТабличнойЧасти	 - Строка, при изменении поля которой возникло событие.
//
Процедура ПринятьАвтомобиль(Форма, ТекСтрока) Экспорт 
	
	ВыданКлиенту = ПредопределенноеЗначение("Перечисление.АрендаСостоянияАвтомобилей.ВыданКлиенту");
	
	Если ТекСтрока.СтатусАвтомобиля = ВыданКлиенту Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВыдатьПринятьАвтомобиль_Завершение", Форма);
		
		ПараметрыВопроса = Новый Структура("Форма, Автомобиль, Оповещение", Форма, ТекСтрока.Автомобиль, Оповещение);
		ПоказатьВопрос(Новый ОписаниеОповещения("ПринятьАвтомобиль_Продолжение", ЭтотОбъект, ПараметрыВопроса),
			НСТР("ru = 'Принять автомобиль от клиента?'"), РежимДиалогаВопрос.ДаНет);
			
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Принять можно только выданный клиенту автомобиль'"));	
	КонецЕсли;
	
КонецПроцедуры

// Используется при ответе на вопрос из процедуры ПринятьАвтомобиль. 
// Если для вида аренды настроено использование шаблона приемки, то открывает форму дефектовочной ведомости,
// в противном случае производится запись выдачи в регистр сведений АрендаСостоянияАвтомобилей
//
// Параметры:
//  Ответ		 - КодВозвратаДиалога - Ответ пользователя на вопрос
//  ДопПараметры - Структура - Содержит дополнительные параметры, в частности,
//  							изменяемый объект, автомобиль и описание оповещения для возврата в форму документа.
//
Процедура ПринятьАвтомобиль_Продолжение(Ответ, ДопПараметры = Неопределено)  Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПараметрыДействия = Новый Структура;
		АрендаАвтомобилейВызовСервера.ПринятьАвтомобиль(ДопПараметры.Форма.Объект,
			ДопПараметры.Автомобиль,
			ПараметрыДействия);
		
		Если ПараметрыДействия.Свойство("ПараметрыФормы") Тогда
			
			ОткрытьФорму("Документ.ДефектовочнаяВедомость.ФормаОбъекта", ПараметрыДействия.ПараметрыФормы, ДопПараметры.Форма,,,,
				ДопПараметры.Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			ВыполнитьОбработкуОповещения(ДопПараметры.Оповещение, Истина);	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти