
#Область ПрограммныйИнтерфейс

// Инициализация дерева причин обращений.
//
// Параметры:
//  ДеревоПричинОбращений	 - ДеревоЗначений, Неопределено - Дерево заполнения.
// 
// Возвращаемое значение:
//  ДеревоЗначений - Пустое дерево причин обращений.
//
Функция ИнициализироватьДеревоПричинОбращений(ДеревоПричинОбращений = Неопределено) Экспорт
	
	ДеревоПричинОбращений = Новый ДеревоЗначений;
	ДеревоПричинОбращений.Колонки.Добавить("ИдентификаторПричиныОбращения");
	ДеревоПричинОбращений.Колонки.Добавить("ПричинаОбращения");
	ДеревоПричинОбращений.Колонки.Добавить("ПричинаОбращенияСодержание");
	ДеревоПричинОбращений.Колонки.Добавить("ТипПричиныОбращения");
	
	Возврат ДеревоПричинОбращений;
	
КонецФункции

#Область ЗаполнениеДанных

// Формирует коллекцию с причинами обращения из документа основания
//
// Параметры:
//  ПричиныОбращенияОснования - ТаблицаЗначений - перечень причин из основания
// 
// Возвращаемое значение:
//  ДеревоЗначений
//
Функция ПолучитьДеревоПричинОбращенийИзОснования(Знач ПричиныОбращенияОснования) Экспорт
	
	ДеревоПричинОбращений = Неопределено;
	ИнициализироватьДеревоПричинОбращений(ДеревоПричинОбращений);
	
	КопируемыеСтроки = Новый Массив();
	ЗапрещенныеПричины = Справочники.ПричиныОбращений.ПолучитьЗапрещенныеКВыборуПричины();
	
	Для Каждого ПроверяемаяСтрока Из ПричиныОбращенияОснования Цикл
		
		Если ЗапрещенныеПричины.Найти(ПроверяемаяСтрока.ПричинаОбращения) = Неопределено Тогда
			
			КопируемыеСтроки.Добавить(ПроверяемаяСтрока);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПричиныОбращенияОснованияКопия = ПричиныОбращенияОснования.Выгрузить(КопируемыеСтроки);
	ПричиныОбращенияКПереносу = ПричиныОбращенияОснования.Выгрузить(КопируемыеСтроки, "ТипПричиныОбращения");
	ПричиныОбращенияКПереносу.Свернуть("ТипПричиныОбращения");
	
	Для Каждого СтрокаЗаказНаряда Из ПричиныОбращенияКПереносу Цикл
		
		НовыйЗаказНаряд = ДеревоПричинОбращений.Строки.Добавить();
		ОтборСтрок = Новый Структура("ТипПричиныОбращения", СтрокаЗаказНаряда.ТипПричиныОбращения);
		МассивПричинОбращения = ПричиныОбращенияОснованияКопия.НайтиСтроки(ОтборСтрок);
		
		Для Каждого СтрокаПричиныОбращения Из МассивПричинОбращения Цикл
			
			НоваяПричинаОбращения = НовыйЗаказНаряд.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяПричинаОбращения, СтрокаПричиныОбращения);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДеревоПричинОбращений;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область УправлениеВнешнимВидом

Процедура УстановитьОтборНаВыбираемыеЭлементы(Список, ИмяПоля = "Ссылка") Экспорт
	
	ЗначениеОтбора = Новый СписокЗначений;
	ЗначениеОтбора.ЗагрузитьЗначения(Справочники.ПричиныОбращений.ПолучитьЗапрещенныеКВыборуПричины());
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			ИмяПоля,
																			ЗначениеОтбора,
																			ВидСравненияКомпоновкиДанных.НеВСписке,,
																			Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти