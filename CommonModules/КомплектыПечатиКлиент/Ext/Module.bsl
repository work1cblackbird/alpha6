
#Область ПрограммныйИнтерфейс

// Общий обработчик события возникающего при открытии настройки комплектов печати документа.
//
// Параметры:
//  МассивСсылок     	- Массив 			- Массив ссылок на объекты, для котороых выполняется обработка события.
//  ПараметрыДействия 	- Структура 		- Набор параметров, использующихся при выполнения операции.
//
Процедура НастроитьКомплектыПечати(МассивСсылок, ПараметрыДействия = Неопределено) Экспорт
	
	Форма = ПараметрыДействия.Форма;
	Объект = Неопределено;

	ПолучитьОсновнойОбъектФормы(Форма, Объект);
	
	ОписаниеКоманды = ПараметрыДействия.ОписаниеКоманды;
	ОписаниеКоманды.Вставить("Форма", Форма);
	
	Если Объект = Неопределено Тогда
		Если Не МассивСсылок.Количество() Тогда
			Возврат;	
		КонецЕсли;
		Ссылка = МассивСсылок[0];
	Иначе
		Ссылка = Объект.Ссылка;
	КонецЕсли;	
	
	ОбъектыПечати = Новый Массив();
	ОбъектыПечати.Добавить(Ссылка);
	ОписаниеКоманды.Вставить("ОбъектыПечати", ОбъектыПечати);

	ПараметрыДействия.Вставить("Результат", Ложь);
	ПараметрыДействия.Вставить("ОписаниеКоманды", ОписаниеКоманды);
	ПараметрыДействия.Вставить("Форма", Форма);
	ПараметрыДействия.Вставить("Источник", Объект);
	
	КлючеваяОперация = "ФормированиеПечатнойФормы" + СтрЗаменить(ТРег(ОписаниеКоманды.Представление), " ", "");
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(, КлючеваяОперация);
	
	Если Не ОписаниеКоманды.РежимЗаписи = "НеЗаписывать" И ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура")
		И (Объект.Ссылка.Пустая() Или Форма.Модифицированность) Тогда
		Если Объект.Ссылка.Пустая() Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Данные еще не записаны.
					|Выполнение действия ""%1"" возможно только после записи данных.
					|Данные будут записаны.'"),
				ОписаниеКоманды.Представление);
				
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьПодключаемуюКомандуПечатиПодтверждениеЗаписи", 
																УправлениеПечатьюСлужебныйКлиент, ПараметрыДействия);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
			Возврат;
		КонецЕсли;
		
		УправлениеСпискомДокументаКлиент.ОбработкаКомандыПечатиКомплекта(ОписаниеКоманды);
		Возврат;
	КонецЕсли;
	
	УправлениеСпискомДокументаКлиент.ОбработкаКомандыПечатиКомплекта(ОписаниеКоманды);

КонецПроцедуры

// Общий обработчик события возникающего при печати комплектов документа.
//
// Параметры:
//  МассивСсылок     	- Массив 			- Массив ссылок на объекты, для котороых выполняется обработка события.
//  ПараметрыДействия 	- Структура 		- Набор параметров, использующихся при выполнения операции.
//
Процедура ОтправитьКомплектыНаПечать(МассивСсылок, ПараметрыДействия = Неопределено) Экспорт
	
	Форма = ПараметрыДействия.Форма;
	Объект = Неопределено;

	ПолучитьОсновнойОбъектФормы(Форма, Объект);

	Если Форма.Элементы.Найти("Список") = Неопределено Тогда
		ПараметрыФормы = Новый Структура("Документ,КомандаИмя", Объект.Ссылка, 
									ПараметрыДействия.ОписаниеКоманды.Идентификатор);
		ОткрытьФорму("Справочник.НастройкаПечатиКомплекта.Форма.ФормаНастройки", ПараметрыФормы, Форма);
	Иначе
		ОбъектыПечати = ОбъектыПечати(Форма.Элементы.Список);
		ПараметрыФормы = Новый Структура("ОбъектыПечати,КомандаИмя", ОбъектыПечати, 
									ПараметрыДействия.ОписаниеКоманды.Идентификатор);
		ОткрытьФорму("Справочник.НастройкаПечатиКомплекта.Форма.ФормаНастройки", ПараметрыФормы, Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует и выводит на экран печатные формы.
// 
// Параметры:
//  ИмяМенеджераПечати - Строка - менеджер печати для печатаемых объектов;
//  ИменаМакетов       - Строка - идентификаторы печатных форм;
//  МассивОбъектов     - ЛюбаяСсылка
//                     - Массив - объекты печати;
//  ВладелецФормы      - ФормаКлиентскогоПриложения - форма, из которой выполняется печать;
//  ПараметрыПечати    - Структура - произвольные параметры для передачи в менеджер печати.
//
// Пример:
//   УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.ПечатнаяФорма", "СписаниеТоваров", 
// ДокументыНаПечать, ЭтотОбъект);
//
Процедура ВыполнитьКомандуПечати(ИмяМенеджераПечати, ИменаМакетов, МассивОбъектов, ВладелецФормы, КомплектПечати, 
							ОбъектыПечати, ПараметрыПечати = Неопределено) Экспорт
	
	Если НЕ ПроверитьКоличествоПереданныхОбъектов(МассивОбъектов) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура(	
							"ИмяМенеджераПечати,ИменаМакетов,ПараметрКоманды,ПараметрыПечати,КоллекцияПечатныхФорм,ОбъектыПечати");
	ПараметрыОткрытия.ИмяМенеджераПечати 		= ИмяМенеджераПечати;
	ПараметрыОткрытия.ИменаМакетов		 		= ИменаМакетов;
	ПараметрыОткрытия.ПараметрКоманды	 		= МассивОбъектов;
	ПараметрыОткрытия.ПараметрыПечати	 		= ПараметрыПечати;
	ПараметрыОткрытия.КоллекцияПечатныхФорм	 	= КомплектПечати;
	ПараметрыОткрытия.ОбъектыПечати	 			= ОбъектыПечати;
	ОткрытьФорму("ОбщаяФорма.ПечатьДокументов", ПараметрыОткрытия, ВладелецФормы, Строка(Новый УникальныйИдентификатор));
	
КонецПроцедуры

// Перед выполнением команды печати проверить, был ли передан хотя бы один объект, так как
// для команд с множественным режимом использования может быть передан пустой массив.
//
Функция ПроверитьКоличествоПереданныхОбъектов(ПараметрКоманды)
	
	Если ТипЗнч(ПараметрКоманды) = Тип("Массив") И ПараметрКоманды.Количество() = 0 Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Возвращает ссылки на объекты, выбранные в данный момент на форме.
//
Функция ОбъектыПечати(Источник) Экспорт
	
	Результат = Новый Массив;
	
	Если ТипЗнч(Источник) = Тип("ТаблицаФормы") Тогда
		ВыделенныеСтроки = Источник.ВыделенныеСтроки;
		Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
			Если ТипЗнч(ВыделеннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
				Продолжить;
			КонецЕсли;
			ТекущаяСтрока = Источник.ДанныеСтроки(ВыделеннаяСтрока);
			Если ТекущаяСтрока <> Неопределено Тогда
				Результат.Добавить(ТекущаяСтрока.Ссылка);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Результат.Добавить(Источник.Ссылка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти