// Общий модуль "Утверждение документов (клиент)"

#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийФормыСпискаИДокумента

// Общий обработчик события возникающего при нажатии программно добавленной кнопки.
//
// Параметры:
//  Форма		- УправляемаяФорма  - Форма, в которой вызвано выполнение команды.
//  Команда		- КомандаФормы    - Команда, в которой возникло данное событие.
//  Документ	- ДокументСсылка  - Документ, для которого выполняется обработка события.
//
Процедура ОбработкаКомандыФормы(Форма, Команда, Знач Документ) Экспорт
	
	Если Документ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДанныеФормыСтруктура") Тогда
		
		// для группировки строк
		Если НЕ Документ.Свойство("Ссылка") Тогда
			Возврат;
		КонецЕсли;
		
		Документ = Документ.Ссылка;
		
	КонецЕсли;
	
	ИмяКоманды = Команда.Имя;
	ИмяСтатуса = "";
	
	Если СтрНайти(ИмяКоманды, "История") > 0 Тогда
		
		// Открываем историю изменения статусов утверждения документ (форму списка регистра с отбором по регистратору).
		Отбор = Новый Структура("Регистратор", Документ);
		ПараметрыФормы = Новый Структура("Отбор", Отбор);
		ОткрытьФорму("РегистрСведений.СостоянияДокументов.ФормаСписка", ПараметрыФормы, Форма, Документ);
		
	Иначе
		
		ПараметрыДействия = Новый Структура;
		ПараметрыДействия.Вставить("Документ", Документ);
		ПараметрыДействия.Вставить("ИмяКоманды", ИмяКоманды);
		Обработчик = Новый ОписаниеОповещения("Подключаемый_ПродолжитьОбработкуКомандыУтвержденияНаСервере", Форма);
		ВыполнитьОбработкуОповещения(Обработчик, ПараметрыДействия);
		
	КонецЕсли;
	
КонецПроцедуры

// Общий обработчик оповещения.
//
// Параметры:
//  Форма		- УправляемаяФорма  - Форма, в которой вызвано оповещение.
//  ИмяСобытия	- Строка 		    - имя события.
//  
Процедура ОбработкаОповещения(Форма, ИмяСобытия) Экспорт
	
	Если ИмяСобытия = "ПослеУтвержденияДокументов" Тогда
		
		НачатьОбновлениеКоманд(Форма);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обновление команд утверждения документов на форме
//
// Параметры:
//  Форма - УправляемаяФорма - форма, для которой выполняется обновление команд
//
Процедура НачатьОбновлениеКоманд(Форма) Экспорт
	
	Форма.ОтключитьОбработчикОжидания("Подключаемый_ОбновитьКомандыУтвержденияДокументов");
	Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбновитьКомандыУтвержденияДокументов", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти