#Область ПрограммныйИнтерфейс

// Процедура получения параметров открытия формы отчета "ПечатьРеестра" 
//
// Параметры: 
//  Список            - Динамический список     - Форма, в которой возникло событие.
//  ПараметрыДействия - Структура 				- Набор параметров, использующихся при выполнения операции.
//
Процедура ПолучитьНастройкиСКД(Список, ПараметрыДействия) Экспорт
	
	Настройки = Список.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	// Проверка вывода "Дата" и "Время" в списке.
	МассивДат = Новый Массив;
	НастройкиВыбор = Настройки.Структура[0].Выбор.Элементы;
	Для Каждого Строка Из НастройкиВыбор Цикл
		Если ТипЗнч(Строка) <> Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		Если Строка.Поле = Новый ПолеКомпоновкиДанных("Дата") Тогда
			МассивДат.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивДат.Количество() > 1 Тогда
		НастройкиВыбор.Удалить(МассивДат[1]);
		
		МассивОформленияУдалить = Новый Массив;
		Для Каждого Строка Из Настройки.УсловноеОформление.Элементы Цикл
			Если Строка.Поля.Элементы.Количество() = 1
				И Строка.Поля.Элементы[0].Поле = Новый ПолеКомпоновкиДанных("Дата") Тогда
				МассивОформленияУдалить.Добавить(Строка);
			КонецЕсли;
		КонецЦикла;
		// Выведем в одной колонке дату и время
		ОформлениеДаты = МассивОформленияУдалить[0];
		ОформлениеДаты.Оформление.УстановитьЗначениеПараметра("Формат", "ДЛФ=DT");
		
		МассивОформленияУдалить.Удалить(0);
		
		Для Каждого Строка Из МассивОформленияУдалить Цикл
			Настройки.УсловноеОформление.Элементы.Удалить(Строка);
		КонецЦикла;
	КонецЕсли;
	
	СхемаКомпоновки = ПоместитьВоВременноеХранилище(Список.ПолучитьИсполняемуюСхемуКомпоновкиДанных(),
		Новый УникальныйИдентификатор);
		
	ПредставлениеСписка = ОбщегоНазначения.ПредставлениеСписка(Список.ТекущаяСтрока.Метаданные());
		
	ПараметрыДействия.Вставить("Схема", 	СхемаКомпоновки);
	ПараметрыДействия.Вставить("Настройки", Настройки);
	ПараметрыДействия.Вставить("Заголовок", " " + ПредставлениеСписка);
	
КонецПроцедуры

#КонецОбласти  
  
#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьКомандуПечатиРеестра(НастройкиФормы, Источники, ПодключенныеОтчетыИОбработки, Команды) Экспорт
	
	СтрокиИсточники = НастройкиФормы.Источники.Строки;
	Если СтрокиИсточники.Количество() Тогда
		МетаданныеОбъекта 	= СтрокиИсточники[0].Метаданные;
		ОбъектМенеджер		= СтрокиИсточники[0].Менеджер;
	КонецЕсли; 
		
	Если Не НастройкиФормы.ЭтоФормаОбъекта Тогда
		СписокОбъектов = Новый СписокЗначений;
		УправлениеПечатьюАльфаАвто.ПриОпределенииОбъектовСПечатьюРеестра(СписокОбъектов);
		Если СписокОбъектов.НайтиПоЗначению(ОбъектМенеджер) = Неопределено Тогда
			Возврат;	
		КонецЕсли;
		
		Команда = Команды.Добавить();
		Команда.Представление      	= НСтр("ru = 'Печать реестра'");
		Команда.Вид                	= "Печать";
		Команда.Идентификатор 		= "ПечатьРеестра";
		Команда.Обработчик			= "ПечатьРеестраДокументовКлиент.ОбработкаПечатиРеестра";
		Команда.РежимЗаписи 		= "НеЗаписывать";
		Команда.Порядок				= 100;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти