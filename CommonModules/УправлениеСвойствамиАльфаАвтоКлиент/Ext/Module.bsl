
// Общий модуль "Управление свойствами клиент (Альфа Авто)"

#Область ПрограммныйИнтерфейс

// Обработчик команд с форм, к которым подключены дополнительные свойства,
// с возможностью открывать форму меток в режиме просмотра.
// 
// Параметры:
//  Форма                - ФормаКлиентскогоПриложения - форма с дополнительными реквизитами, предварительно
//                          настроенная в процедуре УправлениеСвойствами.ПриСозданииНаСервере().
//  Элемент              - ПолеФормы
//                       - КомандаФормы - элемент, нажатие которого необходимо обработать.
//  СтандартнаяОбработка - Булево - возвращаемый параметр, если необходимо выполнить интерактивные
//                          действия с пользователем, то устанавливается в значение Ложь.
//  Объект - ДанныеФормыСтруктура - описание объекта, к которому подключены свойства,
//                                  если свойство не указано или Неопределено, то
//                                  объект будет взят из реквизита формы "Объект".
//  РежимПросмотраМеток  - Булево - открыть форму редактирования меток в режиме просмотра.
//
Процедура ВыполнитьКоманду(Форма,
						   Элемент = Неопределено,
						   СтандартнаяОбработка = Неопределено,
						   Объект = Неопределено,
						   РежимПросмотра = Ложь) Экспорт
	
	Если Элемент = Неопределено Тогда
		УправлениеСвойствамиКлиент.ВыполнитьКоманду(Форма, Элемент, СтандартнаяОбработка, Объект);
		Возврат;
	КонецЕсли;
	
	ИмяКоманды = Элемент.Имя;
	
	Если ИмяКоманды = "РедактироватьМетки"
		ИЛИ СтрНайти(ИмяКоманды, "ОстальныеМетки") = 1
		ИЛИ СтрНайти(ИмяКоманды, "Метка") = 1 Тогда
		РедактироватьМетки(Форма, Объект, РежимПросмотра);
	Иначе
		УправлениеСвойствамиКлиент.ВыполнитьКоманду(Форма, Элемент, СтандартнаяОбработка, Объект);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму редактирования меток для объекта с возможностью открытия в режиме просмотра.
//
// Параметры:
//  Форма           - ФормаКлиентскогоПриложения - обрабатываемая форма.
//  Объект          - ДанныеФормыСтруктура       - описание объекта, к которому подключены свойства,
//                                                 если свойство не указано или Неопределено, то
//                                                 объект будет взят из реквизита формы "Объект".
//  РежимПросмотра  - Булево                     - открыть форму в режиме просмотра.
//
Процедура РедактироватьМетки(Форма, Объект = Неопределено, РежимПросмотра = Ложь) Экспорт
	
	Если Объект = Неопределено Тогда
		ОписаниеОбъекта = Форма.Объект;
	Иначе
		ОписаниеОбъекта = Объект;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОписаниеОбъекта", ОписаниеОбъекта);
	ПараметрыФормы.Вставить("РежимПросмотра", РежимПросмотра);
	
	ОткрытьФорму("ОбщаяФорма.РедактированиеМеток", ПараметрыФормы, Форма, Форма,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОткрытьФормуСозданияМетки(Форма) Экспорт
	
	Если Форма.ДоступныеНаборыСвойств.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	НаборСвойств = Форма.ДоступныеНаборыСвойств[0].Значение;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НаборСвойств", НаборСвойств);
	ПараметрыФормы.Вставить("ВидСвойств", ПредопределенноеЗначение("Перечисление.ВидыСвойств.Метки"));
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", НаборСвойств);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
		ПараметрыФормы, Форма);
	
КонецПроцедуры

Функция ОбрабатыватьОповещения(Форма, ИмяСобытия) Экспорт
	
	Если НЕ Форма.Свойства_ИспользоватьСвойства
		ИЛИ НЕ Форма.Свойства_ИспользоватьДопРеквизиты Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ДополнительныеРеквизитыИСведения" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти