
#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьКомандуДополнительнойФормы(НастройкиФормы, Источники, ПодключенныеОтчетыИОбработки, Команды) Экспорт
	
	ОбъектМетаданных 	= Неопределено;
	ЕстьДопФорма		= Ложь;
	СтрокиИсточники 	= НастройкиФормы.Источники.Строки;
	Если СтрокиИсточники.Количество() Тогда
		ОбъектМетаданных = СтрокиИсточники[0].Метаданные;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоЖурналДокументов(ОбъектМетаданных) Тогда
		ЕстьДопФорма = Истина;
	ИначеЕсли ОбъектМетаданных.ОсновнаяФормаОбъекта <> Неопределено Тогда
		ЕстьДопФорма = ОбъектМетаданных.Формы.Найти(ОбъектМетаданных.ОсновнаяФормаОбъекта.Имя + "Дополнительная") <> Неопределено
	КонецЕсли;
	
	Если ЕстьДопФорма Или ЕстьРеквизит(ОбъектМетаданных, "Комментарий")  Тогда
		Команда 						= Команды.Добавить();
		Команда.Представление      		= НСтр("ru = 'Дополнительная форма'");
		Команда.Вид                		= "ДополнительнаяФорма";
		Команда.Обработчик				= "ДополнительнаяФормаОбъектаКлиент.ОткрытьДополнительнуюФорму";
		Команда.Картинка        		= БиблиотекаКартинок.РедактироватьВДиалоге;
		Команда.ТолькоВоВсехДействиях 	= Истина;
		Команда.МножественныйВыбор 		= Ложь;
		Команда.СочетаниеКлавиш			= Новый СочетаниеКлавиш(Клавиша.L, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииВидовПодключаемыхКоманд(ВидыПодключаемыхКоманд) Экспорт
	
	Вид 			= ВидыПодключаемыхКоманд.Добавить();
	Вид.Имя 		= "ДополнительнаяФорма";
	
КонецПроцедуры

#КонецОбласти  