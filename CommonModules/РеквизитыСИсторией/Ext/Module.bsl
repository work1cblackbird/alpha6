#Область ПрограммныйИнтерфейс

// Получает актуальный КПП.
//
// Параметры:
//  Объект - СправочникСсылка.Контрагенты,
//           СправочникСсылка.Организации,
//           СправочникСсылка.ПодразделенияКомпании - Объект, для которого получаем КПП.
//  НаДату - Дата - Дата получения сведений
//
// Возвращаемое значение:
//  Строка - КПП
//
Функция ПолучитьКППНаДату(Объект, НаДату) Экспорт

	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект);
	
	Попытка
		
		Возврат МенеджерОбъекта.КППНаДату(Объект, НаДату);
		
	Исключение
		
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, "КПП");
		
	КонецПопытки;
	
КонецФункции

// Получает актуальное наименование.
//
// Параметры:
//  Объект - СправочникСсылка - Объект, для которого получаем наименование.
//  НаДату - Дата - Дата получения сведений
//  ТипНаименования - Строка - (необязательный) - тип получаемого наименования.
//
// Возвращаемое значение:
//  Структура - структура наименований.
//
Функция ПолучитьНаименованияНаДату(Объект, НаДату, ТипНаименования = "") Экспорт
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект);
	
	Попытка
	
		СтруктураНаименований = МенеджерОбъекта.НаименованияНаДату(Объект, НаДату);
	
	Исключение
		
		СтруктураНаименований = Новый Структура(
			"СокращенноеНаименование, НаименованиеПолное, НаименованиеДляПечатныхФорм",
			Объект.Наименование,
			Объект.НаименованиеПолное,
			Объект.НаименованиеПолное
		);
		
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ТипНаименования) Тогда
		
		Возврат СтруктураНаименований[ТипНаименования];
		
	КонецЕсли;
	
	Возврат СтруктураНаименований;
	
КонецФункции

#КонецОбласти