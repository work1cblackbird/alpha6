
#Область ПрограммныйИнтерфейс


// Процедура - Сформировать файл уведомления
//
// Параметры:
//  Форма					 - ФормаКлиентскогоПриложения - 
//  Объект					 - ДанныеФормыСтруктура - объект, для которого формируем уведомление
//  Действие				 - Строка	 -  описывает вид уведомления
//  ДополнительныеПараметры	 - Структура, Неопределено - дополнительные параметры операции
//
Процедура СфомрироватьФайлУведомления(Форма, Объект, Действие, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПолучитьОсновнойОбъектФормы(Форма, Объект);
	
	Если ПолучитьЗначениеПараметраСтруктуры(ДополнительныеПараметры, "ПроверятьОбъект", Истина)
		И (НЕ ЗначениеЗаполнено(Объект.Ссылка)
		ИЛИ Форма.Модифицированность) Тогда
		
		ПоказатьПредупреждение(
			,
			НСтр("ru = 'Данные еще не записаны.
			|Выполните данную операцию после записи документа.'"));
		Возврат;
		
	КонецЕсли;
	
	Если Действие = "УведомлениеОбОстатках" Тогда
		СформироватьУведомлениеОбОстаткахПрослеживаемогоТовара(Объект);
	ИначеЕсли Действие = "УведомлениеОВвозе" Тогда
		СформироватьУведомлениеОВвозеПрослеживаемогоТовара(Объект);
	ИначеЕсли Действие = "УведомлениеОПеремещении" Тогда
		СформироватьУведомлениеОПеремещенииПрослеживаемогоТовара(Объект);
	ИначеЕсли Действие = "ОтчетОбОперациях" Тогда
		СформироватьОтчетОбОперациях(Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьФайлВКаталог(ПараметрыДокумента, ИмяФайла = "", Расширение = "xml", Фильтр = "(*.xml)|*.xml")
	
	ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогСохраненияФайла.Заголовок = НСтр("ru = 'Сохранить файл ...'");
	ДиалогСохраненияФайла.ПолноеИмяФайла = ИмяФайла;
	ДиалогСохраненияФайла.Расширение = Расширение;
	ДиалогСохраненияФайла.Фильтр = Фильтр;
	
	Обработчик = Новый ОписаниеОповещения("СохранитьФайлЗапроса", УчетПрослеживаемыхТоваровКлиент, ПараметрыДокумента);
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(Обработчик, ДиалогСохраненияФайла);
	
КонецПроцедуры

Процедура СохранитьФайлЗапроса(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		
		ПолноеИмяФайла = ВыбранныеФайлы[0];
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДополнительныеПараметры.АдресФайла);
		ДвоичныеДанныеФайла.Записать(ПолноеИмяФайла);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Сформирован файл: %1'"), ПолноеИмяФайла));
		
		Если ДополнительныеПараметры.Свойство("ОбработкаРезультата") Тогда
			ВыполнитьОбработкуОповещения(
				ДополнительныеПараметры.ОбработкаРезультата,
				Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьУведомлениеОбОстаткахПрослеживаемогоТовара(ЗНАЧ Документ)
	
	ДанныеУведомления = УчетПрослеживаемыхТоваровВызовСервера.УведомлениеОбОстаткахПрослеживаемогоТовара(Документ);
	
	Если ДанныеУведомления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получим информацию о документе
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("АдресФайла", ДанныеУведомления.АдресФайла);
	
	ЗаписатьФайлВКаталог(ДополнительныеПараметры, ДанныеУведомления.ИмяФайла);
	
КонецПроцедуры

Процедура СформироватьУведомлениеОВвозеПрослеживаемогоТовара(ЗНАЧ Документ)
	
	ДанныеУведомления = УчетПрослеживаемыхТоваровВызовСервера.СформироватьУведомлениеОВвозеПрослеживаемогоТовара(Документ);
	
	Если ДанныеУведомления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получим информацию о документе
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("АдресФайла", ДанныеУведомления.АдресФайла);
	
	ЗаписатьФайлВКаталог(ДополнительныеПараметры, ДанныеУведомления.ИмяФайла);
	
КонецПроцедуры

Процедура СформироватьУведомлениеОПеремещенииПрослеживаемогоТовара(ЗНАЧ Документ)
	
	ДанныеУведомления =
		УчетПрослеживаемыхТоваровВызовСервера.СформироватьУведомлениеОПеремещенииПрослеживаемогоТовара(Документ);
	
	Если ДанныеУведомления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получим информацию о документе
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("АдресФайла", ДанныеУведомления.АдресФайла);
	
	ЗаписатьФайлВКаталог(ДополнительныеПараметры, ДанныеУведомления.ИмяФайла);
	
КонецПроцедуры

Процедура СформироватьОтчетОбОперациях(ЗНАЧ Документ)
	
	ДанныеУведомления =
		УчетПрослеживаемыхТоваровВызовСервера.СформироватьОтчетОбОперациях(Документ);
	
	Если ДанныеУведомления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получим информацию о документе
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("АдресФайла", ДанныеУведомления.АдресФайла);
	
	ЗаписатьФайлВКаталог(ДополнительныеПараметры, ДанныеУведомления.ИмяФайла);
	
КонецПроцедуры

#КонецОбласти
