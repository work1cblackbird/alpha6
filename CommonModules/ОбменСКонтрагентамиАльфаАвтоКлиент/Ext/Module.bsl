

#Область ПрограммныйИнтерфейс

#Область ВыборПрикладныхЗначений

// См. ОбменСКонтрагентамиКлиентПереопределяемый.ОткрытьФормуПодбораТоваров
Процедура ОткрытьФормуПодбораТоваров(ИдентификаторФормы, ОбработкаПродолжения) Экспорт
	
	ПараметрыДействия = Новый Структура();
	
	ПараметрыПодбора = Новый Структура();
	ПараметрыПодбора.Вставить("ПараметрыДействия", ПараметрыДействия);
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("ПараметрыПодбора", ПараметрыПодбора);
	ПараметрыОткрытия.Вставить("РежимВыбора",      Истина);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОткрытия, ПараметрыДействия, Истина);
	ОткрытьФорму(
		"Справочник.Номенклатура.ФормаСписка",
		ПараметрыОткрытия,
		,
		,
		,
		,
		ОбработкаПродолжения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиКлиентПереопределяемый.СоздатьКонтрагентаИнтерактивно
Процедура СоздатьКонтрагентаИнтерактивно(РеквизитыКонтрагента, ОписаниеОповещенияОСозданииКонтрагента) Экспорт
	
	ЗначенияЗаполнения = Новый Структура();
	ЗначенияЗаполнения.Вставить("Наименование", РеквизитыКонтрагента.Наименование);
	ЗначенияЗаполнения.Вставить("НаименованиеПолное", РеквизитыКонтрагента.Наименование);
	ЗначенияЗаполнения.Вставить("ИНН", РеквизитыКонтрагента.ИНН);
	ЗначенияЗаполнения.Вставить("КПП", РеквизитыКонтрагента.КПП);

	ДлинаИННИндивидуальногоПредпринимателя = 12;
	
	Если
		ЗначениеЗаполнено(РеквизитыКонтрагента.ИНН)
		И СтрДлина(РеквизитыКонтрагента.ИНН) = ДлинаИННИндивидуальногоПредпринимателя
	Тогда
	
		ЗначенияЗаполнения.Вставить("ФормаСобственности",
			ПредопределенноеЗначение("Перечисление.ФормыСобственности.ИндивидуальныйПредприниматель"));
			
	Иначе
			
		ЗначенияЗаполнения.Вставить("ФормаСобственности",
			ПредопределенноеЗначение("Перечисление.ФормыСобственности.ЮридическоеЛицо"));
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеОСозданииНовогоОбъекта", ОписаниеОповещенияОСозданииКонтрагента);
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(Неопределено,, ДополнительныеПараметры);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы,,,,, ОписаниеОповещенияОЗакрытии); 
	
КонецПроцедуры

// См. ОбменСКонтрагентамиКлиентПереопределяемый.ПриВыбореДоговораКонтрагента
Процедура ПриВыбореДоговораКонтрагента(Параметры, Владелец, ОповещениеОЗакрытии, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Владелец"   , Параметры.Контрагент);
	Отбор.Вставить("Организация", Параметры.Организация);
	
	ПараметрыФормы = Новый Структура("Отбор,РежимВыбора", Отбор, Истина);
	ОткрытьФорму("Справочник.ДоговорыВзаиморасчетов.ФормаВыбора", ПараметрыФормы, Владелец,,,,ОповещениеОЗакрытии);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиКлиентПереопределяемый.ВыбратьКонтрагента
// Открывает форму выбора контрагента с отбором.
// 
// Параметры:
//  РеквизитыОтбораКонтрганета - Структура - источник заполнения отбора списка контрагентов.
//  ОписаниеОповещенияОЗакрытии - ОписаниеОповещения.
//
Процедура ВыбратьКонтрагента(РеквизитыОтбораКонтрганета, ОписаниеОповещенияОЗакрытии) Экспорт
	
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	
	Если ЗначениеЗаполнено(РеквизитыОтбораКонтрганета.Наименование) Тогда
		
		ПараметрыФормы.Вставить("ОтборПоНаименованию", РеквизитыОтбораКонтрганета.Наименование);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыОтбораКонтрганета.ИНН) Тогда
		
		ПараметрыФормы.Вставить("ОтборПоИНН", РеквизитыОтбораКонтрганета.ИНН);
		
	КонецЕсли;

	ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", ПараметрыФормы,,,,, ОписаниеОповещенияОЗакрытии);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// См. ОбменСКонтрагентамиКлиентПереопределяемый.ПроверитьИспользованиеИнтернетПоддержкаПользователей
Процедура ПроверитьИспользованиеИнтернетПоддержкаПользователей(Использование) Экспорт
	
	Использование = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти