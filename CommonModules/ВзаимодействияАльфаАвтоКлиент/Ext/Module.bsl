
#Область ПрограммныйИнтерфейс

// Выполняет вызов по номеру контакта через СофтФонПро.
//
// Параметры:
//  Контакт - СправочникСсылка.Контрагенты
//  НомерТелефона - Строка
//  ПредметЗвонка - ДокументСсылка
//
Процедура ПозвонитьКонтакту(Контакт, НомерТелефона = "", ПредметЗвонка = Неопределено, НужноВыбратьТелефон = Ложь) Экспорт
	
	Если ПустаяСтрока(НомерТелефона) Тогда
		
		Если Не ЗначениеЗаполнено(Контакт) Тогда
			
			ПоказатьПредупреждение(, НСтр("ru = 'Контакт не заполнен.'"));
			Возврат;
			
		КонецЕсли;
		
		Телефон = ВзаимодействияАльфаАвтоВызовСервера.НомерТелефонаКонтакта(Контакт);
		
	Иначе
		
		Телефон = НомерТелефона;
		
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	
	Если ЗначениеЗаполнено(ПредметЗвонка) Тогда
		
		ДанныеЗаполнения = Новый Структура("ПредметВзаимодействия", ПредметЗвонка);
		ДополнительныеПараметры.Вставить("ДанныеЗаполнения", ДанныеЗаполнения);
		
	КонецЕсли;
	
	Если ПустаяСтрока(Телефон) ИЛИ НужноВыбратьТелефон Тогда
		
		СписокКонтактов = Новый СписокЗначений;
		СписокКонтактов.Добавить(Контакт, Строка(Контакт));
		
		сфпСофтФонПроКлиент.сфпПозвонитьВыбравТелефон(СписокКонтактов, ДополнительныеПараметры);
		
	Иначе
		
		сфпСофтФонПроКлиент.сфпПозвонить(Телефон, Контакт, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует параметры для отправки смс, вызывает процедуру отправки смс
//
// Параметры:
//  Представление - Строка
//  Контакт - СправочникСсылка.Контрагенты
//  ПредметSMS - ДокументСсылка
//
Процедура СоздатьSMS(Представление = "", ИсточникКонтактнойИнформации = "", ПредметSMS = Неопределено) Экспорт 
	
	СведенияОПолучателе = Новый Структура();
	СведенияОПолучателе.Вставить("Телефон",                      СокрЛП(Представление));
	СведенияОПолучателе.Вставить("Представление",                Строка(ИсточникКонтактнойИнформации));
	СведенияОПолучателе.Вставить("ИсточникКонтактнойИнформации", ИсточникКонтактнойИнформации);
	
	НомераПолучателей = Новый Массив;
	НомераПолучателей.Добавить(СведенияОПолучателе);
	
	ДополнительныеПараметры = Новый Структура();	
	ДополнительныеПараметры.Вставить("ПеревестиВТранслит", Ложь);
	ДополнительныеПараметры.Вставить("Предмет", ПредметSMS);  
	
	МодульОтправкаSMSКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОтправкаSMSКлиент");
	МодульОтправкаSMSКлиент.ОтправитьSMS(НомераПолучателей, "", ДополнительныеПараметры);
	
КонецПроцедуры 

#КонецОбласти