///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция НастройкиПечати() Экспорт
	
	Настройки = УправлениеПечатью.НастройкиПечати();
	
	ОбъектыПечати = Новый Соответствие;
	Для Каждого ОбъектПечати Из Настройки.ОбъектыПечати Цикл
		ОбъектыПечати.Вставить(ОбъектПечати, Истина);
	КонецЦикла;
	
	Настройки.ОбъектыПечати = Новый ФиксированноеСоответствие(ОбъектыПечати);
	
	Возврат Настройки;
	
КонецФункции

// +РАРУС Определяет объекты, в которых есть процедура ДобавитьКомандыПечати().
//
// Параметры:
//  СписокОбъектов - Массив - список менеджеров объектов.
//
Функция ОбъектыСКомандамиПечати() Экспорт
	
	СписокОбъектов = Новый Соответствие();
	
	КомандыПечати = УправлениеПечатью.СоздатьКоллекциюКомандПечати();
	
	КлассыОбъектов = Новый Структура;
	КлассыОбъектов.Вставить("Справочники",             Справочники);
	КлассыОбъектов.Вставить("Документы",               Документы);
	КлассыОбъектов.Вставить("ПланыВидовХарактеристик", ПланыВидовХарактеристик);
	КлассыОбъектов.Вставить("ПланыСчетов",             ПланыСчетов);
	КлассыОбъектов.Вставить("ПланыВидовРасчета",       ПланыВидовРасчета);
	
	Для каждого КлассОбъекта Из КлассыОбъектов Цикл
		Для каждого ОбъектМетаданных Из Метаданные[КлассОбъекта.Ключ] Цикл
			Попытка
				КомандыПечати.Очистить();
				МенеджерПечати = КлассОбъекта.Значение[ОбъектМетаданных.Имя];
				МенеджерПечати.ДобавитьКомандыПечати(КомандыПечати);
			Исключение
				ПричинаОшибки();
				Продолжить;
			КонецПопытки;
			СписокОбъектов.Вставить(МенеджерПечати, Истина);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(СписокОбъектов);
	
КонецФункции // ОбъектыСКомандамиПечати() // -РАРУС

#КонецОбласти
