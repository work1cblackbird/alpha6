/////////////////////////////////////////////////////////////////////////////////////////
// Редактирование истории (клиент): механизм редактирования истории реквизитов объекта.
//
/////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Заполняет необходимые для открытия общей формы "Редактирование истории" параметры.
//
// Параметры:
//  Форма     		- УправляемаяФорма		- Форма, в которой возникло событие.
//  История    		- ДанныеФормыКоллекция 	- Таблица с историей изменения реквизита.
//  ТекущееЗначение	- Строка           		- Текущее значение полного наименования.
//  ТолькоПросмотр 	- Булево 				- Определяет режим открытия формы.
//
Процедура ОткрытьИсториюПолныхНаименований(Форма, История, ТекущееЗначение, ТолькоПросмотр=Ложь) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("История", История);
	ПараметрыФормы.Вставить("ТекущееЗначение", ТекущееЗначение);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр ИЛИ ТолькоПросмотр);
	ПараметрыФормы.Вставить("Режим", "НаименованиеПолное");
	
	ОписаниеПриемника = Новый Структура("Таблица,Реквизит", "ИсторияНаименований", "НаименованиеПолное");
	
	ОткрытьФормуИстории(Форма, ПараметрыФормы, ОписаниеПриемника);
	
КонецПроцедуры

// Заполняет необходимые для открытия общей формы "Редактирование истории" параметры.
//
// Параметры:
//  Форма     		- УправляемаяФорма		- Форма, в которой возникло событие.
//  История    		- ДанныеФормыКоллекция 	- Таблица с историей изменения реквизита.
//  ТекущееЗначение	- Строка           		- Текущее значение КПП.
//  ТолькоПросмотр 	- Булево 				- Определяет режим открытия формы.
//
Процедура ОткрытьИсториюКПП(Форма, История, ТекущееЗначение, ТолькоПросмотр=Ложь) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("История", История);
	ПараметрыФормы.Вставить("ТекущееЗначение", ТекущееЗначение);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр ИЛИ ТолькоПросмотр);
	ПараметрыФормы.Вставить("Режим", "КПП");
	
	ОписаниеПриемника = Новый Структура("Таблица,Реквизит", "ИсторияКПП", "КПП");
	
	ОткрытьФормуИстории(Форма, ПараметрыФормы, ОписаниеПриемника);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьФормуИстории(ФормаВладелец, ПараметрыФормы, ОписаниеПриемника)
	
	Обработчик = Новый ОписаниеОповещения("Подключаемый_РедактированиеИсторииЗавершение", ФормаВладелец, ОписаниеПриемника);
	
	ОткрытьФорму(
		"ОбщаяФорма.РедактированиеИстории",
		ПараметрыФормы,
		ФормаВладелец,
		ФормаВладелец,,,
		Обработчик,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти