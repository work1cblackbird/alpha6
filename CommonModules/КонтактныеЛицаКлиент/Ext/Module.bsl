
#Область ПрограммныйИнтерфейс

// Обработчик события возникающего на клиенте перед началом интерактивного добавления строки.
//
// Параметры:
//  Форма           - УправляемаяФорма - Форма, в которой возникло событие.
//  ПараметрыВыбора - Структура        - Содержит параметры выбора необходимые для открытия формы.
//
Процедура ВыбратьКонтрагента(Форма, ПараметрыВыбора) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ВыборКонтрагентаЗавершение",
		КонтактныеЛицаКлиент,
		Новый Структура("Форма,Данные", Форма, ПараметрыВыбора)
	);
	
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	
	Если ПараметрыВыбора.Свойство("Владелец") Тогда
		
		ПараметрыФормы.Вставить("ВидКонтрагента", ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.КонтактноеЛицо"));
		Отбор = Новый Структура(
			"ФормаСобственности",
			ПредопределенноеЗначение("Перечисление.ФормыСобственности.ЧастноеЛицо")
		);
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора",
		ПараметрыФормы,
		Форма,
		Форма,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

// Обработчик события возникающего при выполнении оповещения данной формы о прекращении работы подчиненной.
//
// Параметры:
//  Контрагент     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
Процедура ВыборКонтрагентаЗавершение(Контрагент, ДополнительныеПараметры) Экспорт
	
	Если Контрагент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФормаВладелец      = ДополнительныеПараметры.Форма;
	ЗначенияЗаполнения = ДополнительныеПараметры.Данные;
	
	Если ЗначенияЗаполнения.Свойство("Владелец") Тогда
		ЗначенияЗаполнения.Вставить("КонтактноеЛицо", Контрагент);
	ИначеЕсли ЗначенияЗаполнения.Свойство("КонтактноеЛицо") Тогда
		ЗначенияЗаполнения.Вставить("Владелец", Контрагент);
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполнения.Вставить("Взаимоотношение", ПредопределенноеЗначение("Справочник.ВидыВзаимоотношений.КонтактноеЛицо"));
	
	ОткрытьФорму("РегистрСведений.КонтактныеЛица.ФормаЗаписи",
		Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения),
		ФормаВладелец,
		ФормаВладелец,
		,,
		Новый ОписаниеОповещения("КонтактныеЛицаДобавлениеОкончание", ФормаВладелец),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти