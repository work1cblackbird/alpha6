
#Область ПрограммныйИнтерфейс

// Процедура переноса табличного файла в табличное поле с помощью внешней обработки
//
// Параметры:
//  ОбработкаПреобразования - СправочникСсылка.ДополнительныеОтчетыИОбработки - Подключаемая обработка.
//  ТабличныйДокумент - ТабличныйДокумент - Документ, в который будет перемещен файл.
//  КаталогЗагружаемогоФайла - Строка            - Путь к каталогу сохранения файла.  
//  ИндексТипаЗагружаемогоФайла -  Строка - Индекс типа загружаемого файла.
//
Процедура ЗаполнитьТабличныйДокументИзXML(ОбработкаПреобразования,
		ТабличныйДокумент,
		КаталогЗагружаемогоФайла,
		ИндексТипаЗагружаемогоФайла) Экспорт
	
	// Проверим внешнюю обработку, которой обработаем XML
	Если НЕ ЗначениеЗаполнено(ОбработкаПреобразования) Тогда
		ПоказатьПредупреждение(,НСтр("ru= 'Не указана обработка преобразования файла в табличный документ.'"));
		Возврат;
	КонецЕсли;
	
	// Теперь выберем файл, который будем обрабатывать
	ДиалогВыбораФайла 			= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Каталог 	= КаталогЗагружаемогоФайла;
	
	Если ИндексТипаЗагружаемогоФайла <> Неопределено Тогда
		ДиалогВыбораФайла.ИндексФильтра = ИндексТипаЗагружаемогоФайла;
	КонецЕсли;
	
	ДиалогВыбораФайла.Заголовок = НСтр("ru= 'Прочитать табличный документ из файла'");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ДиалогВыбораФайла",       ДиалогВыбораФайла);
	Параметры.Вставить("ОбработкаПреобразования", ОбработкаПреобразования);
	Параметры.Вставить("ТабличныйДокумент",       ТабличныйДокумент);
	Параметры.Вставить("КаталогЗагружаемогоФайла", КаталогЗагружаемогоФайла);
	Параметры.Вставить("ИндексТипаЗагружаемогоФайла", ИндексТипаЗагружаемогоФайла);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьТабличныйДокументИзXMLПродолжение", ЭтотОбъект, Параметры);
	
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

// Сохранить табличный документ в файл.
//
// Параметры:
//  КаталогЗагружаемогоФайла - Строка            - Путь к каталогу сохранения файла.
//  ТабличныйДокумент        - ТабличныйДокумент - Табличный документ для записи в файл.
//
Процедура СохранитьВФайл(КаталогЗагружаемогоФайла, ТабличныйДокумент) Экспорт
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	Если КаталогЗагружаемогоФайла <> Неопределено Тогда
		ДиалогВыбораФайла.Каталог = КаталогЗагружаемогоФайла;
	КонецЕсли;
	
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Сохранить табличный документ в файл'");
	ДиалогВыбораФайла.Фильтр    = НСтр("ru = 'Формат sst (*.sst)|*.sst|Лист Excel (*.xls)|*.xls|Лист Excel 2007 (*.xlsx)|*.xlsx|Текстовый документ (*.txt)|*.txt|Табличный документ (*.mxl)|*.mxl|'");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДиалогВыбораФайла", ДиалогВыбораФайла);
	ДополнительныеПараметры.Вставить("ТабличныйДокумент", ТабличныйДокумент);
	
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("СохранитьВФайлЗавершение", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

// Процедура устанавливает флаг таблицы
//
// Параметры:
//  ЗначениеФлага - Булево - Значение флага.
//  ТаблицаРеквизитов - ТаблицаЗначений - Таблица, содержащая реквизиты.
//
Процедура УстановитьФлагТаблицы(ЗначениеФлага, ТаблицаРеквизитов) Экспорт
	
	Для Каждого Элемент Из ТаблицаРеквизитов Цикл
		Элемент.Пометка = ЗначениеФлага;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СохранитьВФайлЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбораФайла = ДополнительныеПараметры.ДиалогВыбораФайла;
	ТабличныйДокумент = ДополнительныеПараметры.ТабличныйДокумент;
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		ФайлНаДиске 				= Новый Файл(ДиалогВыбораФайла.ПолноеИмяФайла);
		РасширениеФайла   			= нРег(ФайлНаДиске.Расширение);
		ПолноеИмяСохраняемогоФайла 	= ДиалогВыбораФайла.ПолноеИмяФайла;
		
		Если РасширениеФайла = ".mxl" Тогда
			ТабличныйДокумент.Записать(ПолноеИмяСохраняемогоФайла,ТипФайлаТабличногоДокумента.MXL);
		ИначеЕсли РасширениеФайла = ".xls" Тогда
			ТабличныйДокумент.Записать(ПолноеИмяСохраняемогоФайла,ТипФайлаТабличногоДокумента.XLS);
		ИначеЕсли РасширениеФайла = ".xlsx" Тогда
			ТабличныйДокумент.Записать(ПолноеИмяСохраняемогоФайла,ТипФайлаТабличногоДокумента.XLSX);
		ИначеЕсли РасширениеФайла = ".txt" ИЛИ РасширениеФайла = ".sst" Тогда
			ТабличныйДокумент.Записать(ПолноеИмяСохраняемогоФайла,ТипФайлаТабличногоДокумента.TXT);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьТабличныйДокументИзXMLПродолжение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбораФайла = ДополнительныеПараметры.ДиалогВыбораФайла;
	КаталогЗагружаемогоФайла = ДополнительныеПараметры.КаталогЗагружаемогоФайла;
	ИндексТипаЗагружаемогоФайла = ДополнительныеПараметры.ИндексТипаЗагружаемогоФайла;
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		КаталогЗагружаемогоФайла     = ДиалогВыбораФайла.Каталог;
		ИндексТипаЗагружаемогоФайла  = ДиалогВыбораФайла.ИндексФильтра;
		АдресВХранилище 			 = "";
		
		ОписаниеОповещения =
			Новый ОписаниеОповещения("ЗаполнитьТабличныйДокументИзXMLЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		НачатьПомещениеФайла(ОписаниеОповещения,
			АдресВХранилище,
			ДиалогВыбораФайла.ПолноеИмяФайла,
			Ложь,
			Новый УникальныйИдентификатор);
		
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru= 'Не выбран файл.'"));
	КонецЕсли;

КонецПроцедуры

// Завершение заполнения табличного документа из XML.
//
// Параметры:
//  Результат               - Произвольный - Результат выполнения предыдущего действия.
//  Адрес                   - Строка       - Адрес в хранилище.
//  ВыбранноеИмяФайла       - Строка       - Имя файла с исходными данными.
//  ДополнительныеПараметры - Структура    - Дополнительные параметры процедуры.
//
Процедура ЗаполнитьТабличныйДокументИзXMLЗавершение(Результат,
		Адрес,
		ВыбранноеИмяФайла,
		ДополнительныеПараметры) Экспорт
	
	АдресВХранилище 		= Адрес;
	ДиалогВыбораФайла 		= ДополнительныеПараметры.ДиалогВыбораФайла;
	ОбработкаПреобразования = ДополнительныеПараметры.ОбработкаПреобразования;
	ТабличныйДокумент 		= ДополнительныеПараметры.ТабличныйДокумент;
	
	ЗагрузкаИзЭлектронныхДокументовВызовСервера.
	ЗаполнитьТабличныйДокументИзXMLСервер(ДиалогВыбораФайла.ПолноеИмяФайла,
		ОбработкаПреобразования,
		ТабличныйДокумент,
		АдресВХранилище);
	
КонецПроцедуры

#КонецОбласти