
#Область СлужебныеПроцедурыИФункции

// Параметры:
//  ПараметрыДействия   - Структура - параметры, передаваемые при открытии формы, содержат следующие данные:
//      Респондент        - ОпределяемыйТип.Респондент - респондент, с которым проводится интервью.
//      ДокументОснование - ДокументСсылка - документ, из которого вызывается анкета.
//  ШаблонАнкеты        - СправочникСсылка.ШаблоныАнкет - шаблон, по которому проводится интервью.
//                      - Неопределено - пользователь выбирает шаблон из справочника.
//
Процедура НачатьИнтервью(ПараметрыДействия, ШаблонАнкеты = Неопределено) Экспорт
	
	Если ШаблонАнкеты = Неопределено Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("НачатьИнтервьюСВыборомШаблонаЗавершение", ЭтотОбъект, ПараметрыДействия);
		
		ПоказатьВводЗначения(ОписаниеОповещения, Неопределено, , Тип("СправочникСсылка.ШаблоныАнкет"));
		
	Иначе
		
		ОткрытьФормуИнтервью(ПараметрыДействия, ШаблонАнкеты);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуИнтервью(ПараметрыДействия, ШаблонАнкеты)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Респондент", ПараметрыДействия.Респондент);
	ЗначенияЗаполнения.Вставить("ШаблонАнкеты", ШаблонАнкеты);
	ЗначенияЗаполнения.Вставить("РежимАнкетирования", ПредопределенноеЗначение(
		"Перечисление.РежимыАнкетирования.Интервью"));

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ТолькоФормаЗаполнения", Истина);
	
	Если ПараметрыДействия.Свойство("ДокументОснование") Тогда
		ПараметрыФормы.Вставить("ДокументОснование", ПараметрыДействия.ДокументОснование);
	КонецЕсли;
	
	ФормаАнкеты = ОткрытьФорму("Документ.Анкета.ФормаОбъекта", ПараметрыФормы);
	
	Если ФормаАнкеты <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ФормаАнкеты, ЗначенияЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик результата работы процедуры НачатьИнтервьюСВыборомШаблона.
//
Процедура НачатьИнтервьюСВыборомШаблонаЗавершение(ВыбранныйШаблон, ПараметрыДействия) Экспорт
	
	Если ВыбранныйШаблон = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИнтервью(ПараметрыДействия, ВыбранныйШаблон);
	
КонецПроцедуры

#КонецОбласти