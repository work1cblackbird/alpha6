////////////////////////////////////////////////////////////////////////////////
// Автомобили (клиент-сервер)
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПроцедурыИФункцииРаботыСПробегомАвтомобиля

// Формирует представление о пробегах автомобиля на ключевые даты.
//
// Параметры:
//  Автомобиль - СправочникСсылка.Автомобили - Автомобиль для которого выводится информация о пробегах;
//  КлючевыеДаты - Массив, Дата - Коллекция дат, на которые необходимо получить пробеги;
//  Пробеги - Соответствие - Информация о пробегах автомобиля, полученных ранее.
//
// Возвращаемое значение:
//  Строка - Представление информации о пробегах.
//
Функция ПредставлениеПредыдущихПробегов(Автомобиль, КлючевыеДаты, Знач Пробеги=Неопределено) Экспорт
	
	Если Автомобиль.Пустая() ИЛИ (КлючевыеДаты.Количество() = 0 И Пробеги = Неопределено) Тогда
		
		Возврат ПредставленияНеопределенногоПробега();
		
	КонецЕсли;
	
	// получим пробеги если не передали их из вне
	Если Пробеги = Неопределено Тогда
		
		Пробеги = АвтомобилиВызовСервера.ПробегиАвтомобиляПоДатам(Автомобиль, КлючевыеДаты);
		
	КонецЕсли;
	
	Если Пробеги.Количество() = 0 Тогда
		
		Возврат ПредставленияНеопределенногоПробега();
		
	КонецЕсли;
	
	ПустаяДата = Дата("00010101000000");
	Представление = Новый Массив;
	Для Каждого КлючЗначение Из Пробеги Цикл
		
		Если КлючЗначение.Ключ <> ПустаяДата Тогда
			
			Представление.Добавить(
				СтрШаблон(
					НСтр("ru = 'Пробег на %1: %2'"),
					Формат(КлючЗначение.Ключ, "ДФ='dd.MM.yyyy (ЧЧ:мм)'"),
					КлючЗначение.Значение));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Новый ФорматированнаяСтрока(СтрСоединить(Представление, Символы.ПС),, Новый Цвет(28, 85, 174));
	
КонецФункции

#КонецОбласти

#Область ПредставлениеПолейАвтомобиля

// Формирует представление диапазона объема двигателя автомобиля.
//
// Параметры:
//  Начало - Число - Минимальный объем двигателя;
//  Конец - Число - Максимальный объем двигателя.
//
// Возвращаемое значение:
//  Строка - Представление диапазона объема двигателя автомобиля.
//
Функция ПредставлениеДиапазонаОбъемаДвигателя(Начало, Конец) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Начало) И НЕ ЗначениеЗаполнено(Конец) Тогда
		
		Возврат НСтр("ru = 'Любой'");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Начало) И ЗначениеЗаполнено(Конец) Тогда
		
		Возврат СтрШаблон(
			"%1 - %2",
			Начало,
			Конец
		);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Начало) Тогда
		
		Возврат СтрШаблон("> %1", Начало);
		
	КонецЕсли;
	
	Возврат СтрШаблон("< %1", Конец);
	
КонецФункции

// Формирует представление диапазона года выпуска автомобиля.
//
// Параметры:
//  Начало - Число - Минимальный год выпуска;
//  Конец - Число - Максимальный год выпуска.
//
// Возвращаемое значение:
//  Строка - Представление диапазона года выпуска автомобиля.
//
Функция ПредставлениеДиапазонаГодовВыпуска(Начало, Конец) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Начало) И НЕ ЗначениеЗаполнено(Конец) Тогда
		
		Возврат НСтр("ru = 'Любой'");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Начало) И ЗначениеЗаполнено(Конец) Тогда
		
		Возврат СтрШаблон(
			"%1 - %2",
			Формат(Начало, "ДФ=yyyy"),
			Формат(Конец, "ДФ=yyyy")
		);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Начало) Тогда
		
		Возврат СтрШаблон(
			"> %1",
			Формат(Начало, "ДФ=yyyy")
		);
		
	КонецЕсли;
	
	Возврат СтрШаблон(
		"< %1",
		Формат(Конец, "ДФ=yyyy")
	);
	
КонецФункции

// Формирует представление диапазона пробега автомобиля.
//
// Параметры:
//  Начало - Число - Минимальный пробег;
//  Конец - Число - Максимальный пробег.
//
// Возвращаемое значение:
//  Строка - Представление диапазона пробега автомобиля.
//
Функция ПредставлениеДиапазонаПробегов(Начало, Конец) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Начало) И НЕ ЗначениеЗаполнено(Конец) Тогда
		
		Возврат НСтр("ru = 'Любой'");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Начало) И ЗначениеЗаполнено(Конец) Тогда
		
		Возврат СтрШаблон("%1 - %2", Начало, Конец);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Начало) Тогда
		
		Возврат СтрШаблон("> %1", Начало);
		
	КонецЕсли;
	
	Возврат СтрШаблон("< %1", Конец);
	
КонецФункции

// Формирует представление диапазона цены автомобиля.
//
// Параметры:
//  Начало - Число - Минимальная цена;
//  Конец - Число - Максимальная цена.
//
// Возвращаемое значение:
//  Строка - Представление диапазона цены автомобиля.
//
Функция ПредставлениеДиапазонаЦен(Начало, Конец) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Начало) И НЕ ЗначениеЗаполнено(Конец) Тогда
		
		Возврат НСтр("ru = 'Любая'");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Начало) И ЗначениеЗаполнено(Конец) Тогда
		
		Возврат СтрШаблон("%1 - %2", Начало, Конец);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Начало) Тогда
		
		Возврат СтрШаблон("> %1", Начало);
		
	КонецЕсли;
	
	Возврат СтрШаблон("< %1", Конец);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставленияНеопределенногоПробега()
	
	Возврат Новый ФорматированнаяСтрока(НСтр("ru = 'Данные по пробегу отсутствуют'"),, Новый Цвет(28, 85, 174));
	
КонецФункции

#КонецОбласти


