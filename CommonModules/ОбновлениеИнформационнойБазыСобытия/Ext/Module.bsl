////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обновление версии ИБ".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Процедуры используемые при обмене данными.

// Процедура является обработчиком события ПриОтправкеДанныхПодчиненному.
// Описание обработчика события ПриОтправкеДанныхПодчиненному в синтаксис-помощнике.
// 
// Параметры:
//  ЭлементДанных            - КонстантаМенеджерЗначения - параметр содержит элемент данных,
//                                                         изменение которого зарегистрировано.
//  ОтправкаЭлемента         - ОтправкаЭлементаДанных   -  параметр отправки данных.
//  СозданиеНачальногоОбраза - Булево                   -  параметр вызова события. 
// 
Процедура ПриОтправкеВерсийПодсистем(ЭлементДанных, ОтправкаЭлемента, Знач СозданиеНачальногоОбраза = Ложь) Экспорт
	
	Если ОтправкаЭлемента = ОтправкаЭлементаДанных.Удалить
		ИЛИ ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать Тогда
		
		// Стандартную обработку не переопределяем.
		
	ИначеЕсли ТипЗнч(ЭлементДанных) = Тип("РегистрСведенийНаборЗаписей.ВерсииПодсистем") Тогда
		
		Если СозданиеНачальногоОбраза Тогда
			
			Если ОбщегоНазначения.РазделениеВключено() Тогда
				
				Если ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
					
					Для Каждого СтрокаНабора Из ЭлементДанных Цикл
						
						ТекстЗапроса =
						"ВЫБРАТЬ
						|	ВерсииПодсистемОбластейДанных.Версия КАК Версия
						|ИЗ
						|	РегистрСведений.ВерсииПодсистемОбластейДанных КАК ВерсииПодсистемОбластейДанных
						|ГДЕ
						|	ВерсииПодсистемОбластейДанных.ИмяПодсистемы = &ИмяПодсистемы";
						
						Запрос = Новый Запрос;
						Запрос.УстановитьПараметр("ИмяПодсистемы", СтрокаНабора.ИмяПодсистемы);
						Запрос.Текст = ТекстЗапроса;
						
						Выборка = Запрос.Выполнить().Выбрать();
						
						Если Выборка.Следующий() Тогда
							
							СтрокаНабора.Версия = Выборка.Версия;
							
						Иначе
							
							СтрокаНабора.Версия = "";
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			Иначе
				
				// При создании начального образа с отключенным разделением
				// выгрузку регистра выполняем без дополнительной обработки.
				
			КонецЕсли;
			
		Иначе
			
			// Выгрузку регистра выполняем только при создании начального образа.
			ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
