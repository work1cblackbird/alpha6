
#Область ПрограммныйИнтерфейс

// Обработчик события вызывается при получении имени кассира.
//
// Параметры:
//  ИмяКассира - Строка, Неопределено - Текст, используемый для заполнения документа
//  СтандартнаяОбработка - Булево
//
Процедура ОбработкаЗаполненияИмяКассира(ИмяКассира, СтандартнаяОбработка) Экспорт
	
	// АльфаАвто Определеним параметры Кассир и ИннКассира
	ИННКассира="";
	МенеджерОборудованияВызовСервераПереопределяемый.ТекущийКассир(ИмяКассира, ИННКассира);
	СтандартнаяОбработка = Ложь;
	// Конец АльфаАвто
	
КонецПроцедуры

// Обработчик события вызывается при получении ИНН кассира.
//
// Параметры:
//  ИННКассира - Строка, Неопределено - Текст, используемый для заполнения документа
//  СтандартнаяОбработка - Булево
//
Процедура ОбработкаЗаполненияИННКассира(ИННКассира, СтандартнаяОбработка) Экспорт
	
	// АльфаАвто Определеним параметры Кассир и ИннКассира
	ИмяКассира="";
	МенеджерОборудованияВызовСервераПереопределяемый.ТекущийКассир(ИмяКассира, ИННКассира);
	СтандартнаяОбработка = Ложь;
	// Конец АльфаАвто
	
КонецПроцедуры

// Обработчик события вызывается при заполнения наименования рабочего места.
//
// Параметры:
//  Наименование - Строка - Наименование рабочего места.
//  ИмяПользователя - Строка - Имя пользователя информационной базы.
//  СтандартнаяОбработка - Булево
//
Процедура ОбработкаЗаполненияНаименованияРабочегоМеста(Наименование, ИмяПользователя, СтандартнаяОбработка)  Экспорт
	
КонецПроцедуры

// Обработчик события получения идентификатора клиента для рабочегоМеста.
//
// Параметры:
//  ИдентификаторКлиента - Строка, Неопределено - Идентификатор клиента
//  СтандартнаяОбработка - Булево
//
Процедура ИдентификаторКлиентаДляРабочегоМеста(ИдентификаторКлиента, СтандартнаяОбработка) Экспорт
	
	// АльфаАвто
	#Если Не ВебКлиент Тогда
	
	РежимыПоискаИдентификатораКлиента = МенеджерОборудованияАльфаАвтоКлиентСервер.РежимыПоискаИдентификатораКлиента();
	ПараметрыУникальностиРабочихМест = МенеджерОборудованияАльфаАвтоВызовСервера.ПараметрыПолученияИдентификатораРабочегоМеста();
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Или ВнешнееСоединение Тогда
		ИмяКомпьютера = ПолучитьТекущийСеансИнформационнойБазы().ИмяКомпьютера;
	#ИначеЕсли МобильноеПриложениеСервер Тогда
		ИмяКомпьютера = "";
	#Иначе
		ИмяКомпьютера = ИмяКомпьютера();
	#КонецЕсли
	
	Если ПараметрыУникальностиРабочихМест.РежимПоиска = РежимыПоискаИдентификатораКлиента.ИмяКомпьютераИИмяПользователя Тогда
		
		СтандартнаяОбработка = Ложь;
		ИдентификаторКлиента = СтрШаблон("%1_%2", ИмяКомпьютера, ИмяПользователя());
		
	ИначеЕсли ПараметрыУникальностиРабочихМест.РежимПоиска = РежимыПоискаИдентификатораКлиента.ИмяКомпьютера Тогда
		
		СтандартнаяОбработка = Ложь;
		ИдентификаторКлиента = ИмяКомпьютера;
		
	ИначеЕсли ПараметрыУникальностиРабочихМест.РежимПоиска = РежимыПоискаИдентификатораКлиента.ИдентификаторКлиентаСДопПоиском Тогда
		
		РабочиеМестаПоИД = МенеджерОборудованияВызовСервера.НайтиРабочиеМестаПоИдентификатору(ИдентификаторКлиента, ИмяКомпьютера);
	
		Если ЗначениеЗаполнено(РабочиеМестаПоИД) Тогда
			
			Возврат;
			
		КонецЕсли;
		
		Наименование = СтрШаблон("%1(%2)", ИмяПользователя(), ИмяКомпьютера);
		РабочиеМестаПоИмени = МенеджерОборудованияАльфаАвтоВызовСервера.НайтиРабочиеМестаПоИмени(Наименование);
		
		Если ЗначениеЗаполнено(РабочиеМестаПоИмени) Тогда
			
			ИдентификаторКлиента = РабочиеМестаПоИмени[0];
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецЕсли
	// Конец АльфаАвто
КонецПроцедуры

#КонецОбласти
