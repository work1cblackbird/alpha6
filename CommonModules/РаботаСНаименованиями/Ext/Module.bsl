//***************************************************************//
// Модуль предназначен для работы с наименованиями справочников: //
//  - формирование;                                              //
//  - проверка;                                                  //
//  - обновление;                                                //
//  - отображение.                                               //
//***************************************************************//


#Область ПрограммныйИнтерфейс

// Расширяет состав наименования значениями доп. реквизитов и свойств.
//
// Параметры:
//  ЧастиНаименования - Массив - содержит состав наименования (Строка);
//  ПараметрыДействия - Структура - содержит:
//    * Форма - ФормаКлиентскогоПриложения - форма справочника для которого заполняется наименование.
//
Процедура ДополнитьНаименованиеСвойствами(ЧастиНаименования, ПараметрыДействия) Экспорт
	
	Если
		ПараметрыДействия = Неопределено
		ИЛИ НЕ ПараметрыДействия.Свойство("Форма")
		ИЛИ НЕ ПараметрыДействия.Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьДополнительныеРеквизитыИСведения")
		ИЛИ НЕ ЕстьРеквизит(ПараметрыДействия.Форма, "Свойства_ОписаниеДополнительныхРеквизитов") Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Для Каждого ДополнительноеСвойство Из ПараметрыДействия.Форма.Свойства_ОписаниеДополнительныхРеквизитов Цикл
		
		НаименованиеСвойства = Строка(ДополнительноеСвойство.Наименование);
		
		ЗначениеСвойства = ПараметрыДействия.Форма[ДополнительноеСвойство.ИмяРеквизитаЗначение];
		
		Если ЗначениеЗаполнено(ЗначениеСвойства) Тогда
			
			ЧастиНаименования.Добавить(СтрШаблон("%1: %2", НаименованиеСвойства, ЗначениеСвойства));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
