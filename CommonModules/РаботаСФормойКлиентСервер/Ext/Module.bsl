
#Область ПрограммныйИнтерфейс

// Формирует контейнер для хранения связанных с деревом 
// 
// Параметры:
//  ЭлементыФормы - ЭлементыФормы
//
// Возвращаемое значение:
//  Структура - описание дерева с полями:
//  * Дерево - ПолеФормы - Элемент дерева. По умолчанию ищется элемент с именем "Дерево".
//  * Группа - ГруппаФормы - Элемент группы с деревом. По умолчанию ищется элемент с именем "ГруппаДерево".
//  * Переключатель - КнопкаКоманднойПанели, Неопределено - Если для переключения видимости используется
//  		одна кнопка указывается она. Иначе не заполняется. По умолчанию ищется элемент с именем "ПоказатьСкрытьДерево". 
//  * Показать - ПолеФормы - Элемент для отображения дерева. Если для переключения видимости используются разные кнопки.
//  												 По умолчанию ищется элемент с именем "ПоказатьДерево".
//  * Скрыть - ПолеФормы - Элемент для отображения дерева. Если для переключения видимости используются разные кнопки.
//  											 По умолчанию ищется элемент с именем "СкрытьДерево".
//
Функция НовоеОписаниеДереваИерархии(ЭлементыФормы) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Дерево", ЭлементыФормы.Найти("Дерево"));
	Результат.Вставить("Группа", ЭлементыФормы.Найти("ГруппаДерево"));
	Результат.Вставить("Переключатель", ЭлементыФормы.Найти("ПоказатьСкрытьДерево"));
	Результат.Вставить("Показать", ЭлементыФормы.Найти("ПоказатьДерево"));
	Результат.Вставить("Скрыть", ЭлементыФормы.Найти("СкрытьДерево"));
	Возврат Результат; 
	
КонецФункции

// Устанавливает видимость дерева иерархии.
// 
// Параметры:
//  ОписаниеДерева - см. НовоеОписаниеДереваИерархии
//  Видимость - Булево
// 
Процедура УстановитьВидимостьДереваИерархии(ОписаниеДерева, Видимость) Экспорт
	
	Если ОписаниеДерева.Переключатель <> Неопределено Тогда
		
		ОписаниеДерева.Переключатель.Пометка = Видимость;
		ОписаниеДерева.Дерево.Видимость = Видимость;
		
	ИначеЕсли ОписаниеДерева.Показать <> Неопределено И ОписаниеДерева.Скрыть <> Неопределено Тогда 
		
		ОписаниеДерева.Группа.Видимость = Видимость;
		ОписаниеДерева.Дерево.Видимость = Видимость;
		ОписаниеДерева.Скрыть.Видимость = Видимость;
		ОписаниеДерева.Показать.Видимость = Не Видимость;
		
	КонецЕсли;
	
КонецПроцедуры

// Получает полное имя объекта из имени формы
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  Строка - полное имя объекта
//
Функция ПолноеИмяОбъекта(Форма) Экспорт
	
	МассивИмени = СтрРазделить(Форма.ИмяФормы, ".");
	ПолноеИмяОбъекта = МассивИмени[0] + "." + МассивИмени[1];
	
	Возврат ПолноеИмяОбъекта;
	
КонецФункции

#КонецОбласти