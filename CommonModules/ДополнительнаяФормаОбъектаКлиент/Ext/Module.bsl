
#Область ПрограммныйИнтерфейс

// Открытие дополнительной формы объекта
//
// Параметры:
//  СсылкаНаОбъект         	- ЛюбаяСсылка 	- Ссылка на текущий объект.
//  ПараметрыДействия 		- Структура 	- Набор параметров, использующихся при выполнения операции.
//
Процедура ОткрытьДополнительнуюФорму(СсылкаНаОбъект, ПараметрыДействия = Неопределено) Экспорт
	
	Форма = ПараметрыДействия.Форма;
	Объект = Неопределено;
	ПолучитьОсновнойОбъектФормы(Форма, Объект);
	
	ИмяДополнительнойФормы = УправлениеДиалогомВызовСервера.ПолучитьИмяДополнительнойФормыОбъекта(СсылкаНаОбъект);

	ПараметрыФормы = Новый Структура("Объект,Ссылка,ТолькоПросмотр,Модифицированность,УникальныйИдентификатор");
	
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Форма);
	ПараметрыФормы.Объект = Объект;

	Если Не ТипЗнч(ПараметрыФормы.Объект) = Тип("ДанныеФормыСтруктура") Тогда
		ПараметрыФормы.Ссылка = СсылкаНаОбъект;
	Иначе
		ПараметрыФормы.Ссылка = ПараметрыФормы.Объект.Ссылка;
	КонецЕсли;
	
	ОбработкаРезультатаЗакрытия = Новый ОписаниеОповещения("ОбработкаОткрытияДополнительнойФормы",
		ДополнительнаяФормаОбъектаКлиент, ПараметрыФормы);
	
	ИмяОткрываемойФормы = "ОбщаяФорма.ДополнительнаяФормаОбъектаКомментарий";

	Если ЗначениеЗаполнено(ИмяДополнительнойФормы) Тогда
		ИмяОткрываемойФормы = ИмяДополнительнойФормы;
	КонецЕсли;

	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, Форма,,,,ОбработкаРезультатаЗакрытия,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Процедура по завершению открытия дополнительной формы объекта
//
// Параметры:
//  Результат - Структура  - Результат открытия дополнительной формы.
//  Параметры - Структура  - Дополнительные параметры.
//
Процедура ОбработкаОткрытияДополнительнойФормы(Результат, Параметры) Экспорт
	
	Если КодВозвратаДиалога.Отмена = Результат Или Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(Параметры.Объект) = Тип("ДанныеФормыСтруктура") И НЕ Параметры.ТолькоПросмотр Тогда
		ЗаполнитьЗначенияСвойств(Параметры.Объект,Результат);
		Возврат;
	ИначеЕсли ТипЗнч(Параметры.Объект) = Тип("ДанныеФормыСтруктура") Тогда
		ЗаполнитьЗначенияСвойств(Параметры.Объект,Результат);
	КонецЕсли;
	
	УправлениеДиалогомВызовСервера.ОбновитьЗначениеПоляКомментарий(Параметры.Ссылка, Результат);
	
КонецПроцедуры

#КонецОбласти
