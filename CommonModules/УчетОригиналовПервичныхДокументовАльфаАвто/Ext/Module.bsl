
#Область ПрограммныйИнтерфейс

// Обработчик события "ПриСозданииНаСервере" формы списка.
//
//	Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма списка документа.
//  Список - ТаблицаФормы - основной список формы.
//  Расположение - ПолеФормы - колонка списка, перед которой будут располагаться новые колонки состояний.
//		Если Неопределено, тогда колонки будут расположены в конце списка. Необязательный.
//
Процедура ПриСозданииНаСервере_ФормаСписка(Форма, Список, Расположение = Неопределено) Экспорт
	
	// Передвинем подменю
	Если Форма.Элементы.Найти("ФормаПоиск") <> Неопределено Тогда
		Если Форма.Элементы.Найти("ПодменюУстановитьНастроитьСостояниеОригинала") <> Неопределено Тогда
			Форма.Элементы.Переместить(
				Форма.Элементы.ПодменюУстановитьНастроитьСостояниеОригинала,
				Форма.Элементы.ФормаКоманднаяПанель,
				Форма.Элементы.ФормаПоиск);
		КонецЕсли;
		Если Форма.Элементы.Найти("УстановитьСостояниеОригиналПолучен") <> Неопределено Тогда
			Форма.Элементы.Переместить(
				Форма.Элементы.УстановитьСостояниеОригиналПолучен,
				Форма.Элементы.ФормаКоманднаяПанель,
				Форма.Элементы.ФормаПоиск);
		КонецЕсли;
	КонецЕсли;
	
	УчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаСписка(Форма, Форма.Элементы.Список, Расположение);
	
КонецПроцедуры

// Обработчик события "ПриСозданииНаСервере" формы документа.
//
//	Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма списка документа.
//  Расположение - ГруппаФормы - группа, в которой будет располагаться надпись о текущем состоянии оригинала.
//		Если Неопределено, тогда надпись будет расположена в нижнем правом углу формы. Необязательный.
//  ЕстьПодвал - Булево - Признак того, что есть группа Элементов формы "Подвал" и надо расположить группу выше.
//
Процедура ПриСозданииНаСервере_ФормаДокумента(Форма, Расположение = Неопределено, ЕстьПодвал = Истина) Экспорт
	
	УчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаДокумента(Форма, Расположение);
	
	Если ЕстьПодвал И Расположение = Неопределено
		И Форма.Элементы.Найти("ДекорацияСостояниеОригинала") <> Неопределено Тогда
		Форма.Элементы.Переместить(
			Форма.Элементы.ДекорацияСостояниеОригинала,
			Форма,
			Форма.Элементы.Подвал);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
