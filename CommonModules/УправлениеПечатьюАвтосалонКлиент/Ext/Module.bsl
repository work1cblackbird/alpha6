////////////////////////////////////////////////////////////////////////////////
// Подсистема "Печать".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует печатную форму "Договор купли-продажи автомобиля".
//
// Параметры:
//  ПараметрыПечати - Структура - Параметры печатной формы.
// 
// Возвращаемое значение:
//  ТабличныйДокумент - Результат формирования печатной формы.
//
Функция ПечатьДоговорКуплиПродажи(ПараметрыПечати) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", УправлениеПечатьюАвтосалонКлиент);
	ПараметрыПечатиФорма = ПараметрыПечати.Форма;
	ПараметрыПечати.Удалить("Форма");
	Параметры = Новый Структура;
	Параметры.Вставить("СписокВыбора", ПараметрыПечати.ОбъектыПечати);
	Параметры.Вставить("ПараметрыПечати", ПараметрыПечати);
	ОткрытьФорму("ОбщаяФорма.ВыборАвтомобилейДляПечатиДоговораКуплиПродажи",
		Параметры,
		ПараметрыПечатиФорма,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	Возврат Неопределено;
	
КонецФункции

// Формирует печатную форму "Паспорт технического средства (ПТС)".
//
// Параметры:
//  ПараметрыПечати - Структура - Параметры печатной формы.
// 
// Возвращаемое значение:
//  ТабличныйДокумент - Результат формирования печатной формы.
//
Функция ПечатьПаспортТехническогоСредства(ПараметрыПечати) Экспорт
	
	ПараметрыПечатиФорма = ПараметрыПечати.Форма;
	ПараметрыПечати.Удалить("Форма");
	ПараметрыОткрытия = Новый Структура("ПараметрыПечати", ПараметрыПечати);
	ОткрытьФорму("Справочник.ПодтверждающиеДокументы.Форма.ФормаВыбораСекцииПТС", 
		ПараметрыОткрытия, 
		ПараметрыПечатиФорма,
		,
		,
		, 
		Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", УправлениеПечатьюАвтосалонКлиент, ПараметрыОткрытия));
	
	Возврат Неопределено;
	
КонецФункции

// Формирует печатную форму "Договор купли-продажи автомобиля".
//
// Параметры:
//  ПараметрыПечати - Структура - Параметры печатной формы.
// 
// Возвращаемое значение:
//  ТабличныйДокумент - Результат формирования печатной формы.
//
Функция ПечатьАктСверки(ПараметрыПечати) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", УправлениеПечатьюАвтосалонКлиент);
	ПараметрыПечатиФорма = ПараметрыПечати.Форма;
	ПараметрыПечати.Удалить("Форма");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПараметрыПечати", ПараметрыПечати);
	
	ОткрытьФорму("Документ.АктСверкиВзаиморасчетов.Форма.ФормаНастройкиПараметровПечати",
		Параметры,
		ПараметрыПечатиФорма,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	Возврат Неопределено;
	
КонецФункции

// Обработчик события возникающего при прекращении работы подчиненной формы.
//
// Параметры:
//  РезультатОповещения     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
Процедура Подключаемый_ОбработкаРезультатаОповещения(РезультатОповещения, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ПолучитьЗначениеПараметраСтруктуры(РезультатОповещения, "МассивСекций") <> Неопределено Тогда	
		ДополнительныеПараметры.ПараметрыПечати.ДополнительныеПараметры.Вставить("МассивСекций", РезультатОповещения.МассивСекций);	
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Справочник.ПодтверждающиеДокументы",
			"ПФ_MXL_ПТС",
			ДополнительныеПараметры.ПараметрыПечати.ОбъектыПечати,
			РезультатОповещения.ВладелецФормы,
			ДополнительныеПараметры.ПараметрыПечати);
		Возврат;	
	КонецЕсли;
	
	Если ПолучитьЗначениеПараметраСтруктуры(РезультатОповещения, "ПечататьНомераУПД") <> Неопределено Тогда
		
		РезультатОповещения.ПараметрыПечати.ДополнительныеПараметры.Вставить("ПечататьНомераУПД", РезультатОповещения.ПечататьНомераУПД);
		РезультатОповещения.ПараметрыПечати.ДополнительныеПараметры.Вставить("РазбиватьПоСделкам", РезультатОповещения.РазбиватьПоСделкам);
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.АктСверкиВзаиморасчетов",
			РезультатОповещения.ПараметрыПечати.Идентификатор,
			РезультатОповещения.ПараметрыПечати.ОбъектыПечати,
			РезультатОповещения.ВладелецФормы,
			РезультатОповещения.ПараметрыПечати
		);
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ РезультатОповещения = Неопределено Тогда
		ВладелецФормы   = РезультатОповещения[0];
		ПараметрыПечати = РезультатОповещения[1];
		РезультатОповещения.Удалить(1);
		РезультатОповещения.Удалить(0);
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.РеализацияАвтомобилей",
			"ПФ_MXL_ДоговорКуплиПродажиАвтомобиля",
			РезультатОповещения,
			ВладелецФормы,
			ПараметрыПечати);
	КонецЕсли;
	
КонецПроцедуры // Подключаемый_ОбработкаРезультатаОповещения()

// Формирует интерактивный запрос валюты для печатного документа.
//
// Параметры:
//  ПараметрыПечати  - Структура - дополнительные параметры печати;
//
// Возвращаемое значение:
//   ТабличныйДокумент   - Возвращает сформированный табличный документ.
//
Функция НачатьВыборВалютыДляПечати(ПараметрыПечати) Экспорт
	
	ПараметрыВопросов   = Новый Структура;
	ПараметрыКоманды    = Новый Структура;
	ОбработкаОповещения = Новый ОписаниеОповещения("ВыборВалютыДляПечатиЗавершение",
		УправлениеПечатьюАвтосалонКлиент,
		ПараметрыПечати);
	
	ПоследовательныеОперацииКлиентСервер.ДобавитьВыборСсылки(ПараметрыКоманды,
		"Валюта",
		"Справочник.Валюты.ФормаВыбора",
		Новый Структура("РежимВыбора", Истина),
		,
		Истина,
		,
		ОбработкаОповещения);
	
	ПараметрыВопросов.Вставить("КоллекцияОпераций", ПараметрыКоманды);
	ПараметрыВопросов.Вставить("ОбработчикСобытия", Неопределено);
	
	ПоследовательныеОперацииКлиент.ВыполнитьПоследовательно(ПараметрыВопросов);
	
	Возврат Неопределено;
	
КонецФункции // НачатьВыборВалютыДляПечати()

// Продолжение функции НачатьВыборВалютыДляПечати
//
// Параметры:
//  Результат        - СправочникСсылка.Валюты  -  валюта, в которой выводится печатный документ;
//  ПараметрыПечати  - Структура - дополнительные параметры печати;
//
// Возвращаемое значение:
//   ТабличныйДокумент   - Возвращает сформированный табличный документ.
//
Функция ВыборВалютыДляПечатиЗавершение(Результат, ПараметрыПечати) Экспорт
	
	Если Результат <> Неопределено Тогда
		ПараметрыПечати.ДополнительныеПараметры.Вставить("ВалютаПечатногоДокумента", Результат);
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ПараметрыПечати.МенеджерПечати,
		ПараметрыПечати.Идентификатор,
		ПараметрыПечати.ОбъектыПечати,
		ПараметрыПечати.Форма,
		ПараметрыПечати.ДополнительныеПараметры);
	
КонецФункции // ВыборВалютыДляПечатиЗавершение()

#КонецОбласти