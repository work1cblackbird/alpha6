// Общий модуль "Обработка табличной части тарификация"

#Область ПрограммныйИнтерфейс

// Обработка выполнения пересчета табличной части "Тарификация"
//
// Параметры:
//  Объект				- ДанныеФормыСтруктура		- Объект, для которого выполняется обработка события.
//  ПараметрыДействия	- Структура					- Набор параметров, использующихся при выполнения операции.
//
Процедура ВыполнитьПересчетТабличнойЧасти(Объект, ПараметрыДействия) Экспорт
	
	ТребуетсяПересчетЦен = ПолучитьЗначениеПараметраСтруктуры(ПараметрыДействия, "ТребуетсяПересчетЦен", Ложь);
	
	Если НЕ ТребуетсяПересчетЦен Тогда
		
		Возврат;
		
	КонецЕсли;
	
	МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(Объект.Ссылка));
	
	Если МетаданныеОбъекта = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбъектМетаданныхТЧТарификация = МетаданныеОбъекта.ТабличныеЧасти.Найти("Тарификация");
	
	Если ОбъектМетаданныхТЧТарификация = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СтарыйКурс = ПараметрыДействия.СтарыйКурс;
	НовыйКурс  = Объект.КурсДокумента;
	
	Если (СтарыйКурс * НовыйКурс) = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'При пересчете валюты обнаружен нулевой курс валюты.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	ИначеЕсли СтарыйКурс = НовыйКурс Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КоэффициентСоотношенияКурсов = СтарыйКурс / НовыйКурс;
	
	Для Каждого Строка Из Объект.Тарификация Цикл
		
		Строка.СуммаВсего  = Окр(Строка.СуммаВсего * КоэффициентСоотношенияКурсов, 2);
		Строка.Цена        = Окр(Строка.Цена * КоэффициентСоотношенияКурсов, 2);
		Строка.ЦенаМинута  = Окр(Строка.ЦенаМинута * КоэффициентСоотношенияКурсов, 2);
		Строка.ЦенаЧас     = Окр(Строка.ЦенаЧас * КоэффициентСоотношенияКурсов, 2);
		Строка.ЦенаСутки   = Окр(Строка.ЦенаСутки * КоэффициентСоотношенияКурсов, 2);
		Строка.ЦенаНеделя  = Окр(Строка.ЦенаНеделя * КоэффициентСоотношенияКурсов, 2);
		Строка.ЦенаМесяц   = Окр(Строка.ЦенаМесяц * КоэффициентСоотношенияКурсов, 2);
		Строка.ЦенаКвартал = Окр(Строка.ЦенаКвартал * КоэффициентСоотношенияКурсов, 2);
		Строка.ЦенаГод     = Окр(Строка.ЦенаГод * КоэффициентСоотношенияКурсов, 2);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

