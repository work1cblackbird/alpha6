///////////////////////////////////////////////////////////////////////////////
// Модуль формы регитсра сведений "Выполнение сервисных кампаний"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Автомобиль       = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Автомобиль");
	ТолькоАктуальные = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ТолькоАктуальные", Ложь);
	VIN              = ПолучитьЗначениеПараметраСтруктуры(Параметры, "VIN", Автомобиль.VIN);
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Автомобилю <%1> предписаны следующие сервисные кампании.'"), Автомобиль);
	Элементы.СервисныеКампанииТолькоАктуальные.Пометка = ТолькоАктуальные;
	
	ЭтоРежимПодбора = ПолучитьЗначениеПараметраСтруктуры(Параметры, "РежимПодбора", Ложь);
	
	Элементы.СервисныеКампанииВыбрать.Видимость = ЭтоРежимПодбора;
	
	Если НЕ ЗначениеЗаполнено(Автомобиль) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбновитьСписокСервисныхКампаний();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.СервисныеКампании.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = СервисныеКампании.НайтиПоИдентификатору(Элементы.СервисныеКампании.ТекущаяСтрока);
	
	Закрыть(ТекущиеДанные.СервиснаяКампания);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСписокСервисныхКампаний();
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоАктуальные(Команда)
	
	Если Команда <> Неопределено Тогда
		ТолькоАктуальные = НЕ ТолькоАктуальные;
	КонецЕсли;
	
	Элементы.СервисныеКампанииТолькоАктуальные.Пометка = ТолькоАктуальные;
	
	ОбновитьСписокСервисныхКампаний();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновление списка СК
//
&НаСервере
Процедура ОбновитьСписокСервисныхКампаний()
	
	СервисныеКампании.Загрузить(АвтомобилиСервер.ПроверитьСервиснуюКампанию(Автомобиль,, ТолькоАктуальные, ?(ЗначениеЗаполнено(ЗаказНаряд), ЗаказНаряд, Неопределено)));
	
КонецПроцедуры

#КонецОбласти

