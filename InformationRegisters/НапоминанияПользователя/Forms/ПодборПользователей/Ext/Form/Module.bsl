

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполняем список выбранных пользователей выбранными ранее.
	Если Параметры.Свойство("ПользователиВыбранные") Тогда
		ПользователиВыбранные = Параметры.ПользователиВыбранные;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Перебираем настройки компоновщика, ищем элемент с типом "ОтборКомпоновкиДанных".
	Для каждого ЭлементНастройки Из ПользователиДоступные.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		
		Если ТипЗнч(ЭлементНастройки) = Тип("ОтборКомпоновкиДанных") Тогда
			
			// Получаем и записываем в поле отбора ранее установленный отбор.
			Для каждого ЭлементЭлементаНастройки Из ЭлементНастройки.Элементы Цикл
				
				Если ЭлементЭлементаНастройки.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодразделениеКомпании") Тогда
					
					ОтборПоПодразделению = ЭлементЭлементаНастройки.ПравоеЗначение;
					Прервать;
					
				КонецЕсли;
			КонецЦикла;
			
			// Добавляем отбор для скрытия уже выбранных пользователей (будет обновляться автоматически при изменении списка).
			НовыйЭлементОтбора = ЭлементНастройки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Ссылка");
			НовыйЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеВСписке;
			НовыйЭлементОтбора.ПравоеЗначение = ПользователиВыбранные;
			НовыйЭлементОтбора.Использование  = Истина;
			
			Прервать;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПоПодразделениюПриИзменении(Элемент)
	
	// Перебираем настройки компоновщика, ищем элемент с типом "ОтборКомпоновкиДанных".
	Для каждого ЭлементНастройки Из ПользователиДоступные.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		
		Если ТипЗнч(ЭлементНастройки) = Тип("ОтборКомпоновкиДанных") Тогда
			
			// Удаляем предыдущие отборы по организации или подразделению.
			Для каждого ЭлементЭлементаНастройки Из ЭлементНастройки.Элементы Цикл
				
				Если ЭлементЭлементаНастройки.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") ИЛИ ЭлементЭлементаНастройки.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодразделениеКомпании") Тогда
					ЭлементНастройки.Элементы.Удалить(ЭлементЭлементаНастройки);
				КонецЕсли;
			КонецЦикла;
			
			// Передаем выбранный отбор в динамический список.
			Если ЗначениеЗаполнено(ОтборПоПодразделению) Тогда
				
				НовыйЭлементОтбора = ЭлементНастройки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				НовыйЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПодразделениеКомпании");
				НовыйЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВИерархии;
				НовыйЭлементОтбора.ПравоеЗначение = ОтборПоПодразделению;
				НовыйЭлементОтбора.Использование  = Истина;
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиДоступныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьВСписокВыбранных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВыбранныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	УдалитьИзСпискаВыбранных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВыбранныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВыбранныеПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОповеститьОВыборе(ПользователиВыбранные);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВСписокВыбранных(Команда = Неопределено)
	
	// Добавляем всех выделенных пользователей.
	Для каждого ВыделеннаяСтрока Из Элементы.ПользователиДоступные.ВыделенныеСтроки Цикл
		
		Если ПользователиВыбранные.НайтиПоЗначению(ВыделеннаяСтрока) = Неопределено Тогда
			ПользователиВыбранные.Добавить(ВыделеннаяСтрока);
			ПользователиВыбранные.СортироватьПоЗначению();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИзСпискаВыбранных(Команда = Неопределено)
	
	// Удаляем всех выделенных пользователей.
	Для каждого ВыделеннаяСтрока Из Элементы.ПользователиВыбранные.ВыделенныеСтроки Цикл
		
		ТекЭлемент = ПользователиВыбранные.НайтиПоИдентификатору(ВыделеннаяСтрока);
		
		Если ТекЭлемент <> Неопределено Тогда
			ПользователиВыбранные.Удалить(ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

