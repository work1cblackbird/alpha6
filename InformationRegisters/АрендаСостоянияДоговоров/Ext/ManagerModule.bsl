
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет в регистр сведений по состоянию договора аренды на текущую дату
//
// Параметры:
//  ДоговорАренды	 - ДокументСсылка.ДоговорАренды - Договор, для которого нужно записать состояние
//  Состояние		 - ПеречислениеСсылка.СостоянияДоговораАренды - Записываемое состояние
//
Процедура ЗаписатьСостояние(ДоговорАренды, Состояние) Экспорт 
	
	Если Состояние <> ТекущееСостояние(ДоговорАренды) Тогда
		
		 МенеджерЗаписи = РегистрыСведений.АрендаСостоянияДоговоров.СоздатьМенеджерЗаписи();
		 МенеджерЗаписи.Период = ТекущаяДатаСеанса();
		 МенеджерЗаписи.ДоговорАренды = ДоговорАренды;
		 МенеджерЗаписи.Состояние = Состояние;
		 МенеджерЗаписи.Автор = Пользователи.АвторизованныйПользователь();
		 МенеджерЗаписи.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает текущее состояние договора на переданную дату
//
// Параметры:
//  ДоговорАренды	 - ДокументСсылка.ДоговорАренды - Договор, для которого нужно получить состояние
//  Период			 - Дата - Если не передавать дату, то используется текущая дата
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДоговораАренды - Состояние договора на переданную дату
//
Функция ТекущееСостояние(ДоговорАренды, Период = '00010101') Экспорт 
	
	Состояние = Перечисления.СостоянияДоговораАренды.ПустаяСсылка();
	
	Если Период = '00010101' Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ДоговорАренды.Пустая() Тогда
		Состояние = Перечисления.СостоянияДоговораАренды.Подготовка;
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	АрендаСостоянияДоговоровСрезПоследних.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.АрендаСостоянияДоговоров.СрезПоследних(&Период, ДоговорАренды = &ДоговорАренды) КАК АрендаСостоянияДоговоровСрезПоследних";
		Запрос.УстановитьПараметр("Период",        Период);
		Запрос.УстановитьПараметр("ДоговорАренды", ДоговорАренды);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Состояние = Выборка.Состояние;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

#Область ПараметрыОбработкиРеквизитовОбъекта

Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	
	ОбязательныеРеквизиты.Добавить("Период");
	ОбязательныеРеквизиты.Добавить("ДоговорАренды");
	ОбязательныеРеквизиты.Добавить("Состояние");
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции 

Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	УникальныеРеквизиты = Новый Структура();
	
	Возврат УникальныеРеквизиты;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли