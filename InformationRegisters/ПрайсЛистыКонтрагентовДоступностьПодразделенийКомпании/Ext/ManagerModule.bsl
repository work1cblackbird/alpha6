// Модуль менеджера регистра "Прайс-листы контрагентов доступность подразделений компании".

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Доступность по подразделениям
// 
// Возвращаемое значение:
//  МенеджерВременныхТаблиц - Таблица доступности прайс-листов.
//
Функция ДоступностьПрайсЛистовПоПодразделениямКомпании() Экспорт
	
	ТекстЗапроса = ПоУмолчаниюДоступныеПодразделенияПрайсЛистов();
	
	ТекстЗапроса = ТекстЗапроса +"
	|ВЫБРАТЬ
	|	АктуальныеПрайЛисты.ПрайсЛист,
	|	ПодразделенияКомпании.Ссылка КАК ПодразделениеКомпании,
	|	АктуальныеПрайЛисты.ОтветственноеПодразделение
	|ПОМЕСТИТЬ ПрайсЛистыСПодразделениями
	|ИЗ
	|	АктуальныеПрайЛисты КАК АктуальныеПрайЛисты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрайсЛистыСПодразделениями.ПрайсЛист,
	|	ПрайсЛистыСПодразделениями.ПодразделениеКомпании,
	|	ВЫБОР
	|		КОГДА ПоУмолчаниюДоступныеПодразделения.ПодразделениеКомпании ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.УровниДоступностиПрайсЛистовКонтрагентов.Запрещено)
	|		КОГДА ПрайсЛистыСПодразделениями.ОтветственноеПодразделение = ПрайсЛистыСПодразделениями.ПодразделениеКомпании
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.УровниДоступностиПрайсЛистовКонтрагентов.Редактирование)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.УровниДоступностиПрайсЛистовКонтрагентов.ПрямыеЗаказы)
	|	КОНЕЦ КАК УровеньДоступностиПоУмолчанию
	|ПОМЕСТИТЬ ДоступностьПрайсЛистовПоПодразделениямКомпанииПоУмолчанию
	|ИЗ
	|	ПрайсЛистыСПодразделениями КАК ПрайсЛистыСПодразделениями
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоУмолчаниюДоступныеПодразделения КАК ПоУмолчаниюДоступныеПодразделения
	|		ПО ПрайсЛистыСПодразделениями.ПрайсЛист = ПоУмолчаниюДоступныеПодразделения.ПрайсЛист
	|			И ПрайсЛистыСПодразделениями.ПодразделениеКомпании = ПоУмолчаниюДоступныеПодразделения.ПодразделениеКомпании
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АктуальныеПрайЛисты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПоУмолчаниюДоступныеПодразделения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПрайсЛистыСПодразделениями
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоступностьПрайсЛистовПоПодразделениямКомпанииПоУмолчанию.ПрайсЛист,
	|	ДоступностьПрайсЛистовПоПодразделениямКомпанииПоУмолчанию.ПодразделениеКомпании,
	|	ЕСТЬNULL(ПрайсЛистыКонтрагентовДоступностьПодразделенийКомпании.Уровень, ДоступностьПрайсЛистовПоПодразделениямКомпанииПоУмолчанию.УровеньДоступностиПоУмолчанию) КАК Уровень
	|ПОМЕСТИТЬ ДоступностьПрайсЛистовПоПодразделениямКомпании
	|ИЗ
	|	ДоступностьПрайсЛистовПоПодразделениямКомпанииПоУмолчанию КАК ДоступностьПрайсЛистовПоПодразделениямКомпанииПоУмолчанию
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовДоступностьПодразделенийКомпании КАК ПрайсЛистыКонтрагентовДоступностьПодразделенийКомпании
	|		ПО ДоступностьПрайсЛистовПоПодразделениямКомпанииПоУмолчанию.ПрайсЛист = ПрайсЛистыКонтрагентовДоступностьПодразделенийКомпании.ПрайсЛист
	|			И ДоступностьПрайсЛистовПоПодразделениямКомпанииПоУмолчанию.ПодразделениеКомпании = ПрайсЛистыКонтрагентовДоступностьПодразделенийКомпании.ПодразделениеКомпании";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

// Получение доступных для данного подразделения прайс-листов контрагента
//
// Параметры:
//  ПодразделениеКомпании - СправочникСсылка.ПодразделенияКомпании - Подразделения, для которого происходит
//  																 поиск доступных прайс-листов
//  Владелец              - СправочникСсылка.Контрагенты           - Владелец прайс-листа
//  УровниДоступа         - Массив                                 - Желаемые уровни доступности прайс-листа
// 
// Возвращаемое значение:
//  Массив - Массив прайс-листов
//
Функция ДоступныеПрайсЛистыПодразделенияКомпании(ПодразделениеКомпании, Владелец = Неопределено, УровниДоступа = Неопределено) Экспорт
	
	МенеджерВременныхТаблиц = ДоступностьПрайсЛистовПоПодразделениямКомпании();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДоступностьПрайсЛистовПоПодразделениямКомпании.ПрайсЛист
	|ИЗ
	|	ДоступностьПрайсЛистовПоПодразделениямКомпании КАК ДоступностьПрайсЛистовПоПодразделениямКомпании
	|ГДЕ
	|	ДоступностьПрайсЛистовПоПодразделениямКомпании.ПодразделениеКомпании = &ПодразделениеКомпании
	|	И ДоступностьПрайсЛистовПоПодразделениямКомпании.Уровень В (&УровниДоступа)";
	
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		Запрос.Текст = Запрос.Текст + " И ДоступностьПрайсЛистовПоПодразделениямКомпании.ПрайсЛист.Владелец = &Владелец";
		
		Запрос.УстановитьПараметр("Владелец", Владелец);
	КонецЕсли;
	
	Если УровниДоступа = Неопределено Тогда
		УровниДоступа = Новый Массив;
		
		УровниДоступа.Добавить(Перечисления.УровниДоступностиПрайсЛистовКонтрагентов.ПрямыеЗаказы);
		УровниДоступа.Добавить(Перечисления.УровниДоступностиПрайсЛистовКонтрагентов.Редактирование);
	ИначеЕсли ТипЗнч(УровниДоступа) <> Тип("Массив") Тогда
		ВызватьИсключение НСтр("ru = 'Параметр уровни доступа должен иметь тип ""Массив""'");
	КонецЕсли;
	Запрос.УстановитьПараметр("УровниДоступа", УровниДоступа);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	МенеджерВременныхТаблиц.Закрыть();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПоУмолчаниюДоступныеПодразделенияПрайсЛистов()
	// сформируем таблицу предков
	Пролог = "ВЫБРАТЬ Родитель НачалоДуги, Ссылка КонецДуги ПОМЕСТИТЬ ЗамыканияДлины1 ИЗ Справочник.ПодразделенияКомпании
	| ГДЕ Родитель <> Значение(Справочник.ПодразделенияКомпании.ПустаяСсылка)
	| ОБЪЕДИНИТЬ ВЫБРАТЬ Ссылка, Ссылка ИЗ Справочник.ПодразделенияКомпании;";
	
	Рефрен = "ВЫБРАТЬ РАЗЛИЧНЫЕ ПерваяДуга.НачалоДуги, ВтораяДуга.КонецДуги ПОМЕСТИТЬ ЗамыканияДлины%2 ИЗ ЗамыканияДлины%1 КАК ПерваяДуга
	| ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗамыканияДлины%1 КАК ВтораяДуга ПО ПерваяДуга.КонецДуги = ВтораяДуга.НачалоДуги;
	| УНИЧТОЖИТЬ ЗамыканияДлины%1;";
	
	Эпилог = "ВЫБРАТЬ НачалоДуги Предок, КонецДуги Потомок ПОМЕСТИТЬ РодителиПодразделенийКомпании ИЗ ЗамыканияДлины%1 ГДЕ НачалоДуги <> КонецДуги;";
	
	МаксимальнаяДлинаЗамыканий = 1;
	ЗапросТекст = Пролог;
	
	Пока МаксимальнаяДлинаЗамыканий < 256 Цикл
		ЗапросТекст = ЗапросТекст + СтрШаблон(Рефрен, Формат(МаксимальнаяДлинаЗамыканий, "ЧГ=0"), Формат(2 * МаксимальнаяДлинаЗамыканий, "ЧГ=0"));
		//СтрЗаменить(СтрЗаменить(Рефрен, "#1", Формат(МаксимальнаяДлинаЗамыканий, "ЧГ=0")), "#2", Формат(2 * МаксимальнаяДлинаЗамыканий, "ЧГ=0"));
		
		МаксимальнаяДлинаЗамыканий = 2 * МаксимальнаяДлинаЗамыканий;
	КонецЦикла;
	ЗапросТекст = ЗапросТекст + СтрШаблон(Эпилог, Формат(МаксимальнаяДлинаЗамыканий, "ЧГ=0"));
	
	ЗапросТекст = ЗапросТекст +
	"ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.Ссылка КАК ПрайсЛист,
	|	ПрайсЛистыКонтрагентов.ОтветственноеПодразделение
	|ПОМЕСТИТЬ АктуальныеПрайЛисты
	|ИЗ
	|	Справочник.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|ГДЕ
	|	НЕ ПрайсЛистыКонтрагентов.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктуальныеПрайЛисты.ПрайсЛист КАК ПрайсЛист,
	|	РодителиПодразделенийКомпании.Потомок КАК ПодразделениеКомпании
	|ПОМЕСТИТЬ ПоУмолчаниюДоступныеПодразделения
	|ИЗ
	|	АктуальныеПрайЛисты КАК АктуальныеПрайЛисты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РодителиПодразделенийКомпании КАК РодителиПодразделенийКомпании
	|		ПО АктуальныеПрайЛисты.ОтветственноеПодразделение = РодителиПодразделенийКомпании.Предок
	|ГДЕ
	|	НЕ РодителиПодразделенийКомпании.Потомок ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АктуальныеПрайЛисты.ПрайсЛист,
	|	АктуальныеПрайЛисты.ОтветственноеПодразделение
	|ИЗ
	|	АктуальныеПрайЛисты КАК АктуальныеПрайЛисты;
	|
	|УНИЧТОЖИТЬ РодителиПодразделенийКомпании;";
	
	Возврат ЗапросТекст;
КонецФункции

#Область ПараметрыОбработкиРеквизитовОбъекта

Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции 

Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	УникальныеРеквизиты = Новый Структура();
	
	Возврат УникальныеРеквизиты;
	
КонецФункции 

#КонецОбласти

#КонецОбласти

#КонецЕсли