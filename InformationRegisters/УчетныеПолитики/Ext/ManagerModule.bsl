// Модуль менеджера регистра сведений "Учетные политики"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет начальными данными регистр "Учетные политики"
//
Процедура ВыполнитьНачальноеЗаполнение() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаСреза = Дата('19800101');
	
	// Заполним систему налогооблажения
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	УчетныеПолитикиСрезПоследних.Объект КАК Объект
	               |ПОМЕСТИТЬ УстановленныеПараметры
	               |ИЗ
	               |	РегистрСведений.УчетныеПолитики.СрезПоследних(&ДатаСреза, Параметр = ЗНАЧЕНИЕ(ПланВидовХарактеристик.УчетныеПолитики.СистемаНалогообложения)) КАК УчетныеПолитикиСрезПоследних
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	УчетныеПолитикиСрезПоследних.Объект
	               |ИЗ
	               |	РегистрСведений.УчетныеПолитики.СрезПоследних(&ДатаСреза, Параметр = ЗНАЧЕНИЕ(ПланВидовХарактеристик.УчетныеПолитики.ОбъектНалогообложения)) КАК УчетныеПолитикиСрезПоследних
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	УчетныеПолитикиСрезПоследних.Объект
	               |ИЗ
	               |	РегистрСведений.УчетныеПолитики.СрезПоследних(&ДатаСреза, Параметр = ЗНАЧЕНИЕ(ПланВидовХарактеристик.УчетныеПолитики.ВидНалога)) КАК УчетныеПолитикиСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Организации.Ссылка КАК Ссылка,
	               |	Организации.УдалитьОсвобожденОтНДС КАК УдалитьОсвобожденОтНДС
	               |ИЗ
	               |	Справочник.Организации КАК Организации
	               |		ЛЕВОЕ СОЕДИНЕНИЕ УстановленныеПараметры КАК УстановленныеПараметры
	               |		ПО Организации.Ссылка = УстановленныеПараметры.Объект
	               |ГДЕ
	               |	УстановленныеПараметры.Объект ЕСТЬ NULL
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПодразделенияКомпании.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
	               |		ЛЕВОЕ СОЕДИНЕНИЕ УстановленныеПараметры КАК УстановленныеПараметры
	               |		ПО ПодразделенияКомпании.Ссылка = УстановленныеПараметры.Объект
	               |ГДЕ
	               |	УстановленныеПараметры.Объект ЕСТЬ NULL
	               |	И ПодразделенияКомпании.УдалитьОсвобожденОтНДС";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	
	// Заполним для организации
	Выборка = ПакетЗапроса[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СистемаНалогообложения = Перечисления.СистемыНалогообложения.ПустаяСсылка();
		ОбъектНалогообложения  = Перечисления.ВидыОбъектовНалогообложения.ПустаяСсылка();
		
		Если Выборка.УдалитьОсвобожденОтНДС Тогда
			СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная;
			ОбъектНалогообложения = Перечисления.ВидыОбъектовНалогообложения.Доходы;
		Иначе
			СистемаНалогообложения=Перечисления.СистемыНалогообложения.Общая;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.УчетныеПолитики.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
		НаборЗаписей.Прочитать();
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период = ДатаСреза;
		НоваяЗапись.Объект = Выборка.Ссылка;
		НоваяЗапись.Параметр = ПланыВидовХарактеристик.УчетныеПолитики.СистемаНалогообложения;
		НоваяЗапись.Значение = СистемаНалогообложения;
		
		Если ЗначениеЗаполнено(ОбъектНалогообложения) Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Период = ДатаСреза;
			НоваяЗапись.Объект = Выборка.Ссылка;
			НоваяЗапись.Параметр = ПланыВидовХарактеристик.УчетныеПолитики.ОбъектНалогообложения;
			НоваяЗапись.Значение = ОбъектНалогообложения;
		КонецЕсли;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
				НСтр("ru = 'Не удалось записать систему налогоблажения организации %1 по причине: %2'"),
					Строка(Выборка.Ссылка),
					ОписаниеОшибки()));
		КонецПопытки;
		
	КонецЦикла;
	
	// Заполним для подразделения
	Выборка = ПакетЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.УчетныеПолитики.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
		НаборЗаписей.Прочитать();
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период = ДатаСреза;
		НоваяЗапись.Объект = Выборка.Ссылка;
		НоваяЗапись.Параметр = ПланыВидовХарактеристик.УчетныеПолитики.ВидНалога;
		НоваяЗапись.Значение = Перечисления.ВидыНалогов.ЕНВД;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
				НСтр("ru = 'Не удалось записать систему налогоблажения организации %1 по причине: %2'"),
					Строка(Выборка.Ссылка),
					ОписаниеОшибки()));

		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры // ВыполнитьНачальноеЗаполнение()

Процедура ЗаполнитьНалогообложениеНДСприУСН() Экспорт 
	
	УстановитьПривилегированныйРежим(Истина); 
	
	ТекущаяДата = НачалоМесяца(ТекущаяДатаСеанса());	
	ДатаНачала = Дата(2025,1,1);	
	ДатаСреза = ?(ТекущаяДата> ДатаНачала, ТекущаяДата, ДатаНачала);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОрганизацииУСН.Объект КАК Ссылка,
		|	ЕСТЬNULL(ОрганизацииНДС.Значение, ЗНАЧЕНИЕ(Перечисление.НалогообложениеНДСПриУСН.ПустаяСсылка)) КАК НалогообложениеНДС
		|ИЗ
		|	РегистрСведений.УчетныеПолитики.СрезПоследних(&ДатаСреза, Значение = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)) КАК ОрганизацииУСН
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетныеПолитики.СрезПоследних(&ДатаСреза, Значение = ЗНАЧЕНИЕ(Перечисление.НалогообложениеНДСПриУСН.ПустаяСсылка)) КАК ОрганизацииНДС
		|		ПО ОрганизацииУСН.Объект = ОрганизацииНДС.Объект";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если  Выборка.НалогообложениеНДС = Перечисления.НалогообложениеНДСПриУСН.ПустаяСсылка() Тогда
			
			НаборЗаписей = РегистрыСведений.УчетныеПолитики.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
			НаборЗаписей.Прочитать();
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Период = ДатаСреза;
			НоваяЗапись.Объект = Выборка.Ссылка;
			НоваяЗапись.Параметр = ПланыВидовХарактеристик.УчетныеПолитики.НалогообложениеНДСПриУСН;
			НоваяЗапись.Значение = Перечисления.НалогообложениеНДСПриУСН.ПродажаНеОблагаетсяНДС;
			
			Попытка
				НаборЗаписей.Записать();
			Исключение
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
					НСтр("ru = 'Не удалось записать ""Налогообложение НДС при УСН"" организации %1 по причине: %2'"),
						Строка(Выборка.Ссылка),
						ОписаниеОшибки()));

			КонецПопытки;
					
		КонецЕсли;
		
	КонецЦикла;
 	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПараметрыОбработкиРеквизитовОбъекта

Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	ОбязательныеРеквизиты.Добавить("Параметр");
	ОбязательныеРеквизиты.Добавить("Значение");
		
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	УникальныеРеквизиты = Новый Структура();
	
	Возврат УникальныеРеквизиты;
	
КонецФункции 

#КонецОбласти

#КонецОбласти

#КонецЕсли