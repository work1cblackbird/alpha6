///////////////////////////////////////////////////////////////////////////////
// Модуль формы записи регистра "ТоварыВОборудовании"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	
	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	НастроитьПараметрыВыбораЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаТоваровПриИзменении(Элемент)
	
	ГруппаТоваровПриИзмененииНаСервере();
	
КонецПроцедуры  

&НаСервере
Процедура ГруппаТоваровПриИзмененииНаСервере()
	
	УстановитьПараметрыВыбораНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомРегистраСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	УстановитьПараметрыВыбораНоменклатуры()
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомРегистраСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораНоменклатуры();
	
	МассивОграничений = Новый Массив;
	ИмяПараметра = "Отбор.ТипНоменклатуры";
	
	Если Запись.ГруппаТоваров.РежимЗаполнения = 3 Тогда
		
		МассивОграничений = Запись.ГруппаТоваров.ОграниченияЗаполненияГруппы.ВыгрузитьКолонку("ТипГруппаНоменклатуры");
		
	ИначеЕсли Запись.ГруппаТоваров.РежимЗаполнения = 4 Тогда
		
		МассивОграничений = ПодчиненныеГруппыНоменклатуры(
			Запись.ГруппаТоваров.ОграниченияЗаполненияГруппы.ВыгрузитьКолонку("ТипГруппаНоменклатуры")
		);
		
	ИначеЕсли Запись.ГруппаТоваров.РежимЗаполнения = 2 Тогда
		
		МассивОграничений.Добавить(Справочники.ТипыНоменклатуры.Услуга);
		МассивОграничений.Добавить(Справочники.ТипыНоменклатуры.Весовой);
		
	ИначеЕсли Запись.ГруппаТоваров.РежимЗаполнения = 1 Тогда
		
		МассивОграничений.Добавить(Справочники.ТипыНоменклатуры.Весовой);
		
	КонецЕсли;
	
	Ограничения = Новый ФиксированныйМассив(МассивОграничений);
	НовыйПараметр = Новый ПараметрВыбора(ИмяПараметра, Ограничения);
	
	Элементы.Номенклатура.ПараметрыВыбора = Новый ФиксированныйМассив(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НовыйПараметр)
	);
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПодчиненныеГруппыНоменклатуры(ГруппыНоменклатуры)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГруппыНоменклатуры", ГруппыНоменклатуры);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЭтоГруппа
	|	И Номенклатура.Ссылка В ИЕРАРХИИ(&ГруппыНоменклатуры)";
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, ГруппыНоменклатуры, Истина);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

