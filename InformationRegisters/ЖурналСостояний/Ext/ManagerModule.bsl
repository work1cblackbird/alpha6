
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Добавляет новую запись в журнал состояний
Функция ЗаписьВЖурналСостояний(ОбъектСсылка,Состояние=Неопределено,ТекДата = Неопределено, ПротоколУстановки="") Экспорт
	
	НовоеСостояние=Состояние;
	Если НовоеСостояние=Неопределено Тогда
		НовоеСостояние=ОбъектСсылка.Состояние;
	КонецЕсли;
	
	ДобавитьЗаписьВЖурнал=Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ЗначениеЗаполнено(ТекДата) Тогда
		ТекДата=ТекущаяДатаСеанса();
	КонецЕсли;
	ПоследнееСостояние=РегистрыСведений.ЖурналСостояний.СрезПоследних(ТекДата,Новый Структура("Объект",ОбъектСсылка));
	Если ПоследнееСостояние.Количество()=0 Тогда
		ДобавитьЗаписьВЖурнал=Истина;
	Иначе
		Если ПоследнееСостояние[0].Состояние<>НовоеСостояние Тогда
			ДобавитьЗаписьВЖурнал=Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ДобавитьЗаписьВЖурнал Тогда
		НоваяЗапись=РегистрыСведений.ЖурналСостояний.СоздатьМенеджерЗаписи();
		НоваяЗапись.Период=ТекДата;
		НоваяЗапись.Объект=ОбъектСсылка;
		НоваяЗапись.Состояние=НовоеСостояние;
		НоваяЗапись.Автор=Пользователи.ТекущийПользователь();
		НоваяЗапись.Компьютер=ПараметрыСеанса.РабочееМестоКлиента;
		НоваяЗапись.ПротоколУстановки=ПротоколУстановки;
		Попытка
			НоваяЗапись.Записать(Истина);
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru='Ошибка записи в журнал изменения состояния: %1'"),
					ОписаниеОшибки()
				)
			);
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции // ЗаписьВЖурналСостояний() 

#Область ПараметрыОбработкиРеквизитовОбъекта

Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции 

Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	УникальныеРеквизиты = Новый Структура();
	
	Возврат УникальныеРеквизиты;
	
КонецФункции 

#КонецОбласти

Функция ПолучитьСостояниеОбъекта(ОбъектСсылка, ТекДата = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТекДата) Тогда
		ТекДата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ПоследнееСостояние = РегистрыСведений.ЖурналСостояний.СрезПоследних(ТекДата, Новый Структура("Объект", ОбъектСсылка));
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ПоследнееСостояние.Количество() = 0 Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат ПоследнееСостояние[0].Состояние;
	
КонецФункции

#КонецОбласти

#КонецЕсли