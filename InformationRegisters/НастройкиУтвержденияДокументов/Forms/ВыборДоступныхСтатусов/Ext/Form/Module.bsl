///////////////////////////////////////////////////////////////////////////////
// Модуль формы регистра "Выбор доступных статусов"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполняем статусы из параметров.
	ДоступныеСтатусы = Неопределено;
	Если Параметры.Свойство("ДоступныеСтатусы", ДоступныеСтатусы) И ДоступныеСтатусы <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДоступныеСтатусы);
	КонецЕсли;
	
	// Заполняем ДокументСиноним из параметров, устанавливаем по нему заголовок.
	ДокументСиноним = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ДокументСиноним", "");
	Заголовок       = ?(ЗначениеЗаполнено(ДокументСиноним), ДокументСиноним, "<" + НСтр("ru = 'множественный выбор'") +">");
	
	// Всегда активируем статус "Подготовлен";
	Подготовлен = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		// Формируем описание обработчика перехвата закрытия формы
		ОбработчикВопроса = Новый ОписаниеОповещения("ОбработкаЗакрытияОкна", ЭтотОбъект);
		
		// Формируем текст вопроса
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить данные?'");
		
		// Получаем подтверждение операции от пользователя
		ПоказатьВопрос(ОбработчикВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытием()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФлагСтатусаПриИзменении(Элемент)
	
	Отклонен = Согласован ИЛИ Утвержден;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда = Неопределено, Отказ = Ложь)
	
	Модифицированность = Ложь;
	
	// Формируем структуру результата работы формы.
	ДоступныеСтатусы = Новый СписокЗначений;
	Для каждого ТекСтатус Из Элементы.ДоступныеСтатусы.ПодчиненныеЭлементы Цикл
		
		Если ЭтотОбъект[ТекСтатус.Имя] Тогда
			ДоступныеСтатусы.Добавить(ТекСтатус.Имя);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыЗакрытия = Новый Структура("ДоступныеСтатусы", ДоступныеСтатусы);
	
	// Закрываем форму и возвращаем параметры.
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСнятьВсе(Команда)
	
	Для каждого ТекСтатус Из Элементы.ДоступныеСтатусы.ПодчиненныеЭлементы Цикл
		
		Если НЕ ТекСтатус.Имя = "Подготовлен" Тогда
			
			ЭтотОбъект[ТекСтатус.Имя] = СтрНайти(Команда.Имя, "Установить") > 0;
		КонецЕсли;
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаЗакрытияОкна(РезультатОтвета, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатОтвета = КодВозвратаДиалога.Да Тогда
		Применить();
	Иначе
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗакрытияОкна()

#КонецОбласти

