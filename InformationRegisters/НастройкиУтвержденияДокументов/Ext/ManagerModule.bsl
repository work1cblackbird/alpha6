
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура сверяет список метаданных со списком регистра и удаляет или добавляет необходимые записи.
//
Процедура ВыполнитьНачальноеЗаполнение() Экспорт
	
	// сформирует текущую таблицу метаданных конфигурации
	ТаблицаМетаданных = Новый ТаблицаЗначений;
	ТаблицаМетаданных.Колонки.Добавить("Объект",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(150)));
	ТаблицаМетаданных.Колонки.Добавить("ЕстьРегламентированныйУчет",Новый ОписаниеТипов("Булево"));
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		
		ЭлементУтверждения = Метаданные.ОбщиеРеквизиты.СтатусУтверждения.Состав.Найти(Документ.Имя);
		
		Если ЭлементУтверждения.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать Тогда
			НоваяСтрока        = ТаблицаМетаданных.Добавить();
			НоваяСтрока.Объект = Документ.ПолноеИмя();
		КонецЕсли;
		
	КонецЦикла;
	
	// Дозаполним по группе доступа
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаМетаданных", ТаблицаМетаданных);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УтверждениеДокументов.Объект КАК Объект
	|ПОМЕСТИТЬ ТаблицаМетаданных
	|ИЗ
	|	&ТаблицаМетаданных КАК УтверждениеДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УтверждениеДокументов.Документ КАК Документ,
	|	УтверждениеДокументов.Объект КАК Объект,
	|	УтверждениеДокументов.Согласован,
	|	УтверждениеДокументов.Утвержден
	|ИЗ
	|	РегистрСведений.НастройкиУтвержденияДокументов КАК УтверждениеДокументов
	|ГДЕ
	|	УтверждениеДокументов.Документ В
	|			(ВЫБРАТЬ
	|				ТаблицаМетаданных.Объект
	|			ИЗ
	|				ТаблицаМетаданных КАК ТаблицаМетаданных)
	|ИТОГИ ПО
	|	Объект";
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		ГруппаПравИНастроек = ГруппаНастройкиПользователя(Выборка.Объект);
		
		Если НЕ ЗначениеЗаполнено(ГруппаПравИНастроек) Тогда
			Продолжить;
		КонецЕсли;
		
		ДетальныеЗаписи = Выборка.Выбрать();
		НаборЗаписей = РегистрыСведений.НастройкиУтвержденияДокументов.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.Объект.Установить(ГруппаПравИНастроек);
		Пока ДетальныеЗаписи.Следующий() Цикл 
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Объект   = ГруппаПравИНастроек; 
			НоваяЗапись.Документ = ДетальныеЗаписи.Документ;
			НоваяЗапись.Согласован = ДетальныеЗаписи.Согласован; 
			НоваяЗапись.Утвержден = ДетальныеЗаписи.Утвержден;
		КонецЦикла;
		НаборЗаписей.Записать();
	КонецЦикла;
	
	// получим расхождения
	ЗапросРасхождения = Новый Запрос;
	ЗапросРасхождения.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗапросРасхождения.УстановитьПараметр("ТаблицаМетаданных", ТаблицаМетаданных);
	ЗапросРасхождения.Текст = 
	"ВЫБРАТЬ
	|	УтверждениеДокументов.Объект КАК Объект
	|ПОМЕСТИТЬ ТаблицаМетаданных
	|ИЗ
	|	&ТаблицаМетаданных КАК УтверждениеДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УтверждениеДокументов.Документ КАК Документ,
	|	УтверждениеДокументов.Объект
	|ИЗ
	|	РегистрСведений.НастройкиУтвержденияДокументов КАК УтверждениеДокументов
	|ГДЕ
	|	НЕ УтверждениеДокументов.Документ В
	|				(ВЫБРАТЬ
	|					ТаблицаМетаданных.Объект
	|				ИЗ
	|					ТаблицаМетаданных КАК ТаблицаМетаданных)";
	РезультатЗапроса = ЗапросРасхождения.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// удаляем или добавляем записи
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// удалим запись
		Менеджер = РегистрыСведений.НастройкиУтвержденияДокументов.СоздатьМенеджерЗаписи();
		Менеджер.Документ = Выборка.Документ;
		Менеджер.Объект = Выборка.Объект;
		Менеджер.Удалить();
		
	КонецЦикла;
	
КонецПроцедуры

#Область ПараметрыОбработкиРеквизитовОбъекта

Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции 

Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	УникальныеРеквизиты = Новый Структура();
	
	Возврат УникальныеРеквизиты;
	
КонецФункции 

#КонецОбласти

#КонецОбласти

#КонецЕсли