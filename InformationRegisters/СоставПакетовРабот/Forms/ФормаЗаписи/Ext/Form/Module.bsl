///////////////////////////////////////////////////////////////////////////////
// Модуль формы записи регистра "СоставПакетовРабот"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
			
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	
	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьСпискиВыбора();
	
	НастроитьПараметрыВыбораЭлементовФормы();

	УправлениеДиалогомНаСервере();
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ЗаказНарядПриИзмененииНаСервере()
	
	ЗаполнитьСпискиВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказНарядПриИзменении(Элемент)
	
	ЗаказНарядПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСпискиВыбора()
	Элементы.Авторабота.СписокВыбора.Очистить();
	Элементы.ПакетРабот.СписокВыбора.Очистить();
	
	Если НЕ Запись.ЗаказНаряд.Пустая() Тогда
		Для Каждого Авторабота Из Запись.ЗаказНаряд.Автоработы Цикл
			Элементы.Авторабота.СписокВыбора.Добавить(Авторабота.ИдентификаторРаботы, Авторабота.Авторабота);
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставПакетовРабот.ПакетРабот,
		|	ЕСТЬNULL(ИнформацияОПакетахАвторабот.Представление, СоставПакетовРабот.ПакетРабот) КАК Представление
		|ИЗ
		|	РегистрСведений.СоставПакетовРабот КАК СоставПакетовРабот
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияОПакетахАвторабот КАК ИнформацияОПакетахАвторабот
		|		ПО СоставПакетовРабот.ПакетРабот = ИнформацияОПакетахАвторабот.ПакетРабот
		|ГДЕ
		|	СоставПакетовРабот.ЗаказНаряд = &ЗаказНаряд
		|
		|СГРУППИРОВАТЬ ПО
		|	СоставПакетовРабот.ПакетРабот,
		|	ИнформацияОПакетахАвторабот.Представление";
		Запрос.УстановитьПараметр("ЗаказНаряд", Запись.ЗаказНаряд);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ЕстьПустой = Ложь;
		Пока Выборка.Следующий() Цикл
			Если РаботаСПакетамиРаботКлиентСервер.ЭтоПустойПакетРабот(Выборка.ПакетРабот) Тогда
				ЕстьПустой = Истина;
			КонецЕсли;
			Элементы.ПакетРабот.СписокВыбора.Добавить(Выборка.ПакетРабот, Выборка.Представление);
		КонецЦикла;
		
		Если НЕ ЕстьПустой Тогда
			ИнформацияПоПустомуПакету = РаботаСПакетамиРаботПовтИсп.ИнформацияПоПустомуПакету();
			Элементы.ПакетРабот.СписокВыбора.Добавить(
				ИнформацияПоПустомуПакету.ПакетРабот,
				ИнформацияПоПустомуПакету.Представление);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомРегистраСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры 

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомРегистраСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	
КонецПроцедуры 

#КонецОбласти

#КонецОбласти

