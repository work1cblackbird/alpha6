///////////////////////////////////////////////////////////////////////////////
// Модуль формы записи регистра "Настройки передачи товаров между организациями"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	
	Если Отказ Тогда
		
		Возврат;
		
	ИначеЕсли Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для настройки передачи товаров между организациями (требуются полные права).'");
		
	ИначеЕсли ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		// Требуется, чтобы период был завтрашний день
		ВызватьИсключение НСтр("ru = 'Создайте новую настройку вручную (без копирования)'");
		
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		Элементы.Период.ТолькоПросмотр = Истина;
		
		СекундВДне = 86400;
		Запись.Период = ТекущаяДатаСеанса() + СекундВДне;
		Запись.СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.Продажа;
		
	Иначе
		
		ИзменитьДоступностьБлокируемыхРеквизитов(Ложь);
		
	КонецЕсли;
		
	НастроитьПараметрыВыбораЭлементовФормы();
	
	УправлениеДиалогомНаСервере();
	
	ИзменитьДоступностьНеНужныхРеквизитовДляСпособаПередачиНеПередается();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ИзменитьДоступностьБлокируемыхРеквизитов(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазрешитьРедактированияРеквизитов(Команда)
	
	ЗаголовокДиалога = НСтр("ru = 'Разрешение редактирования реквизитов'");
	
	БлокируемыеРеквизиты = ПолучитьБлокируемыеРеквизиты();
	
	РеквизитыПредставление = "";
	Для Каждого БлокируемыйРеквизит Из БлокируемыеРеквизиты Цикл
		РеквизитыПредставление = РеквизитыПредставление + БлокируемыйРеквизит + "," + Символы.ПС;
	КонецЦикла;
	РеквизитыПредставление = Лев(РеквизитыПредставление, СтрДлина(РеквизитыПредставление) - 2);
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для того чтобы не допустить рассогласования данных в программе,
					   |а также блокировку проведения старых документов
					   |следующие реквизиты не доступны для редактирования:
					   |%1.
					   |
					   |Перед тем, как разрешить их редактирование, рекомендуется оценить последствия,
					   |вручную проверив движения по регистру <Товары организаций к передаче>'"),
			РеквизитыПредставление);
			
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Разрешить'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена'"));
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ПроверитьСсылкиНаОбъектПослеПодтвержденияПроверки",
			ЭтотОбъект, Параметры),
		ТекстВопроса, Кнопки, , КодВозвратаДиалога.Да, ЗаголовокДиалога);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособПередачиТоваровПриИзменении(Элемент)
	
	ИзменитьДоступностьНеНужныхРеквизитовДляСпособаПередачиНеПередается();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтправительПриИзменении(Элемент)
	
	РеквизитыОрганизацииВладельца = КонтрагентИнтеркампаниОрганизации(Запись.ОрганизацияОтправитель);
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОрганизацииВладельца.КонтрагентИнтеркампани) Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'У выбранной организации не указан <Контрагент интеркампани>'"));
		Запись.ОрганизацияОтправитель = Неопределено;
		Возврат;
	КонецЕсли;

	Если Запись.КонтрагентПоставки <> РеквизитыОрганизацииВладельца.КонтрагентИнтеркампани Тогда
		Запись.КонтрагентПоставки = РеквизитыОрганизацииВладельца.КонтрагентИнтеркампани;
		Запись.ДоговорВзаиморасчетовПоставки = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПолучательПриИзменении(Элемент)
	
	РеквизитыОрганизацииПродавца = КонтрагентИнтеркампаниОрганизации(Запись.ОрганизацияПолучатель);
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОрганизацииПродавца.КонтрагентИнтеркампани) Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'У выбранной организации не указан <Контрагент интеркампани>'"));
		Запись.ОрганизацияПолучатель = Неопределено;
		Возврат;
	КонецЕсли;
	
	Если Запись.Контрагент <> РеквизитыОрганизацииПродавца.КонтрагентИнтеркампани Тогда
		Запись.Контрагент = РеквизитыОрганизацииПродавца.КонтрагентИнтеркампани;
		Запись.ДоговорВзаиморасчетов = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция КонтрагентИнтеркампаниОрганизации(ОрганизацияСсылка)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОрганизацияСсылка, "КонтрагентИнтеркампани");
	
КонецФункции

&НаКлиенте
Процедура ПроверитьСсылкиНаОбъектПослеПодтвержденияПроверки(Ответ, Параметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьПредупреждение(, НСтр("ru = 'Редактирование реквизитов разрешено'"));
	
	Элементы.РазрешитьРедактированияРеквизитов.Доступность = Ложь;
	ИзменитьДоступностьБлокируемыхРеквизитов(Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВидыДоговоровПоХозОперации(ХозОперация)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ХозОперацииВидыДоговоров.ВидДоговора
		|ИЗ
		|	Справочник.ХозОперации.ВидыДоговоров КАК ХозОперацииВидыДоговоров
		|ГДЕ
		|	ХозОперацииВидыДоговоров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ХозОперация);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидДоговора");
	
КонецФункции

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		УправлениеДиалогомРегистраСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	КонецЕсли;
	
	ВидыДоговоровПродажи = ПолучитьВидыДоговоровПоХозОперации(Справочники.ХозОперации.ПередачаТоваровМеждуОрганизациями);
	УправлениеДиалогомСервер.ОбновитьПараметрВыбора(Элементы.ДоговорВзаиморасчетов.ПараметрыВыбора, "Отбор.ВидДоговора", ВидыДоговоровПродажи);
	
	ВидыДоговоровПокупки = Новый Массив;
	ВидыДоговоровПокупки.Добавить(Перечисления.ВидыДоговоров.Покупка);
	ВидыДоговоровПокупки.Добавить(Перечисления.ВидыДоговоров.Прочее);
	УправлениеДиалогомСервер.ОбновитьПараметрВыбора(Элементы.ДоговорВзаиморасчетовПоставки.ПараметрыВыбора, "Отбор.ВидДоговора", ВидыДоговоровПокупки);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		УправлениеДиалогомРегистраСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьДоступностьБлокируемыхРеквизитов(ДоступностьРеквизитов)
	
	БлокируемыеРеквизиты = ПолучитьБлокируемыеРеквизиты();
	
	Для Каждого БлокируемыйРеквизит Из БлокируемыеРеквизиты Цикл
	
		Элементы[БлокируемыйРеквизит].ТолькоПросмотр = НЕ ДоступностьРеквизитов;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьДоступностьНеНужныхРеквизитовДляСпособаПередачиНеПередается()
	
	ДоступностьРеквизитов = Запись.СпособПередачиТоваров <> Перечисления.СпособыПередачиТоваров.НеПередается;
	
	НеНужныеРеквизиты = РегистрыСведений.НастройкиПередачиТоваровМеждуОрганизациями.ПолучитьНеНужныеРеквизитыДляСпособаПередачиНеПередается();
	
	Для Каждого НеНужныйРеквизит Из НеНужныеРеквизиты Цикл
	
		Элементы[НеНужныйРеквизит].Доступность = ДоступностьРеквизитов;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьБлокируемыеРеквизиты()
	
	Возврат РегистрыСведений.НастройкиПередачиТоваровМеждуОрганизациями.ПолучитьБлокируемыеРеквизиты();
	
КонецФункции

#КонецОбласти

#КонецОбласти
