///////////////////////////////////////////////////////////////////////////////
// Модуль формы записи регистра "Замены"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	ЕстьПроизводительНоменклатуры = Ложь;
	Если Параметры.Свойство("ДополнительныеПараметры") Тогда
		Если Параметры.ДополнительныеПараметры.Свойство("Производитель") Тогда
			ПроизводительНоменклатуры = ПолучитьЗначениеПараметраСтруктуры(
				Параметры.ДополнительныеПараметры,
				"Производитель");
			ИмяПроизводителяНоменклатуры = ПолучитьЗначениеПараметраСтруктуры(
				Параметры.ДополнительныеПараметры,
				"ИмяПроизводителя");
			ЕстьПроизводительНоменклатуры = Истина;
		КонецЕсли;
		
		Если Параметры.ДополнительныеПараметры.Свойство("ДатаОбновления") Тогда
			Запись.ДатаОбновления = Параметры.ДополнительныеПараметры.ДатаОбновления;
		КонецЕсли;
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	АртикулПриИзменении(Неопределено);
	АртикулЗаменыПриИзменении(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ПараметрыЗаписи.Свойство("НеВыполнятьПроверкуПроизводителя")
		И ЕстьПроизводительНоменклатуры
		И ПроизводительНоменклатуры <> Запись[ИмяПроизводителяНоменклатуры]
	Тогда
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
		ОбработчикОповещения = Новый ОписаниеОповещения("Подключаемый_ЗаписьЗамены", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(
			ОбработчикОповещения,
			СтрШаблон(
				Нстр("ru = 'Указанный производитель <%1> отличается от производителя номенклатуры <%2>. Продолжить?'"),
					Запись[ИмяПроизводителяНоменклатуры], ПроизводительНоменклатуры),
			РежимДиалогаВопрос.ДаНет,,,
			Нстр("ru = 'Запись замены'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура АртикулОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Номенклатура") Тогда
		ДанныеСсылки = ПолучитьДанныеСсылкиНаНоменклатуру(ВыбранноеЗначение);
		
		Запись.Артикул          = ДанныеСсылки.Артикул;
		Запись.АртикулДляПоиска = ДанныеСсылки.АртикулДляПоиска;
		Запись.Производитель    = ДанныеСсылки.Производитель;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АртикулЗаменыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Номенклатура") Тогда
		ДанныеСсылки = ПолучитьДанныеСсылкиНаНоменклатуру(ВыбранноеЗначение);
		
		Запись.АртикулЗамены          = ДанныеСсылки.Артикул;
		Запись.АртикулЗаменыДляПоиска = ДанныеСсылки.АртикулДляПоиска;
		Запись.ПроизводительЗамены    = ДанныеСсылки.Производитель;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АртикулАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ВыборГруппИЭлементов" , ПредопределенноеЗначение("ИспользованиеГруппИЭлементов.Элементы"));
	ПараметрыПодбора.Вставить("СтрокаПоиска"         , Текст);
	
	ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.Номенклатура"), ПараметрыПодбора);
	
КонецПроцедуры

&НаКлиенте
Процедура АртикулПриИзменении(Элемент)
	
	Запись.АртикулДляПоиска = ОбщегоНазначенияАвтосалонКлиентСервер.ВАртикулДляПоиска(Запись.Артикул);
	
КонецПроцедуры

&НаКлиенте
Процедура АртикулЗаменыПриИзменении(Элемент)
	
	Запись.АртикулЗаменыДляПоиска = ОбщегоНазначенияАвтосалонКлиентСервер.ВАртикулДляПоиска(Запись.АртикулЗамены);
	
КонецПроцедуры

&НаКлиенте
Процедура АртикулЗаменыАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ВыборГруппИЭлементов" , ПредопределенноеЗначение("ИспользованиеГруппИЭлементов.Элементы"));
	ПараметрыПодбора.Вставить("СтрокаПоиска"         , Текст);
	
	ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.Номенклатура"), ПараметрыПодбора);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АртикулНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	ОповещениеРезультата = Новый ОписаниеОповещения("ОбработкаРезультатаОповещенияВыборНоменклатуры", ЭтотОбъект, Новый Структура("Артикул", Элемент.Имя));
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, ЭтотОбъект,,,, ОповещениеРезультата, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеСсылкиНаНоменклатуру(СсылкаНаНоменклатуру)
	
	СтруктураНоменклатуры = Новый Структура;
	
	СтруктураНоменклатуры.Вставить("Артикул", СсылкаНаНоменклатуру.Артикул);
	СтруктураНоменклатуры.Вставить("АртикулДляПоиска" , СсылкаНаНоменклатуру.АртикулДляПоиска);
	СтруктураНоменклатуры.Вставить("Производитель", СсылкаНаНоменклатуру.Производитель);
	
	Возврат СтруктураНоменклатуры;
	
КонецФункции 

&НаКлиенте
Процедура ОбработкаРезультатаОповещенияВыборНоменклатуры(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если РезультатОповещения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	// Получим данные выбранной номенклатуры
	Структура = ПолучитьДанныеСсылкиНаНоменклатуру(РезультатОповещения);
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") И ДополнительныеПараметры.Свойство("Артикул") Тогда
		Запись[ДополнительныеПараметры.Артикул] = Структура.Артикул;
		Если ДополнительныеПараметры.Артикул = "Артикул" Тогда
			Запись.АртикулДляПоиска = Структура.АртикулДляПоиска;
			Запись.Производитель = Структура.Производитель;
		Иначе
			Запись.АртикулЗаменыДляПоиска = Структура.АртикулДляПоиска;
			Запись.ПроизводительЗамены = Структура.Производитель;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ЗаписьЗамены(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если РезультатОповещения = КодВозвратаДиалога.Да Тогда
		ПараметрыЗаписи = ПолучитьЗначениеПараметраСтруктуры(ДополнительныеПараметры, "ПараметрыЗаписи", Новый Структура);
		ПараметрыЗаписи.Вставить("НеВыполнятьПроверкуПроизводителя");
		
		Если Записать(ПараметрыЗаписи) Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомРегистраСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры 

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомРегистраСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	
КонецПроцедуры 

#КонецОбласти

#КонецОбласти

