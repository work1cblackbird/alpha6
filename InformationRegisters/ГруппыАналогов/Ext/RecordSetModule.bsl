
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выполняет объединение групп аналогов в единую группу.
//
// Параметры:
//  ГлавнаяГруппа        - Строка - Уникальный идентификатор поглощающей группы;
//  ПрисоединяемаяГруппа - Строка - Уникальный идентификатор поглощаемой группы;
//
Процедура ОбъединитьГруппы(ГлавнаяГруппа, ПрисоединяемаяГруппа) Экспорт
	
	// Получим элементы основной группы
	НаборЗаписейГлавнойГруппы = РегистрыСведений.ГруппыАналогов.СоздатьНаборЗаписей();
	НаборЗаписейГлавнойГруппы.Отбор.ИдентификаторГруппы.Значение      = ГлавнаяГруппа;
	НаборЗаписейГлавнойГруппы.Отбор.ИдентификаторГруппы.Использование = Истина;
	НаборЗаписейГлавнойГруппы.Прочитать();
	тзНаборЗаписейГлавнойГруппы = НаборЗаписейГлавнойГруппы.Выгрузить();
	
	// Создадим набор записей присоединяемой
	НаборЗаписейПрисоединяемойГруппы = РегистрыСведений.ГруппыАналогов.СоздатьНаборЗаписей();
	НаборЗаписейПрисоединяемойГруппы.Отбор.ИдентификаторГруппы.Значение      = ПрисоединяемаяГруппа;
	НаборЗаписейПрисоединяемойГруппы.Отбор.ИдентификаторГруппы.Использование = Истина;
	НаборЗаписейПрисоединяемойГруппы.Прочитать();
	
	// Будем просматривать присоединяемую группу и переливать ее в главную
	Пока НаборЗаписейПрисоединяемойГруппы.Количество() > 0 Цикл
		
		Если тзНаборЗаписейГлавнойГруппы.Найти(НаборЗаписейПрисоединяемойГруппы[0].Артикул,"Артикул") = Неопределено Тогда
			
			НоваяЗапись = НаборЗаписейГлавнойГруппы.Добавить();
			НоваяЗапись.ИдентификаторГруппы = ГлавнаяГруппа;
			ЗаполнитьЗначенияСвойств(
				НоваяЗапись,
				НаборЗаписейПрисоединяемойГруппы[0],
				"Артикул,Производитель,Наименование,ГлавныйПоГруппе,ГлавныйПоПроизводителю"
			);
			
		КонецЕсли;
		
		НаборЗаписейПрисоединяемойГруппы.Удалить(НаборЗаписейПрисоединяемойГруппы[0]);
		
	КонецЦикла;
	
	НаборЗаписейПрисоединяемойГруппы.Записать();
	НаборЗаписейГлавнойГруппы.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтотОбъект.Количество() > 0 И ДанныеЗаполнения <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект[0], ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ОбработкаСобытийРегистраСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ЭтотОбъект[0].АртикулДляПоиска <> Отбор.АртикулДляПоиска.Значение
		ИЛИ ЭтотОбъект[0].ИдентификаторГруппы <> Отбор.ИдентификаторГруппы.Значение
		ИЛИ ЭтотОбъект[0].Производитель <> Отбор.Производитель.Значение Тогда
		
		РегистрыСведенийГруппыАналогов = РегистрыСведений.ГруппыАналогов.СоздатьНаборЗаписей();
		РегистрыСведенийГруппыАналогов.Отбор.ИдентификаторГруппы.Установить(ЭтотОбъект[0].ИдентификаторГруппы, Истина);
		РегистрыСведенийГруппыАналогов.Отбор.АртикулДляПоиска.Установить(ЭтотОбъект[0].АртикулДляПоиска, Истина);
		РегистрыСведенийГруппыАналогов.Отбор.Производитель.Установить(ЭтотОбъект[0].Производитель, Истина);
		РегистрыСведенийГруппыАналогов.Прочитать();
		
		Если РегистрыСведенийГруппыАналогов.Количество() > 0 Тогда
			
			РегистрыСведенийГруппыАналогов.Очистить();
			РегистрыСведенийГруппыАналогов.Записать(Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// АПК:75 - выкл

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
		
	ДатаИзменения = ТекущаяДатаСеанса();
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.Автор = Пользователи.ТекущийПользователь();
		Запись.ДатаИзменения = ДатаИзменения;
	КонецЦикла;
	
	Если ЭтотОбъект.Количество() = 0
		И ЭтотОбъект.Модифицированность() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыАналогов.ИдентификаторГруппы КАК ИдентификаторГруппы,
		|	ГруппыАналогов.АртикулДляПоиска КАК АртикулДляПоиска,
		|	ГруппыАналогов.Производитель КАК Производитель,
		|	ГруппыАналогов.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
		|ГДЕ
		|	ГруппыАналогов.ИдентификаторГруппы = &ИдентификаторГруппы";
		
		Запрос.УстановитьПараметр("ИдентификаторГруппы", Отбор.ИдентификаторГруппы.Значение);
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Количество() = 2 Тогда
			//Должен остаться отбор только по ИдентификаторГруппы
			Отбор.АртикулДляПоиска.Использование = Ложь;
			Отбор.Производитель.Использование = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// АПК:75 - вкл

#КонецОбласти

#КонецЕсли