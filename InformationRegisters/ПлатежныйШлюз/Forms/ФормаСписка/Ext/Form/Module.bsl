
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОткрытьСчет(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СчетНаОплату = ТекДанные.СчетНаОплату;
	ИмяФормыСчета = ПолучитьИмяФормыСчета(СчетНаОплату);
	ПараметрыОткрытия = Новый Структура("Ключ", СчетНаОплату);
	ОткрытьФорму(ИмяФормыСчета, ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьОснование(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Основание = ТекДанные.Основание;
	ИмяФормыОснования = ПолучитьИмяФормыОснования(Основание);
	Если ИмяФормыОснования = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПараметрыОткрытия = Новый Структура("Ключ", Основание);
	ОткрытьФорму(ИмяФормыОснования, ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьЗаявку(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Заявка = ТекДанные.ЗаявкаНаРасходДС;
	Если Заявка = ПредопределенноеЗначение("Документ.ЗаявкаНаРасходДС.ПустаяСсылка") Тогда
		Возврат;
	КонецЕсли;
	ПараметрыОткрытия = Новый Структура("Ключ", Заявка);
	ОткрытьФорму("Документ.ЗаявкаНаРасходДС.ФормаОбъекта", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьЧек(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧекНаОплату = ТекДанные.ЧекНаОплату;
	Если ЧекНаОплату = ПредопределенноеЗначение("Документ.ЧекНаОплату.ПустаяСсылка") Тогда
		Возврат;
	КонецЕсли;
	ПараметрыОткрытия = Новый Структура("Ключ", ЧекНаОплату);
	ОткрытьФорму("Документ.ЧекНаОплату.ФормаОбъекта", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДеревоПодчиненных(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Документ", ТекДанные.СчетНаОплату);
	ОткрытьФорму("Обработка.ПодчиненныеДокументы.Форма", ПараметрыОткрытия);
	
КонецПроцедуры

&НаСервере
Процедура КомандаУточнитьСтатусНаСервере(СчетНаОплату)
	
	РезультатВыполнения = ОнлайнОплата.ПолучитьСтатусОплаты(СчетНаОплату);
	Для каждого ТекстСообщения Из РезультатВыполнения.ТекстыСообщений Цикл
		ВывестиСообщение(ТекстСообщения, ЭтотОбъект);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУточнитьСтатус(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КомандаУточнитьСтатусНаСервере(ТекДанные.СчетНаОплату);
	
	Элементы.Список.Обновить();
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаСервере
Процедура КомандаВыполнитьРегламентноеЗаданиеНаСервере()
	
	ОнлайнОплата.ЗапросСтатусовОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнитьРегламентноеЗадание(Команда)
	
	КомандаВыполнитьРегламентноеЗаданиеНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьИмяФормыСчета(СчетНаОплату)
	
	Если ТипЗнч(СчетНаОплату) = Тип("ДокументСсылка.СчетНаОплату") Тогда
		Возврат "Документ.СчетНаОплату.ФормаОбъекта";
	Иначе
		Возврат "Документ.СчетНаОплатуЗаАвтомобили.ФормаОбъекта";
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИмяФормыОснования(Основание)
	
	Если Основание = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		Возврат "Документ.ЗаказНаряд.ФормаОбъекта";
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		Возврат "Документ.ЗаказПокупателя.ФормаОбъекта";
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		Возврат "Документ.ЗаказНаАвтомобиль.ФормаОбъекта";
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
		Возврат "Документ.ЗаявкаНаХранениеШин.ФормаОбъекта";
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
		Возврат "Документ.РеализацияАвтомобилей.ФормаОбъекта";
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		Возврат "Документ.РеализацияТоваров.ФормаОбъекта";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
