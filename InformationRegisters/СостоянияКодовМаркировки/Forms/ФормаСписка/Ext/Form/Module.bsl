///////////////////////////////////////////////////////////////////////////////
// Модуль формы списка регистра "Состояния кодов маркировки."
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормой.НастроитьОсновнойДинамическийСписокФормы(ЭтотОбъект);
	
	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование =
		МенеджерОборудованияВызовСервераПереопределяемый.ИспользоватьПодключаемоеОборудование();
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ВводДоступен() И ИмяСобытия = "ScanData" Тогда
		
		Штрихкод = ШтрихкодированиеКлиент.ПолучитьШтрихкодИзПараметровОборудования(ИмяСобытия, Параметр);
		ПараметрыДействия = Новый Структура;
		ШтрихкодированиеКлиент.ОбработатьПолныйШтрихкод(Штрихкод, ПараметрыДействия);
		Штрихкод = ПараметрыДействия.Штрихкод;
		
		Если ЗначениеЗаполнено(Штрихкод) Тогда
		
			ПозицияСерийныйНомер = СтрНайти(Штрихкод, "(21)");
			ПозицияКод = СтрНайти(Штрихкод, "(01)");
			Если ПозицияКод <> 0 И ПозицияСерийныйНомер <> 0 Тогда
				Код = Сред(Штрихкод, 5, ПозицияСерийныйНомер - 5);
				СерийныйНомер = Прав(Штрихкод, СтрДлина(Штрихкод) - ПозицияСерийныйНомер - 3 );
				УстановитьОтбор("GTIN", Код);
				УстановитьОтбор("СерийныйНомер", СерийныйНомер);
			ИначеЕсли СтрДлина(Штрихкод) > 20 Тогда
				
				Код = Лев(Штрихкод, 14);
				Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Код)
					И МенеджерОборудованияКлиентСервер.РассчитатьКонтрольныйСимволGTIN14(Код) = Прав(Код, 1) Тогда
					СерийныйНомер = Сред(Штрихкод, 15, 7);
					УстановитьОтбор("GTIN", Код);
					УстановитьОтбор("СерийныйНомер", СерийныйНомер);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	// ОценкаПроизводительности
	Если Копирование Тогда
		
		ОценкаПроизводительностиКлиент.ЗамерВремени("КопированиеРегистраСведенийСостоянияКодовМаркировки");
		
	Иначе
		
		ОценкаПроизводительностиКлиент.ЗамерВремени("СозданиеРегистраСведенийСостоянияКодовМаркировки");
		
	КонецЕсли;
	// Конец ОценкаПроизводительности

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)

	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеЗаписиРегистраСведенийСостоянияКодовМаркировки");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтбор(ИмяПоля, ЗначениеОтбора)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		ИмяПоля,
		ЗначениеОтбора,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина
	);
	
КонецПроцедуры

#КонецОбласти
