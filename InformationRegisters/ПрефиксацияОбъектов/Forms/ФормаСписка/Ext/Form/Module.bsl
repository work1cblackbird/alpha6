///////////////////////////////////////////////////////////////////////////////
// Модуль формы списка регистра "Префиксация объектов"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормой.НастроитьОсновнойДинамическийСписокФормы(ЭтотОбъект);
		
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	// ОценкаПроизводительности
	Если Копирование Тогда
		
		ОценкаПроизводительностиКлиент.ЗамерВремени("КопированиеРегистраСведенийПрефиксацияОбъектов");
		
	Иначе
		
		ОценкаПроизводительностиКлиент.ЗамерВремени("СозданиеРегистраСведенийПрефиксацияОбъектов");
		
	КонецЕсли;
	// Конец ОценкаПроизводительности

КонецПроцедуры 

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеЗаписиРегистраСведенийПрефиксацияОбъектов");
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьЗначенияПоУмолчанию(Команда)

	Кнопки = Новый СписокЗначений;
	
	Кнопки.Добавить("УстановитьПоУмолчанию", НСтр("ru = 'Установить по умолчанию'"));
	Кнопки.Добавить("Отмена", НСтр("ru = 'Отмена'"));
	
	Оповещение = Новый ОписаниеОповещения("УстановитьПоУмолчанию", ЭтотОбъект);

	ПараметрыФормы = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ПараметрыФормы.КнопкаПоУмолчанию = "Отмена";
	ПараметрыФормы.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	ПараметрыФормы.Заголовок = НСтр("ru = 'Предупреждение'");

	ТекстВопроса = НСтр("ru = 'Изменения в настройке префиксации объектов будут сброшены в значение по умолчанию. Продолжить?'");
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, Кнопки, ПараметрыФормы);
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьПоУмолчанию(Результат, ДополнительныеПараметры) Экспорт
	Если Результат.Значение = "УстановитьПоУмолчанию" Тогда
		УстановитьЗначенияПоУмолчаниюНаСервере();
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры 

&НаСервере
Процедура УстановитьЗначенияПоУмолчаниюНаСервере()
	
	РегистрыСведений.ПрефиксацияОбъектов.ВосстановитьЗначенияПоУмолчанию();
	
КонецПроцедуры 

&НаКлиенте
Процедура СинхронизироватьСКонфигурацией(Команда)
	
	СинхронизироватьСКонфигурациейНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры 

&НаСервере
Процедура СинхронизироватьСКонфигурациейНаСервере()
	
	РегистрыСведений.ПрефиксацияОбъектов.ВыполнитьНачальноеЗаполнение();
	
КонецПроцедуры 

#КонецОбласти

