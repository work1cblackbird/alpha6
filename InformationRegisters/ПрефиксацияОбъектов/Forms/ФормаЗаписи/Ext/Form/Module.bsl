
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// заполним список предопределенных значений
	ЗаполнитьСписокПредопределенныхЗначенийПрефикса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НовыйШаблон = Запись.Шаблон;
	ЗаполнитьСоставНаОснованииШаблона();
	ЗаполнитьНовыйШаблон();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.Шаблон = НовыйШаблон;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Заголовок = НСтр("ru = 'Префиксация объекта:'") + " "+ ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Запись.Объект).Наименование;
КонецПроцедуры

&НаКлиенте
Процедура НовыйШаблонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить(1, НСтр("ru = 'По умолчанию'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Из таблицы'"));
	ОповещениеВыбора = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", ЭтотОбъект, "ЗаполнениеШаблона");
	ПоказатьВыборИзСписка(ОповещениеВыбора, СписокВыбора, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НовыйШаблонПриИзменении(Элемент)
	ЗаполнитьСоставНаОснованииШаблона();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВСостав(Команда)
		
	СтрокаДобавления = ТаблицаВозможныхЭлементов.НайтиПоИдентификатору(Элементы.ТаблицаВозможныхЭлементов.ТекущаяСтрока);
	
	// посмотрим может такой префикс уже добавлен
	ПараметрыОтбора = Новый Структура("Префикс,Расшифровка");
	ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаДобавления);
	НайденныеСтроки = ТаблицаВыбранныхЭлементов.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.ТаблицаВыбранныхЭлементов.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ЗаполнитьЗначенияСвойств(ТаблицаВыбранныхЭлементов.Добавить(), СтрокаДобавления);
	ЗаполнитьНовыйШаблон();
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьИзСостава(Команда)
	Если ТаблицаВыбранныхЭлементов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ТаблицаВыбранныхЭлементов.Удалить(ТаблицаВыбранныхЭлементов.НайтиПоИдентификатору(Элементы.ТаблицаВыбранныхЭлементов.ТекущаяСтрока));
	ЗаполнитьНовыйШаблон();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВозможныхЭлементовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтрокаДобавления = ТаблицаВозможныхЭлементов.НайтиПоИдентификатору(Элементы.ТаблицаВозможныхЭлементов.ТекущаяСтрока);
	ДобавитьВСостав(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВыбранныхЭлементовПриИзменении(Элемент)
	ЗаполнитьНовыйШаблон();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокПредопределенныхЗначенийПрефикса()
	ПрефиксОрганизации=ПараметрыСеанса.Организация.Префикс;
	ПрефиксПодразделения=ПараметрыСеанса.ПодразделениеКомпании.Префикс;
	ПрефиксУРБД=Константы.ПрефиксУзлаРаспределеннойИнформационнойБазы.Получить();
	ТекДата=ТекущаяДатаСеанса();
	ПрефиксГода2=Формат(ТекДата,"ДФ=yy");
	ПрефиксГода4=Формат(ТекДата,"ДФ=yyyy");
	ПрефиксМесяца1=Формат(ТекДата,"ДФ=M");
	ПрефиксМесяца2=Формат(ТекДата,"ДФ=MM");
	ПрефиксЧисла1=Формат(ТекДата,"ДФ=d");
	ПрефиксЧисла2=Формат(ТекДата,"ДФ=dd");
	
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[У]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс УРБД'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксУРБД)),"<пусто>",ПрефиксУРБД);
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[О]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс организации'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксОрганизации)),"<пусто>",ПрефиксОрганизации);
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[П]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс подразделения'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксПодразделения)),"<пусто>",ПрефиксПодразделения);
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[yy]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс года (2 знака)'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксГода2)),"<пусто>",ПрефиксГода2);
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[yyyy]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс года (4 знака)'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксГода4)),"<пусто>",ПрефиксГода4);
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[M]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс месяца (1-2 знака)'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксМесяца1)),"<пусто>",ПрефиксМесяца1);
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[MM]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс месяца (2 знака)'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксМесяца2)),"<пусто>",ПрефиксМесяца2);
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[d]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс числа (1-2 знака)'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксЧисла1)),"<пусто>",ПрефиксЧисла1);
	НоваяСтрока=ТаблицаВозможныхЭлементов.Добавить();
	НоваяСтрока.Префикс="[dd]"; НоваяСтрока.Расшифровка=НСтр("ru = 'Префикс числа (2 знака)'");
	НоваяСтрока.Пример=?(ПустаяСтрока(СокрЛП(ПрефиксЧисла2)),"<пусто>",ПрефиксЧисла2);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНовыйШаблон()
	НовыйШаблон = "";
	НовыйШаблонПредПросмотр = "";
	Для Каждого Префикс Из ТаблицаВыбранныхЭлементов Цикл
		НовыйШаблонПредПросмотр = НовыйШаблонПредПросмотр + Префикс.Пример;
	КонецЦикла;
	НовыйШаблон=Запись.Шаблон;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСоставНаОснованииШаблона()
	
	СтрокаРазбора = НовыйШаблон;
	ТаблицаВыбранныхЭлементов.Очистить();
	
	Пока Истина Цикл
		ПозицияПрефиксаНачало = СтрНайти(СтрокаРазбора,"[");
		ПозицияПрефиксаКонец  = СтрНайти(СтрокаРазбора,"]");
		
		Если ПозицияПрефиксаНачало = 0 ИЛИ ПозицияПрефиксаКонец = 0 Тогда
			Прервать;
		КонецЕсли;
		СтрокаПрефикса = Сред(СтрокаРазбора,ПозицияПрефиксаНачало, ПозицияПрефиксаКонец + 1 - ПозицияПрефиксаНачало);
		СтрокаРазбора = СтрЗаменить(СтрокаРазбора,СтрокаПрефикса,"");
		
		НайденныеСтроки = ТаблицаВозможныхЭлементов.НайтиСтроки(Новый Структура("Префикс",СтрокаПрефикса));
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаВыбранныхЭлементов.Добавить(),НайденныеСтроки[0]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаОповещения(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если ДополнительныеПараметры = "ЗаполнениеШаблона" И НЕ РезультатОповещения = Неопределено Тогда
		Если РезультатОповещения.Значение = 1 Тогда
			НовыйШаблон = Запись.ПоУмолчанию;
		ИначеЕсли РезультатОповещения.Значение = 2 Тогда
			ЗаполнитьШаблонИзСостава();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // Подключаемый_ОбработкаРезультатаОповещения()

&НаСервере
Процедура ЗаполнитьШаблонИзСостава()
	
	ТЗ = ТаблицаВыбранныхЭлементов.Выгрузить().ВыгрузитьКолонку("Префикс");
	НовыйШаблон = СтрСоединить(ТЗ);
	
КонецПроцедуры

#КонецОбласти

