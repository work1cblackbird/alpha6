////////////////////////////////////////////////////////////////////////////////
// Модуль формы "Замены номенклатуры".
// Служит для составления соответствия замен номенклатуры.
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("Номенклатура") Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Номенклатура = Параметры.Номенклатура;
	ЗаполнитьРеквизитыНоменклатуры();
	
	Элементы.ДопПараметры.ТолькоПросмотр = НЕ ПравоДоступа("Изменение", 
			Метаданные.РегистрыСведений.ДополнительныеРеквизитыНоменклатуры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МинимальныйОстатокПриИзменении(Элемент)
	
	ДопРеквизит = ДополнительныйРеквизитПоРеквизитуФормы(Элемент.Имя);
	
	Если Элементы.СписокПодразделений.ТекущиеДанные <> Неопределено Тогда
		
		ОбновитьЗначениеДополнительногоРеквизитыНоменклатуры(
			Номенклатура, Элементы.СписокПодразделений.ТекущиеДанные.Ссылка, ДопРеквизит, МинимальныйОстаток);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НормаПотерьПриИзменении(Элемент)
	
	ДопРеквизит = ДополнительныйРеквизитПоРеквизитуФормы(Элемент.Имя);
	
	Если Элементы.СписокПодразделений.ТекущиеДанные <> Неопределено Тогда
		
		ОбновитьЗначениеДополнительногоРеквизитыНоменклатуры(
			Номенклатура, Элементы.СписокПодразделений.ТекущиеДанные.Ссылка, ДопРеквизит, НормаПотерь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МаксимальныйЗапасПриИзменении(Элемент)
	
	ДопРеквизит = ДополнительныйРеквизитПоРеквизитуФормы(Элемент.Имя);
	
	Если Элементы.СписокПодразделений.ТекущиеДанные <> Неопределено Тогда
		
		ОбновитьЗначениеДополнительногоРеквизитыНоменклатуры(
			Номенклатура, Элементы.СписокПодразделений.ТекущиеДанные.Ссылка, ДопРеквизит, МаксимальныйЗапас);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентОтходовПриИзменении(Элемент)
	
	ДопРеквизит = ДополнительныйРеквизитПоРеквизитуФормы(Элемент.Имя);
	
	Если Элементы.СписокПодразделений.ТекущиеДанные <> Неопределено Тогда
		
		ОбновитьЗначениеДополнительногоРеквизитыНоменклатуры(
			Номенклатура, Элементы.СписокПодразделений.ТекущиеДанные.Ссылка, ДопРеквизит, ПроцентОтходов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПодразделений

&НаКлиенте
Процедура СписокПодразделенийПриАктивизацииСтроки(Элемент)
	
	Если Элементы.СписокПодразделений.ТекущиеДанные <> Неопределено Тогда
		
		ДопПараметры = ЗначенияДополнительныеРеквизитыНоменклатурыДляПодразделения(Номенклатура, 
													Элементы.СписокПодразделений.ТекущиеДанные.Ссылка);
		
		Для Каждого Параметр Из ДопПараметры Цикл
			
			ЭтотОбъект[РеквизитыДопПараметров.Получить(Параметр.Ключ)] = Параметр.Значение;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыНоменклатуры()
	
	РеквизитыДопПараметровБаза = Новый Соответствие;
	РеквизитыДопПараметровБаза.Вставить(Перечисления.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас, 
																								"МаксимальныйЗапас");
	РеквизитыДопПараметровБаза.Вставить(Перечисления.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток, 
																								"МинимальныйОстаток");
	РеквизитыДопПараметровБаза.Вставить(Перечисления.ДополнительныеРеквизитыНоменклатуры.НормаПотерь, "НормаПотерь");
	РеквизитыДопПараметровБаза.Вставить(Перечисления.ДополнительныеРеквизитыНоменклатуры.ПроцентОтходов, "ПроцентОтходов");
	
	РеквизитыДопПараметров = Новый ФиксированноеСоответствие(РеквизитыДопПараметровБаза);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначенияДополнительныеРеквизитыНоменклатурыДляПодразделения(Номенклатура, ПодразделениеКомпании)
	
	Образец = Новый Соответствие;
	Образец.Вставить(Перечисления.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас, 0);
	Образец.Вставить(Перечисления.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток, 0);
	Образец.Вставить(Перечисления.ДополнительныеРеквизитыНоменклатуры.НормаПотерь, 0);
	Образец.Вставить(Перечисления.ДополнительныеРеквизитыНоменклатуры.ПроцентОтходов, 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДополнительныеРеквизитыНоменклатурыСрезПоследних.ДополнительныйРеквизит КАК ДополнительныйРеквизит,
	|	ДополнительныеРеквизитыНоменклатурыСрезПоследних.ЗначениеРеквизита КАК ЗначениеРеквизита
	|ИЗ
	|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(
	|			,
	|			Номенклатура = &Номенклатура
	|				И ПодразделениеКомпании = &ПодразделениеКомпании) КАК ДополнительныеРеквизитыНоменклатурыСрезПоследних";
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Образец;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Образец.Получить(Выборка.ДополнительныйРеквизит) = Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Образец.Вставить(Выборка.ДополнительныйРеквизит, Выборка.ЗначениеРеквизита);
		
	КонецЦикла;
	
	Возврат Образец;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОбновитьЗначениеДополнительногоРеквизитыНоменклатуры(
	Номенклатура, ПодразделениеКомпании, ДополнительныйРеквизит, ЗначениеРеквизита)
	
	Менеджер = РегистрыСведений.ДополнительныеРеквизитыНоменклатуры.СоздатьМенеджерЗаписи();
	Менеджер.Номенклатура = Номенклатура;
	Менеджер.ПодразделениеКомпании = ПодразделениеКомпании;
	Менеджер.ДополнительныйРеквизит = ДополнительныйРеквизит;
	Менеджер.ЗначениеРеквизита = ЗначениеРеквизита;
	Менеджер.Период = ТекущаяДатаСеанса();
	
	Менеджер.Записать();
	
КонецПроцедуры

&НаКлиенте
Функция ДополнительныйРеквизитПоРеквизитуФормы(Реквизит)
	
	Для Каждого ДопРеквизит Из РеквизитыДопПараметров Цикл
		
		Если ДопРеквизит.Значение = Реквизит Тогда
			
			Возврат ДопРеквизит.Ключ;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

#КонецОбласти






