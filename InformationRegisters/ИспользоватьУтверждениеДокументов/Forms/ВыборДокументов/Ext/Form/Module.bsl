
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПереченьДокументов();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаНастроитьИспользованиеУтвержденияПоПользователям",
		"Видимость",
		УтверждениеДокументовСервер.ИспользоватьУтверждениеДокументов()
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		ВопросОСохраненииНастроек();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПереченьДокументов

&НаКлиенте
Процедура ПереченьДокументовИспользованиеОтклонениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПереченьДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные.Изменено = Истина;
		Модифицированность     = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереченьДокументовИспользоватьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПереченьДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные.Изменено = Истина;
		Модифицированность     = Истина;
		
		Если НЕ ТекущиеДанные.Использование Тогда
			ТекущиеДанные.ОтклонениеПриОтменеПроведения = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда = Неопределено)
	
	Записать();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда = Неопределено)
	
	Если Модифицированность Тогда
		
		ЗаписатьНастройки();
		Модифицированность = Ложь;
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ИспользоватьУтверждениеДокументов"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьИспользованиеУтвержденияПоПользователям(Команда)
	
	Если Модифицированность Тогда
		
		// Форма была изменена, зададим вопрос пользователю.
		Оповещение   = Новый ОписаниеОповещения("Подключаемый_НастройкаИспользованияУтверждения", ЭтотОбъект);
		
		ТекстВопроса     = НСтр("ru = 'Настройки были изменены. Сохранить?'");
		ЗаголовокВопроса = НСтр("ru = 'Сохранение настроек'");
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да, ЗаголовокВопроса);
		
	Иначе
		
		ПродолжитьНастройкуИспользованияУтверждения();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПереченьДокументов()
	
	ПереченьДокументов.Очистить();
	
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("ДокументИмя",     Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(100)));
	ТаблицаДокументов.Колонки.Добавить("ДокументСиноним", Новый ОписаниеТипов("Строка"));
	
	Для Каждого ЭлементМетаданных Из Метаданные.Документы Цикл
		
		// Не выводим документы, у которых не включено использование общего реквизита "Статус утверждения".
		ЭлементУтверждения = Метаданные.ОбщиеРеквизиты.СтатусУтверждения.Состав.Найти(ЭлементМетаданных.Имя);
		Если ЭлементУтверждения.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать Тогда
			
			НоваяСтрокаДокумента = ТаблицаДокументов.Добавить();
			НоваяСтрокаДокумента.ДокументИмя     = ЭлементМетаданных.ПолноеИмя();
			НоваяСтрокаДокумента.ДокументСиноним = ЭлементМетаданных.Синоним;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументов.ДокументИмя КАК ДокументИмя,
	|	ТаблицаДокументов.ДокументСиноним КАК ДокументСиноним
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	&ТаблицаДокументов КАК ТаблицаДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.ДокументИмя КАК ДокументИмя,
	|	ТаблицаДокументов.ДокументСиноним КАК ДокументСиноним,
	|	ЕСТЬNULL(ИспользоватьУтверждениеДокументов.ОтклонениеПриОтменеПроведения, ЛОЖЬ) КАК ОтклонениеПриОтменеПроведения,
	|	ЕСТЬNULL(ИспользоватьУтверждениеДокументов.Использовать, ЛОЖЬ) КАК Использование
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИспользоватьУтверждениеДокументов КАК ИспользоватьУтверждениеДокументов
	|		ПО ТаблицаДокументов.ДокументИмя = ИспользоватьУтверждениеДокументов.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументИмя";
	
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаДокументов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ПереченьДокументов.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройки()
	
	// Записываем только измененные настройки.
	Для Каждого ТекДокумент Из ПереченьДокументов.НайтиСтроки(Новый Структура("Изменено", Истина)) Цикл
		
		ЗаписьРегистра = РегистрыСведений.ИспользоватьУтверждениеДокументов.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.Документ = ТекДокумент.ДокументИмя;
		ЗаписьРегистра.Прочитать();
		
		Если ТекДокумент.Использование Тогда
			
			ЗаписьРегистра.Документ                      = ТекДокумент.ДокументИмя;
			ЗаписьРегистра.Использовать                  = ТекДокумент.Использование;
			ЗаписьРегистра.ОтклонениеПриОтменеПроведения = ТекДокумент.ОтклонениеПриОтменеПроведения;
			ЗаписьРегистра.Записать();
			
		Иначе
			
			ЗаписьРегистра.Удалить();
			
		КонецЕсли;
		
		// Сбрасываем признак изменения настроек.
		ТекДокумент.Изменено = Ложь;
		
	КонецЦикла;
	
	// Сбрасываем модифицированность, если не осталось измененных настроек.
	Модифицированность = ПереченьДокументов.НайтиСтроки(Новый Структура("Изменено", Истина)).Количество() > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииНастроек()
	
	Если Модифицированность Тогда
		
		// Форма была изменена, зададим вопрос пользователю.
		Оповещение   = Новый ОписаниеОповещения("Подключаемый_РеакцияНаВопросОСохраненииНастроек", ЭтотОбъект);
		
		ТекстВопроса     = НСтр("ru = 'Настройки были изменены. Сохранить?'");
		ЗаголовокВопроса = НСтр("ru = 'Сохранение настроек'");
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да, ЗаголовокВопроса);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработка результата ответа на вопрос пользователю о сохранении измененных настроек.
//
// Параметры:
//  Результат - КодВозвратаДиалога, Неопределено - результат ответа.
//  ПараметрыДействия - Структура - Набор параметров, использующихся при выполнения операции.
//
&НаКлиенте
Процедура Подключаемый_РеакцияНаВопросОСохраненииНастроек(Результат, ПараметрыДействия) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да ИЛИ Результат = КодВозвратаДиалога.Нет Тогда
		
		Если Результат = КодВозвратаДиалога.Да Тогда
			// Сохраняем настройки.
			Записать();
		КонецЕсли;
		
		Модифицированность = Ложь;
		
		// Продолжаем начатое действие.
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НастройкаИспользованияУтверждения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Записать();
	КонецЕсли;
	
	ПродолжитьНастройкуИспользованияУтверждения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьНастройкуИспользованияУтверждения()
	
	ОткрытьФорму(
		"РегистрСведений.НастройкиУтвержденияДокументов.Форма.ПерсональныеНастройки",
		,
		ЭтотОбъект,
		, , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

#КонецОбласти