///////////////////////////////////////////////////////////////////////////////
// Модуль формы записи регистра "Использовать утверждение документов"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	
	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Отказ = НЕ ПолноеИмяДокументаКорректно(Запись.Документ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

//
&НаСервере
Процедура ДокументПриИзмененииНаСервере()
	
	ПолноеИмяДокументаКорректно(Запись.Документ);
	
КонецПроцедуры

//
&НаКлиенте
Процедура ДокументПриИзменении(Элемент)
	
	ДокументПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолноеИмяДокументаКорректно(Документ)
	
	ИмяКорректно = Истина;
	
	ЭлементМетаданных = Метаданные.НайтиПоПолномуИмени(Запись.Документ);
	
	Если ЭлементМетаданных = Неопределено ИЛИ НЕ Метаданные.Документы.Содержит(ЭлементМетаданных) Тогда
		// Введено некорректное имя документа/
		ТекстОшибки = НСтр("ru = 'Указано некорректное полное имя документа (документ не найден в метаданных)'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		
		ИмяКорректно = Ложь;
	Иначе
		// Не используем документы, у которых не включено использование общего реквизита "Статус утверждения".
		ЭлементУтверждения = Метаданные.ОбщиеРеквизиты.СтатусУтверждения.Состав.Найти(ЭлементМетаданных.Имя);
		
		Если НЕ ЭлементУтверждения.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать Тогда
			
			// Документ не используется в подсистеме утверждения/
			ТекстОшибки = НСтр("ru = 'В документе %1 не используется общий реквизит «Статус утверждения», документ не предназначен для использования в подсистеме утверждения.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ЭлементМетаданных.Имя);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			
			ИмяКорректно = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИмяКорректно;
	
КонецФункции 

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомРегистраСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры 

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомРегистраСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	
КонецПроцедуры 

#КонецОбласти

#КонецОбласти

