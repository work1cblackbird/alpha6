
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура сверяет список метаданных со списком регистра и удаляет или добавляет необходимые записи.
//
Процедура ВыполнитьНачальноеЗаполнение() Экспорт
	
	// сформирует текущую таблицу метаданных конфигурации
	ТаблицаМетаданных = Новый ТаблицаЗначений;
	ТаблицаМетаданных.Колонки.Добавить("Объект",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(150)));
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		
		ЭлементУтверждения = Метаданные.ОбщиеРеквизиты.СтатусУтверждения.Состав.Найти(Документ.Имя);
		
		Если ЭлементУтверждения.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать Тогда
			НоваяСтрока        = ТаблицаМетаданных.Добавить();
			НоваяСтрока.Объект = Документ.ПолноеИмя();
		КонецЕсли;
		
	КонецЦикла;
	
	// получим расхождения
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаМетаданных", ТаблицаМетаданных);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УтверждениеДокументов.Объект КАК Объект
	|ПОМЕСТИТЬ ТаблицаМетаданных
	|ИЗ
	|	&ТаблицаМетаданных КАК УтверждениеДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ИспользоватьУтверждениеДокументов.Документ, """") КАК Документ,
	|	ЕСТЬNULL(ТаблицаМетаданных.Объект, """") КАК Объект
	|ИЗ
	|	ТаблицаМетаданных КАК ТаблицаМетаданных
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ИспользоватьУтверждениеДокументов КАК ИспользоватьУтверждениеДокументов
	|		ПО ТаблицаМетаданных.Объект = ИспользоватьУтверждениеДокументов.Документ";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// удаляем или добавляем записи
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ПустаяСтрока(Выборка.Объект) Тогда
			// удалим запись
			Менеджер = РегистрыСведений.ИспользоватьУтверждениеДокументов.СоздатьМенеджерЗаписи();
			Менеджер.Документ = Выборка.Документ;
			Менеджер.Удалить();
			Продолжить;
		КонецЕсли;
		
		Если ПустаяСтрока(Выборка.Документ) Тогда
			
			// добавляем Строку
			Менеджер = РегистрыСведений.ИспользоватьУтверждениеДокументов.СоздатьМенеджерЗаписи();
			Менеджер.Документ      = Выборка.Объект;
			Менеджер.Записать(Истина);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры 

#Область ПараметрыОбработкиРеквизитовОбъекта

Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции 

Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	УникальныеРеквизиты = Новый Структура();
	
	Возврат УникальныеРеквизиты;
	
КонецФункции 

#КонецОбласти

#КонецОбласти

#КонецЕсли