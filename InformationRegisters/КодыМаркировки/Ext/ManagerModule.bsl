// Модуль менеджера регистра "Коды маркировки."

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обработчик обновления на версию 6.1.03.13
//
Процедура ПерезаполнитьНекорректныеЗаписиBase64() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыМаркировки.GTIN КАК GTIN,
	|	КодыМаркировки.СерийныйНомер КАК СерийныйНомер,
	|	КодыМаркировки.КодМаркировкиBASE64 КАК КодМаркировкиBASE64,
	|	КодыМаркировки.Обновлено КАК Обновлено,
	|	КодыМаркировки.Пробито КАК Пробито
	|ИЗ
	|	РегистрСведений.КодыМаркировки КАК КодыМаркировки
	|ГДЕ
	|	НЕ КодыМаркировки.КодМаркировкиBASE64 = """"";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.КодыМаркировки.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.GTIN = Выборка.GTIN;
		МенеджерЗаписи.СерийныйНомер = Выборка.СерийныйНомер;
		МенеджерЗаписи.Прочитать();
		
		ДанныеСтроки = ПреобразоватьИзBase64(Выборка.КодМаркировкиBASE64);
		
		Если ДанныеСтроки.ВсеОК Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗаписи.GTIN = Выборка.GTIN;
		МенеджерЗаписи.СерийныйНомер = Выборка.СерийныйНомер;
		МенеджерЗаписи.КодМаркировкиBASE64 = ДанныеСтроки.КодМаркировкиBASE64;
		МенеджерЗаписи.Записать();
	
	КонецЦикла;
	
КонецПроцедуры

// Записывает полный код маркировки в формате BASE64 в регистр.
//
// Параметры:
//  СтруктураМаркировки - Структура - см. МенеджерОборудованияМаркировкаКлиентСервер.РазобратьШтриховойКодТовара()
//
Процедура ЗаписатьПолныйШтрихкод(СтруктураМаркировки) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.КодыМаркировки.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.GTIN = СтруктураМаркировки.GTIN;
	МенеджерЗаписи.СерийныйНомер = СтруктураМаркировки.СерийныйНомер;
	МенеджерЗаписи.Прочитать();
	
	Если
		НЕ МенеджерЗаписи.Выбран() ИЛИ МенеджерЗаписи.Выбран() И НЕ ЗначениеЗаполнено(МенеджерЗаписи.КодМаркировкиBASE64)
	Тогда
		РезультатПроверки = ПреобразоватьИзBase64(
			СтруктураМаркировки.ШтрихкодBase64,
			ПолучитьЗначениеПараметраСтруктуры(СтруктураМаркировки, "ПотребительскаяУпаковкаТабачнойПродукции", Ложь));
		Если РезультатПроверки.ВсеОК Тогда
			МенеджерЗаписи.GTIN = СтруктураМаркировки.GTIN;
			МенеджерЗаписи.СерийныйНомер = СтруктураМаркировки.СерийныйНомер;
			МенеджерЗаписи.КодМаркировкиBASE64 = СтруктураМаркировки.ШтрихкодBase64;
			МенеджерЗаписи.Обновлено = ТекущаяДатаСеанса();
			МенеджерЗаписи.Записать();
		Иначе
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Введен неполный код маркировки: %1'"),
				МаркировкаТоваровКлиентСервер.СформироватьКодМаркировки(СтруктураМаркировки)
			);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает полные коды маркировки по массиву кодов для пробития на ФР
// 
// Параметры:
//  КодыМаркировки - Массив, Строка - Коды маркировки для получения полных кодов.
//
// Возвращаемое значение:
//  Соответствие - Ключ - код маркировки, Значение - полный код маркировки в формате BASE64
//
Функция КодыМаркировкиВBASE64(КодыМаркировки) Экспорт
	
	Результат = Новый Соответствие;
	
	ТипЗначенияКодыМаркировки = ТипЗнч(КодыМаркировки);
	Если ТипЗначенияКодыМаркировки = Тип("Строка") Тогда
		КодыМаркировкиДляРазбора = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодыМаркировки);
	ИначеЕсли ТипЗначенияКодыМаркировки = Тип("Массив") Тогда
		КодыМаркировкиДляРазбора = КодыМаркировки;
	Иначе
		// непонятно что на входе
		Возврат Результат;
	КонецЕсли;
	
	ТаблицаРазбора = Новый ТаблицаЗначений;
	ТаблицаРазбора.Колонки.Добавить("GTIN", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРазбора.Колонки.Добавить("СерийныйНомер", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРазбора.Колонки.Добавить("КодМаркировки", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	
	Для Каждого КодМаркировки Из КодыМаркировкиДляРазбора Цикл
		СтруктураМаркировки = МаркировкаТоваровСервер.РазобратьШтриховойКодТовара(КодМаркировки);
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаРазбора.Добавить();
		НоваяСтрока.КодМаркировки = КодМаркировки;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураМаркировки, "GTIN, СерийныйНомер");
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаРазбора", ТаблицаРазбора);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаРазбора.GTIN КАК GTIN,
	|	ТаблицаРазбора.СерийныйНомер КАК СерийныйНомер
	|ПОМЕСТИТЬ ТаблицаРазбора
	|ИЗ
	|	&ТаблицаРазбора КАК ТаблицаРазбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРазбора.GTIN КАК GTIN,
	|	ТаблицаРазбора.СерийныйНомер КАК СерийныйНомер,
	|	ЕСТЬNULL(КодыМаркировки.КодМаркировкиBASE64, """") КАК КодМаркировкиBASE64
	|ИЗ
	|	ТаблицаРазбора КАК ТаблицаРазбора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КодыМаркировки КАК КодыМаркировки
	|		ПО ТаблицаРазбора.GTIN = КодыМаркировки.GTIN
	|			И ТаблицаРазбора.СерийныйНомер = КодыМаркировки.СерийныйНомер";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПолныеКодыМаркировки = РезультатЗапроса.Выбрать();
	
	Пока ПолныеКодыМаркировки.Следующий() Цикл
		
		ПараметрыОтбора = Новый Структура("GTIN, СерийныйНомер");
		ЗаполнитьЗначенияСвойств(ПараметрыОтбора, ПолныеКодыМаркировки);
		НайденныеСтроки = ТаблицаРазбора.НайтиСтроки(ПараметрыОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Результат.Вставить(НайденныеСтроки[0].КодМаркировки, ПолныеКодыМаркировки.КодМаркировкиBASE64);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Записывает время пробития полного кода маркировки в регистр "Коды маркировки"
//
// Параметры:
//  КодыМаркировки - Массив, Строка - Коды маркировки для получения полных кодов.
//
Процедура ПробитьКодыМаркировки(КодыМаркировки) Экспорт
	
	ТипЗначенияКодыМаркировки = ТипЗнч(КодыМаркировки);
	Если ТипЗначенияКодыМаркировки = Тип("Строка") Тогда
		КодыМаркировкиДляЗаписи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодыМаркировки);
	ИначеЕсли ТипЗначенияКодыМаркировки = Тип("Массив") Тогда
		КодыМаркировкиДляЗаписи = КодыМаркировки;
	Иначе
		// непонятно что на входе
		Возврат;
	КонецЕсли;
	
	Для Каждого КодМаркировки Из КодыМаркировки Цикл
		
		СтруктураМаркировки = МаркировкаТоваровСервер.РазобратьШтриховойКодТовара(КодМаркировки);
		
		МенеджерЗаписи = РегистрыСведений.КодыМаркировки.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.GTIN = СтруктураМаркировки.GTIN;
		МенеджерЗаписи.СерийныйНомер = СтруктураМаркировки.СерийныйНомер;
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Пробито = ТекущаяДатаСеанса();
			МенеджерЗаписи.Записать();
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПреобразоватьИзBase64(Знач КодМаркировкиBASE64, ЭтоПотребительскаяУпаковкаТабачнойПродукции = Ложь)
	
	Результат = Новый Структура("ВсеОК,КодМаркировкиBASE64", Ложь, "");
	
	ИсходнаяСтрока = МенеджерОборудованияКлиентСервер.Base64ВШтрихкод(КодМаркировкиBASE64);
	
	Если СтрНайти(ИсходнаяСтрока, ОбщегоНазначенияБПОКлиентСервер.РазделительGS1()) <> 0
		ИЛИ ЭтоПотребительскаяУпаковкаТабачнойПродукции Тогда
		// нашли разделитель - значит с кодом все ОК
		Результат.ВсеОК = Истина;
		Возврат Результат;
	КонецЕсли;
	
	ЭкранированныйСимвол = ОбщегоНазначенияБПОКлиентСервер.ЭкранированныйСимволGS1();
	
	Если СтрНайти(ИсходнаяСтрока, ЭкранированныйСимвол) <> 0 Тогда
		
		МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			ИсходнаяСтрока,
			ЭкранированныйСимвол,
			Ложь
		);
		
		Если МассивСтрок.Количество() = 3 Тогда
			
			КодМаркировки = СтрЗаменить(
				ИсходнаяСтрока,
				ЭкранированныйСимвол,
				ОбщегоНазначенияБПОКлиентСервер.РазделительGS1()
			);
			Результат.КодМаркировкиBASE64 = МенеджерОборудованияКлиентСервер.ШтрихкодВBase64(КодМаркировки);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли