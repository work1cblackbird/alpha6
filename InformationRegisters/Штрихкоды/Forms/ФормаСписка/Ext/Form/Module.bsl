///////////////////////////////////////////////////////////////////////////////
// Модуль формы списка регистра "Штрих коды"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	УстановитьЗаголовок = Ложь;
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Объект") Тогда
		
		Владелец = Параметры.Отбор.Объект;
		
		Если НЕ ОбщегоНазначения.ОбъектЯвляетсяГруппой(Владелец) Тогда
			
			ВладелецДляОтбора = ВладелецШтрихкодов(Владелец);
			Владелец = ВладелецДляОтбора;
			Параметры.Отбор.Объект = ВладелецДляОтбора;
			УстановитьЗаголовок = Истина;
			Объект = Владелец;
			
		Иначе
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Использование для групп запрещено.'"),,,, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	
	РаботаСФормой.НастроитьОсновнойДинамическийСписокФормы(ЭтотОбъект);
	
	// ПодключаемоеОборудование
	ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	ТипыОборудования.СканерШтрихкода = Истина;
	ТипыОборудования.СчитывательМагнитныхКарт = Истина;
	МенеджерОборудования.ПриСозданииНаСервере(ЭтаФорма, ТипыОборудования);
	// Конец ПодключаемоеОборудование
	
	Если УстановитьЗаголовок Тогда
		ОбновитьЗаголовокФормы(ЭтотОбъект, ВладелецДляОтбора);
	КонецЕсли;
	
	СкрытьОбъект = Параметры.Свойство("СкрыватьОбъект");
	Закрепить = Параметры.Свойство("ЗакрепитьСПрава");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
	Если Закрепить Тогда
		
		УправлениеДиалогомКлиент.ЗакрепитьФорму();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "АктивизацияСтрокиНоменклатуры" И Источник = ВладелецФормы И СкрытьОбъект Тогда
		
		Если НЕ ЗначениеЗаполнено(Параметр) ИЛИ ОбъектЯвляетсяГруппой(Параметр) Тогда
			Доступность = Ложь;
		Иначе
			Доступность = Истина;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Объект",
			Параметр,
			ВидСравненияКомпоновкиДанных.Равно,
			"Объект",
			Истина
		);
		
		ОбновитьЗаголовокФормы(ЭтотОбъект, Параметр);
		
	КонецЕсли;
	
	Если ВводДоступен() И Источник = "ПодключаемоеОборудование" Тогда
		Штрихкод = ШтрихкодированиеКлиент.ПолучитьШтрихкодИзПараметровОборудования(ИмяСобытия, Параметр);
		ПараметрыДействия = Новый Структура;
		ШтрихкодированиеКлиент.ОбработатьПолныйШтрихкод(Штрихкод, ПараметрыДействия);
		Штрихкод = ПараметрыДействия.Штрихкод;
		
		Если Штрихкод <> "" Тогда 
			УстановитьПоискПоШтрихкоду(Штрихкод);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	// ОценкаПроизводительности
	Если Копирование Тогда
		
		ОценкаПроизводительностиКлиент.ЗамерВремени("КопированиеРегистраСведенийШтрихкоды");
		
	Иначе
		
		ОценкаПроизводительностиКлиент.ЗамерВремени("СозданиеРегистраСведенийШтрихкоды");
		
	КонецЕсли;
	// Конец ОценкаПроизводительности
	
КонецПроцедуры 

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
		
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеЗаписиРегистраСведенийШтрихкоды");
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Видимость
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Представление = НСтр("ru = 'Скрытие колонки объект'");
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Объект");
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СкрытьОбъект");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбъектЯвляетсяГруппой(Объект)
	
	Возврат ОбщегоНазначения.ОбъектЯвляетсяГруппой(Объект);
	
КонецФункции

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервереБезКонтекста
Функция ВладелецШтрихкодов(Владелец)
	
	// для документов владелец - сам документ
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Владелец)) И ОбщегоНазначения.ЭтоДокумент(Владелец.Метаданные()) Тогда
		Возврат Владелец;
	КонецЕсли;
	
	ДанныеВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Владелец,
		Новый Структура("ТипНоменклатуры,ИспользованиеШтрихКодов", "ТипНоменклатуры", "ТипНоменклатуры.ИспользованиеШтрихКодов"));
	
	Возврат ?(ДанныеВладельца.ИспользованиеШтрихКодов > 0, Владелец, Неопределено);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗаголовокФормы(Форма, ВладелецДляОтбора)
	
	ПредставлениеВладельца = ?(ВладелецДляОтбора = Неопределено, "", СтрШаблон("(%1)",СокрЛП(ВладелецДляОтбора)));
	
	Если ВладелецДляОтбора = Неопределено Тогда
		ТекстЗаголовка = НСтр("ru = 'Штрихкодирование выбранного типа номенклатуры не ведется'");
	Иначе
		ШаблонЗаголовка = НСтр("ru = 'Штрихкоды номенклатуры %1'");
		ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка, ПредставлениеВладельца);
	КонецЕсли;
	
	Форма.АвтоЗаголовок                  = Ложь;
	Форма.Заголовок                      = ТекстЗаголовка;
	Форма.Элементы.Список.ТолькоПросмотр = (НЕ ЗначениеЗаполнено(ВладелецДляОтбора));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПоискПоШтрихкоду(Штрихкод)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Штрихкод", Штрихкод);
	Отбор.Вставить("Объект", Объект);
	Ключ =РегистрыСведений.Штрихкоды.СоздатьКлючЗаписи(Отбор);
	Элементы.Список.ТекущаяСтрока = Ключ;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

