
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыДействия = Новый Структура;
	Если Параметры.Свойство("ДоступныеПоля") Тогда
		ПараметрыДействия.Вставить("ДоступныеПоля", Параметры.ДоступныеПоля);
	КонецЕсли;
	
	ВидимостьЕдиниц = Ложь;
	ВидимостьХарактеристик = Ложь;
	
	ОбъектыПоШтрихкоду = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "НайденныйОбъект", Новый Массив);
	
	Для Каждого ТекСтрока Из ОбъектыПоШтрихкоду Цикл
		
		НоваяСтрока = ТаблицаОбъектов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		
		Если ЗначениеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
			ВидимостьЕдиниц = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекСтрока.ХарактеристикаНоменклатуры) Тогда
			ВидимостьХарактеристик = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ТаблицаОбъектовЕдиницаИзмерения.Видимость = ВидимостьЕдиниц;
	Элементы.ТаблицаОбъектовХарактеристикаНоменклатуры.Видимость = ВидимостьХарактеристик;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаОбъектов

&НаКлиенте
Процедура ТаблицаОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьТекущуюСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТекущуюСтроку()
	
	ТекущаяСтрока = Элементы.ТаблицаОбъектов.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектПоШтрихкоду = Новый Структура;
	ОбъектПоШтрихкоду.Вставить("Объект", ТекущаяСтрока.Объект);
	ОбъектПоШтрихкоду.Вставить("ЕдиницаИзмерения", ТекущаяСтрока.ЕдиницаИзмерения);
	ОбъектПоШтрихкоду.Вставить("ХарактеристикаНоменклатуры", ТекущаяСтрока.ХарактеристикаНоменклатуры);
	ОбъектПоШтрихкоду.Вставить("Штрихкод", ТекущаяСтрока.Штрихкод);
	ОбъектПоШтрихкоду.Вставить("Запрет", ТекущаяСтрока.Запрет);
	ОбъектПоШтрихкоду.Вставить("GTIN", ТекущаяСтрока.GTIN);
	ОбъектПоШтрихкоду.Вставить("Вес", ТекущаяСтрока.Вес);
	
	ОбъектыПоШтрихкоду = Новый Массив;
	ОбъектыПоШтрихкоду.Добавить(ОбъектПоШтрихкоду);
	
	ПараметрыДействия.Вставить("ОбъектыПоШтрихкоду", ОбъектыПоШтрихкоду);
	ШтрихкодированиеВызовСервера.ОбработкаОбъектовПоШтрихкоду(ПараметрыДействия);
	
	Закрыть(ПараметрыДействия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьТекущуюСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъект(Команда)
	
	ТекущаяСтрока = Элементы.ТаблицаОбъектов.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ПоказатьЗначение(,ТекущаяСтрока.Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
