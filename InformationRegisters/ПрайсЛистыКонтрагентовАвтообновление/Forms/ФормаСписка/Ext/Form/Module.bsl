///////////////////////////////////////////////////////////////////////////////
// Модуль формы списка регистра "Прайс-листы контрагентов автообновление"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормой.НастроитьОсновнойДинамическийСписокФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	// ОценкаПроизводительности
	Если Копирование Тогда
		ОценкаПроизводительностиКлиент.ЗамерВремени("КопированиеРегистраСведенийПрайсЛистыКонтрагентовАвтообновление");
	Иначе
		ОценкаПроизводительностиКлиент.ЗамерВремени("СозданиеРегистраСведенийПрайсЛистыКонтрагентовАвтообновление");
	КонецЕсли;
	// Конец ОценкаПроизводительности
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеЗаписиРегистраСведенийПрайсЛистыКонтрагентовАвтообновление");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаКлиенте
Процедура СброситьИнформациюОЗагруженныхФайлах(Команда)
	
	Если НЕ СброситьИнформациюОЗагруженныхФайлахНаСервере(Элементы.Список.ВыделенныеСтроки) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не удалось выполнить сброс.Подробности см. в журнале регитсрации.'"));
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СброситьИнформациюОЗагруженныхФайлахНаСервере(Знач ОчищаемыеЗаписи)
	
	Набор = РегистрыСведений.ПрайсЛистыКонтрагентовАвтообновление.СоздатьНаборЗаписей();
	
	Для Каждого ОчищаемаяЗапись Из ОчищаемыеЗаписи Цикл
		Набор.Отбор.ПрайсЛист.Установить(ОчищаемаяЗапись.ПрайсЛист);
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентовАвтообновление");
		ЭлементБлокировки.УстановитьЗначение("ПрайсЛист", ОчищаемаяЗапись.ПрайсЛист);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		Набор.Прочитать();
		
		Для Каждого Запись Из Набор Цикл
			Запись.ДатаЗагруженного      = Неопределено;
			Запись.ДатаФайла             = Неопределено;
			Запись.ДатаСледующейПроверки = Неопределено;
			Запись.ХешЗагруженного       = "";
			Запись.ХешФайла              = "";
		КонецЦикла;
		
		Попытка
			Набор.Записать(Истина);
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'ПрайсЛистыКонтрагентов.Автообновление.Сброс'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				,
				ОчищаемаяЗапись,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецОбласти

