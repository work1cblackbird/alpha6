// Модуль менеджера регистра сведений "Упущенный спрос"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Заменить артикул
//
// Параметры:
//  АртикулДляПоиска      - Строка
//  НовыйАртикулДляПоиска - Строка
//  Артикул               - Строка.
//
Процедура ЗаменитьАртикул(АртикулДляПоиска, НовыйАртикулДляПоиска, Производитель, Артикул) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.АртикулДляПоиска.Установить(АртикулДляПоиска);
	Набор.Отбор.Производитель.Установить(Производитель);
	
	НовыйНабор = СоздатьНаборЗаписей();
	НовыйНабор.Отбор.АртикулДляПоиска.Установить(НовыйАртикулДляПоиска);
	НовыйНабор.Отбор.Производитель.Установить(Производитель);
	
	Набор.Прочитать();
	
	Для Каждого Запись Из Набор Цикл
		НоваяЗапись = НовыйНабор.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Запись);
		
		НоваяЗапись.АртикулДляПоиска = НовыйАртикулДляПоиска;
		НоваяЗапись.Артикул          = Артикул;
	КонецЦикла;
	
	Набор.Очистить();
	Набор.Записать();
	
	НовыйНабор.Записать();
	
КонецПроцедуры

// Процедура - Номенклатура при изменении
//
// Параметры:
//  Запись            - РегистрСведений.МенеджерЗаписи - Запись регистра сведений
//  ПараметрыДействия - Структура                      - Дополнительные параметры обработки.
//
Процедура НоменклатураПриИзменении(Запись, ПараметрыДействия = Неопределено) Экспорт
	
	ЗаполнитьЗначенияСвойств(Запись, Запись.Номенклатура, "АртикулДляПоиска,Производитель,Артикул");
	
	Запись.КоличествоОстаток = 0;
	Запись.Количество        = 1;
	Если НЕ Запись.Номенклатура.Пустая() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток КАК СвободныйОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		|			,
		|			СкладКомпании.ПодразделениеКомпании В ИЕРАРХИИ (&ПодразделениеКомпании)
		|				И Номенклатура = &Номенклатура) КАК ОстаткиТоваровКомпанииОстатки";
		Запрос.УстановитьПараметр("ПодразделениеКомпании", ПараметрыСеанса.ПодразделениеКомпании);
		Запрос.УстановитьПараметр("Номенклатура"         , Запись.Номенклатура);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Запись.КоличествоОстаток = РезультатЗапроса.Выгрузить()[0].СвободныйОстаток;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Артикул при изменении
//
// Параметры:
//  Запись            - РегистрСведений.МенеджерЗаписи - Запись регистра сведений
//  ПараметрыДействия - Структура                      - Дополнительные параметры обработки.
//
Процедура АртикулПриИзменении(Запись, ПараметрыДействия = Неопределено) Экспорт
	
	Запись.АртикулДляПоиска = ОбщегоНазначенияАвтосалонКлиентСервер.ВАртикулДляПоиска(Запись.Артикул);
	Запись.Номенклатура     = Справочники.Номенклатура.ПустаяСсылка();
	
КонецПроцедуры

#Область ПараметрыОбработкиРеквизитовОбъекта

Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	
	ОбязательныеРеквизиты.Добавить("Артикул");
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции 

Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	УникальныеРеквизиты = Новый Структура();
	
	Возврат УникальныеРеквизиты;
	
КонецФункции 
#КонецОбласти

#КонецОбласти

#КонецЕсли