// Модуль набора записей регистра "Ограничение ассортимента подразделения"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ДокументОбъект Экспорт;               // Документ объект
Перем ПодразделениеКомпании Экспорт;		// Подразделение компании
Перем ДатаИзмененияАссортимента Экспорт;    // Дата изменения ассортимента
Перем РезультатЗапросаПоТоварам Экспорт;	// РезультатЗапроса или ТаблицаЗначений.
Перем ОперацияСАссортиментом Экспорт;       // Операция с ассортиментом

// ускоряющие переменные
Перем ВалютаДокумента, КурсДокумента, ВалютаРеглУчета, КурсРеглВалюты;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция установки ассортимента
//
// Возвращаемое значение:
//  Булево - признак возможности дальнейшей обработки.
//
Функция УстановитьАссортимент() Экспорт
	Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам.Выгрузить();
	КонецЕсли;
	
	// ускоряющие переменные
	ВалютаДокумента	= ДокументОбъект.ВалютаДокумента;
	КурсДокумента	= ДокументОбъект.КурсДокумента;
	ВалютаРеглУчета	= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КурсРеглВалюты	= РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРеглУчета, ДокументОбъект.МоментВремени()).Курс;
	
	Для Каждого СтрокаТовар Из РезультатЗапросаПоТоварам Цикл
		ЦенаРозничная = РаботаСКурсамиВалютПлатформа.ПересчетПоВалюте(СтрокаТовар.ЦенаРозничная,ВалютаДокумента,КурсДокумента,ВалютаРеглУчета,КурсРеглВалюты);
		
		НоваяЗапись	= Добавить();
		НоваяЗапись.Период					= ДатаИзмененияАссортимента;
		НоваяЗапись.Регистратор				= ДокументОбъект.Ссылка;
		НоваяЗапись.ПодразделениеКомпании	= ПодразделениеКомпании;
		НоваяЗапись.Номенклатура			= СтрокаТовар.Номенклатура;
		НоваяЗапись.Состояние				= ОперацияСАссортиментом;
		НоваяЗапись.Цена					= СтрокаТовар.Цена;
		НоваяЗапись.ПроцентНаценки			= СтрокаТовар.ПроцентНаценки;
		НоваяЗапись.ЦенаРозничная			= ЦенаРозничная;
		НоваяЗапись.СтавкаНДС				= СтрокаТовар.СтавкаНДС;
		НоваяЗапись.БазовыйАссортимент		= СтрокаТовар.БазовыйАссортимент;
	КонецЦикла;
	
	// скинем некоторые переменные
	РезультатЗапросаПоТоварам = Неопределено;
	// убиваем циклическую ссылку
	ДокументОбъект = Неопределено;
	
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ОбработкаСобытийРегистраСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты) Тогда
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#КонецЕсли