// Модуль менеджера регистра сведений "Фактическое время"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ПоследнийИнтервалИсполнителя(исполнитель, толькоОткрытые = Ложь) Экспорт
	
	результат = Новый Структура("Период,ЗаказНаряд,Цех,Исполнитель,ПакетРабот,Состояние,ДатаОкончания,Продолжительность,ПричинаОбращения,Автомобиль");
	
	запрос = Новый Запрос;
	запрос.Текст = СтрШаблон(
	"ВЫБРАТЬ
	|	ФактическоеВремяСрезПоследних.Период,
	|	ФактическоеВремяСрезПоследних.ЗаказНаряд,
	|	ФактическоеВремяСрезПоследних.Цех,
	|	ФактическоеВремяСрезПоследних.Исполнитель,
	|	ФактическоеВремяСрезПоследних.ПакетРабот,
	|	ФактическоеВремяСрезПоследних.Состояние,
	|	ФактическоеВремяСрезПоследних.ДатаОкончания,
	|	ФактическоеВремяСрезПоследних.Продолжительность,
	|	ФактическоеВремяСрезПоследних.ПричинаОбращения,
	|	ФактическоеВремяСрезПоследних.Автомобиль
	|ИЗ
	|	РегистрСведений.ФактическоеВремя.СрезПоследних(, Исполнитель = &Исполнитель) КАК ФактическоеВремяСрезПоследних
	|%1",
	?(толькоОткрытые,
	"ГДЕ
	|	ФактическоеВремяСрезПоследних.Продолжительность = 0",
	""));
	
	запрос.УстановитьПараметр("Исполнитель", исполнитель);
	
	результатЗапроса = запрос.Выполнить();
	Если НЕ результатЗапроса.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(результат, результатЗапроса.Выгрузить()[0]);
	КонецЕсли;
	
	Возврат результат;
	
КонецФункции

#Область ПараметрыОбработкиРеквизитовОбъекта

Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	
	ОбязательныеРеквизиты.Добавить("ЗаказНаряд");
	ОбязательныеРеквизиты.Добавить("Период");
	ОбязательныеРеквизиты.Добавить("Цех");
	ОбязательныеРеквизиты.Добавить("Исполнитель");
	ОбязательныеРеквизиты.Добавить("Состояние");
	ОбязательныеРеквизиты.Добавить("ПакетРабот");
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции 

Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	УникальныеРеквизиты = Новый Структура();
	
	Возврат УникальныеРеквизиты;
	
КонецФункции 

#КонецОбласти

#КонецОбласти

#КонецЕсли