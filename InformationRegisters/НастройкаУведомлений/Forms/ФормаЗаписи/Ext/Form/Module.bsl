///////////////////////////////////////////////////////////////////////////////
// Модуль формы записи регистра "Настройка уведомлений"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораДокументов();
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ТекущийОбъект.ПредставлениеДокумента = СписокДокументов.НайтиПоЗначению(Запись.ТипДокумента).Представление;
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура КаналСвязиПриИзмененииНаСервере()
	
	Запись.КонтактнаяИнформация = "";
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КаналСвязиПриИзменении(Элемент)
	
	КаналСвязиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыРедактирования = УправлениеКонтактнойИнформациейАльфаАвтоКлиент.НовыеПараметрыРедактирования();
	ПараметрыРедактирования.ПутьКДанным = "Запись.КонтактнаяИнформация";
	ПараметрыРедактирования.ВидКонтактнойИнформации = ПредопределенноеЗначение(
		"Справочник.ВидыКонтактнойИнформации.ТелефонСотрудника");
	УправлениеКонтактнойИнформациейАльфаАвтоКлиент.НачатьРедактированиеКонтактнойИнформации(
	    ЭтотОбъект,
	    Элемент.ТекстРедактирования,
	    ПараметрыРедактирования
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораДокументов()
	
	СписокДокументовНапоминаний = Элементы.ТипДокумента.СписокВыбора;
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(Метаданные.Документы.ЗаявкаНаРемонт);
	МассивДокументов.Добавить(Метаданные.Документы.СводныйРемонтныйЗаказ);
	
	Для Каждого ТекущийДокумент Из МассивДокументов Цикл
		ПолноеИмя = ТекущийДокумент.ПолноеИмя();
		ИмяОбъекта = СтрРазделить(ПолноеИмя, ".")[1];
		СписокДокументовНапоминаний.Добавить(ИмяОбъекта, ТекущийДокумент.Представление());
	КонецЦикла;
	
	СписокДокументов = ОбщегоНазначения.СкопироватьРекурсивно(СписокДокументовНапоминаний);
	
КонецПроцедуры

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомРегистраСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры 

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомРегистраСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	
	Напоминание = (Запись.КаналСвязи = Перечисления.ТипыКаналовОтправкиУведомлений.Напоминание);
	СМС = (Запись.КаналСвязи = Перечисления.ТипыКаналовОтправкиУведомлений.СМС);
	ЭлПочта = (Запись.КаналСвязи = Перечисления.ТипыКаналовОтправкиУведомлений.ЭлектронноеПисьмо);
	
	Элементы.Контакт.Видимость = СМС ИЛИ ЭлПочта;
	Элементы.ОтправлятьСразу.Видимость = СМС ИЛИ ЭлПочта;
	Элементы.НомерТелефона.Видимость = СМС;
	Элементы.КонтактнаяИнформация.Видимость = ЭлПочта;
	Элементы.УчетнаяЗапись.Видимость = ЭлПочта;
	Элементы.Ответственный.Видимость = Напоминание;
	
КонецПроцедуры 

#КонецОбласти

#КонецОбласти

