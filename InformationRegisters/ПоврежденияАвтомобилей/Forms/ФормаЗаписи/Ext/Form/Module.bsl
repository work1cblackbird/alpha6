//////////////////////////////////////////////////////////////
// Форма записи регистра сведений "Повреждения автомобилей" //
//////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Отказ Или РаботаСФормой.НужноОтменитьОткрытиеФормы();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Запись.ИдентификаторПовреждения) Тогда
		Запись.ИдентификаторПовреждения = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	ОбновитьАвтораИДатуИзменения(АвторИзменение, Запись.Автор, Запись.ДатаИзменения);
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		НастроитьПараметрыВыбораЭлементовФормы();
		УправлениеДиалогомНаСервере();
	КонецЕсли;
	
	ЭтотОбъект.ТолькоПросмотр = НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПоврежденияАвтомобилей);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	НастроитьПараметрыВыбораЭлементовФормы();
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьАвтораИДатуИзменения(АвторИзменение, Запись.Автор, Запись.ДатаИзменения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ОбновитьАвтораИДатуИзменения(Представление, Автор, ДатаИзменения)
	
	Если НЕ ЗначениеЗаполнено(Автор) Тогда
		Представление = Новый ФорматированнаяСтрока(НСтр("ru = 'Нет информации'"));
		Возврат;
	КонецЕсли;
	
	Ссылка = ПолучитьНавигационнуюСсылку(Автор);
	Представление = Новый ФорматированнаяСтрока(
		НСтр("ru = 'Автор:'"),
		" ",
		Новый ФорматированнаяСтрока(Строка(Автор),, ЦветаСтиля.ТекстИнформационнойНадписи,, Ссылка),
		НСтр("ru = ', создание:'"),
		" ",
		Формат(ДатаИзменения, "ДФ='dd.MM.yyyy HH:mm'"),
		НСтр("ru = ', изменение:'"),
		" ",
		Формат(ДатаИзменения, "ДФ='dd.MM.yyyy HH:mm'"));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПараметрыВыбораЭлементовФормы()
	
	УправлениеДиалогомРегистраСервер.НастроитьПараметрыВыбораЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	УправлениеДиалогомРегистраСервер.УправлениеДиалогомНаСервере(ЭтотОбъект);
	
КонецПроцедуры 

#КонецОбласти