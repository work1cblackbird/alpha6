// Модуль менеджера регистра "Информация о запланированных ТО"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьДанныеПоЗапланированнымТО() Экспорт
	
	ФоновыеЗадания.Выполнить("Автосервис.ЗаполнитьДанныеПоЗапланированнымТОВФоне");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписьЗначенияРегистраСведения(Автомобиль, Объект) Экспорт
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		ДатаЗаписи = Объект.Дата;
	Иначе
		ДатаЗаписи = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// Значение параметра изменилось
	ЗаписьРегСведений = РегистрыСведений.ЗапланированныеТО.СоздатьМенеджерЗаписи();
	ЗаписьРегСведений.Период                 = ДатаЗаписи;
	ЗаписьРегСведений.Автомобиль             = Автомобиль;
	ЗаписьРегСведений.ПробегСледующегоТО     = Объект.ПробегСледующегоТО;
	ЗаписьРегСведений.ДатаСледующегоТО       = Объект.ДатаСледующегоТО;
	ЗаписьРегСведений.Автор                  = Пользователи.АвторизованныйПользователь();
	ЗаписьРегСведений.Источник               = Объект.Ссылка;
	ЗаписьРегСведений.ПредставлениеИсточника = Строка(Объект.Ссылка);
	
	Попытка
		ЗаписьРегСведений.Записать(Истина);
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Ошибка записи в регистр ""ЗапланированныеТО""'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			,
			Объект.Ссылка,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная
		);
	КонецПопытки;
	
КонецПроцедуры

Процедура ЧтениеЗначенияРегистраСведения(Автомобиль, ЭтотОбъект) Экспорт
	
	ЭтотОбъект.ПробегСледующегоТО = 0;
	ЭтотОбъект.ДатаСледующегоТО = Дата(1, 1, 1);
	
	Если Не ЗначениеЗаполнено(Автомобиль) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапланированныеТОСрезПоследних.ПробегСледующегоТО КАК ПробегСледующегоТО,
	|	ЗапланированныеТОСрезПоследних.ДатаСледующегоТО КАК ДатаСледующегоТО,
	|	ЗапланированныеТОСрезПоследних.Автомобиль КАК Автомобиль
	|ИЗ
	|	РегистрСведений.ЗапланированныеТО.СрезПоследних КАК ЗапланированныеТОСрезПоследних
	|ГДЕ
	|	ЗапланированныеТОСрезПоследних.Автомобиль = &Автомобиль";
	
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		ЭтотОбъект.ПробегСледующегоТО = ВыборкаДетальныеЗаписи.ПробегСледующегоТО;
		ЭтотОбъект.ДатаСледующегоТО = ВыборкаДетальныеЗаписи.ДатаСледующегоТО;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ПараметрыОбработкиРеквизитовОбъекта

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет обязательности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Массив - Возвращаемый массив содержит строковые идентификаторы реквизитов.
//
Функция ПолучитьОбязательныеРеквизиты(Объект) Экспорт
	
	// Обязательные реквизиты регистра
	ОбязательныеРеквизиты = ОбработкаСобытийРегистраСервер.ПолучитьСтандартныеОбязательныеРеквизиты(Объект);
	ОбязательныеРеквизиты.Добавить("ДатаСледующегоТО");
	
	// Возвращаем сформированные параметры проверки
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Производит формирование параметров проверки заполнения реквизитов объекта на предмет уникальности.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется получение параметров проверки.
//
// Возвращаемое значение:
//  Структура - Возвращаемая структура содержит строковые идентификаторы реквизитов.
//  Для описания проверки табличных частей используется
//  вложенная структура.
//
Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	// Структура уникальных реквизитов
	УникальныеРеквизиты = Новый Структура();
	
	// Возвращаем сформированные параметры проверки
	Возврат УникальныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

#КонецОбласти

#КонецОбласти

#КонецЕсли