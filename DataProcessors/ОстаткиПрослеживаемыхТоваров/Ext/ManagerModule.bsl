#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формирует документы инвентаризации 
//
// Параметры:
//  Параметры - ТаблицаИнвентаризации - товары, для которых проводится инвентаризация 
//  АдресРезультата - адрес временного хранилища
//
Процедура СформироватьИнвентаризации(Параметры, АдресРезультата) Экспорт
	
	Результат = Новый Структура("ТаблицаИнвентаризации, ТекстОшибки", Неопределено, "");	
	// Отберем строки, где нет у складов инвентаризаций
	СтруктураПоиска = Новый Структура("Инвентаризация", Неопределено);
	СтрокиБезИнвентаризаций = Параметры.ТаблицаИнвентаризации.НайтиСтроки(СтруктураПоиска);
	СкладыБезИнвентаризации = Новый Массив;
	СкладыБезИнвентаризацииАвтомобилей = Новый Массив;
	Для Каждого ТекущаяСтрока Из СтрокиБезИнвентаризаций Цикл
		Если ТекущаяСтрока.ЭтоТовар Тогда
			СкладыБезИнвентаризации.Добавить(ТекущаяСтрока.СкладКомпании);
		Иначе
			СкладыБезИнвентаризацииАвтомобилей.Добавить(ТекущаяСтрока.СкладКомпании);
		КонецЕсли;
	КонецЦикла;
	Если СкладыБезИнвентаризации.Количество() = 0
		И СкладыБезИнвентаризацииАвтомобилей.Количество() = 0 Тогда
		// По всем складам прошла инвентаризация
		Результат.ТекстОшибки = НСтр("ru = 'Инвентаризация по всем складам пройдена ранее. Операция отменена'");
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
		Возврат;
	КонецЕсли;	
	// Найдем прослеживаемый товар на складах
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ПартииТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ПартииТоваровКомпанииОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиТоваровПоПартиям
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
	|			&НаДату,
	|			Номенклатура.Прослеживаемый
	|				И СкладКомпании В (&СкладыКомпании)) КАК ПартииТоваровКомпанииОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпанииОстатки.Номенклатура,
	|	ПартииТоваровКомпанииОстатки.СкладКомпании,
	|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГТДПартийТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ГТДПартийТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиПоГТД
	|ИЗ
	|	РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
	|			&НаДату,
	|			ГТД.РНПТ
	|				И Номенклатура.Прослеживаемый
	|				И СкладКомпании В (&СкладыКомпании)) КАК ГТДПартийТоваровКомпанииОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДПартийТоваровКомпанииОстатки.СкладКомпании,
	|	ГТДПартийТоваровКомпанииОстатки.Номенклатура,
	|	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиТоваровПоПартиям.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровПоПартиям.СкладКомпании КАК СкладКомпании,
	|	ОстаткиТоваровПоПартиям.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровПоПартиям.КоличествоОстаток - ЕСТЬNULL(ОстаткиПоГТД.КоличествоОстаток, 0) КАК КоличествоФакт
	|ИЗ
	|	ОстаткиТоваровПоПартиям КАК ОстаткиТоваровПоПартиям
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоГТД КАК ОстаткиПоГТД
	|		ПО ОстаткиТоваровПоПартиям.Номенклатура = ОстаткиПоГТД.Номенклатура
	|			И ОстаткиТоваровПоПартиям.ХарактеристикаНоменклатуры = ОстаткиПоГТД.ХарактеристикаНоменклатуры
	|			И ОстаткиТоваровПоПартиям.СкладКомпании = ОстаткиПоГТД.СкладКомпании
	|ГДЕ
	|	ОстаткиТоваровПоПартиям.КоличествоОстаток - ЕСТЬNULL(ОстаткиПоГТД.КоличествоОстаток, 0) > 0
	|ИТОГИ
	|	СУММА(КоличествоФакт)
	|ПО
	|	СкладКомпании
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	|	ОстаткиАвтомобилейОстатки.СкладКомпании КАК СкладКомпании,
	|	ОстаткиАвтомобилейОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(
	|			&НаДату,
	|			Автомобиль.Прослеживаемый
	|				И (НЕ Автомобиль.ГТД.РНПТ ИЛИ Автомобиль.ГТД = ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка))
	|				И СкладКомпании В (&СкладыКомпанииАвтомобилей)) КАК ОстаткиАвтомобилейОстатки
	|ИТОГИ ПО
	|	СкладКомпании";
	Запрос.УстановитьПараметр("НаДату", Параметры.НаДату);
	Запрос.УстановитьПараметр("СкладыКомпании", СкладыБезИнвентаризации);
	Запрос.УстановитьПараметр("СкладыКомпанииАвтомобилей", СкладыБезИнвентаризацииАвтомобилей);
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	// Сначала сформируем инвентаризацию для товаров
	ВыборкаПоСкладу = ПакетЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПоСкладуАвтомобиля = ПакетЗапроса[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ПараметрыДействия = Новый Структура();
	ПараметрыДействия.Вставить("РежимПолученияЦеныПоСебестоимости", Истина);
	ПараметрыДействия.Вставить("МоментВремени", Параметры.НаДату);	
	СтруктураСтроки = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Себестоимость,Количество");
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	Попытка
		
		// Для каждого склада создадим отдельные инвентаризации
		Пока ВыборкаПоСкладу.Следующий() Цикл
			
			Выборка = ВыборкаПоСкладу.Выбрать();
			
			Если Выборка.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Инвентаризация = Документы.Инвентаризация.СоздатьДокумент();
			Инвентаризация.Заполнить(Неопределено);
			Инвентаризация.Дата = Параметры.НаДату;
			Инвентаризация.СкладКомпании = ВыборкаПоСкладу.СкладКомпании;
			
			// Заполним табличную часть товарами
			Пока Выборка.Следующий() Цикл
				
				НоваяСтрока = Инвентаризация.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				ПараметрыДействия.Вставить("КоличествоКнижн", Выборка.КоличествоФакт);
				Документы.Инвентаризация.ТоварыНоменклатураПриИзменении(Инвентаризация, НоваяСтрока, ПараметрыДействия);
				НоваяСтрока.КоличествоФакт = Выборка.КоличествоФакт / ?(НоваяСтрока.Коэффициент = 0, 1, НоваяСтрока.Коэффициент);
				Документы.Инвентаризация.ТоварыКоличествоФактПриИзменении(Инвентаризация, НоваяСтрока, ПараметрыДействия);
				
			КонецЦикла;
			
			Инвентаризация.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			СтруктураПоиска = Новый Структура("СкладКомпании,Инвентаризация,ЭтоТовар", 
					ВыборкаПоСкладу.СкладКомпании, Неопределено, Истина);
			СтрокиСклада = Параметры.ТаблицаИнвентаризации.НайтиСтроки(СтруктураПоиска);
			Если СтрокиСклада.Количество() > 0 Тогда
				СтрокиСклада[0].Инвентаризация = Инвентаризация.Ссылка;
			КонецЕсли;
			
		КонецЦикла;
		
		Пока ВыборкаПоСкладуАвтомобиля.Следующий() Цикл
			
			Выборка = ВыборкаПоСкладуАвтомобиля.Выбрать();
			Если Выборка.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			Инвентаризация = Документы.ИнвентаризацияАвтомобилей.СоздатьДокумент();
			Инвентаризация.Заполнить(Неопределено);
			Инвентаризация.Дата = Параметры.НаДату;
			Инвентаризация.СкладКомпании = ВыборкаПоСкладуАвтомобиля.СкладКомпании;
			Инвентаризация.СтатьяСписанияОбнаруженнойНедостачиТМЦ = 
					Справочники.СтатьиДоходовИРасходов.СписаниеОбнаруженнойНедостачиТМЦ;
			Инвентаризация.СтатьяОприходованияОбнаруженныхИзлишковТМЦ = 
					Справочники.СтатьиДоходовИРасходов.ОприходованиеОбнаруженныхИзлишковТМЦ;
			
			// Заполним табличную часть автомобилями
			Пока Выборка.Следующий() Цикл
				
				НоваяСтрока = Инвентаризация.Автомобили.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				Документы.ИнвентаризацияАвтомобилей.АвтомобилиАвтомобильПриИзменении(Инвентаризация, НоваяСтрока);
				Документы.ИнвентаризацияАвтомобилей.АвтомобилиЦенаПриИзменении(Инвентаризация, НоваяСтрока);
				
			КонецЦикла;
			
			Инвентаризация.Записать(РежимЗаписиДокумента.Проведение);
			СтруктураПоиска = Новый Структура("СкладКомпании,Инвентаризация,ЭтоТовар", 
					ВыборкаПоСкладуАвтомобиля.СкладКомпании, Неопределено, Ложь);
			СтрокиСклада = Параметры.ТаблицаИнвентаризации.НайтиСтроки(СтруктураПоиска);
			Если СтрокиСклада.Количество() > 0 Тогда
				СтрокиСклада[0].Инвентаризация = Инвентаризация.Ссылка;
			КонецЕсли;
			
		КонецЦикла;
		
		Результат.ТаблицаИнвентаризации = Параметры.ТаблицаИнвентаризации;
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
		ЗафиксироватьТранзакцию();
		
	Исключение
			
			ОтменитьТранзакцию();
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка записи документа ""Инвентаризация""'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				,
				,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
				РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная
			);
			ВызватьИсключение НСтр("ru = 'Не удалось создать документы инвентаризации.
				|Подробнее в журнале регистрации.'");
		
	КонецПопытки;

КонецПроцедуры

// Формирует документ уведомление об остатках прослеживаемых товаров
//
// Параметры:
//  Параметры - ТаблицаИнвентаризации - товары, для которых проводится инвентаризация 
//  АдресРезультата - адрес временного хранилища
//
Процедура СформироватьУведомления(Параметры, АдресРезультата) Экспорт
	
	Результат = Новый Структура("СписокУведомлений, ТекстОшибки", Неопределено, Новый Массив);
	
	// Сформируем список инвентаризаций
	ТаблицаИнвентаризации = Параметры.ТаблицаИнвентаризации;
	СписокУведомлений = Параметры.СписокУведомлений;
	НаДату = Параметры.НаДату;
	
	СписокУведомлений.Очистить();
	
	СписокИнвентаризаций = ТаблицаИнвентаризации.ВыгрузитьКолонку("Инвентаризация");
	
	// Исключим из нее те, по которым уже были созданы уведомления
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка КАК УведомлениеОбОстатках,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент КАК ПервичныйДокумент,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК КодТНВЭД
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент В(&ПервичныеДокументы)
	|	И НЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ПометкаУдаления";
	Запрос.УстановитьПараметр("ПервичныеДокументы", СписокИнвентаризаций);
	
	// Добавим полученные уведомления в список уведомлений
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = СписокУведомлений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
	ТекстОшибки = Новый Массив;
	
	ЗаполнитьУведомления(ТаблицаИнвентаризации, СписокУведомлений, НаДату, ТекстОшибки);
	
	Результат.Вставить("ТекстОшибки", ТекстОшибки);
	Результат.Вставить("СписокУведомлений", СписокУведомлений);
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьУведомления(ТаблицаИнвентаризации, СписокУведомлений, НаДату, ТекстОшибки)
	
	СписокИнвентаризацийАвтомобилей = Новый Массив;
	СписокИнвентаризацийТоваров = Новый Массив;
	
	Для Каждого ТекущаяСтрока Из ТаблицаИнвентаризации Цикл
		
		Если ТекущаяСтрока.ЭтоТовар Тогда
			СписокИнвентаризацийТоваров.Добавить(ТекущаяСтрока.Инвентаризация);
		Иначе
			СписокИнвентаризацийАвтомобилей.Добавить(ТекущаяСтрока.Инвентаризация);
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаИнвентаризаций.ПервичныйДокумент КАК Инвентаризация,
		|	ТаблицаИнвентаризаций.КодТНВЭД КАК КодТНВЭД
		|ПОМЕСТИТЬ ТаблицаИнвентаризацийСУведомлениями
		|ИЗ
		|	&ТаблицаИнвентаризаций КАК ТаблицаИнвентаризаций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИнвентаризацияТовары.Ссылка КАК ПервичныйДокумент,
		|	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
		|	ИнвентаризацияТовары.КоличествоФакт КАК КоличествоФакт,
		|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ИнвентаризацияТовары.Номенклатура.КодТНВЭД КАК КодТНВЭД,
		|	ИнвентаризацияТовары.Ссылка.СкладКомпании КАК СкладКомпании,
		|	ИнвентаризацияТовары.СуммаФакт КАК СуммаФакт,
		|	ИнвентаризацияТовары.Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаПрослеживаемости,
		|	ИнвентаризацияТовары.Номенклатура.КодОКПД2 КАК КодОКПД2
		|ПОМЕСТИТЬ ИнвентаризацииТоваровПрослеж
		|ИЗ
		|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
		|ГДЕ
		|	ИнвентаризацияТовары.Ссылка В(&СписокИнвентаризацийТоваров)
		|	И ИнвентаризацияТовары.Номенклатура.Прослеживаемый
		|	И ИнвентаризацияТовары.КоличествоФакт > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
		|	ИнвентаризацияТовары.КоличествоФакт КАК КоличествоФакт,
		|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ИнвентаризацияТовары.КодТНВЭД КАК КодТНВЭД,
		|	ИнвентаризацияТовары.СуммаФакт КАК СуммаФакт,
		|	ИнвентаризацияТовары.СкладКомпании КАК СкладКомпании,
		|	ИнвентаризацияТовары.ЕдиницаПрослеживаемости КАК ЕдиницаПрослеживаемости,
		|	ИнвентаризацияТовары.КодОКПД2 КАК КодОКПД2,
		|	ИнвентаризацияТовары.ПервичныйДокумент КАК ПервичныйДокумент
		|ПОМЕСТИТЬ ТаблицаИнвентаризаций
		|ИЗ
		|	ИнвентаризацииТоваровПрослеж КАК ИнвентаризацияТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИнвентаризацийСУведомлениями КАК ТаблицаИнвентаризацийСУведомлениями
		|		ПО ИнвентаризацияТовары.ПервичныйДокумент = ТаблицаИнвентаризацийСУведомлениями.Инвентаризация
		|			И ИнвентаризацияТовары.Номенклатура.КодТНВЭД = ТаблицаИнвентаризацийСУведомлениями.КодТНВЭД
		|ГДЕ
		|	ТаблицаИнвентаризацийСУведомлениями.Инвентаризация ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
		|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ПартииТоваровКомпанииОстатки.СуммаБезНДСОстаток КАК СуммаБезНДСОстаток,
		|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПартииТоваровКомпанииОстатки.СуммаБезНДСУпрОстаток КАК СуммаБезНДСУпрОстаток
		|ПОМЕСТИТЬ СуммыТоваровБезНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
		|			&НаДату,
		|			(Номенклатура, СкладКомпании) В
		|				(ВЫБРАТЬ
		|					ТаблицаИнвентаризаций.Номенклатура КАК Номенклатура,
		|					ТаблицаИнвентаризаций.СкладКомпании КАК СкладКомпании
		|				ИЗ
		|					ТаблицаИнвентаризаций КАК ТаблицаИнвентаризаций)) КАК ПартииТоваровКомпанииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИнвентаризаций.Номенклатура КАК Номенклатура,
		|	ТаблицаИнвентаризаций.КоличествоФакт КАК КоличествоФакт,
		|	ТаблицаИнвентаризаций.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаИнвентаризаций.КодТНВЭД КАК КодТНВЭД,
		|	ТаблицаИнвентаризаций.ЕдиницаПрослеживаемости КАК ЕдиницаПрослеживаемости,
		|	ТаблицаИнвентаризаций.ПервичныйДокумент КАК ПервичныйДокумент,
		|	СуммыТоваровБезНДС.СуммаБезНДСОстаток КАК СуммаБезНДСОстаток,
		|	СуммыТоваровБезНДС.КоличествоОстаток КАК КоличествоОстаток,
		|	ТаблицаИнвентаризаций.КодОКПД2 КАК КодОКПД2
		|ПОМЕСТИТЬ ТаблицаИнвекнтаризацииБезНДС
		|ИЗ
		|	ТаблицаИнвентаризаций КАК ТаблицаИнвентаризаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТоваровБезНДС КАК СуммыТоваровБезНДС
		|		ПО ТаблицаИнвентаризаций.СкладКомпании = СуммыТоваровБезНДС.СкладКомпании
		|			И ТаблицаИнвентаризаций.Номенклатура = СуммыТоваровБезНДС.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИнвентаризацияАвтомобилейАвтомобили.Ссылка КАК ПервичныйДокумент,
		|	ИнвентаризацияАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
		|	ИнвентаризацияАвтомобилейАвтомобили.Автомобиль.КодТНВЭД КАК КодТНВЭД,
		|	ИнвентаризацияАвтомобилейАвтомобили.Автомобиль.КодОКПД2 КАК КодОКПД2,
		|	ИнвентаризацияАвтомобилейАвтомобили.КоличествоУчет КАК Количество,
		|	ИнвентаризацияАвтомобилейАвтомобили.КоличествоУчет КАК КоличествоПрослеживаемости
		|ПОМЕСТИТЬ ИнвентаризацииПрослеживаемогоАвто
		|ИЗ
		|	Документ.ИнвентаризацияАвтомобилей.Автомобили КАК ИнвентаризацияАвтомобилейАвтомобили
		|ГДЕ
		|	ИнвентаризацияАвтомобилейАвтомобили.Ссылка В(&СписокИнвентаризацийАвтомобилей)
		|	И ИнвентаризацияАвтомобилейАвтомобили.КоличествоУчет > 0
		|	И ИнвентаризацияАвтомобилейАвтомобили.Автомобиль.Прослеживаемый
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИнвентаризацииПрослеживаемогоАвто.ПервичныйДокумент КАК ПервичныйДокумент,
		|	ИнвентаризацииПрослеживаемогоАвто.Номенклатура КАК Номенклатура,
		|	ИнвентаризацииПрослеживаемогоАвто.КодТНВЭД КАК КодТНВЭД,
		|	ИнвентаризацииПрослеживаемогоАвто.КодОКПД2 КАК КодОКПД2,
		|	ИнвентаризацииПрослеживаемогоАвто.Количество КАК Количество,
		|	ИнвентаризацииПрослеживаемогоАвто.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	ЕСТЬNULL(ОстаткиАвтомобилейОстатки.СуммаБезНДСОстаток, 0) КАК Сумма,
		|	ЕСТЬNULL(ОстаткиАвтомобилейОстатки.СуммаБезНДСУпрОстаток, 0) КАК СуммаУпр
		|ПОМЕСТИТЬ ИнвентаризацииПрослеживаемогоАвтоССуммой
		|ИЗ
		|	ИнвентаризацииПрослеживаемогоАвто КАК ИнвентаризацииПрослеживаемогоАвто
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей.Остатки(
		|				&НаДату,
		|				Автомобиль В
		|					(ВЫБРАТЬ
		|						ИнвентаризацииПрослеживаемогоАвто.Номенклатура
		|					ИЗ
		|						ИнвентаризацииПрослеживаемогоАвто КАК ИнвентаризацииПрослеживаемогоАвто)) КАК ОстаткиАвтомобилейОстатки
		|		ПО ИнвентаризацииПрослеживаемогоАвто.Номенклатура = ОстаткиАвтомобилейОстатки.Автомобиль
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаИнвекнтаризацииБезНДС.Номенклатура КАК Номенклатура,
		|	ТаблицаИнвекнтаризацииБезНДС.КоличествоФакт КАК Количество,
		|	ТаблицаИнвекнтаризацииБезНДС.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаИнвекнтаризацииБезНДС.СуммаБезНДСОстаток КАК Сумма,
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоПрослеживаемости,
		|	ТаблицаИнвекнтаризацииБезНДС.КодТНВЭД КАК КодТНВЭД,
		|	ТаблицаИнвекнтаризацииБезНДС.ЕдиницаПрослеживаемости КАК ЕдиницаИзмеренияПрослеживаемости,
		|	ТаблицаИнвекнтаризацииБезНДС.ПервичныйДокумент КАК ПервичныйДокумент,
		|	ТаблицаИнвекнтаризацииБезНДС.КодОКПД2 КАК КодОКПД2,
		|	ИСТИНА КАК ЭтоТовар,
		|	ТаблицаИнвекнтаризацииБезНДС.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	ТаблицаИнвекнтаризацииБезНДС КАК ТаблицаИнвекнтаризацииБезНДС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		|				&НаДату,
		|				(Номенклатура, СкладКомпании) В
		|					(ВЫБРАТЬ
		|						ТаблицаИнвентаризаций.Номенклатура КАК Номенклатура,
		|						ТаблицаИнвентаризаций.СкладКомпании КАК СкладКомпании
		|					ИЗ
		|						ТаблицаИнвентаризаций КАК ТаблицаИнвентаризаций)) КАК ОстаткиТоваровКомпанииОстатки
		|		ПО ТаблицаИнвекнтаризацииБезНДС.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
		|ГДЕ
		|	НЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИнвентаризацииПрослеживаемогоАвтоССуммой.Номенклатура,
		|	ИнвентаризацииПрослеживаемогоАвтоССуммой.Количество,
		|	""шт"",
		|	ИнвентаризацииПрослеживаемогоАвтоССуммой.Сумма,
		|	ИнвентаризацииПрослеживаемогоАвтоССуммой.КоличествоПрослеживаемости,
		|	ИнвентаризацииПрослеживаемогоАвтоССуммой.КодТНВЭД,
		|	""шт"",
		|	ИнвентаризацииПрослеживаемогоАвтоССуммой.ПервичныйДокумент,
		|	ИнвентаризацииПрослеживаемогоАвтоССуммой.КодОКПД2,
		|	ЛОЖЬ,
		|	1
		|ИЗ
		|	ИнвентаризацииПрослеживаемогоАвтоССуммой КАК ИнвентаризацииПрослеживаемогоАвтоССуммой
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИнвентаризацийСУведомлениями КАК ТаблицаИнвентаризацийСУведомлениями
		|		ПО ИнвентаризацииПрослеживаемогоАвтоССуммой.ПервичныйДокумент = ТаблицаИнвентаризацийСУведомлениями.Инвентаризация
		|			И ИнвентаризацииПрослеживаемогоАвтоССуммой.КодТНВЭД = ТаблицаИнвентаризацийСУведомлениями.КодТНВЭД
		|ГДЕ
		|	ТаблицаИнвентаризацийСУведомлениями.Инвентаризация ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТаблицаИнвентаризаций", СписокУведомлений);
	Запрос.УстановитьПараметр("НаДату", НаДату + 1);
	Запрос.УстановитьПараметр("СписокИнвентаризацийАвтомобилей", СписокИнвентаризацийАвтомобилей);
	Запрос.УстановитьПараметр("СписокИнвентаризацийТоваров", СписокИнвентаризацийТоваров);
	
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	
	ЗаписатьДокументУведомления(ПакетЗапроса[7].Выгрузить(), СписокУведомлений, ТекстОшибки);
	
КонецПроцедуры

Процедура ЗаписатьДокументУведомления(ТаблицыТоваров, СписокУведомлений, ТекстОшибки)
	
	// Сформируем количество документов
	ТаблицаДокументов = ТаблицыТоваров.Скопировать();
	ТаблицаДокументов.Свернуть("КодТНВЭД,ЕдиницаИзмерения,ЕдиницаИзмеренияПрослеживаемости,ПервичныйДокумент,ЭтоТовар");
	
	СтруктураПоиска = Новый Структура("КодТНВЭД,ЕдиницаИзмерения,ЕдиницаИзмеренияПрослеживаемости,ПервичныйДокумент,ЭтоТовар");
	
	Для Каждого ТекущийДокумент Из ТаблицаДокументов Цикл
		
		СтрокаУведомления = СписокУведомлений.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаУведомления, ТекущийДокумент);
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущийДокумент);
		
		УведомлениеОбОстатках = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.СоздатьДокумент();
		Если ТекущийДокумент.ЭтоТовар Тогда
			УведомлениеОбОстатках.ХозОперация = Справочники.ХозОперации.УведомлениеОбОстаткахПрослеживаемыхТоваров;
		Иначе
			УведомлениеОбОстатках.ХозОперация = Справочники.ХозОперации.УведомлениеОбОстаткахПрослеживаемыхАвтомобилей;
		КонецЕсли;
		
		УведомлениеОбОстатках.Заполнить(Неопределено);
		ЗаполнитьЗначенияСвойств(УведомлениеОбОстатках, ТекущийДокумент);
		УведомлениеОбОстатках.Дата = ТекущийДокумент.ПервичныйДокумент.Дата + 1;
		УведомлениеОбОстатках.Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.Новый;
		
		// Заполним табличную часть
		СтрокиТоваров = ТаблицыТоваров.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого СтрокаТовара Из СтрокиТоваров Цикл
			
			НоваяСтрока = УведомлениеОбОстатках.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара);
			Если НЕ ТекущийДокумент.ЭтоТовар Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока.КоличествоПрослеживаемости = СтрокаТовара.Количество * СтрокаТовара.Единицаизмерения.Коэффициент;
			Если СтрокаТовара.КоличествоОстаток > НоваяСтрока.КоличествоПрослеживаемости Тогда
				НоваяСтрока.Сумма = СтрокаТовара.Сумма / СтрокаТовара.КоличествоОстаток * НоваяСтрока.КоличествоПрослеживаемости;
			КонецЕсли;
			
		КонецЦикла;
		
		УведомлениеОбОстатках.СтранаПроисхождения =
			Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.СтранаПроисхождения(УведомлениеОбОстатках);
		Попытка
			УведомлениеОбОстатках.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстОшибки.Добавить(СтрШаблон(
				НСтр("ru = 'Не удалось сформировать уведомление для товаров с кодом ТН ВЭД %1 для инвентаризации %2 по причине %3%4'"),
				ТекущийДокумент.КодТНВЭД,
				ТекущийДокумент.ПервичныйДокумент,
				Символы.ПС,
				ОписаниеОшибки())
			);
			СписокУведомлений.Удалить(СтрокаУведомления);
			Продолжить;
		КонецПопытки;
		
		СтрокаУведомления.УведомлениеОбОстатках = УведомлениеОбОстатках.Ссылка;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
  
#КонецЕсли