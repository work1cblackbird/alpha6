#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Процедура обработки печати.
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_МХ1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
															"ПФ_MXL_МХ1",
															НСтр("ru = 'МХ-1 (Акт о приеме-передачи ТМЦ)'"),
															ПечатьМХ1Общая(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_МХ3") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
															"ПФ_MXL_МХ3",
															НСтр("ru = 'МХ-3 (Акт о возврате ТМЦ)'"),
															ПечатьМХ3Общая(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;													
КонецПроцедуры

Функция ПечатьМХ1Общая(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина; 
	Для Каждого Документ Из МассивОбъектов Цикл
		
		Если НЕ ПервыйДокумент Тогда // новый документ должен быть на отдельной странице
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ФорматВыводаСуммы      = УправлениеПечатьюПлатформа.ПолучитьФорматВыводаСуммы(Документ);
		ФорматВыводаКоличества = УправлениеПечатьюПлатформа.ПолучитьФорматВыводаКоличества(Документ);
		
		// Зададим параметры макета
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		// Получим данные из документа
		ДанныеДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Документ).ПолучитьДанныеДляПечатиМХ1(Документ);
		Если ЗначениеЗаполнено(ДанныеДокумента) Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьМХ.ПФ_MXL_МХ1");
			ОбластьЗаголовок        = Макет.ПолучитьОбласть("Заголовок");
			ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("Шапка");
			ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
			ОбластьИтого            = Макет.ПолучитьОбласть("Итого");
			ОбластьПодвал           = Макет.ПолучитьОбласть("Подвал");
			
			ОбластьЗаголовок.Параметры.Заполнить(ДанныеДокумента);
			
			СтруктураПредставления = Новый Структура;
			СтруктураПредставления.Вставить("Наименование",     "");
			СтруктураПредставления.Вставить("ИНН",              "ИНН ");
			СтруктураПредставления.Вставить("КПП",              "КПП ");
			СтруктураПредставления.Вставить("АдресЮридический", "");
			СтруктураПредставления.Вставить("ТелефонРабочий",   "тел.: ");
			СтруктураПредставления.Вставить("Факс",             "факс : ");
			
			ОбластьЗаголовок.Параметры.НомерДокумента                   = ДанныеДокумента.Номер;
			ОбластьЗаголовок.Параметры.ДатаДокумента                    = ДанныеДокумента.Дата;
			ОбластьЗаголовок.Параметры.ОрганизацияПоОКПО                = ДанныеДокумента.Организация.КодПоОКПО;
			ОбластьЗаголовок.Параметры.ОрганизацияВидДеятельностиПоОКДП = ДанныеДокумента.Организация.КодПоОКДП;
			ОбластьЗаголовок.Параметры.ПредставлениеАдресаХранителя     = ДанныеДокумента.СкладКомпании;
			
			// Подразделение полное наименование
			ОбластьЗаголовок.Параметры.ПодразделениеКомпании = УправлениеПечатьюПлатформа.ПолучитьНаименованиеСправочника(
				ОбластьЗаголовок.Параметры.ПодразделениеКомпании, Документ.Дата);
			
			ДополнительныеПараметры = УправлениеПечатьюПлатформа.ПолучитьПараметрыВызоваПредставленияСправочника();
			ДополнительныеПараметры.Вставить("ПодразделениеКомпании", Документ.ПодразделениеКомпании);
			ДополнительныеПараметры.ИспользоватьКИПодразделения = Истина;
			ДополнительныеПараметры.ИспользоватьКПППодразделения = Истина;
			ДополнительныеПараметры.НаДату = Документ.Дата;
			ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = УправлениеПечатьюПлатформа.ПолучитьПредставлениеСправочника(
				ОбластьЗаголовок.Параметры.Организация, СтруктураПредставления, ДополнительныеПараметры);
			
			Если ЗначениеЗаполнено(ДанныеДокумента.ВладелецТовара) Тогда
				ДополнительныеПараметры = УправлениеПечатьюПлатформа.ПолучитьПараметрыВызоваПредставленияСправочника();
				ДополнительныеПараметры.НаДату = Документ.Дата;
				ОбластьЗаголовок.Параметры.ПредставлениеВладельца = УправлениеПечатьюПлатформа.ПолучитьПредставлениеСправочника(
					ДанныеДокумента.ВладелецТовара, СтруктураПредставления, ДополнительныеПараметры);
				Если Тип("СправочникСсылка.СкладыКомпании") = ТипЗнч(ДанныеДокумента.ВладелецТовара) Тогда
					ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ДанныеДокумента.ВладелецТовара.Организация.КодПоОКПО;
				Иначе
					ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ДанныеДокумента.ВладелецТовара.КодПоОКПО;
				КонецЕсли;
			КонецЕсли;
			
			НомерСтраницы = 1;
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = НомерСтраницы;
			
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
			
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = НомерСтраницы + 1;
			
			ВыборкаСтрок = ДанныеДокумента.Товары;
			НомерСтроки = 1;
			КоличествоСтрок = ВыборкаСтрок.Количество();
			ОбластиДляПроверкиВывода = Неопределено;
			
			Для Каждого СтрокаТоваров Из ВыборкаСтрок Цикл
				ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
				ОбластьСтрока.Параметры.Номенклатура = Строка(СтрокаТоваров.Номенклатура); 
				ОбластьСтрока.Параметры.Код              =  УправлениеПечатьюПлатформа.ПолучитьЗначениеКолонкиКода(
						СтрокаТоваров.Номенклатура); 

				Если Тип("СправочникСсылка.Номенклатура") = ТипЗнч(СтрокаТоваров.Номенклатура) Тогда
					ОбластьСтрока.Параметры.Характеристика   = СтрокаТоваров.ХарактеристикаНоменклатуры;
					ОбластьСтрока.Параметры.ЕдиницаИзмерения = СтрокаТоваров.ЕдиницаИзмерения;
					ОбластьСтрока.Параметры.КодОКЕИ          = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
				КонецЕсли;
				ОбластьСтрока.Параметры.Цена = ?(СтрокаТоваров.Цена = 0, "", СтрокаТоваров.Цена);
				ОбластьСтрока.Параметры.Стоимость = ?(СтрокаТоваров.Сумма = 0, "", СтрокаТоваров.Сумма);
				ОбластьСтрока.Параметры.Количество = Формат(СтрокаТоваров.Количество, ФорматВыводаКоличества);
				
				Если НомерСтроки = КоличествоСтрок Тогда
					ОбластиДляПроверкиВывода = Новый Массив;
					ОбластиДляПроверкиВывода.Добавить(ОбластьИтого);
					ОбластиДляПроверкиВывода.Добавить(ОбластьПодвал);
				КонецЕсли;
				
				НомерСтраницы = УправлениеПечатьюПлатформа.ВывестиГоризонтальнуюОбласть(ТабличныйДокумент, ОбластьСтрока,
						ОбластьЗаголовокТаблицы, , НомерСтраницы, , Документ, ОбластиДляПроверкиВывода);
				
				Если НомерСтраницыПред <> НомерСтраницы Тогда
					НомерСтраницыПред = НомерСтраницы;
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = НомерСтраницы;
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			ОбластьИтого.Параметры.Количество = Формат(ВыборкаСтрок.Итог("Количество"), ФорматВыводаКоличества);
			ОбластьИтого.Параметры.ИтогЦена = ?(ВыборкаСтрок.Итог("Сумма") = 0, "", ВыборкаСтрок.Итог("Сумма"));
			ОбластьИтого.Параметры.ИтогСтоимость = ?(ВыборкаСтрок.Итог("Сумма") = 0, "", ВыборкаСтрок.Итог("Сумма"));
			ТабличныйДокумент.Вывести(ОбластьИтого);
			
			ОбластьПодвал.Параметры.Заполнить(ДанныеДокумента.Отпустил);
			ОбластьПодвал.Параметры.Заполнить(ДанныеДокумента.Получил);
			ТабличныйДокумент.Вывести(ОбластьПодвал);
		КонецЕсли;
		
		// Отметим конец области документа
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Документ);
		
	КонецЦикла;
	
	// Добавим установку параметров печати
	ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_МХ1Общая";
	УправлениеПечатьюПлатформа.УстановитьСтандартныеПараметрыПечати(ИмяПараметровПечати, ТабличныйДокумент);
	ТабличныйДокумент.ИмяПараметровПечати = ИмяПараметровПечати;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьМХ3Общая(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	Для Каждого Документ Из МассивОбъектов Цикл
		Если НЕ ПервыйДокумент Тогда // новый документ должен быть на отдельной странице
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ФорматВыводаСуммы      = УправлениеПечатьюПлатформа.ПолучитьФорматВыводаСуммы(Документ);
		ФорматВыводаКоличества = УправлениеПечатьюПлатформа.ПолучитьФорматВыводаКоличества(Документ);
		
		// Зададим параметры макета
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		// Получим данные из документа
		ДанныеДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Документ).ПолучитьДанныеДляПечатиМХ3(Документ);
		Если ЗначениеЗаполнено(ДанныеДокумента) Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьМХ.ПФ_MXL_МХ3");
			
			ОбластьЗаголовок        = Макет.ПолучитьОбласть("Заголовок");
			ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("Шапка");
			ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
			ОбластьИтого            = Макет.ПолучитьОбласть("Итого");
			ОбластьПодвал	        = Макет.ПолучитьОбласть("Подвал");
			
			ОбластьЗаголовок.Параметры.Заполнить(ДанныеДокумента);
			
			СтруктураПредставления = Новый Структура;
			СтруктураПредставления.Вставить("Наименование",     "");
			СтруктураПредставления.Вставить("ИНН",              "ИНН ");
			СтруктураПредставления.Вставить("КПП",              "КПП ");
			СтруктураПредставления.Вставить("АдресЮридический", "");
			СтруктураПредставления.Вставить("ТелефонРабочий",   "тел.: ");
			СтруктураПредставления.Вставить("Факс",   "факс : ");
			
			ОбластьЗаголовок.Параметры.НомерДокумента                   = ДанныеДокумента.Номер;
			ОбластьЗаголовок.Параметры.ДатаДокумента                    = ДанныеДокумента.Дата;
			ОбластьЗаголовок.Параметры.ОрганизацияПоОКПО                = ДанныеДокумента.Организация.КодПоОКПО;
			ОбластьЗаголовок.Параметры.ОрганизацияВидДеятельностиПоОКДП = ДанныеДокумента.Организация.КодПоОКДП;
			
			ДополнительныеПараметры = УправлениеПечатьюПлатформа.ПолучитьПараметрыВызоваПредставленияСправочника();
			ДополнительныеПараметры.Вставить("ПодразделениеКомпании", Документ.ПодразделениеКомпании);
			ДополнительныеПараметры.ИспользоватьКИПодразделения = Истина;
			ДополнительныеПараметры.ИспользоватьКПППодразделения = Истина;
			ДополнительныеПараметры.НаДату = ДанныеДокумента.Дата;
			ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = УправлениеПечатьюПлатформа.ПолучитьПредставлениеСправочника(
				ОбластьЗаголовок.Параметры.Организация, СтруктураПредставления, ДополнительныеПараметры);
			
			// Подразделение полное наименование.
			ОбластьЗаголовок.Параметры.ПодразделениеКомпании =
				УправлениеПечатьюПлатформа.ПолучитьНаименованиеСправочника(ОбластьЗаголовок.Параметры.ПодразделениеКомпании, Документ.Дата);
			
			Если ЗначениеЗаполнено(ДанныеДокумента.ВладелецТовара) Тогда
				
				ДополнительныеПараметры = УправлениеПечатьюПлатформа.ПолучитьПараметрыВызоваПредставленияСправочника();
				ДополнительныеПараметры.НаДату = ДанныеДокумента.Дата;
				ОбластьЗаголовок.Параметры.ПредставлениеВладельца = УправлениеПечатьюПлатформа.ПолучитьПредставлениеСправочника(
					ДанныеДокумента.ВладелецТовара, СтруктураПредставления, ДополнительныеПараметры);
				
				Если ТипЗнч(ДанныеДокумента.ВладелецТовара) = Тип("СправочникСсылка.СкладыКомпании") Тогда
					ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ДанныеДокумента.ВладелецТовара.Организация.КодПоОКПО;
				Иначе
					ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ДанныеДокумента.ВладелецТовара.КодПоОКПО;
				КонецЕсли;
				
			КонецЕсли;
			
			НомерСтраницы = 1;
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = НомерСтраницы;
			
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
			
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = НомерСтраницы + 1;
			
			ВыборкаСтрок = ДанныеДокумента.Товары;
			НомерСтроки = 1;
			
			КоличествоСтрок = ВыборкаСтрок.Количество();
			ОбластиДляПроверкиВывода = Неопределено;
			
			ЕстьЦена = НЕ (ВыборкаСтрок.Колонки.Найти("Цена") = Неопределено);
			ЕстьСуммаВсего = НЕ (ВыборкаСтрок.Колонки.Найти("СуммаВсего") = Неопределено);
			
			Для Каждого СтрокаТоваров Из ВыборкаСтрок Цикл
				ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
				ОбластьСтрока.Параметры.Номенклатура = Строка(СтрокаТоваров.Номенклатура); 
				ОбластьСтрока.Параметры.Код              =  УправлениеПечатьюПлатформа.ПолучитьЗначениеКолонкиКода(
						СтрокаТоваров.Номенклатура); 

				Если Тип("СправочникСсылка.Номенклатура") = ТипЗнч(СтрокаТоваров.Номенклатура) Тогда
					ОбластьСтрока.Параметры.Характеристика 	 = СтрокаТоваров.ХарактеристикаНоменклатуры;
					ОбластьСтрока.Параметры.ЕдиницаИзмерения = СтрокаТоваров.ЕдиницаИзмерения;
					ОбластьСтрока.Параметры.КодОКЕИ          = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
				ИначеЕсли Тип("СправочникСсылка.Автомобили") = ТипЗнч(СтрокаТоваров.Номенклатура) Тогда
					ОбластьСтрока.Параметры.Код 			 = СтрокаТоваров.Номенклатура.VIN;
					ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(796);
					ОбластьСтрока.Параметры.ЕдиницаИзмерения = ?(ЕдиницаИзмерения = Неопределено, "", ЕдиницаИзмерения);
					ОбластьСтрока.Параметры.КодОКЕИ          = ?(ЕдиницаИзмерения = Неопределено, "", ЕдиницаИзмерения.Код);
				КонецЕсли;
				ОбластьСтрока.Параметры.Количество = Формат(СтрокаТоваров.Количество, ФорматВыводаКоличества);
				ОбластьСтрока.Параметры.Цена       = ?(ЕстьЦена, Формат(СтрокаТоваров.Цена, ФорматВыводаСуммы), "");
				ОбластьСтрока.Параметры.СуммаВсего = ?(ЕстьСуммаВсего, Формат(СтрокаТоваров.СуммаВсего, ФорматВыводаСуммы), "");
				
				Если НомерСтроки = КоличествоСтрок Тогда
					ОбластиДляПроверкиВывода = Новый Массив;
					ОбластиДляПроверкиВывода.Добавить(ОбластьИтого);
				КонецЕсли;
				
				НомерСтраницы = УправлениеПечатьюПлатформа.ВывестиГоризонтальнуюОбласть(ТабличныйДокумент, ОбластьСтрока,
						ОбластьЗаголовокТаблицы, , НомерСтраницы, , Документ, ОбластиДляПроверкиВывода);
				
				Если НомерСтраницыПред <> НомерСтраницы Тогда
					НомерСтраницыПред = НомерСтраницы;
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = НомерСтраницы;
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			ОбластьИтого.Параметры.Количество = Формат(ВыборкаСтрок.Итог("Количество"), ФорматВыводаКоличества);
			ОбластьИтого.Параметры.Цена       = ?(ЕстьЦена, Формат(ВыборкаСтрок.Итог("Цена"), ФорматВыводаСуммы), "");
			ОбластьИтого.Параметры.СуммаВсего =
				?(ЕстьСуммаВсего, Формат(ВыборкаСтрок.Итог("СуммаВсего"), ФорматВыводаСуммы), "");
			ТабличныйДокумент.Вывести(ОбластьИтого);
			
			ОбластьПодвал.Параметры.Заполнить(ДанныеДокумента.Отпустил);
			ОбластьПодвал.Параметры.Заполнить(ДанныеДокумента.Получил);
			УправлениеПечатьюПлатформа.
			ВывестиГоризонтальнуюОбласть(ТабличныйДокумент, ОбластьПодвал,,, НомерСтраницы, , Документ);
			
		КонецЕсли;
			
			// Отметим конец области документа
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Документ);
			
		КонецЦикла;
		
	// Добавим установку параметров печати
	ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_МХ3Общая";
	УправлениеПечатьюПлатформа.УстановитьСтандартныеПараметрыПечати(ИмяПараметровПечати, ТабличныйДокумент);
	ТабличныйДокумент.ИмяПараметровПечати = ИмяПараметровПечати;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли