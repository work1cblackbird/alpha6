#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ИмяФайла;
Перем Файл;
Перем Макет;
Перем СтруктураМакета;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция получения макета прайс-листа.
//
// Параметры:
//  ПрайсЛист  - СправочникСсылка.ПрайсЛисты - Прайс-лист, по которому берется макет.
//
// Возвращаемое значение:
//  ОбработкаОбъект.ПисательПрайсЛистаВТабличныйДокумент - Объект обработки с указанным шаблоном.
//
Функция Инициализировать(ПрайсЛист) Экспорт
	
	ИмяФайла = ИмяФайла(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрайсЛист, "ШаблонИмениФайла"));
	Макет = ПолучитьМакет("Шаблон");
	СформироватьСтруктуруМакета(ПрайсЛист);
	Возврат ЭтотОбъект;
	
КонецФункции

// Вывод шапки табличного документа.
//
Процедура Начать() Экспорт
	
	Файл = Новый ТабличныйДокумент;
	ВывестиОбласть("Шапка");
	
КонецПроцедуры

// Запись файла с результатом формирования табличного документа.
//
// Возвращаемое значение:
//  Файл - записанный файл с результатом.
//
Функция Закончить() Экспорт
	
	Файл.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
	Файл = Неопределено;
	Возврат Новый Файл(ИмяФайла);
	
КонецФункции

// Сохранение строки в табличный документ.
//
// Параметры:
//  Строка  - Структура - данные для заполнения строки в табличном документе.
//
Процедура СохранитьСтроку(Строка) Экспорт
	
	ВывестиОбласть("Строка", Строка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяФайла(Шаблон)
	
	Если ПустаяСтрока(Шаблон) Тогда
		
		Возврат ПолучитьИмяВременногоФайла("xlsx");
		
	КонецЕсли;
	
	// Доступны шаблонные значения:
	// %г: год;
	// %м: месяц;
	// %д: день;
	// %ч: час;
	// %и: минута;
	// %с: секунда.
	ДатаСеанса = ТекущаяДатаСеанса();
	Имя = СтрЗаменить(Шаблон, "%г", Формат(Год(ДатаСеанса), "ЧГ=0"));
	Имя = СтрЗаменить(Имя, "%м",  Формат(Месяц(ДатаСеанса), "ЧЦ=2; ЧВН="));
	Имя = СтрЗаменить(Имя, "%д", Формат(День(ДатаСеанса), "ЧЦ=2; ЧВН="));
	Имя = СтрЗаменить(Имя, "%ч", Формат(Час(ДатаСеанса), "ЧЦ=2; ЧВН="));
	Имя = СтрЗаменить(Имя, "%и", Формат(Минута(ДатаСеанса), "ЧЦ=2; ЧВН="));
	Имя = СтрЗаменить(Имя, "%с", Формат(Секунда(ДатаСеанса), "ЧЦ=2; ЧВН="));
	
	Возврат СтрШаблон("%1%2.%3", КаталогВременныхФайлов(), Имя, "xlsx");
	
КонецФункции

Процедура СформироватьСтруктуруМакета(ПрайсЛист)
	
	ХранилищеЗначенияКолонокДляВыгрузки = ПрайсЛист.Ссылка.НастройкаКолонокДляВыгрузки;	
	НастройкиКолонокДляВыгрузки = Справочники.ПрайсЛисты.ПолучитьНастройкиКолонокДляВыгрузки(ХранилищеЗначенияКолонокДляВыгрузки);
	
	МассивОбластейСтрок = Новый Массив;
	МассивОбластейСтрок.Добавить("Шапка");
	МассивОбластейСтрок.Добавить("Строка");
	
	СтруктураМакета = Новый Структура;
	
	Для Каждого ИмяОбласти Из МассивОбластейСтрок Цикл
		
		МассивОбластей = Новый Массив;
		Для Каждого Настройка Из НастройкиКолонокДляВыгрузки Цикл
			Если НЕ Настройка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			МассивОбластей.Добавить(Макет.ПолучитьОбласть(ИмяОбласти + "|_"+ (Настройка.Индекс + 1)));
		КонецЦикла;
		
		СтруктураМакета.Вставить(ИмяОбласти, МассивОбластей); 
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВывестиОбласть(ИмяОбласти, Строка = Неопределено)
	
	МассивОбластей = СтруктураМакета[ИмяОбласти];
	ПерваяСтрока = Истина;
	
	Для Каждого Область Из МассивОбластей Цикл
		
		Если НЕ Строка = Неопределено Тогда
			Область.Параметры.Заполнить(Строка);
		КонецЕсли;
		
		Если ПерваяСтрока Тогда
			Файл.Вывести(Область);
			ПерваяСтрока = Ложь;
		Иначе
			Файл.Присоединить(Область); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли