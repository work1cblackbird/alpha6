
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Читаем параметры из регламентного задания.
	ПрочитатьПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	// Записываем параметры в регламентное задание.
	ЗаписатьПараметры();
	
	// Закрываем форму настроек.
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьПараметры()
	
	// Получаем регламентное задание.
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.НапоминаниеПроСнятиеРезервов);
	
	// Получаем текущие параметры.
	ПараметрыРегламентногоЗадания = РегламентноеЗадание.Параметры;
	
	//  Проверяем заполнение массива параметров.
	Если ПараметрыРегламентногоЗадания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем из массива структуру параметров и проверяем ее тип.
	СтруктураПараметров = ПараметрыРегламентногоЗадания[0];
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("НапоминаниеЗаказыПокупателей") Тогда
		НапоминаниеЗаказыПокупателей = СтруктураПараметров.НапоминаниеЗаказыПокупателей;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("НапоминаниеЗаказыНаАвтомобиль") Тогда
		НапоминаниеЗаказыНаАвтомобиль = СтруктураПараметров.НапоминаниеЗаказыНаАвтомобиль;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("НапоминаниеЗаказыСПредоплатой") Тогда
		НапоминаниеЗаказыСПредоплатой = СтруктураПараметров.НапоминаниеЗаказыСПредоплатой;
	КонецЕсли;
	
КонецПроцедуры // ПрочитатьПараметры()

&НаСервере
Процедура ЗаписатьПараметры()
	
	// Получаем регламентное задание.
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.НапоминаниеПроСнятиеРезервов);
	
	// Очищаем параметры.
	РегламентноеЗадание.Параметры.Очистить();
	
	// Формируем и заполняем новую структуру параметров.
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НапоминаниеЗаказыПокупателей",  НапоминаниеЗаказыПокупателей);
	СтруктураПараметров.Вставить("НапоминаниеЗаказыНаАвтомобиль", НапоминаниеЗаказыНаАвтомобиль);
	СтруктураПараметров.Вставить("НапоминаниеЗаказыСПредоплатой", НапоминаниеЗаказыСПредоплатой);
	
	// Добавляем структуру параметров в массив.
	ПараметрыРегламентногоЗадания = Новый Массив;
	ПараметрыРегламентногоЗадания.Добавить(СтруктураПараметров);
	
	// Передаем массив параметров в регламентное задание и записываем.
	РегламентноеЗадание.Параметры = ПараметрыРегламентногоЗадания;
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура НапоминаниеЗаказыПокупателейПриИзменении(Элемент)
	
	СброситьНапоминаниеЗаказыСПредоплатой()
	
КонецПроцедуры

&НаКлиенте
Процедура НапоминаниеЗаказыНаАвтомобильПриИзменении(Элемент)
	
	СброситьНапоминаниеЗаказыСПредоплатой()
	
КонецПроцедуры

&НаКлиенте
Процедура НапоминаниеЗаказыСПредоплатойПриИзменении(Элемент)
	
	СброситьНапоминаниеЗаказыСПредоплатой()
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьНапоминаниеЗаказыСПредоплатой()
	
	Если НЕ НапоминаниеЗаказыНаАвтомобиль И НЕ НапоминаниеЗаказыПокупателей Тогда
		НапоминаниеЗаказыСПредоплатой = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
