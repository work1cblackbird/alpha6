
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Читаем параметры из регламентного задания.
	ПрочитатьПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	// Записываем параметры в регламентное задание.
	ЗаписатьПараметры();
	
	// Закрываем форму настроек.
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьПараметры()
	
	// Получаем регламентное задание.
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ОтправкаSMS);
	
	// Получаем текущие параметры.
	ПараметрыРегламентногоЗадания = РегламентноеЗадание.Параметры;
	
	//  Проверяем заполнение массива параметров.
	Если ПараметрыРегламентногоЗадания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем из массива структуру параметров и проверяем её тип.
	СтруктураПараметров = ПараметрыРегламентногоЗадания[0];
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем параметр из структуры параметров.
	Если СтруктураПараметров.Свойство("АктуальноДней") Тогда
		АктуальноДней = СтруктураПараметров.АктуальноДней;
	КонецЕсли;
	
КонецПроцедуры // ПрочитатьПараметры()

&НаСервере
Процедура ЗаписатьПараметры()
	
	// Получаем регламентное задание.
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ОтправкаSMS);
	
	// Очищаем параметры.
	РегламентноеЗадание.Параметры.Очистить();
	
	// Формируем и заполняем новую структуру параметров.
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("АктуальноДней", АктуальноДней);
	
	// Добавляем структуру параметров в массив.
	ПараметрыРегламентногоЗадания = Новый Массив;
	ПараметрыРегламентногоЗадания.Добавить(СтруктураПараметров);
	
	// Передаем массив параметров в регламентное задание и записываем.
	РегламентноеЗадание.Параметры = ПараметрыРегламентногоЗадания;
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

#КонецОбласти
