
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Читаем параметры из регламентного задания.
	Если Параметры.Свойство("Идентификатор") Тогда
		Идентификатор = Параметры.Идентификатор; 
	КонецЕсли;
	ПрочитатьПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	// Записываем параметры в регламентное задание.
	ЗаписатьПараметры();
	
	// Закрываем форму настроек.
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьПараметры()
	
	// Получаем регламентное задание.
	РегламентноеЗадание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(Новый УникальныйИдентификатор(Идентификатор));
	
	// Получаем текущие параметры.
	ПараметрыРегламентногоЗадания = РегламентноеЗадание.Параметры;
	
	//  Проверяем заполнение массива параметров.
	Если ПараметрыРегламентногоЗадания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем из массива структуру параметров и проверяем её тип.
	СтруктураПараметров = ПараметрыРегламентногоЗадания[0];
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем параметры из структуры параметров.
	Если СтруктураПараметров.Свойство("ИмяМетода") Тогда
		ИмяПроизвольногоМетода = СтруктураПараметров.ИмяМетода;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("МассивПараметров") Тогда
		СписокПараметров.ЗагрузитьЗначения(СтруктураПараметров.МассивПараметров);
	КонецЕсли;

	
КонецПроцедуры // ПрочитатьПараметры()

&НаСервере
Процедура ЗаписатьПараметры()
	
	// Получаем регламентное задание.
	РегламентноеЗадание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(Новый УникальныйИдентификатор(Идентификатор));	
	// Очищаем параметры.
	РегламентноеЗадание.Параметры.Очистить();
	
	// Формируем и заполняем новую структуру параметров.
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИмяМетода",   ИмяПроизвольногоМетода); 
	СтруктураПараметров.Вставить("МассивПараметров", СписокПараметров.ВыгрузитьЗначения());
	
	// Добавляем структуру параметров в массив.
	ПараметрыРегламентногоЗадания = Новый Массив;
	ПараметрыРегламентногоЗадания.Добавить(СтруктураПараметров);
	
	// Передаем массив параметров в регламентное задание и записываем.
	РегламентноеЗадание.Параметры = ПараметрыРегламентногоЗадания;
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

#КонецОбласти
