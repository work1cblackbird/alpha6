
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Читаем параметры из регламентного задания.
	ПрочитатьПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	// Записываем параметры в регламентное задание.
	ЗаписатьПараметры();
	
	// Закрываем форму настроек.
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьПараметры()
	
	// Получаем регламентное задание.
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.АвтоматическоеСнятиеРезервов);
	
	// Получаем текущие параметры.
	ПараметрыРегламентногоЗадания = РегламентноеЗадание.Параметры;
	
	// Проверяем заполнение массива параметров.
	Если ПараметрыРегламентногоЗадания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем из массива структуру параметров и проверяем ее тип.
	СтруктураПараметров = ПараметрыРегламентногоЗадания[0];
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("ЗаказыПокупателей") Тогда
		ЗаказыПокупателей = СтруктураПараметров.ЗаказыПокупателей;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("ЗаказыВнутренние") Тогда
		ЗаказыВнутренние = СтруктураПараметров.ЗаказыВнутренние;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("ЗаказыНаАвтомобиль") Тогда
		ЗаказыНаАвтомобиль = СтруктураПараметров.ЗаказыНаАвтомобиль;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("КорректировкаЗаказа") Тогда
		КорректировкаЗаказа = СтруктураПараметров.КорректировкаЗаказа;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("СписаниеРезервовСПредоплатой") Тогда
		СписаниеРезервовСПредоплатой = СтруктураПараметров.СписаниеРезервовСПредоплатой;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("ФормированиеНапоминаний") Тогда
		ФормированиеНапоминаний = СтруктураПараметров.ФормированиеНапоминаний;
	КонецЕсли;
	
КонецПроцедуры // ПрочитатьПараметры()

&НаСервере
Процедура ЗаписатьПараметры()
	
	// Получаем регламентное задание.
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.АвтоматическоеСнятиеРезервов);
	
	// Очищаем параметры.
	РегламентноеЗадание.Параметры.Очистить();
	
	// Формируем и заполняем новую структуру параметров.
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЗаказыПокупателей",            ЗаказыПокупателей);
	СтруктураПараметров.Вставить("ЗаказыВнутренние",             ЗаказыВнутренние);
	СтруктураПараметров.Вставить("ЗаказыНаАвтомобиль",           ЗаказыНаАвтомобиль);
	СтруктураПараметров.Вставить("КорректировкаЗаказа",          КорректировкаЗаказа);
	СтруктураПараметров.Вставить("СписаниеРезервовСПредоплатой", СписаниеРезервовСПредоплатой);
	СтруктураПараметров.Вставить("ФормированиеНапоминаний",      ФормированиеНапоминаний);
	
	// Добавляем структуру параметров в массив.
	ПараметрыРегламентногоЗадания = Новый Массив;
	ПараметрыРегламентногоЗадания.Добавить(СтруктураПараметров);
	
	// Передаем массив параметров в регламентное задание и записываем.
	РегламентноеЗадание.Параметры = ПараметрыРегламентногоЗадания;
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

#КонецОбласти

