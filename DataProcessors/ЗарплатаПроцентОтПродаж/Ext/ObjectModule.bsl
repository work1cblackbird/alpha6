#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ДатаНачала Экспорт; // дата начала
Перем ДатаКонца Экспорт;  // дата конца
Перем Сотрудник Экспорт;  // ссылка на сотрудника

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получить базовую сумму
Функция БазоваяСумма() Экспорт
	БазоваяСумма=0;
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ТекстЗапроса ="ВЫБРАТЬ
		              |	ЕСТЬNULL(тбл1.СуммаПродажи, 0) КАК СуммаКупленные,
		              |	ЕСТЬNULL(тбл2.СуммаПродажи, 0) КАК СуммаПринятые,
		              |	ЕСТЬNULL(тбл3.СуммаПродажи, 0) КАК СуммаОтданные
		              |ИЗ
		              |	(ВЫБРАТЬ
		              |		СУММА(ПродажиОбороты.СуммаУпрОборот) КАК СуммаПродажи
		              |	ИЗ
		              |		РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ) КАК ПродажиОбороты
		              |	ГДЕ
		              |		ТИПЗНАЧЕНИЯ(ПродажиОбороты.Регистратор) В (ТИП(Документ.РеализацияТоваров), ТИП(Документ.РеализацияАктивов), ТИП(Документ.ЗаказНаряд))
		              |		И ПродажиОбороты.Регистратор.Менеджер = &Менеджер
		              |		И ПродажиОбороты.СтатусПартии = &СтатусПартииКупленный) КАК тбл1,
		              |	(ВЫБРАТЬ
		              |		СУММА(РеализованныеТовары1.СуммаПродажиПриход) КАК СуммаПродажи
		              |	ИЗ
		              |		РегистрНакопления.РеализованныеТовары.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ) КАК РеализованныеТовары
		              |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РеализованныеТовары.Обороты(, , Регистратор, ) КАК РеализованныеТовары1
		              |			ПО РеализованныеТовары.ДокументПередачи = РеализованныеТовары1.ДокументПередачи
		              |				И РеализованныеТовары.Номенклатура = РеализованныеТовары1.Номенклатура
		              |				И РеализованныеТовары.ХарактеристикаНоменклатуры = РеализованныеТовары1.ХарактеристикаНоменклатуры
		              |				И РеализованныеТовары.Контрагент = РеализованныеТовары1.Контрагент
		              |				И РеализованныеТовары.ДоговорВзаиморасчетов = РеализованныеТовары1.ДоговорВзаиморасчетов
		              |				И (РеализованныеТовары1.Регистратор.Менеджер = &Менеджер)
		              |				И (РеализованныеТовары.Регистратор ССЫЛКА Документ.ОтчетКомитенту)
		              |				И (РеализованныеТовары1.Регистратор ССЫЛКА Документ.РеализацияТоваров)) КАК тбл2,
		              |	(ВЫБРАТЬ
		              |		СУММА(ПартииТоваровОтданныеОбороты.СуммаУпрРасход) КАК СуммаПродажи
		              |	ИЗ
		              |		РегистрНакопления.ПартииТоваровОтданные.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ДокументПередачи.Менеджер = &Менеджер) КАК ПартииТоваровОтданныеОбороты
		              |	ГДЕ
		              |		ПартииТоваровОтданныеОбороты.Регистратор ССЫЛКА Документ.ОтчетКомиссионера
		              |		И ПартииТоваровОтданныеОбороты.ДокументПередачи ССЫЛКА Документ.РеализацияТоваров) КАК тбл3";
		Запрос = Новый Запрос();
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ДатаНачала", Новый Граница(НачалоДня(ДатаНачала),ВидГраницы.Включая));
		Запрос.УстановитьПараметр("ДатаКонца", Новый Граница(КонецДня(ДатаКонца),ВидГраницы.Включая));
		Запрос.УстановитьПараметр("Менеджер", Сотрудник);
		Запрос.УстановитьПараметр("СтатусПартииКупленный", Перечисления.СтатусыПартий.ТоварКупленный);
		// выполняем запрос
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			БазоваяСумма = Выборка.СуммаКупленные + Выборка.СуммаПринятые + Выборка.СуммаОтданные;
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат БазоваяСумма;
КонецФункции

#КонецОбласти

#КонецЕсли