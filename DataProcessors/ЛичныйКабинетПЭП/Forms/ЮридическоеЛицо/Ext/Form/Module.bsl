#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторУчастника = Параметры.ИдентификаторУчастника;
	ЮридическоеЛицо = Параметры.ЮридическоеЛицо;
	
	Если ЗначениеЗаполнено(ЮридическоеЛицо) Тогда
		
		ЮридическоеЛицоПриИзмененииНаСервере();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Наименование) Тогда
		
		Наименование = Параметры.Наименование;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИНН) Тогда
		
		ИНН = Параметры.ИНН;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Адрес) Тогда
		
		Адрес = Параметры.Адрес;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЮридическоеЛицоПриИзменении(Элемент)
	
	ЮридическоеЛицоПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЮридическоеЛицо(Команда)
	
	Если ПоляЗаполненыВерно() Тогда
		
		Если Не ЗначениеЗаполнено(ИдентификаторУчастника) Тогда
			
			ИдентификаторУчастника = УчастникиПЭПВызовСервера.ИдентификаторУчастника(ЮридическоеЛицо);
			
		КонецЕсли;
		
		Результат = Новый Структура();
		Результат.Вставить("ИдентификаторУчастника", ИдентификаторУчастника);
		Результат.Вставить("Наименование", Наименование);
		Результат.Вставить("ИНН", ИНН);
		Результат.Вставить("Адрес", Адрес);
		Результат.Вставить("ЮридическоеЛицо", ЮридическоеЛицо);
		Закрыть(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЮридическоеЛицоПриИзмененииНаСервере()
	
	ДанныеЮридическогоЛица = УчастникиПЭП.ДанныеЮридическогоЛица(ЮридическоеЛицо);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЮридическогоЛица, "Наименование,ИНН,Адрес");
	
	ИдентификаторУчастника = УчастникиПЭП.ИдентификаторУчастника(ЮридическоеЛицо);
	
КонецПроцедуры

&НаКлиенте
Функция ПоляЗаполненыВерно()
	
	Если Не ЗначениеЗаполнено(ЮридическоеЛицо) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "ЮридическоеЛицо";
		Сообщение.Текст = НСтр("ru = 'Не заполнено юр. лицо'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "Наименование";
		Сообщение.Текст = НСтр("ru = 'Не заполнено наименование'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "ИНН";
		Сообщение.Текст = НСтр("ru = 'Не заполнен ИНН'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "Адрес";
		Сообщение.Текст = НСтр("ru = 'Не заполнен адрес'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
