
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТарифы(ОбщегоНазначенияПЭП.МодульИнтеграции().ТарифнаяСетка());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЮридическоеЛицоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ЮридическоеЛицо) Тогда
		
		ЮридическоеЛицоПриИзмененииНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТарифы

&НаКлиенте
Процедура ТарифыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Сумма = Тарифы.НайтиПоИдентификатору(ВыбраннаяСтрока).Цена;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьСчет(Команда)
	
	Если РеквизитыЗаполеныВерно() Тогда
		
		Закрыть(УпаковатьЗначения());
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТарифы(ТарифнаяСетка)
	
	Тарифы.Очистить();
	
	Для Каждого Тариф Из ТарифнаяСетка Цикл
		
		НовыйТариф = Тарифы.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйТариф, Тариф);
		НовыйТариф.Действие = "Выбрать тариф";
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция РеквизитыЗаполеныВерно()
	
	Если Не ЗначениеЗаполнено(ЮридическоеЛицо) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "ЮридическоеЛицо";
		Сообщение.Текст = НСтр("ru = 'Не заполнено юридическое лицо для выставления счета на оплату'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресПочты) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "АдресПочты";
		Сообщение.Текст = НСтр("ru = 'Не заполнен адрес почты для выставления счета'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Телефон) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "Телефон";
		Сообщение.Текст = НСтр("ru = 'Не заполнен телефон для связи'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Сумма) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "Сумма";
		Сообщение.Текст = НСтр("ru = 'Не заполнена сумма пополнения'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция УпаковатьЗначения()
	
	//@skip-check structure-consructor-too-many-keys
	Возврат Новый Структура("ЮридическоеЛицо,АдресПочты,Телефон,Сумма", ЮридическоеЛицо, АдресПочты, Телефон, Сумма);
	
КонецФункции

&НаСервере
Процедура ЮридическоеЛицоПриИзмененииНаСервере()
	
	АдресПочты = УчастникиПЭП.ПочтаУчастника(ЮридическоеЛицо);
	Телефон = УчастникиПЭП.ТелефонУчастинка(ЮридическоеЛицо);
	
КонецПроцедуры

#КонецОбласти
