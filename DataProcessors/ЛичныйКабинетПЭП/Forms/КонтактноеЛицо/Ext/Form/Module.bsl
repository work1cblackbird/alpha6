
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторУчастника = Параметры.ИдентификаторУчастника;
	КонтактноеЛицо = Параметры.КонтактноеЛицо;
	
	Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		
		КонтактноеЛицоПриИзмененииНаСервере();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Имя) Тогда
		
		Имя = Параметры.Имя;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Почта) Тогда
		
		Почта = Параметры.Почта;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Телефон) Тогда
		
		Телефон = Параметры.Телефон;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)
	
	КонтактноеЛицоПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьКонтактноеЛицо(Команда)
	
	Если ПоляЗаполненыВерно() Тогда
		
		Если Не ЗначениеЗаполнено(ИдентификаторУчастника) Тогда
			
			ИдентификаторУчастника = УчастникиПЭПВызовСервера.ИдентификаторУчастника(КонтактноеЛицо);
			
		КонецЕсли;
		
		Результат = Новый Структура();
		Результат.Вставить("Имя", Имя);
		Результат.Вставить("ИдентификаторУчастника", ИдентификаторУчастника);
		Результат.Вставить("Почта", Почта);
		Результат.Вставить("Телефон", Телефон);
		Результат.Вставить("КонтактноеЛицо", КонтактноеЛицо);
		Закрыть(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура КонтактноеЛицоПриИзмененииНаСервере()
	
	ДанныеКонтактногоЛица = УчастникиПЭП.ДанныеКонтактногоЛица(КонтактноеЛицо);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеКонтактногоЛица);
	ИдентификаторУчастника = УчастникиПЭПВызовСервера.ИдентификаторУчастника(КонтактноеЛицо);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеОрганизации(Организация)
	
	Возврат Новый Структура(
		"ЮридическоеЛицо,ИдентификаторЮрЛица",
		Организация,
		УчастникиПЭП.ИдентификаторУчастника(Организация)
	);
	
КонецФункции

&НаКлиенте
Функция ПоляЗаполненыВерно()
	
	Если Не ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "КонтактноеЛицо";
		Сообщение.Текст = НСтр("ru = 'Не заполнено контактное лицо'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Имя) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "Имя";
		Сообщение.Текст = НСтр("ru = 'Не заполнено имя'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Телефон) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "Телефон";
		Сообщение.Текст = НСтр("ru = 'Не заполнен номер телефона.'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Почта) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле = "Почта";
		Сообщение.Текст = НСтр("ru = 'Не заполнен адрес электронной почты.'");
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
