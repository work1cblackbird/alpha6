#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Обработчик печати.
//
// Параметры:
//  МассивОбъектов         - Массив           - массив объектов для формирования печатных форм
//  ПараметрыПечати        - Структура        - дополнительные параметры печати
//  КоллекцияПечатныхФорм  - Таблица значений - таблица с идентификаторами печатных форм и макетами печати
//  ОбъектыПечати          - Структура        - список областей печатной формы в разрезе документов
//  ПараметрыВывода        - Структура        - дополнительные параметры вывода формы.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СопоставлениеКодовМаркировки") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"СопоставлениеКодовМаркировки",
		НСтр("ru = 'Сопоставление кодов маркировки'"),
		ПечатьСопоставлениеКодовМаркировки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
КонецПроцедуры // Печать()

Функция ПечатьСопоставлениеКодовМаркировки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	СписокКодовМаркировки = ПараметрыПечати.СписокКодовМаркировки;
	
	ТабДокумент = Новый ТабличныйДокумент();
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет   = ПолучитьМакет("СопоставлениеКодовМаркировки");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = НСтр("ru = 'Сопоставление кодов маркировки по документу'");
	
	Если ЗначениеЗаполнено(ПараметрыПечати.Документ) Тогда
		ОбластьМакета.Параметры.ТекстЗаголовка = 
			ОбластьМакета.Параметры.ТекстЗаголовка + Символы.ПС + Строка(ПараметрыПечати.Документ);
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	Для Каждого ТекущаяСтрока Из СписокКодовМаркировки Цикл
		
		Если ТекущаяСтрока.ДобавленаСтрока И НЕ ТекущаяСтрока.ВыборСтроки Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Номенклатура = ТекущаяСтрока.Номенклатура;
		ОбластьСтрока.Параметры.КодМаркировки = ТекущаяСтрока.КодМаркировки;
		
		Если ТекущаяСтрока.ДобавленаСтрока Тогда
			Статус = НСтр("ru = 'Добавлен при сверке'");
		ИначеЕсли ТекущаяСтрока.Подтверждено Тогда
			Статус = НСтр("ru = 'Подтвержден'");
		Иначе
			Статус = НСтр("ru = 'Не подтвержден'");
		КонецЕсли;
		ОбластьСтрока.Параметры.Сопоставление = Статус;
		
		ТабДокумент.Вывести(ОбластьСтрока);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьСопоставлениеКодовМаркировки()

#КонецОбласти

#КонецОбласти

#КонецЕсли