
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполним параметры
	Объект.Модель = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Модель");
	Объект.ВариантКомплектации = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ВариантКомплектации");
	
	Автомобиль = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Автомобиль");
	
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		
		ДанныеАвтомобиля = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Автомобиль, "Модель,ВариантКомплектации");
		Объект.Модель = ДанныеАвтомобиля.Модель;
		Объект.ВариантКомплектации = ДанныеАвтомобиля.ВариантКомплектации;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Модель) Тогда
		
		Производитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Модель, "Производитель");
		
	КонецЕсли;
	
	// Пока работаем только с одним типом файлов AutoData Online
	Объект.ТипФайла = "AutoData Online";
	КэшТипФайла = Объект.ТипФайла;
	
	Объект.ДатаДокумента = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Дата", ТекущаяДатаСеанса());
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Запретим самостоятельный вызов обработки
	Если ВладелецФормы = Неопределено Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Данная обработка используется другими объектами конфигурации!
			|Запрещен самостоятельный вызов!'"));
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяФайлаПриИзменении(Элемент)
	
	Если НЕ ПустаяСтрока(Объект.ИмяФайла) Тогда
		НачатьПомещениеФайлаЗагрузки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборФайла();
	
КонецПроцедуры // ИмяФайлаНачалоВыбора()

&НаКлиенте
Процедура ТипФайлаПриИзменении(Элемент)
		
	Если ЗначениеЗаполнено(Объект.ИмяФайла) 
		И Объект.ТипФайла <> КэшТипФайла Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаИзмененияТипаФайла", ЭтотОбъект);
    	ПоказатьВопрос(ОписаниеОповещения, Нстр("ru = 'Очистить таблицы и путь к файлу?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;	
	
	КэшТипФайла = Объект.ТипФайла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПометки(Команда)
	
	Значение = (Команда.Имя = "УстановитьПометки");
	Таблица  = ?(Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаАвтоработы, "Автоработы", "Запчасти");
	
	Для Каждого Строка Из Объект[Таблица] Цикл
		Строка.Пометка = Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦены(Команда)
	
	// Откроем форму документа Изменение цен авторабот.
	
	// Найдем есть ли выбранные работы из списка.
	ВыбранныеРаботы = Объект.Автоработы.НайтиСтроки(Новый Структура("Пометка", Истина));
	
	Если ВыбранныеРаботы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Сформируем данные для документа.
	ДанныеЗаполнения = ДанныеДокументаУстановкиЦен();
	
	// Не удалось сформировать данные.
	Если ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму(
		"Документ.ИзменениеЦенАвторабот.ФормаОбъекта",
		Новый Структура("Основание", ДанныеЗаполнения));
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	АдресХраненияРезультата = ВыгрузитьДанныеИзКаталога();
	
	Если АдресХраненияРезультата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(АдресХраненияРезультата);
	
КонецПроцедуры // ПеренестиВДокумент()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборФайла()
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипФайла) Тогда
		
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не выбран тип файла загрузки'"));
		Возврат;
		
	КонецЕсли;
	
	// Получим файл с данными
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите файл загрузки'");
	ДиалогВыбораФайла.Фильтр = ФильтрФайлаПоТипу();
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	
	Обработчик = Новый ОписаниеОповещения(
		"ВыборФайлаПродолжение",
		ЭтотОбъект);
	
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(Обработчик, ДиалогВыбораФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаПродолжение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	// Не выбрали файл для загрузки
	Если ВыбранныеФайлы = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Объект.ИмяФайла = ВыбранныеФайлы[0];
	НачатьПомещениеФайлаЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПомещениеФайлаЗагрузки()
	
	ОбработчикОповещенияОЗакрытии = Новый ОписаниеОповещения("ЗавершениеПомещенияФайла", ЭтотОбъект);
	НачатьПомещениеФайлов(ОбработчикОповещенияОЗакрытии,, Объект.ИмяФайла, Ложь, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПомещенияФайла(ПомещенныеФайлы, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ПомещенныеФайлы <> Неопределено И ПомещенныеФайлы.Количество() > 0 Тогда
		ФайлЗагрузки = Новый Файл(Объект.ИмяФайла);
		ЗагрузкаДанныхИзФайла(ПомещенныеФайлы[0].Хранение, ФайлЗагрузки.Расширение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузкаДанныхИзФайла(ПутьКФайлу, Расширение)
	
	НовыйПуть = ПрайсЛистыКонтрагентов.РаспаковатьФайл(ПутьКФайлу, Расширение);
	ТекущееИмяФайла = Объект.ИмяФайла;
	Объект.ИмяФайла = НовыйПуть;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СообщениеОбОшибке = "";
	ОбработкаОбъект.ПолучитьДанныеИзФайла(СообщениеОбОшибке);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	Объект.ИмяФайла = ТекущееИмяФайла;
	
	Если НЕ ПустаяСтрока(СообщениеОбОшибке) Тогда
		ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеДокументаУстановкиЦен()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ДанныеДокумента = ОбработкаОбъект.ДанныеДокументаУстановкиЦен();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Возврат ДанныеДокумента;
	
КонецФункции // ДанныеДокументаУстановкиЦен()

&НаСервере
Функция ВыгрузитьДанныеИзКаталога()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Результат = ОбработкаОбъект.ВыгрузитьДанныеИзКаталога();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(Результат);
	
КонецФункции // ВыгрузитьДанныеИзКаталога()

&НаКлиенте
Функция ФильтрФайлаПоТипу()
	
	Если Объект.ТипФайла = "AutoData Online" Тогда
		
		Возврат "Файл AutoData Online (*.xml)|*.xml";
		
	ИначеЕсли Объект.ТипФайла = "Автонормы" Тогда
		
		Возврат "Файл Автонормы (*.xml)|*.xml";
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПослеВопросаИзмененияТипаФайла(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ИмяФайла = "";		
		Объект.Автоработы.Очистить();
		Объект.Запчасти.Очистить();
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти
