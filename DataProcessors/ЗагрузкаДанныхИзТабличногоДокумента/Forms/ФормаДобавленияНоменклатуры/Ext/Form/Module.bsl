
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.СоответствиеНоменклатуры=Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Объект.СоответствиеНоменклатуры.Загрузить(Параметры.СоответствиеНоменклатуры.Выгрузить());
	
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды "Добавить".
//
&НаКлиенте
Процедура Добавить(Команда)
	
	ЗаполненыОбязательныеРеквизиты = Истина;
	
	Если НЕ ЗначениеЗаполнено(ТипНоменклатуры) Тогда
		ЗаполненыОбязательныеРеквизиты = Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru= 'Реквизит <Тип номенклатуры> не заполнен.'"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда
		// Если ставки ндс заполнены в таблица соответствия номенклатуры для тех позиций, которые собираемся загружать,
		//  то реквизит формы СтавкаНДС может оставаться не заполненным.
		СтавкаНДСОбязательна = Объект.СоответствиеНоменклатуры.НайтиСтроки(Новый Структура("Загружать, СтавкаНДС", Истина, ПредопределенноеЗначение("Справочник.СтавкиНДС.ПустаяСсылка"))).Количество()>0;
		Если СтавкаНДСОбязательна Тогда
			ЗаполненыОбязательныеРеквизиты = Ложь;
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru= 'Реквизит <Ставка НДС> не заполнен.'"));
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ЗаполненыОбязательныеРеквизиты=Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru= 'Реквизит <Единица измерения> не заполнен.'"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВалютаУчета) Тогда
		ЗаполненыОбязательныеРеквизиты=Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru= 'Реквизит <Валюта учета> не заполнен.'"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Артикул) Тогда
		ЗаполненыОбязательныеРеквизиты=Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru= 'Реквизит <№ по каталогу> не заполнен.'"));
	КонецЕсли;


	Если НЕ ЗаполненыОбязательныеРеквизиты Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьНоменклатуру();
	
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура создания номенклатуры
//
&НаСервере
Процедура СоздатьНоменклатуру()
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ЕдиницаИзмерения",    ЕдиницаИзмерения);
	ДанныеЗаполнения.Вставить("СтавкаНДС",           СтавкаНДС);
	ДанныеЗаполнения.Вставить("СтранаПроисхождения", СтранаПроисхождения);
	ДанныеЗаполнения.Вставить("ВалютаУчета",         ВалютаУчета);
	ДанныеЗаполнения.Вставить("Артикул",              Артикул);
	ЗагрузкаИзЭлектронныхДокументовВызовСервера.СоздатьНоменклатуру(Объект.СоответствиеНоменклатуры.Выгрузить(), 
																ГруппаНоменклатуры, 
																ТипНоменклатуры, 
																ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти


