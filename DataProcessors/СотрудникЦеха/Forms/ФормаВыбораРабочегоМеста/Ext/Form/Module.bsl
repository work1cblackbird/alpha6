#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("АвторизованныйСотрудник", Объект.АвторизованныйСотрудник);
	Параметры.Свойство("ТекущийЗаказНаряд", Объект.ТекущийЗаказНаряд);
	
	ЗаполнитьТаблицуРабочихМест();
	
	ПереключитьСтраницуНаСервере(1);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыборПоста(Команда)
	
	Идентификатор = Число(СтрЗаменить(Команда.Имя, "ВыборРабочегоМеста", ""));
	
	Строка = РабочиеМеста.НайтиПоИдентификатору(Идентификатор);
	
	Если Строка <> Неопределено Тогда
		Закрыть(Строка.РабочееМесто);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницу(Команда)
	
	ПереключитьСтраницуНаСервере(?(Команда.Имя = "СтраницаВперед", 1, -1));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УсловноеОформлениеНаСервере()
	
	Элементы.ПереключениеМеждуСтраницами.Видимость = (МаксимальнаяСтраница > 1);
	
	Элементы.СтраницаНазад.Доступность  = (ТекущаяСтраница > 1);
	Элементы.СтраницаВперед.Доступность = (ТекущаяСтраница < МаксимальнаяСтраница);
	
	Элементы.ДекорацияТекущаяСтраница.Заголовок = СтрШаблон("%1 из %2", ТекущаяСтраница, МаксимальнаяСтраница);
	
КонецПроцедуры

// Получение списка доступных рабочих мест
//
&НаСервере
Процедура ЗаполнитьТаблицуРабочихМест()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Цеха.Ссылка КАК РабочееМесто,
	|	Цеха.Родитель КАК Родитель
	|ПОМЕСТИТЬ РабочиеМестаУРВ
	|ИЗ
	|	Справочник.Цеха КАК Цеха
	|ГДЕ
	|	НЕ Цеха.ПометкаУдаления
	|	И Цеха.ИспользуетсяВУРВ
	|	И Цеха.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеСотрудника)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФактическоеВремяСрезПоследних.Цех КАК РабочееМесто,
	|	ФактическоеВремяСрезПоследних.Состояние КАК Состояние
	|ПОМЕСТИТЬ ОтметкиПоЦехам
	|ИЗ
	|	РегистрСведений.ФактическоеВремя.СрезПоследних(
	|			,
	|			Цех В
	|				(ВЫБРАТЬ
	|					РабочиеМестаУРВ.РабочееМесто
	|				ИЗ
	|					РабочиеМестаУРВ КАК РабочиеМестаУРВ)) КАК ФактическоеВремяСрезПоследних
	|ГДЕ
	|	ФактическоеВремяСрезПоследних.Продолжительность = 0
	|	И ФактическоеВремяСрезПоследних.ЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль <> &Автомобиль
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РабочиеМестаУРВ.РабочееМесто КАК РабочееМесто,
	|	РабочиеМестаУРВ.Родитель КАК Родитель
	|ИЗ
	|	РабочиеМестаУРВ КАК РабочиеМестаУРВ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОтметкиПоЦехам КАК ОтметкиПоЦехам
	|		ПО РабочиеМестаУРВ.РабочееМесто = ОтметкиПоЦехам.РабочееМесто
	|ГДЕ
	|	ОтметкиПоЦехам.РабочееМесто ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ПодразделениеСотрудника", Объект.АвторизованныйСотрудник.ПодразделениеКомпании);
	// можно выбрать цех, в котором уже ведется работа по выбираемому автомобилю
	Запрос.УстановитьПараметр("Автомобиль", Объект.ТекущийЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль);
	
	РабочиеМеста.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ИндексСтраницы = 1;
	Сч = 1;
	МаксимумВСтранице = 9;
	Для Каждого РабочееМесто Из РабочиеМеста Цикл
		
		Если Сч > МаксимумВСтранице Тогда
			ИндексСтраницы = ИндексСтраницы + 1;
			Сч = 1;
		КонецЕсли;
		
		РабочееМесто.ИндексСтраницы = ИндексСтраницы;
		
		РабочееМесто.Представление = СтрШаблон(
				"%1%2",
				РабочееМесто.РабочееМесто,
				?(ЗначениеЗаполнено(РабочееМесто.Родитель), СтрШаблон(" (%1)", РабочееМесто.Родитель), ""));
		
		Сч = Сч + 1;
		
	КонецЦикла;
	
	МаксимальнаяСтраница = ИндексСтраницы;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьКомандыВыбора()
	
	// удаляем элементы
	УдаляемыеЭлементы = Новый Массив;
	Для Каждого Элемент Из Элементы.ПерваяСтрока.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(Элемент);
	КонецЦикла;
	Для Каждого Элемент Из Элементы.ВтораяСтрока.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(Элемент);
	КонецЦикла;
	Для Каждого Элемент Из Элементы.ТретьяСтрока.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(Элемент);
	КонецЦикла;
	
	Для Каждого Элемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьКомандыВыбора(ИндексСтраницы)
	
	ОчиститьКомандыВыбора();
	
	КомандыСтраницы = РабочиеМеста.НайтиСтроки(Новый Структура("ИндексСтраницы", ИндексСтраницы));
	Если КомандыСтраницы.Количество() > 0 Тогда
		НомерСтроки = 1; Сч = 0;
		Для Каждого КомандаСтраницы Из КомандыСтраницы Цикл
			Если Сч > 2 Тогда
				НомерСтроки = НомерСтроки + 1; Сч = 0;
				Если НомерСтроки > 3 Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
			Если НомерСтроки = 1 Тогда
				Группа = Элементы.ПерваяСтрока;
			ИначеЕсли НомерСтроки = 2 Тогда
				Группа = Элементы.ВтораяСтрока;
			Иначе
				Группа = Элементы.ТретьяСтрока;
			КонецЕсли;
			
			ДобавитьКомандуВыбора(КомандаСтраницы.ПолучитьИдентификатор(), КомандаСтраницы.Представление, Группа);
			
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	
	// добавим декорации чтоб форма не прыгала
	Если Элементы.ПерваяСтрока.ПодчиненныеЭлементы.Количество() = 0 Тогда
		НоваяДекорация = Элементы.Добавить("ОтступВПервойСтроке", Тип("ДекорацияФормы"), Элементы.ПерваяСтрока);
		НоваяДекорация.Высота = 3;
		НоваяДекорация.РастягиватьПоВертикали = Истина;
	КонецЕсли;
	
	Если Элементы.ВтораяСтрока.ПодчиненныеЭлементы.Количество() = 0 Тогда
		НоваяДекорация = Элементы.Добавить("ОтступВоВторойСтроке", Тип("ДекорацияФормы"), Элементы.ВтораяСтрока);
		НоваяДекорация.Высота = 3;
		НоваяДекорация.РастягиватьПоВертикали = Истина;
	КонецЕсли;
	
	Если Элементы.ТретьяСтрока.ПодчиненныеЭлементы.Количество() = 0 Тогда
		НоваяДекорация = Элементы.Добавить("ОтступВТретьейСтроке", Тип("ДекорацияФормы"), Элементы.ТретьяСтрока);
		НоваяДекорация.Высота = 3;
		НоваяДекорация.РастягиватьПоВертикали = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьКомандуВыбора(Идентификатор, Представление, Группа)
	
	ИмяКоманды = СтрШаблон("ВыборРабочегоМеста%1", Идентификатор);
	
	НоваяКоманда = Команды.Найти(ИмяКоманды);
	Если НоваяКоманда = Неопределено Тогда
		НоваяКоманда          = Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Действие = "ВыборПоста";
	КонецЕсли;
	
	НоваяКнопка = Элементы.Добавить(СтрШаблон("КнопкаВыборПоста%1", Идентификатор), Тип("КнопкаФормы"), Группа);
	
	НоваяКнопка.Ширина       = 30;
	НоваяКнопка.Высота       = 3;
	НоваяКнопка.Заголовок    = Представление;
	НоваяКнопка.ИмяКоманды   = НоваяКоманда.Имя;
	НоваяКнопка.ЦветФона     = ЦветаСтиля.ЦветФонаШапкиОтчета;
	НоваяКнопка.Шрифт        = Новый Шрифт(ШрифтыСтиля.ОченьКрупныйШрифтТекста,,, Истина);
	
КонецФункции

&НаСервере
Процедура ПереключитьСтраницуНаСервере(Направление)
	
	ТекущаяСтраница = ТекущаяСтраница + Направление;
	СформироватьКомандыВыбора(ТекущаяСтраница);
	
	УсловноеОформлениеНаСервере();
	
КонецПроцедуры

#КонецОбласти

