#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьВидыОтметокВремени();
	ПереключитьСтраницуНаСервере(1);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыборОтметкиВремени(Команда)
	
	Идентификатор = Число(СтрЗаменить(Команда.Имя, "ВыборОтметкиВремени", ""));
	
	Строка = ВидыОтметокВремени.НайтиПоИдентификатору(Идентификатор);
	
	Если Строка <> Неопределено Тогда
		Закрыть(Строка.ОтметкаВремени);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницу(Команда)
	
	ПереключитьСтраницуНаСервере(?(Команда.Имя = "СтраницаВперед", 1, -1));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УсловноеОформлениеНаСервере()
	
	Элементы.ПереключениеМеждуСтраницами.Видимость = (МаксимальнаяСтраница > 1);
	
	Элементы.СтраницаНазад.Доступность  = (ТекущаяСтраница > 1);
	Элементы.СтраницаВперед.Доступность = (ТекущаяСтраница < МаксимальнаяСтраница);
	
	Элементы.ДекорацияТекущаяСтраница.Заголовок = СтрШаблон("%1 из %2", ТекущаяСтраница, МаксимальнаяСтраница);
	
КонецПроцедуры

&НаСервере
Процедура ПереключитьСтраницуНаСервере(Направление)
	
	ТекущаяСтраница = ТекущаяСтраница + Направление;
	СформироватьКомандыВыбора(ТекущаяСтраница);
	
	УсловноеОформлениеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьКомандыВыбора()
	
	// удаляем элементы
	УдаляемыеЭлементы = Новый Массив;
	Для Каждого Элемент Из Элементы.ПерваяСтрока.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(Элемент);
	КонецЦикла;
	Для Каждого Элемент Из Элементы.ВтораяСтрока.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(Элемент);
	КонецЦикла;
	Для Каждого Элемент Из Элементы.ТретьяСтрока.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(Элемент);
	КонецЦикла;
	
	Для Каждого Элемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыОтметокВремени()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыОтметокВремени.Ссылка КАК ОтметкаВремени
	|ИЗ
	|	Справочник.ВидыОтметокВремени КАК ВидыОтметокВремени
	|ГДЕ
	|	ВидыОтметокВремени.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.Работа)
	|	И ВидыОтметокВремени.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.НерабочееВремя)
	|	И ВидыОтметокВремени.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.РаботаПоУдаленномуПакету)";
	
	ВидыОтметокВремени.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ИндексСтраницы = 1;
	Сч = 1;
	МаксимумВСтранице = 9;
	Для Каждого Отметка Из ВидыОтметокВремени Цикл
		Если Сч > МаксимумВСтранице Тогда
			ИндексСтраницы = ИндексСтраницы + 1;
			Сч = 1;
		КонецЕсли;
		Отметка.ИндексСтраницы = ИндексСтраницы;
		Сч = Сч + 1;
	КонецЦикла;
	
	МаксимальнаяСтраница = ИндексСтраницы;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьКомандыВыбора(ИндексСтраницы)
	
	ОчиститьКомандыВыбора();
	
	КомандыСтраницы = ВидыОтметокВремени.НайтиСтроки(Новый Структура("ИндексСтраницы", ИндексСтраницы));
	Если КомандыСтраницы.Количество() > 0 Тогда
		НомерСтроки = 1; Сч = 0;
		Для Каждого КомандаСтраницы Из КомандыСтраницы Цикл
			Если Сч > 2 Тогда
				НомерСтроки = НомерСтроки + 1; Сч = 0;
				Если НомерСтроки > 3 Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
			Если НомерСтроки = 1 Тогда
				Группа = Элементы.ПерваяСтрока;
			ИначеЕсли НомерСтроки = 2 Тогда
				Группа = Элементы.ВтораяСтрока;
			Иначе
				Группа = Элементы.ТретьяСтрока;
			КонецЕсли;
			
			ДобавитьКомандуВыбора(КомандаСтраницы.ПолучитьИдентификатор(), КомандаСтраницы.ОтметкаВремени, Группа);
			
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	
	// добавим декорации чтоб форма не прыгала
	Если Элементы.ПерваяСтрока.ПодчиненныеЭлементы.Количество() = 0 Тогда
		НоваяДекорация = Элементы.Добавить("ОтступВПервойСтроке", Тип("ДекорацияФормы"), Элементы.ПерваяСтрока);
		НоваяДекорация.Высота = 3;
		НоваяДекорация.РастягиватьПоВертикали = Истина;
	КонецЕсли;
	
	Если Элементы.ВтораяСтрока.ПодчиненныеЭлементы.Количество() = 0 Тогда
		НоваяДекорация = Элементы.Добавить("ОтступВоВторойСтроке", Тип("ДекорацияФормы"), Элементы.ВтораяСтрока);
		НоваяДекорация.Высота = 3;
		НоваяДекорация.РастягиватьПоВертикали = Истина;
	КонецЕсли;
	
	Если Элементы.ТретьяСтрока.ПодчиненныеЭлементы.Количество() = 0 Тогда
		НоваяДекорация = Элементы.Добавить("ОтступВТретьейСтроке", Тип("ДекорацияФормы"), Элементы.ТретьяСтрока);
		НоваяДекорация.Высота = 3;
		НоваяДекорация.РастягиватьПоВертикали = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьКомандуВыбора(Идентификатор, Представление, Группа)
	
	ИмяКоманды = СтрШаблон("ВыборОтметкиВремени%1", Идентификатор);
	
	НоваяКоманда = Команды.Найти(ИмяКоманды);
	Если НоваяКоманда = Неопределено Тогда
		НоваяКоманда          = Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Действие = "ВыборОтметкиВремени";
	КонецЕсли;
	
	НоваяКнопка = Элементы.Добавить(СтрШаблон("КнопкаВыборИнтервала%1", Идентификатор), Тип("КнопкаФормы"), Группа);
	
	НоваяКнопка.Ширина       = 30;
	НоваяКнопка.Высота       = 3;
	НоваяКнопка.Заголовок    = Строка(Представление);
	НоваяКнопка.ИмяКоманды   = НоваяКоманда.Имя;
	НоваяКнопка.ЦветФона     = ЦветаСтиля.ЦветФонаШапкиОтчета;
	НоваяКнопка.Шрифт        = Новый Шрифт(ШрифтыСтиля.ОченьКрупныйШрифтТекста,,, Истина);
	
КонецФункции

#КонецОбласти
