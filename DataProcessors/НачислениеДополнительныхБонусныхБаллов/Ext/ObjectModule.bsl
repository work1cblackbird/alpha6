
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс


Функция СоздатьДокументыКорректировкиДополнительныхБонусов(Бонусы) Экспорт
	    	
		// создадим документ
		НовыйДокумент = Документы.КорректировкаБонусов.СоздатьДокумент();
		НовыйДокумент.Заполнить(Неопределено);
		НовыйДокумент.УстановитьНовыйНомер();
		НовыйДокумент.СозданРегламентно = Истина;
		НовыйДокумент.Комментарий = "Создан регламентным заданием ""Начисление дополнительных бонусных баллов""";
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Начисление дополнительных баллов'"), 
		УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Начато регламентное начисление дополнительных баллов'"));
		
		Для Каждого Элемент Из Бонусы Цикл			
			ДатаСписания = ?(Элемент.БонуснаяПрограмма.СрокСгоранияБонусов <> 0, НачалоДня(ТекущаяДатаСеанса() + 24*60*60*Элемент.БонуснаяПрограмма.СрокСгоранияБонусов), Дата("39990101"));
			НоваяСтрока = НовыйДокумент.БонусыКНачислению.Добавить();
			НоваяСтрока.Карточка              = Элемент.БонуснаяКарта;
			НоваяСтрока.ДатаСписания          = ДатаСписания;
			НоваяСтрока.Количество            = Элемент.КоличествоБонусов;
			НоваяСтрока.КоличествоКНачислению = 0;
			
			НовыйДокумент.БонусыКНачислению.Свернуть("Карточка,ДатаСписания", "Количество,КоличествоКНачислению");
			
			// добавим строку в рассылку
			ИспользоватьSMSРассылку   = Элемент.БонуснаяПрограмма.ИспользоватьSMSРассылку И ЗначениеЗаполнено(ШаблонРассылкиSMS) И Элемент.Контрагент.СогласиеНаПолучениеSMS;
			ИспользоватьEMailРассылку = Элемент.БонуснаяПрограмма.ИспользоватьEMailРассылку И ЗначениеЗаполнено(ШаблонРассылкиEMail);
			
			Если ИспользоватьSMSРассылку ИЛИ ИспользоватьEMailРассылку Тогда
				Рассылка = ТаблицаРассылок.Добавить();
				Рассылка.Контрагент        = Элемент.Контрагент;
				Рассылка.БонуснаяПрограмма = Элемент.БонуснаяПрограмма;
				Рассылка.БонуснаяКарта     = Элемент.БонуснаяКарта;
				Рассылка.ДатаСписания      = ДатаСписания;
				Рассылка.Количество        = Элемент.КоличествоБонусов;
				Рассылка.SMS               = ИспользоватьSMSРассылку;
				Рассылка.Email             = ИспользоватьEMailРассылку;
			КонецЕсли;
			
		КонецЦикла;
		
		Отказ = Ложь;
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
			
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Начисление дополнительных баллов'"), 
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Сформирован документ '")+ Строка(НовыйДокумент.Ссылка) + ".");
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке(); Отказ = Истина;
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Начисление дополнительных баллов'"), 
			УровеньЖурналаРегистрации.Ошибка, , ,
			НСтр("ru = 'Не сформирован регламентый документ для начисления дополнительных баллов.'"));
		КонецПопытки;
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Начисление дополнительных баллов'"), 
		УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Закончена регламентная процедура начисления дополнительных баллов'"));
		
		Если Отказ Тогда
			ТаблицаРассылок.Очистить();
		КонецЕсли;
		
		Возврат НовыйДокумент.Ссылка;
			
	Возврат Неопределено;
	
КонецФункции

Процедура ВыполнитьРассылкуУведомлений(ДокументОснование) Экспорт
	
	ИспользоватьПрочиеВзаимодействия = ПолучитьФункциональнуюОпцию("ИспользоватьПрочиеВзаимодействия");
	ИспользоватьПочтовыйКлиент       = ПолучитьФункциональнуюОпцию("ИспользоватьПочтовыйКлиент");
	
	// Проверим возможность выполнения рассылки
	Если НЕ ИспользоватьПрочиеВзаимодействия И НЕ ИспользоватьПочтовыйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	// создаем документ
	ДокументыКОтправкеSMS   = Новый Массив;
	ДокументыКОтправкеПисем = Новый Массив;
	
	БонусныеПрограммыСервер.ЗаполнитьКонтактнуюИнфрмациюРассылок(ТаблицаРассылок);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Рассылка оповещений о бонусных баллах'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,
		, ,
		НСтр("ru = 'Формирование документов и рассылка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())
	);
	
	Если ТаблицаРассылок.Количество() > 0 Тогда
		
		Для Каждого Рассылка Из ТаблицаРассылок Цикл
			
			// SMS
			Если ИспользоватьПрочиеВзаимодействия
				И Рассылка.SMS
				И НЕ ПустаяСтрока(Рассылка.Телефон)
				И ЗначениеЗаполнено(ШаблонРассылкиSMS) Тогда
				
				СтруктураРассылки = БонусныеПрограммыСервер.СтрокаРассылки();
				ЗаполнитьЗначенияСвойств(СтруктураРассылки, Рассылка);
				ДополнительныеПараметры = Новый Структура("СтрокаДанныхРассылки", СтруктураРассылки);
				
				СтруктураСообщения = ШаблоныСообщений.СформироватьСообщение(
					ШаблонРассылкиSMS,
					ЭтотОбъект,
					Новый УникальныйИдентификатор,
					ДополнительныеПараметры);
				ДокументыКОтправкеSMS.Добавить(БонусныеПрограммыСервер.СоздатьSMS(
							СтруктураСообщения,
							ДокументОснование, 
							Рассылка));
				
			КонецЕсли;
			
			// Email
			Если ИспользоватьПочтовыйКлиент
				И Рассылка.Email
				И НЕ ПустаяСтрока(Рассылка.Адрес)
				И ЗначениеЗаполнено(УчетнаяЗаписьЭлектроннойПочты)
				И ЗначениеЗаполнено(ШаблонРассылкиEMail) Тогда
				
				СтруктураРассылки = БонусныеПрограммыСервер.СтрокаРассылки();
				ЗаполнитьЗначенияСвойств(СтруктураРассылки, Рассылка);
				ДополнительныеПараметры = Новый Структура("СтрокаДанныхРассылки", СтруктураРассылки);
				
				СтруктураСообщения = ШаблоныСообщений.СформироватьСообщение(
					ШаблонРассылкиEMail,
					ЭтотОбъект,
					Новый УникальныйИдентификатор,
					ДополнительныеПараметры);
				ДокументыКОтправкеПисем.Добавить(БонусныеПрограммыСервер.СоздатьEmail(
							СтруктураСообщения,
							ДокументОснование,
							УчетнаяЗаписьЭлектроннойПочты, 
							Рассылка));
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ДокументыКОтправкеSMS.Количество() > 0 Тогда
		
		БонусныеПрограммыСервер.ОтправитьSMS(ДокументыКОтправкеSMS);
		
	КонецЕсли;
	
	Если ДокументыКОтправкеПисем.Количество() > 0 И ЗначениеЗаполнено(УчетнаяЗаписьЭлектроннойПочты) Тогда
		
		БонусныеПрограммыСервер.ОтправитьEmail(ДокументыКОтправкеПисем, УчетнаяЗаписьЭлектроннойПочты);		
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Рассылка оповещений о бонусных баллах'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,
		, ,
		НСтр("ru = 'Окончание формирования документов и рассылки'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())
	);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти

#КонецЕсли