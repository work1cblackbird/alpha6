
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПечати = Параметры.ПараметрыПечати;
	ПараметрыПечатиДляПринтераЭтикеток = Параметры.ПараметрыПечатиДляПринтераЭтикеток;
	
	ВыгружаемыеКолонки = "Выбран, ШаблонЦенника, КоличествоЦенников, ШаблонЭтикетки, КоличествоЭтикеток";
	КолонкиГруппировки = "Выбран, ШаблонЦенника, ШаблонЭтикетки";
	СуммируемыеКолонки = "КоличествоЦенников, КоличествоЭтикеток";
	
	РежимПечати = Параметры.ПараметрыПечати.РежимПечати;
	Если Найти(РежимПечати, "Ценники") Тогда
		ПечатьЦенников = Истина;
	Иначе
		ПечатьЦенников = Ложь;
	КонецЕсли;
	Если Найти(РежимПечати, "Этикетки") Тогда
		ПечатьЭтикеток = Истина;
	Иначе
		ПечатьЭтикеток = Ложь;
	КонецЕсли;
	
	Товары = ЭтаФорма.Параметры.ПараметрыПечати.Форма.Товары.Выгрузить(, ВыгружаемыеКолонки);
	Товары.Свернуть(КолонкиГруппировки, СуммируемыеКолонки);
	
	Для Каждого ТекТовар Из Товары Цикл
		
		Если ТекТовар.Выбран Тогда
			
			Если ПечатьЦенников И ТекТовар.ШаблонЦенника.ТипШаблона = Перечисления.ТипыШаблонов.ЭтикеткаЦенникПринтераЭтикеток Тогда
				
				НайденныеСтроки = ЭтикеткиИЦенники.НайтиСтроки(Новый Структура("Макет", ТекТовар.ШаблонЦенника));
				
				Если НайденныеСтроки.Количество()>0 Тогда
					НайденныеСтроки[0].КоличествоЦенников = НайденныеСтроки[0].КоличествоЦенников + ТекТовар.КоличествоЦенников;
				Иначе
					
					НоваяСтрока = ЭтикеткиИЦенники.Добавить();
					НоваяСтрока.Макет = ТекТовар.ШаблонЦенника;
					НоваяСтрока.РазмерМакета = ТекТовар.ШаблонЦенника.РазмерМакета;
					НоваяСтрока.ТипМакета = 1;
					НоваяСтрока.КоличествоЦенников = ТекТовар.КоличествоЦенников;
					
					Если Элементы.ТекущийРазмер.СписокВыбора.НайтиПоЗначению(НоваяСтрока.РазмерМакета)=Неопределено Тогда
						Элементы.ТекущийРазмер.СписокВыбора.Добавить(НоваяСтрока.РазмерМакета, НоваяСтрока.РазмерМакета);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ПечатьЭтикеток И ТекТовар.ШаблонЭтикетки.ТипШаблона = Перечисления.ТипыШаблонов.ЭтикеткаЦенникПринтераЭтикеток Тогда
				
				НайденныеСтроки = ЭтикеткиИЦенники.НайтиСтроки(Новый Структура("Макет", ТекТовар.ШаблонЭтикетки));
				
				Если НайденныеСтроки.Количество()>0 Тогда
					НайденныеСтроки[0].КоличествоЭтикеток = НайденныеСтроки[0].КоличествоЭтикеток + ТекТовар.КоличествоЭтикеток;
				Иначе
					
					НоваяСтрока = ЭтикеткиИЦенники.Добавить();
					НоваяСтрока.Макет = ТекТовар.ШаблонЭтикетки;
					НоваяСтрока.РазмерМакета = ТекТовар.ШаблонЭтикетки.РазмерМакета;
					НоваяСтрока.ТипМакета = 1;
					НоваяСтрока.КоличествоЭтикеток = ТекТовар.КоличествоЭтикеток;
					
					Если Элементы.ТекущийРазмер.СписокВыбора.НайтиПоЗначению(НоваяСтрока.РазмерМакета)=Неопределено Тогда
						Элементы.ТекущийРазмер.СписокВыбора.Добавить(НоваяСтрока.РазмерМакета, НоваяСтрока.РазмерМакета);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьВидимостьКолонок(ПечатьЦенников, ПечатьЭтикеток);
	
	Если ЭтикеткиИЦенники.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьКнопкиПечать();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийРазмерПриИзменении(Элемент)
	
	УстановитьДоступностьКнопкиПечать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	Если ЗначениеЗаполнено(ТекущийРазмер) Тогда
		ОтправитьНаПечать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаКомандыПечатиЦенниковИЭтикетокНаПринтереЭтикеток(ПараметрыПечати)
	
	Обработка = ПараметрыПечати.ОбъектыПечати[0];
	
	Если Обработка.Товары.НайтиСтроки(Новый Структура("Выбран", Истина)).Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбрано ни одного товара'"));
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПечати.Свойство("ИдентификаторФормы") Тогда
		ИдентификаторФормы = ПараметрыПечати.ИдентификаторФормы;
	Иначе
		ИдентификаторФормы = ПараметрыПечати.Форма.УникальныйИдентификатор;
	КонецЕсли;
	
	ДанныеДляПринтераЭтикеток = АльфаАвтоВызовСервера.ПодготовитьСтруктуруДанныхЦенниковИЭтикетокДляПринтераЭтикеток(Обработка, ПараметрыПечати.МенеджерПечати, ПараметрыПечати.ТекущийРазмер);
	
	Если ДанныеДляПринтераЭтикеток.Количество() > 0 Тогда
		
		Для Каждого ТекМакет Из ДанныеДляПринтераЭтикеток Цикл
			
			ЗавершениеПечати = Новый ОписаниеОповещения("ОбработкаКомандыПечатиЦенниковИЭтикетокНаПринтереЭтикетокЗавершение", ЭтотОбъект, ТекМакет);
			СтруктураПараметров = ОборудованиеПринтерыЭтикетокКлиент.ПараметрыОперацииПечатиЭтикеток(
																		ТекМакет.XML, ТекМакет.Этикетки);
			ОборудованиеПринтерыЭтикетокКлиент.НачатьПечатьЭтикеток(
													ЗавершениеПечати, ИдентификаторФормы, Неопределено, СтруктураПараметров); 					
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыПечатиЦенниковИЭтикетокНаПринтереЭтикетокЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		Возврат;
		
	Иначе
		
		ТекстСообщения = НСтр(
			"ru = 'При печати произошла ошибка.
			|Дополнительное описание:
			|%ДополнительноеОписание%'"
		);
		ТекстСообщения = СтрЗаменить(
			ТекстСообщения,
			"%ДополнительноеОписание%",
			РезультатВыполнения.ОписаниеОшибки
		);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаПечать()
	
	ПараметрыПечатиДляПринтераЭтикеток.Вставить("ТекущийРазмер", ТекущийРазмер);
	ОбработкаКомандыПечатиЦенниковИЭтикетокНаПринтереЭтикеток(ПараметрыПечатиДляПринтераЭтикеток);
	
	Для Каждого ТекСтрока Из ЭтикеткиИЦенники Цикл
		
		Если ТекСтрока.РазмерМакета = ТекущийРазмер Тогда
			ТекСтрока.Напечатано = Истина;
		КонецЕсли;
			
	КонецЦикла;
	
	Элементы.ТекущийРазмер.СписокВыбора.Удалить(Элементы.ТекущийРазмер.СписокВыбора.НайтиПоЗначению(ТекущийРазмер));
	ТекущийРазмер = Неопределено;
	УстановитьДоступностьКнопкиПечать();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопкиПечать()
	Элементы.Печать.Доступность = ЗначениеЗаполнено(ТекущийРазмер);
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКолонок(ПечатьЦенников, ПечатьЭтикеток)
	
	Элементы.ЭтикеткиИЦенникиКоличествоЦенников.Видимость = ПечатьЦенников;
	Элементы.ЭтикеткиИЦенникиКоличествоЭтикеток.Видимость = ПечатьЭтикеток;
	
КонецПроцедуры

#КонецОбласти