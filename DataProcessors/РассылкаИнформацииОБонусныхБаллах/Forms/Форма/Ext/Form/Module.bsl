///////////////////////////////////////////////////////////////////////////////
// Модуль основной формы обработки "Рассылка информации о бонусных баллах"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		Объект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СхемаКомпановкиДанных = Обработки.РассылкаИнформацииОБонусныхБаллах.ПолучитьМакет("Макет");
	АдресСхемыКомпановкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпановкиДанных, УникальныйИдентификатор);
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпановкиДанных));
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпановкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события нажатия кнопки "Выполнить рассылку" в контексте сервера.
//
&НаСервере
Процедура ВыполнитьРассылкуНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект", Тип("ОбработкаОбъект.РассылкаИнформацииОБонусныхБаллах"));
	Обработки.РассылкаИнформацииОБонусныхБаллах.ВыполнитьРассылку(ОбработкаОбъект);
	
КонецПроцедуры // ВыполнитьРассылкуНаСервере()

// Обработчик события нажатия кнопки "Выполнить рассылку".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура ВыполнитьРассылку(Команда)
	
	ВыполнитьРассылкуНаСервере();
	
КонецПроцедуры // ВыполнитьРассылку()

// Обработчик события нажатия кнопки "Сформировать" в контексте сервера.
//
&НаСервере
Процедура СформироватьНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект", Тип("ОбработкаОбъект.РассылкаИнформацииОБонусныхБаллах"));
	ОбработкаОбъект.СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпановкиДанных);
	ОбработкаОбъект.КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(КомпоновщикНастроекКомпоновкиДанных.Настройки);
	
	Объект.ДанныеДляРассылки.Загрузить(Обработки.РассылкаИнформацииОБонусныхБаллах.ПолучитьДанныеДляРассылки(ОбработкаОбъект));
	
КонецПроцедуры // СформироватьНаСервере()

// Обработчик события нажатия кнопки "Сформировать".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьНаСервере();
	
КонецПроцедуры // Сформировать()

#КонецОбласти

