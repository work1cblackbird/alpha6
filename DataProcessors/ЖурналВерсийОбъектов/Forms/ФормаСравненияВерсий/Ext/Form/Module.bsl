
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектСтарыйXML       = Параметры.Объект1;
	ОбъектНовыйXML        = Параметры.Объект2;
	ОбъектВерсионирования = Параметры.Объект;
	ДатаПредыдущейВерсии  = Параметры.Дата1;
	ДатаТекущейВерсии     = Параметры.Дата2;
	Пользователь          = Параметры.Пользователь;
	Компьютер             = Параметры.Компьютер;
	Событие               = Параметры.Событие;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПостроитьДерево();
	
	// Развернем дерево
	ПерваяСтрока = ДеревоИзменений.ПолучитьЭлементы()[0].ПолучитьИдентификатор();
	Элементы.ДеревоИзменений.Развернуть(ПерваяСтрока, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСжатуюВерсиюОбъекта()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивДат = Новый Массив;
	МассивДат.Добавить(ДатаПредыдущейВерсии);
	МассивДат.Добавить(ДатаТекущейВерсии);
	
	// Получим текущие версии по регистру
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВерсииОбъектов.ДатаВерсии КАК ДатаВерсии,
	               |	ВерсииОбъектов.ВерсияОбъекта КАК ВерсияОбъекта
	               |ИЗ
	               |	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	               |ГДЕ
	               |	ВерсииОбъектов.Объект = &Объект
	               |	И ВерсииОбъектов.ДатаВерсии В(&ДатаВерсии)";
	Запрос.УстановитьПараметр("Объект", ОбъектВерсионирования);
	Запрос.УстановитьПараметр("ДатаВерсии", МассивДат);
	
	ТаблицаВерсий = Запрос.Выполнить().Выгрузить();
	
	ОбъектПредыдущий = Неопределено;
	Если НЕ ЗначениеЗаполнено(ОбъектСтарыйXML) Тогда
		ЗаполнитьОбъект(ТаблицаВерсий, ОбъектПредыдущий, ДатаПредыдущейВерсии);
	Иначе
		ОбъектПредыдущий = ОбъектСтарыйXML;
	КонецЕсли;
	
	ОбъектТекущий = Неопределено;
	Если НЕ ЗначениеЗаполнено(ОбъектНовыйXML) Тогда
		ЗаполнитьОбъект(ТаблицаВерсий, ОбъектТекущий, ДатаТекущейВерсии);
	Иначе
		ОбъектТекущий = ОбъектНовыйXML;
	КонецЕсли;
	
	Возврат ВерсионированиеОбъектовПлатформа.ОпределитьРазличиеВерсийОбъекта(
		ОбъектПредыдущий,
		ОбъектТекущий,
		ОбъектВерсионирования);
	
КонецФункции // ПолучитьСжатуюВерсиюОбъекта()

&НаСервере
Процедура ЗаполнитьОбъект(ТаблицаВерсий, ОбъектXML, ДатаВерсии)
	
	АрхивОбъекта = ТаблицаВерсий.Найти(ДатаВерсии, "ДатаВерсии").ВерсияОбъекта.Получить();
	Если АрхивОбъекта <> Неопределено И ТипЗнч(АрхивОбъекта) = Тип("Структура") Тогда
		ДвоичныеДанные = АрхивОбъекта.Объект;
		ОбъектXML = Новый ЧтениеFastInfoset;
		ОбъектXML.УстановитьДвоичныеДанные(ДвоичныеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПостроитьДерево()
	ТипОбъекта = ТипЗнч(ОбъектВерсионирования);
	
	// Очистка
	ДеревоИзменений.ПолучитьЭлементы().Очистить();
	
	ВеткаРодителя = ДеревоИзменений.ПолучитьЭлементы().Добавить();
	
	ВеткаРодителя.Объект       = ОбъектВерсионирования;
	ВеткаРодителя.Период       = ДатаТекущейВерсии;
	ВеткаРодителя.Пользователь = Пользователь;
	ВеткаРодителя.Компьютер    = Компьютер;
	ВеткаРодителя.Событие      = Событие;
	
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипОбъекта) Тогда
		ВеткаРодителя.НомерМакета = 4;
	ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипОбъекта) Тогда
		ВеткаРодителя.НомерМакета = 5;
	ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипОбъекта) Тогда
		ВеткаРодителя.НомерМакета = 4;
	Иначе
		ВеткаРодителя.НомерМакета = 4; // Непонятно что. Считаем, что справочник
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОбъектСтарыйXML) ИЛИ НЕ ЗначениеЗаполнено(ОбъектНовыйXML) Тогда
		Таблица = ПолучитьСжатуюВерсиюОбъекта();
	Иначе
		Таблица = ВерсионированиеОбъектовПлатформа.ОпределитьРазличиеВерсийОбъекта(ОбъектСтарыйXML, ОбъектНовыйXML);
	КонецЕсли;
	
	Таблица.Сортировать("ИмяТЧ, НомерСтроки, НомерМетаданные");
	
	ВеткаТЧ = Неопределено;
	ТекТЧ = "";
	ВеткаСтрокаТЧ = Неопределено;
	ТекСтрокаТЧ = 0;
	Для Каждого ТекСтрока Из Таблица Цикл
		
		Если ТекСтрока.ИмяТЧ = "Реквизиты" Тогда
			
			НоваяСтрока = ВеткаРодителя.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Объект = ТекСтрока.Реквизит;
			НоваяСтрока.СтароеЗначение = ТекСтрока.СтароеЗначение;
			НоваяСтрока.НовоеЗначение  = ТекСтрока.НовоеЗначение;
			НоваяСтрока.НомерМакета = 7; // Реквизит
			Продолжить;
			
		КонецЕсли;
		
		Если ТекТЧ <> ТекСтрока.ИмяТЧ Тогда
			ТекСтрокаТЧ = 0;
			НоваяСтрока = ВеткаРодителя.ПолучитьЭлементы().Добавить();
			
			ТекТЧ = ТекСтрока.ИмяТЧ;
			ВеткаТЧ = НоваяСтрока;
			
			НоваяСтрока.Объект = Сред(ТекТЧ, 4);
			НоваяСтрока.НомерМакета = 6; // Табличная часть
			
		КонецЕсли;
		
		Если ТекСтрокаТЧ <> ТекСтрока.НомерСтроки Тогда
			
			ТекСтрокаТЧ = ТекСтрока.НомерСтроки;
			ВеткаСтрокаТЧ = ВеткаТЧ.ПолучитьЭлементы().Добавить();
			ВеткаСтрокаТЧ.Объект = "Строка №" + СокрЛП(ТекСтрокаТЧ);
			ВеткаСтрокаТЧ.НомерМакета = 8; // Строка ТЧ
			
		КонецЕсли;
		
		НоваяСтрока = ВеткаСтрокаТЧ.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Объект = ТекСтрока.Реквизит;
		НоваяСтрока.СтароеЗначение = ТекСтрока.СтароеЗначение;
		НоваяСтрока.НовоеЗначение  = ТекСтрока.НовоеЗначение;
		НоваяСтрока.НомерМакета = 7; // Реквизит
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
