#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляСхемыКомпоновкиДанных(ПараметрыИнициализации) Экспорт
	
	// Заполним параметры инициализации
	ПоставщикНеЗаполнен = ПараметрыИнициализации.ПоставщикНеЗаполнен;
	РабочийТипЦенЗакупки = ПараметрыИнициализации.РабочийТипЦенЗакупки;
	ЗакупкиВВалютеУчета = РабочийТипЦенЗакупки.ВВалютеУчета;
	РабочийТипЦенПродажи = ПараметрыИнициализации.РабочийТипЦенПродажи;
	ПродажиВВалютеУчета = РабочийТипЦенПродажи.ВВалютеУчета;
	ТекущееПодразделение = ПараметрыИнициализации.ТекущееПодразделение;
	РасчетТолькоПоГлавнымАналогам = ПараметрыИнициализации.РасчетТолькоПоГлавнымАналогам;
	
	ТекстПодразделений = "";
	ПараметрыПодразделений = Новый Структура;
	
	#Область ТекстЗапросаПоПодразделениям
	Если НЕ ЗначениеЗаполнено(ТекущееПодразделение) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПодразделенияКомпании.Ссылка КАК Подразделение
		|ИЗ
		|	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
		|ГДЕ
		|	ПодразделенияКомпании.Родитель = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка)");
		ТекущееПодразделение = Запрос.Выполнить().Выгрузить()[0].Подразделение;
	КонецЕсли;
	
	Уровень = 0;
	
	Пока ЗначениеЗаполнено(ТекущееПодразделение) Цикл
		ТекстПодразделений = ТекстПодразделений + ?(Уровень = 0, "", Символы.ПС + " ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС);
		ТекстПодразделений = ТекстПодразделений
		+ "ВЫБРАТЬ
		|	&Подразделение" + Уровень + " КАК Подразделение,
		|	" + Уровень + " КАК Уровень";
		ПараметрыПодразделений.Вставить("Подразделение" + Уровень, ТекущееПодразделение);
		
		// Получаем родителя подразделения
		ТекущееПодразделение = ТекущееПодразделение.Родитель;
		Уровень = Уровень + 1;
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаПодразделений.Подразделение КАК Справочник.ПодразделенияКомпании) КАК Подразделение,
	|	ТаблицаПодразделений.Уровень КАК Уровень
	|	ПОМЕСТИТЬ ТаблицаПодразделений
	|ИЗ
	|	(" + ТекстПодразделений + "
	|	ОБЪЕДИНИТЬ ВСЕ
	|	  ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка),
	|		" + Уровень + "
	|	) КАК ТаблицаПодразделений
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;";
	#КонецОбласти
	
	#Область ОсновнойЗапрос
	ТекстЗапроса = ТекстЗапроса + "
	|// ПОСЛЕДНЯЯ ДАТА ПОСТУПЛЕНИЯ ТОВАРА
	|ВЫБРАТЬ
	|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ПартииТоваровКомпании.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ПартииТоваровКомпании.Номенклатура.ЦеноваяГруппа КАК ЦеноваяГруппаНоменклатуры,
	|	МАКСИМУМ(ПартииТоваровКомпании.Период) КАК ДатаПоследнейПоставки
	|ПОМЕСТИТЬ
	|	ТаблицаТовары
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ПартииТоваровКомпании.Количество > 0" + ?(ПоставщикНеЗаполнен, "", "
	|	И ПартииТоваровКомпании.Партия.Контрагент = &Поставщик") + "
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.Номенклатура.ТипНоменклатуры,
	|	ПартииТоваровКомпании.Номенклатура.ЦеноваяГруппа
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Главные аналоги
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Ссылка КАК Номенклатура,
	|	ТаблицаНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаНоменклатуры.ЦеноваяГруппа КАК ЦеноваяГруппаНоменклатуры,
	|	ТаблицаАналогов.ИдентификаторГруппы КАК ГруппаАналогов
	|ПОМЕСТИТЬ ТаблицаГлавныхАналоговПоГруппе
	|ИЗ
	|	Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГруппыАналогов КАК ТаблицаАналогов
	|	ПО ТаблицаНоменклатуры.Артикул = ТаблицаАналогов.Артикул
	|	И ТаблицаАналогов.ГлавныйПоГруппе
	|ИНДЕКСИРОВАТЬ ПО
	|	ГруппаАналогов
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.ЦеноваяГруппаНоменклатуры КАК ЦеноваяГруппаНоменклатуры
	|ПОМЕСТИТЬ ТаблицаЦеновыхГрупп
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|" + ?(РасчетТолькоПоГлавнымАналогам, "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаГлавныхАналоговПоГруппе.ЦеноваяГруппаНоменклатуры
	|ИЗ
	|	ТаблицаГлавныхАналоговПоГруппе КАК ТаблицаГлавныхАналоговПоГруппе
	|;", ";") + "
	|
	|" + РазверткаТиповЦенДоБазовых() + "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаЦеновыхГрупп
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТиповЦен.ТипЦен КАК ТипЦен,
	|	ТаблицаТиповЦен.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ТаблицаТиповЦен.БазовыйТипЦен.АлгоритмПолученияЦены КАК АлгоритмПолученияЦены,
	|	ТаблицаТиповЦен.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	ТаблицаТиповЦен.КоэффициентЦены КАК КоэффициентЦены
	|ПОМЕСТИТЬ БазовыеТипыЦенЗакупки
	|ИЗ
	|	ТаблицаТиповЦен КАК ТаблицаТиповЦен
	|ГДЕ
	|	ТаблицаТиповЦен.ТипЦен = &ЗакупочнаяЦена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТиповЦен.ТипЦен КАК ТипЦен,
	|	ТаблицаТиповЦен.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ТаблицаТиповЦен.БазовыйТипЦен.АлгоритмПолученияЦены КАК АлгоритмПолученияЦены,
	|	ТаблицаТиповЦен.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	ТаблицаТиповЦен.КоэффициентЦены КАК КоэффициентЦены
	|ПОМЕСТИТЬ БазовыеТипыЦенПродажи
	|ИЗ
	|	ТаблицаТиповЦен КАК ТаблицаТиповЦен
	|ГДЕ
	|	ТаблицаТиповЦен.ТипЦен = &РозничнаяЦена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаТиповЦен
	|;
	|
	|//ЗАКУПОЧНЫЕ ЦЕНЫ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодразделенияКомпании.Уровень КАК Уровень,
	|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныСрезПоследних.ТипЦен КАК ТипЦен,
	|	ЦеныСрезПоследних.Номенклатура.ЦеноваяГруппа КАК ЦеноваяГруппаНоменклатуры,
	|	ЦеныСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЦены,
	|	ЦеныСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЦеныСрезПоследних.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ЦеныСрезПоследних.ЕдиницаИзмерения=ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|			1
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ЦеныСрезПоследних.ЕдиницаИзмерения.Коэффициент = 0 ТОГДА
	|				1
	|			ИНАЧЕ
	|				ЦеныСрезПоследних.ЕдиницаИзмерения.Коэффициент
	|			КОНЕЦ
	|	КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|	ЦеныСрезПоследних.Цена КАК Цена
	|	
	|ПОМЕСТИТЬ ПредварительнаяТаблицаВсехЗакупочныхЦен
	|ИЗ
	|	ТаблицаПодразделений КАК ПодразделенияКомпании
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(
	|				&ДатаРасчета,
	|				ТипЦен В (ВЫБРАТЬ БазовыйТипЦен ИЗ БазовыеТипыЦенЗакупки)" + ?(ПоставщикНеЗаполнен, "
	|				И Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)", "
	|				И Контрагент = &Поставщик") + "
	|				И ПодразделениеКомпании В (ВЫБРАТЬ Подразделение ИЗ ТаблицаПодразделений)) КАК ЦеныСрезПоследних
	|	ПО ПодразделенияКомпании.Подразделение = ЦеныСрезПоследних.ПодразделениеКомпании
	|ГДЕ
	|	ЦеныСрезПоследних.Цена > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень КАК Уровень,
	|	ТаблицаВсехЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаВсехЦен.ТипЦен КАК ТипЦен,
	|	ТаблицаВсехЦен.ЦеноваяГруппаНоменклатуры КАК ЦеноваяГруппаНоменклатуры,
	|	ТаблицаВсехЦен.КоэффициентЕдиницыИзмерения КАК КоэффициентЕдиницыИзмерения,
	|	ТаблицаВсехЦен.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаВсехЦен.ЕдиницаИзмеренияЦены КАК ЕдиницаИзмеренияЦены,
	|	ТаблицаВсехЦен.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаВсехЦен.Цена * БазовыеТипыЦен.КоэффициентЦены КАК Цена,
	|	БазовыеТипыЦен.АлгоритмПолученияЦены КАК АлгоритмПолученияЦены
	|ПОМЕСТИТЬ ПредварительнаяТаблицаЗакупочныхЦенРазвернутая
	|ИЗ
	|	ПредварительнаяТаблицаВсехЗакупочныхЦен КАК ТаблицаВсехЦен
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ БазовыеТипыЦенЗакупки КАК БазовыеТипыЦен
	|	ПО ТаблицаВсехЦен.ТипЦен = БазовыеТипыЦен.БазовыйТипЦен
	|		И ТаблицаВсехЦен.ЦеноваяГруппаНоменклатуры = БазовыеТипыЦен.ЦеноваяГруппа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ БазовыеТипыЦенЗакупки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаВсехЗакупочныхЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Учет цен по Характеристике
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень КАК Уровень,
	|	ТаблицаВсехЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаВсехЦен.Цена КАК Цена
	|ПОМЕСТИТЬ ПредварительнаяТаблицаЗакупочныхЦенРазвернутаяСОтборами
	|ИЗ
	|	ПредварительнаяТаблицаЗакупочныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоХарактеристике)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|	И НЕ ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И НЕ ТаблицаВсехЦен.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура,
	|	ТаблицаВсехЦен.Цена
	|ИЗ
	|	ПредварительнаяТаблицаЗакупочныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоХарактеристике)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ТипНоменклатуры.ИспользованиеХарактеристик = 3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Учет цен по единице измерения
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура,
	|	ТаблицаВсехЦен.Цена / ТаблицаВсехЦен.КоэффициентЕдиницыИзмерения
	|ИЗ
	|	ПредварительнаяТаблицаЗакупочныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоЕдиницеИзмерения)
	|	И ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ТаблицаВсехЦен.Номенклатура.ОсновнаяЕдиницаИзмерения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура,
	|	ТаблицаВсехЦен.Цена / ТаблицаВсехЦен.КоэффициентЕдиницыИзмерения
	|ИЗ
	|	ПредварительнаяТаблицаЗакупочныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоЕдиницеИзмерения)
	|	И ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|	И НЕ ТаблицаВсехЦен.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Учет по номенклатуре
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура,
	|	ТаблицаВсехЦен.Цена
	|ИЗ
	|	ПредварительнаяТаблицаЗакупочныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаЗакупочныхЦенРазвернутая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень КАК Уровень,
	|	ТаблицаВсехЦен.Номенклатура КАК Номенклатура,
	|	МИНИМУМ(ТаблицаВсехЦен.Цена) КАК Цена
	|ПОМЕСТИТЬ ПредварительнаяТаблицаЗакупочныхЦен
	|ИЗ
	|	ПредварительнаяТаблицаЗакупочныхЦенРазвернутаяСОтборами КАК ТаблицаВсехЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаЗакупочныхЦенРазвернутаяСОтборами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура,
	|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
	|ПОМЕСТИТЬ ОтборПодразделенийЗакупочныхЦен
	|ИЗ
	|	ПредварительнаяТаблицаЗакупочныхЦен КАК ТаблицаЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Номенклатура
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаЦен.Цена КАК Цена
	|	
	|ПОМЕСТИТЬ ТаблицаЗакупочныхЦенВВалютеЦен
	|
	|ИЗ
	|	ПредварительнаяТаблицаЗакупочныхЦен КАК ТаблицаЦен
	|ГДЕ
	|	(ТаблицаЦен.Номенклатура,
	|	 ТаблицаЦен.Уровень) В (ВЫБРАТЬ
	|		ТаблицаЦен.Номенклатура,
	|		ТаблицаЦен.Уровень
	|	ИЗ
	|		ОтборПодразделенийЗакупочныхЦен КАК ТаблицаЦен)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаЗакупочныхЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОтборПодразделенийЗакупочныхЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс/ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1)/&КурсВалютыОтчета КАК Цена
	|	
	|ПОМЕСТИТЬ ТаблицаЗакупочныхЦен
	|
	|ИЗ
	|	ТаблицаЗакупочныхЦенВВалютеЦен КАК ТаблицаЦен" + ?(ЗакупкиВВалютеУчета, "
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРасчета, ) КАК КурсыВалютСрезПоследних
	|		ПО ТаблицаЦен.Номенклатура.ВалютаУчета = КурсыВалютСрезПоследних.Валюта", ",
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРасчета, Валюта=&ВалютаЗакупки) КАК КурсыВалютСрезПоследних") + "
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаЗакупочныхЦенВВалютеЦен
	|;
	|
	|//РОЗНИЧНЫЕ ЦЕНЫ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодразделенияКомпании.Уровень КАК Уровень,
	|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныСрезПоследних.ТипЦен КАК ТипЦен,
	|	ЦеныСрезПоследних.Номенклатура.ЦеноваяГруппа КАК ЦеноваяГруппаНоменклатуры,
	|	ЦеныСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЦены,
	|	ЦеныСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЦеныСрезПоследних.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ЦеныСрезПоследних.ЕдиницаИзмерения=ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|			1
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ЦеныСрезПоследних.ЕдиницаИзмерения.Коэффициент = 0 ТОГДА
	|				1
	|			ИНАЧЕ
	|				ЦеныСрезПоследних.ЕдиницаИзмерения.Коэффициент
	|			КОНЕЦ
	|	КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|	ЦеныСрезПоследних.Цена КАК Цена
	|	
	|ПОМЕСТИТЬ ПредварительнаяТаблицаВсехРозничныеЦен
	|ИЗ
	|	ТаблицаПодразделений КАК ПодразделенияКомпании
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(
	|				&ДатаРасчета,
	|				ТипЦен В (ВЫБРАТЬ БазовыйТипЦен ИЗ БазовыеТипыЦенПродажи)
	|				И Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ПодразделениеКомпании В (ВЫБРАТЬ Подразделение ИЗ ТаблицаПодразделений)) КАК ЦеныСрезПоследних
	|ПО 
	|	ПодразделенияКомпании.Подразделение = ЦеныСрезПоследних.ПодразделениеКомпании
	|ГДЕ
	|	ЦеныСрезПоследних.Цена > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаПодразделений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень КАК Уровень,
	|	ТаблицаВсехЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаВсехЦен.ТипЦен КАК ТипЦен,
	|	ТаблицаВсехЦен.ЦеноваяГруппаНоменклатуры КАК ЦеноваяГруппаНоменклатуры,
	|	ТаблицаВсехЦен.КоэффициентЕдиницыИзмерения КАК КоэффициентЕдиницыИзмерения,
	|	ТаблицаВсехЦен.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаВсехЦен.ЕдиницаИзмеренияЦены КАК ЕдиницаИзмеренияЦены,
	|	ТаблицаВсехЦен.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаВсехЦен.Цена * БазовыеТипыЦен.КоэффициентЦены КАК Цена,
	|	БазовыеТипыЦен.АлгоритмПолученияЦены КАК АлгоритмПолученияЦены
	|ПОМЕСТИТЬ ПредварительнаяТаблицаРозничныхЦенРазвернутая
	|ИЗ
	|	ПредварительнаяТаблицаВсехРозничныеЦен КАК ТаблицаВсехЦен
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ БазовыеТипыЦенПродажи КАК БазовыеТипыЦен
	|	ПО ТаблицаВсехЦен.ТипЦен = БазовыеТипыЦен.БазовыйТипЦен
	|		И ТаблицаВсехЦен.ЦеноваяГруппаНоменклатуры = БазовыеТипыЦен.ЦеноваяГруппа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ БазовыеТипыЦенПродажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаВсехРозничныеЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Учет цен по Характеристике
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень КАК Уровень,
	|	ТаблицаВсехЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаВсехЦен.Цена КАК Цена
	|ПОМЕСТИТЬ ПредварительнаяТаблицаРозничныхЦенРазвернутаяСОтборами
	|ИЗ
	|	ПредварительнаяТаблицаРозничныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоХарактеристике)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|	И НЕ ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И НЕ ТаблицаВсехЦен.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура,
	|	ТаблицаВсехЦен.Цена
	|ИЗ
	|	ПредварительнаяТаблицаРозничныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоХарактеристике)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ТипНоменклатуры.ИспользованиеХарактеристик = 3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Учет цен по единице измерения
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура,
	|	ТаблицаВсехЦен.Цена / ТаблицаВсехЦен.КоэффициентЕдиницыИзмерения
	|ИЗ
	|	ПредварительнаяТаблицаРозничныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоЕдиницеИзмерения)
	|	И ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ТаблицаВсехЦен.Номенклатура.ОсновнаяЕдиницаИзмерения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура,
	|	ТаблицаВсехЦен.Цена / ТаблицаВсехЦен.КоэффициентЕдиницыИзмерения
	|ИЗ
	|	ПредварительнаяТаблицаРозничныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоЕдиницеИзмерения)
	|	И ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|	И НЕ ТаблицаВсехЦен.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Учет по номенклатуре
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура,
	|	ТаблицаВсехЦен.Цена
	|ИЗ
	|	ПредварительнаяТаблицаРозничныхЦенРазвернутая КАК ТаблицаВсехЦен
	|ГДЕ
	|	ТаблицаВсехЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ТаблицаВсехЦен.ЕдиницаИзмеренияЦены = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаРозничныхЦенРазвернутая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВсехЦен.Уровень КАК Уровень,
	|	ТаблицаВсехЦен.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ТаблицаВсехЦен.Цена) КАК Цена
	|ПОМЕСТИТЬ ПредварительнаяТаблицаРозничныхЦен
	|ИЗ
	|	ПредварительнаяТаблицаРозничныхЦенРазвернутаяСОтборами КАК ТаблицаВсехЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВсехЦен.Уровень,
	|	ТаблицаВсехЦен.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаРозничныхЦенРазвернутаяСОтборами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура,
	|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
	|ПОМЕСТИТЬ ОтборПодразделенийРозничныхЦен
	|ИЗ
	|	ПредварительнаяТаблицаРозничныхЦен КАК ТаблицаЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Номенклатура
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаЦен.Цена КАК Цена
	|ПОМЕСТИТЬ ТаблицаРозничныхЦенВВалютеЦен
	|ИЗ
	|	ПредварительнаяТаблицаРозничныхЦен КАК ТаблицаЦен
	|ГДЕ
	|	(ТаблицаЦен.Номенклатура,
	|	 ТаблицаЦен.Уровень) В (ВЫБРАТЬ
	|		ТаблицаЦен.Номенклатура,
	|		ТаблицаЦен.Уровень
	|	ИЗ
	|		ОтборПодразделенийРозничныхЦен КАК ТаблицаЦен)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаРозничныхЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс/ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1)/&КурсВалютыОтчета КАК Цена
	|ПОМЕСТИТЬ ТаблицаРозничныхЦен
	|ИЗ
	|	ТаблицаРозничныхЦенВВалютеЦен КАК ТаблицаЦен" + ?(ПродажиВВалютеУчета, "
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРасчета, ) КАК КурсыВалютСрезПоследних
	|		ПО ТаблицаЦен.Номенклатура.ВалютаУчета = КурсыВалютСрезПоследних.Валюта", ",
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРасчета, Валюта=&ВалютаПродажи) КАК КурсыВалютСрезПоследних") + "
	|ИНДЕКСИРОВАТЬ ПО 
	|	Номенклатура
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаРозничныхЦенВВалютеЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаНоменклатуры.ДатаПоследнейПоставки КАК ДатаПоследнейПоставки,
	|	ТаблицаАналогов.ИдентификаторГруппы КАК ИдентификаторГруппы
	|ПОМЕСТИТЬ
	|	ТаблицаТоваровАналоги
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаНоменклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ГруппыАналогов КАК ТаблицаАналогов
	|ПО
	|	ТаблицаНоменклатуры.Номенклатура.Артикул = ТаблицаАналогов.Артикул
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторГруппы
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаТовары
	|;
	|
	|// ОБОРОТ ПРОДАЖ НЕПРОИЗВЕДЕННОГО ТОВАРА ЗА ПОСЛЕДНИЕ ДНИ
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПродаж.Номенклатура КАК Номенклатура,
	|	СУММА(ТаблицаПродаж.Количество) КАК КоличествоПродажЗаПоследниеДни
	|ПОМЕСТИТЬ
	|	ОборотыПродажЗаПоследниеДни
	|ИЗ
	|	РегистрНакопления.Продажи КАК ТаблицаПродаж
	|ГДЕ
	|	ТаблицаПродаж.Период МЕЖДУ &НачалоПоследнихДней И &ДатаРасчета
	|	И ТаблицаПродаж.ПодразделениеКомпании В ИЕРАРХИИ (&Подразделение)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродаж.Номенклатура
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|// ОБОРОТЫ ПРОДАЖ ПО МЕСЯЦАМ
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц1 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|		  КОНЕЦ) КАК ПродажиМесяц1,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц2 ТОГДА 
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|		  КОНЕЦ) КАК ПродажиМесяц2,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц3 ТОГДА 
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ 
	|				0
	|		  КОНЕЦ) КАК ПродажиМесяц3,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц4 ТОГДА 
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|		  КОНЕЦ) КАК ПродажиМесяц4,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц5 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ) КАК ПродажиМесяц5,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц6 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ) КАК ПродажиМесяц6,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц7 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ) КАК ПродажиМесяц7,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц8 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ) КАК ПродажиМесяц8,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц9 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ) КАК ПродажиМесяц9,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц10 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ) КАК ПродажиМесяц10,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц11 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ) КАК ПродажиМесяц11,
	|	СУММА(ВЫБОР
	|			КОГДА ПродажиОбороты.Период = &Месяц12 ТОГДА
	|				ПродажиОбороты.КоличествоОборот
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ) КАК ПродажиМесяц12,
	|	ПродажиОбороты.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ОборотыПродажПоМесяцам
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&Месяц1, &ПоследнийМесяц, Месяц,
	|		ПодразделениеКомпании В ИЕРАРХИИ (&Подразделение)) КАК ПродажиОбороты
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|ВЫБРАТЬ
	|" +?(РасчетТолькоПоГлавнымАналогам, "// ОБЩИЕ
	|	ЕСТЬNULL(ТаблицаГлавныхАналоговПоГруппе.Номенклатура, ТаблицаТоваровАналоги.Номенклатура)", "
	|	ТаблицаТоваровАналоги.Номенклатура")+" КАК Номенклатура,
	|	ЕСТЬNULL(ТаблицаГлавныхАналоговПоГруппе.Номенклатура, ТаблицаТоваровАналоги.Номенклатура) КАК ГлавныйАналог,
	|	МАКСИМУМ(ТаблицаТоваровАналоги.ДатаПоследнейПоставки) КАК ДатаПоследнейПоставки
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ ТаблицаТоваровАналоги КАК ТаблицаТоваровАналоги
	|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаГлавныхАналоговПоГруппе КАК ТаблицаГлавныхАналоговПоГруппе
	|	ПО ТаблицаТоваровАналоги.ИдентификаторГруппы = ТаблицаГлавныхАналоговПоГруппе.ГруппаАналогов
	|СГРУППИРОВАТЬ ПО
	|" + ?(Не РасчетТолькоПоГлавнымАналогам, "ТаблицаТоваровАналоги.Номенклатура,", "") + "
	|	ЕСТЬNULL(ТаблицаГлавныхАналоговПоГруппе.Номенклатура, ТаблицаТоваровАналоги.Номенклатура)
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|// ПРОДАЖИ ЗА ГОД
	|ВЫБРАТЬ
	|	ПродажиОбороты.Номенклатура КАК Номенклатура,
	|	СУММА(ПродажиОбороты.КоличествоОборот) КАК КоличествоОборот,
	|	КОЛИЧЕСТВО(ПродажиОбороты.Регистратор) КАК КоличествоПродаж
	|ПОМЕСТИТЬ ОборотыПродажЗаГод
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&Месяц1, &ПоследнийМесяц, Регистратор, 
	|			ПодразделениеКомпании В ИЕРАРХИИ (&Подразделение)) КАК ПродажиОбороты
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.ГлавныйАналог КАК ГлавныйАналог,
	|// ОСТАТКИ И РЕЗЕРВЫ НА СКЛАДЕ
	|	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0)) КАК ОстатокНаСкладе,
	|	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0)) КАК РезервНаСкладе,
	|	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0)) КАК СвободныйОстатокНаСкладе,
	|// ОБОРОТ ПОТРЕБЛЕНИЯ В ПРОИЗВОДСТВЕ ЗА ПОСЛЕДНИЕ ДНИ
	|	0 КАК ИзрасходованоВПроизводствеЗаПоследниеДни,
	|// ЗНАЧЕНИЕ МИНИМАЛЬНОГО ОСТАТКА ТОВАРА
	|	МАКСИМУМ(ЕСТЬNULL(ДополнительныеРеквизитыНоменклатурыСрезПоследних.ЗначениеРеквизита, 0)) КАК МинимальныйЗапас,
	|// РАСЧЕТНЫЙ ОБОРОТ ПРОДАЖ ТОВАРА ЗА ПОСЛЕДНИЕ ДНИ
	|	СУММА(ЕСТЬNULL(ОборотыПродажЗаГод.КоличествоОборот, 0)) * &КоэффициентРасчетногоКоличества КАК РасчетноеКоличествоПродажЗаПоследниеДни,
	|// ОБОРОТ ПРОДАЖ НЕПРОИЗВЕДЕННОГО ТОВАРА ЗА ПОСЛЕДНИЕ ДНИ
	|	СУММА(ЕСТЬNULL(ОборотыПродажЗаПоследниеДни.КоличествоПродажЗаПоследниеДни, 0)) КАК КоличествоПродажЗаПоследниеДни,
	|// КОЛИЧЕСТВО ПРОДАЖ ЗА ПРОИЗВОЛЬНЫЙ ПЕРИОД
	|	СУММА(ЕСТЬNULL(ОборотыПродажЗаПроизвольныйПериод.КоличествоОборот, 0)) КАК КоличествоПродажЗаПроизвольныйПериод,
	|// ОБОРОТЫ ПРОДАЖ ПО МЕСЯЦАМ
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц1, 0)) КАК ПродажиМесяц1,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц2, 0)) КАК ПродажиМесяц2,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц3, 0)) КАК ПродажиМесяц3,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц4, 0)) КАК ПродажиМесяц4,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц5, 0)) КАК ПродажиМесяц5,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц6, 0)) КАК ПродажиМесяц6,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц7, 0)) КАК ПродажиМесяц7,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц8, 0)) КАК ПродажиМесяц8,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц9, 0)) КАК ПродажиМесяц9,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц10, 0)) КАК ПродажиМесяц10,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц11, 0)) КАК ПродажиМесяц11,
	|	СУММА(ЕСТЬNULL(ОборотыПродажПоМесяцам.ПродажиМесяц12, 0)) КАК ПродажиМесяц12,
	|// ЗАКАЗЫ ПОСТАВЩИКУ ДЛЯ СОБСТВЕННЫХ НУЖД
	|	СУММА(ЕСТЬNULL(ЗаказыПоставщикамОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК ЗаказаноПоставщику,
	|// ПОСЛЕДНЯЯ ДАТА ПОСТУПЛЕНИЯ ТОВАРА
	|	МАКСИМУМ(ТаблицаТоваров.ДатаПоследнейПоставки) КАК ДатаПоследнейПоставки,
	|// ПРОДАЖИ ЗА ГОД
	|	СУММА(ЕСТЬNULL(ОборотыПродажЗаГод.КоличествоОборот, 0)) КАК КоличествоПродажЗаПоследнийГод,
	|	СУММА(ЕСТЬNULL(ОборотыПродажЗаГод.КоличествоПродаж, 0)) КАК КоличествоДокументовПродажЗаГод,
	|// ТОЧКА ЗАКАЗА
	|	СУММА(ЕСТЬNULL(ОборотыПродажЗаПоследниеДни.КоличествоПродажЗаПоследниеДни, 0)) + МАКСИМУМ(ЕСТЬNULL(ДополнительныеРеквизитыНоменклатурыСрезПоследних.ЗначениеРеквизита, 0)) КАК ТочкаЗаказа
	|ПОМЕСТИТЬ ТаблицаСвернутаяПоГлавнымАналогам
	|	// ОБЩИЕ				
	|ИЗ ТаблицаТоваров КАК ТаблицаТоваров
	|// ОСТАТКИ И РЕЗЕРВЫ НА СКЛАДЕ
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаРасчета,) КАК ОстаткиТоваровКомпанииОстатки
	|	ПО ТаблицаТоваров.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
	|// ОБОРОТ ПОТРЕБЛЕНИЯ В ПРОИЗВОДСТВЕ ЗА ПОСЛЕДНИЕ ДНИ
	|// ЗНАЧЕНИЕ МИНИМАЛЬНОГО ОСТАТКА ТОВАРА
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(
	|	&ДатаРасчета,
	|	ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток)
	|	И ПодразделениеКомпании = &Подразделение) КАК ДополнительныеРеквизитыНоменклатурыСрезПоследних
	|	ПО ТаблицаТоваров.Номенклатура = ДополнительныеРеквизитыНоменклатурыСрезПоследних.Номенклатура
	|// ОБОРОТ ПРОДАЖ НЕПРОИЗВЕДЕННОГО ТОВАРА ЗА ПОСЛЕДНИЕ ДНИ
	|ЛЕВОЕ СОЕДИНЕНИЕ ОборотыПродажЗаПоследниеДни КАК ОборотыПродажЗаПоследниеДни
	|	ПО ТаблицаТоваров.Номенклатура = ОборотыПродажЗаПоследниеДни.Номенклатура
	|// КОЛИЧЕСТВО ПРОДАЖ ЗА ПРОИЗВОЛЬНЫЙ ПЕРИОД
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи.Обороты(&НачалоПроизвольногоПериода, &КонецПроизвольногоПериода,,
	|	ПодразделениеКомпании В ИЕРАРХИИ (&Подразделение)) КАК ОборотыПродажЗаПроизвольныйПериод
	|ПО ТаблицаТоваров.Номенклатура = ОборотыПродажЗаПроизвольныйПериод.Номенклатура
	|// ОБОРОТЫ ПРОДАЖ ПО МЕСЯЦАМ
	|ЛЕВОЕ СОЕДИНЕНИЕ ОборотыПродажПоМесяцам КАК ОборотыПродажПоМесяцам
	|	ПО ТаблицаТоваров.Номенклатура = ОборотыПродажПоМесяцам.Номенклатура
	|// ЗАКАЗЫ ПОСТАВЩИКУ ДЛЯ СОБСТВЕННЫХ НУЖД	
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам.Остатки(&ДатаРасчета, ) КАК ЗаказыПоставщикамОстатки
	|	ПО ТаблицаТоваров.Номенклатура = ЗаказыПоставщикамОстатки.Номенклатура
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(&ДатаРасчета, ) КАК ЗаказыРаспределениеОстатки
	|	ПО ТаблицаТоваров.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура
	|// ПРОДАЖИ ЗА ГОД
	|ЛЕВОЕ СОЕДИНЕНИЕ ОборотыПродажЗаГод КАК ОборотыПродажЗаГод
	|	ПО ТаблицаТоваров.Номенклатура = ОборотыПродажЗаГод.Номенклатура
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.ГлавныйАналог
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|ВЫБРАТЬ
	|// ОБЩИЕ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.Артикул КАК НомерПоКаталогу,
	|	ТаблицаТоваров.Номенклатура.Производитель КАК Производитель,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения КАК НоменклатураБазоваяЕдиницаИзмерения,
	|	ТаблицаТоваров.ГлавныйАналог КАК ГлавныйАналог,
	|// ОСТАТКИ И РЕЗЕРВЫ НА СКЛАДЕ
	|	ТаблицаТоваров.ОстатокНаСкладе КАК ОстатокНаСкладе,
	|	ТаблицаТоваров.РезервНаСкладе КАК РезервНаСкладе,
	|	ТаблицаТоваров.СвободныйОстатокНаСкладе КАК СвободныйОстатокНаСкладе,
	|// ОБОРОТ ПОТРЕБЛЕНИЯ В ПРОИЗВОДСТВЕ ЗА ПОСЛЕДНИЕ ДНИ
	|	ТаблицаТоваров.ИзрасходованоВПроизводствеЗаПоследниеДни КАК ИзрасходованоВПроизводствеЗаПоследниеДни,
	|// ЗНАЧЕНИЕ МИНИМАЛЬНОГО ОСТАТКА ТОВАРА
	|	ТаблицаТоваров.МинимальныйЗапас КАК МинимальныйЗапас,
	|// РАСЧЕТНЫЙ ОБОРОТ ПРОДАЖ ТОВАРА ЗА ПОСЛЕДНИЕ ДНИ
	|	ТаблицаТоваров.РасчетноеКоличествоПродажЗаПоследниеДни КАК РасчетноеКоличествоПродажЗаПоследниеДни,
	|// ОБОРОТ ПРОДАЖ НЕПРОИЗВЕДЕННОГО ТОВАРА ЗА ПОСЛЕДНИЕ ДНИ
	|	ТаблицаТоваров.КоличествоПродажЗаПоследниеДни КАК КоличествоПродажЗаПоследниеДни,
	|// КОЛИЧЕСТВО ПРОДАЖ ЗА ПРОИЗВОЛЬНЫЙ ПЕРИОД
	|	ТаблицаТоваров.КоличествоПродажЗаПроизвольныйПериод КАК КоличествоПродажЗаПроизвольныйПериод,
	|// ОБОРОТЫ ПРОДАЖ ПО МЕСЯЦАМ					
	|	ТаблицаТоваров.ПродажиМесяц1 КАК ПродажиМесяц1,
	|	ТаблицаТоваров.ПродажиМесяц2 КАК ПродажиМесяц2,
	|	ТаблицаТоваров.ПродажиМесяц3 КАК ПродажиМесяц3,
	|	ТаблицаТоваров.ПродажиМесяц4 КАК ПродажиМесяц4,
	|	ТаблицаТоваров.ПродажиМесяц5 КАК ПродажиМесяц5,
	|	ТаблицаТоваров.ПродажиМесяц6 КАК ПродажиМесяц6,
	|	ТаблицаТоваров.ПродажиМесяц7 КАК ПродажиМесяц7,
	|	ТаблицаТоваров.ПродажиМесяц8 КАК ПродажиМесяц8,
	|	ТаблицаТоваров.ПродажиМесяц9 КАК ПродажиМесяц9,
	|	ТаблицаТоваров.ПродажиМесяц10 КАК ПродажиМесяц10,
	|	ТаблицаТоваров.ПродажиМесяц11 КАК ПродажиМесяц11,
	|	ТаблицаТоваров.ПродажиМесяц12 КАК ПродажиМесяц12,
	|// ЗАКАЗЫ ПОСТАВЩИКУ ДЛЯ СОБСТВЕННЫХ НУЖД
	|	ТаблицаТоваров.ЗаказаноПоставщику КАК ЗаказаноПоставщику,
	|// ПОСЛЕДНЯЯ ДАТА ПОСТУПЛЕНИЯ ТОВАРА
	|	ТаблицаТоваров.ДатаПоследнейПоставки КАК ДатаПоследнейПоставки,
	|// ПРОДАЖИ ЗА ГОД
	|		ТаблицаТоваров.КоличествоПродажЗаПоследнийГод КАК КоличествоПродажЗаПоследнийГод,
	|	ТаблицаТоваров.КоличествоДокументовПродажЗаГод КАК КоличествоДокументовПродажЗаГод,
	|// ТОЧКА ЗАКАЗА
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ТочкаЗаказа > 0 ТОГДА
	|			ТаблицаТоваров.ТочкаЗаказа
	|		ИНАЧЕ 
	|			0
	|	КОНЕЦ КАК ТочкаЗаказа,
	|// ЦЕНЫ ЗАКУПКИ
	|	ЕСТЬNULL(ТаблицаЗакупочныхЦен.Цена, 0)  КАК ЦенаЗакупки,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ТочкаЗаказа-ТаблицаТоваров.СвободныйОстатокНаСкладе > 0 ТОГДА
	|			(ЕСТЬNULL(ТаблицаЗакупочныхЦен.Цена, 0) ) * ВЫРАЗИТЬ(ТаблицаТоваров.ТочкаЗаказа-ТаблицаТоваров.СвободныйОстатокНаСкладе КАК ЧИСЛО(15, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК СуммаЗакупки,
	|// ЦЕНЫ ПРОДАЖ
	|	ЕСТЬNULL(ТаблицаРозничныхЦен.Цена, 0) КАК ЦенаПродажи,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ТочкаЗаказа-ТаблицаТоваров.СвободныйОстатокНаСкладе > 0 ТОГДА
	|			(ЕСТЬNULL(ТаблицаРозничныхЦен.Цена, 0)) * ВЫРАЗИТЬ(ТаблицаТоваров.ТочкаЗаказа-ТаблицаТоваров.СвободныйОстатокНаСкладе КАК ЧИСЛО(15, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК СуммаПродажи,
	|// РЕКОМЕНДУЕМОЕ КОЛИЧЕСТВО
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ТочкаЗаказа-ТаблицаТоваров.СвободныйОстатокНаСкладе > 0 ТОГДА
	|			ТаблицаТоваров.ТочкаЗаказа-ТаблицаТоваров.СвободныйОстатокНаСкладе
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК РекомендуемоеКоличество,
	|// СКЛАДСКАЯ НАЛИЧНОСТЬ
	|	ТаблицаТоваров.СвободныйОстатокНаСкладе + ТаблицаТоваров.ЗаказаноПоставщику КАК СкладскаяНаличность,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ТочкаЗаказа-ТаблицаТоваров.СвободныйОстатокНаСкладе > 0 ТОГДА
	|			ВЫРАЗИТЬ(ТаблицаТоваров.ТочкаЗаказа-ТаблицаТоваров.СвободныйОстатокНаСкладе КАК ЧИСЛО(15, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Количество
	|
	|ИЗ
	|	ТаблицаСвернутаяПоГлавнымАналогам КАК ТаблицаТоваров
	|// ЦЕНЫ ЗАКУПКИ
	|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗакупочныхЦен КАК ТаблицаЗакупочныхЦен
	|	ПО ТаблицаТоваров.Номенклатура = ТаблицаЗакупочныхЦен.Номенклатура
	|// ЦЕНЫ ПРОДАЖ
	|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРозничныхЦен КАК ТаблицаРозничныхЦен
	|	ПО ТаблицаТоваров.Номенклатура = ТаблицаРозничныхЦен.Номенклатура";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Для Каждого Элемент Из ПараметрыИнициализации Цикл
		Запрос.УстановитьПараметр(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	Для Каждого Элемент Из ПараметрыПодразделений Цикл
		Запрос.УстановитьПараметр(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("РозничнаяЦена", РабочийТипЦенПродажи);
	Запрос.УстановитьПараметр("ЗакупочнаяЦена", РабочийТипЦенЗакупки);
	
	// Сделан чтоб не давать доступ к партиеобразующим документам.
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Формирует текст запроса развертки типов цен до базовых
//
// 
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция РазверткаТиповЦенДоБазовых() Экспорт
	
	Пролог =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаЦеновыхГрупп.ЦеноваяГруппаНоменклатуры КАК Ссылка
	|ПОМЕСТИТЬ ЦеновыеГруппыВсе
	|ИЗ
	|	ТаблицаЦеновыхГрупп КАК ТаблицаЦеновыхГрупп
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТипыЦен.Ссылка КАК Ссылка,
	|	ТипыЦен.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ТипыЦен.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|	ЦеновыеГруппыВсе.Ссылка КАК ЦеноваяГруппа
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	Справочник.ТипыЦен КАК ТипыЦен,
	|	ЦеновыеГруппыВсе КАК ЦеновыеГруппыВсе
	|ГДЕ
	|	ТипыЦен.Рассчитывается
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТипыЦен.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ТипыЦен.БазовыйТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
	|			ТОГДА ТипыЦен.Ссылка
	|		ИНАЧЕ ТипыЦен.БазовыйТипЦен
	|	КОНЕЦ КАК БазовыйТипЦен,
	|	ЦеновыеГРуппыВсе.Ссылка КАК ЦеноваяГруппа,
	|	1 + ТипыЦен.ПроцентСкидкиНаценки / 100 КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ТипыЦен.Рассчитывается
	|			ТОГДА ТипыЦен.БазовыйТипЦен.Рассчитывается
	|		ИНАЧЕ ТипыЦен.Рассчитывается
	|	КОНЕЦ КАК БазовыйТипЦенРассчитывается,
	|	2 КАК Уровень
	|ПОМЕСТИТЬ ПредИсходнаяТаблица1
	|ИЗ
	|	Справочник.ТипыЦен КАК ТипыЦен,
	|	ЦеновыеГРуппыВсе КАК ЦеновыеГРуппыВсе
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ.Ссылка,
	|	ЕСТЬNULL(ТипыЦенПроцентыСкидкиНаценки.БазовыйТипЦен, ВТ.БазовыйТипЦен),
	|	ЕСТЬNULL(ТипыЦенПроцентыСкидкиНаценки.ЦеноваяГруппа, ВТ.ЦеноваяГруппа),
	|	1 + ЕСТЬNULL(ТипыЦенПроцентыСкидкиНаценки.ПроцентСкидкиНаценки, ВТ.ПроцентСкидкиНаценки) / 100,
	|	(ЕСТЬNULL(ТипыЦенПроцентыСкидкиНаценки.БазовыйТипЦен, ВТ.БазовыйТипЦен)).Рассчитывается,
	|	1
	|ИЗ
	|	ВТ КАК ВТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК ТипыЦенПроцентыСкидкиНаценки
	|		ПО ВТ.Ссылка = ТипыЦенПроцентыСкидкиНаценки.Ссылка
	|			И ВТ.ЦеноваяГруппа = ТипыЦенПроцентыСкидкиНаценки.ЦеноваяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЦеновыеГруппыВсе
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредИсходнаяТаблица1.Ссылка КАК Ссылка,
	|	ПредИсходнаяТаблица1.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ПредИсходнаяТаблица1.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	МАКСИМУМ(ПредИсходнаяТаблица1.Коэффициент) КАК Коэффициент,
	|	ПредИсходнаяТаблица1.БазовыйТипЦенРассчитывается КАК БазовыйТипЦенРассчитывается,
	|	ПредИсходнаяТаблица1.Уровень КАК Уровень
	|ПОМЕСТИТЬ ИсходнаяТаблица1
	|ИЗ
	|	ПредИсходнаяТаблица1 КАК ПредИсходнаяТаблица1
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредИсходнаяТаблица1.Ссылка,
	|	ПредИсходнаяТаблица1.БазовыйТипЦен,
	|	ПредИсходнаяТаблица1.ЦеноваяГруппа,
	|	ПредИсходнаяТаблица1.БазовыйТипЦенРассчитывается,
	|	ПредИсходнаяТаблица1.Уровень
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредИсходнаяТаблица1
	|;";
	
	Рефрен =
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.Ссылка КАК Ссылка,
	|	ИсходнаяТаблица.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ИсходнаяТаблица.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	ИсходнаяТаблица.Коэффициент КАК Коэффициент,
	|	ИсходнаяТаблица.БазовыйТипЦенРассчитывается КАК БазовыйТипЦенРассчитывается,
	|	ИсходнаяТаблица.Уровень КАК Уровень
	|ПОМЕСТИТЬ Итог%1
	|ИЗ
	|	ИсходнаяТаблица%1 КАК ИсходнаяТаблица
	|ГДЕ
	|	НЕ ИсходнаяТаблица.БазовыйТипЦенРассчитывается
	|;
	|ВЫБРАТЬ
	|	ВТ.Ссылка КАК Ссылка,
	|	ВТ.Коэффициент КАК Коэффициент,
	|	ВТ.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ВТ.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	ВТ.БазовыйТипЦенРассчитывается КАК БазовыйТипЦенРассчитывается,
	|	ВТ.Уровень КАК Уровень
	|ПОМЕСТИТЬ ТаблицаРассчетныхЦен%1
	|ИЗ
	|	ИсходнаяТаблица%1 КАК ВТ
	|ГДЕ
	|	ВТ.БазовыйТипЦенРассчитывается
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРассчетныхЦен.Ссылка КАК Ссылка,
	|	ТаблицаРассчетныхЦен.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Итог.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ТаблицаРассчетныхЦен.Коэффициент * Итог.Коэффициент КАК Коэффициент,
	|	Итог.БазовыйТипЦенРассчитывается КАК БазовыйТипЦенРассчитывается,
	|	ТаблицаРассчетныхЦен.Уровень КАК Уровень
	|ПОМЕСТИТЬ ПредИсходнаяТаблица%2
	|ИЗ
	|	ТаблицаРассчетныхЦен%1 КАК ТаблицаРассчетныхЦен
	|		ЛЕВОЕ СОЕДИНЕНИЕ Итог%1 КАК Итог
	|		ПО ТаблицаРассчетныхЦен.БазовыйТипЦен = Итог.Ссылка
	|			И ТаблицаРассчетныхЦен.ЦеноваяГруппа = Итог.ЦеноваяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Итог.Ссылка КАК Ссылка,
	|	Итог.БазовыйТипЦен КАК БазовыйТипЦен,
	|	Итог.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Итог.Коэффициент КАК Коэффициент,
	|	Итог.БазовыйТипЦенРассчитывается КАК БазовыйТипЦенРассчитывается,
	|	Итог.Уровень КАК Уровень
	|ПОМЕСТИТЬ ИсходнаяТаблица%2
	|ИЗ
	|	Итог%1 КАК Итог
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредИсходнаяТаблица.Ссылка,
	|	ПредИсходнаяТаблица.БазовыйТипЦен,
	|	ПредИсходнаяТаблица.ЦеноваяГруппа,
	|	ПредИсходнаяТаблица.Коэффициент,
	|	ПредИсходнаяТаблица.БазовыйТипЦенРассчитывается,
	|	ПредИсходнаяТаблица.Уровень КАК Уровень
	|ИЗ
	|	ПредИсходнаяТаблица%2 КАК ПредИсходнаяТаблица
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Итог%1
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредИсходнаяТаблица%2
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИсходнаяТаблица%1
	|;";
	
	Эпилог =
	"ВЫБРАТЬ
	|	ИсходнаяТаблица.Ссылка КАК ТипЦен,
	|	ИсходнаяТаблица.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ИсходнаяТаблица.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	ИсходнаяТаблица.Коэффициент КАК КоэффициентЦены,
	|	ИсходнаяТаблица.Уровень КАК Уровень
	|ПОМЕСТИТЬ
	|	ПредварительнаяТаблицаТиповЦен
	|ИЗ
	|	ИсходнаяТаблица%1 КАК ИсходнаяТаблица
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.ТипЦен КАК ТипЦен,
	|	ИсходнаяТаблица.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ИсходнаяТаблица.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	МИНИМУМ(ИсходнаяТаблица.Уровень) КАК Уровень
	|ПОМЕСТИТЬ
	|	ОтборТиповЦен
	|ИЗ
	|	ПредварительнаяТаблицаТиповЦен КАК ИсходнаяТаблица
	|СГРУППИРОВАТЬ ПО
	|	ИсходнаяТаблица.ТипЦен,
	|	ИсходнаяТаблица.БазовыйТипЦен,
	|	ИсходнаяТаблица.ЦеноваяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.ТипЦен КАК ТипЦен,
	|	ИсходнаяТаблица.БазовыйТипЦен КАК БазовыйТипЦен,
	|	ИсходнаяТаблица.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	ИсходнаяТаблица.КоэффициентЦены КАК КоэффициентЦены
	|ПОМЕСТИТЬ
	|	ТаблицаТиповЦен
	|ИЗ
	|	ПредварительнаяТаблицаТиповЦен КАК ИсходнаяТаблица
	|ГДЕ
	|	(ИсходнаяТаблица.ТипЦен,
	|	ИсходнаяТаблица.БазовыйТипЦен,
	|	ИсходнаяТаблица.ЦеноваяГруппа,
	|	ИсходнаяТаблица.Уровень) В (ВЫБРАТЬ
	|		ТаблицаЦен.ТипЦен,
	|		ТаблицаЦен.БазовыйТипЦен,
	|		ТаблицаЦен.ЦеноваяГруппа,
	|		ТаблицаЦен.Уровень
	|	ИЗ
	|		ОтборТиповЦен КАК ТаблицаЦен)
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОтборТиповЦен
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредварительнаяТаблицаТиповЦен
	|;
	|";
	
	Запрос = Новый Запрос("ВЫБРАТЬ КОЛИЧЕСТВО(*) КАК Количество ИЗ Справочник.ТипыЦен ГДЕ Рассчитывается");
	Счетчик = 1;
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	МаксимальнаяВложенность = Выборка.Количество;
	ЧастиЗапроса = Новый Массив();
	ЧастиЗапроса.Добавить(Пролог);
	
	Пока Счетчик <= МаксимальнаяВложенность Цикл
		
		ЧастиЗапроса.Добавить(СтрШаблон(Рефрен, Формат(Счетчик, "ЧГ=0"), Формат(Счетчик + 1, "ЧГ=0")));
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
	ЧастиЗапроса.Добавить(СтрШаблон(Эпилог, Формат(Счетчик, "ЧГ=0")));
	ТекстЗапроса = СтрСоединить(ЧастиЗапроса, Символы.ПС);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли