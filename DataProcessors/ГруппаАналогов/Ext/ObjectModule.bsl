
#Область СлужебныеПроцедурыИФункции

// Выполняет поиск группы аналогов для номенклатуры
//
// Параметры:
//  АртикулДляПоиска - Строка - Артикул для поиска номенклатуры
//  Производитель    - СправочникСсылка.Производители - Производитель номенклатуры.
// 
// Возвращаемое значение:
//  Идентификатор группы, Пустая строка.
//
Процедура ИдентификаторГруппыНоменклатуры()
	
	Если ПустаяСтрока(АртикулДляПоиска) Тогда
		ИдентификаторГруппы = "";
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ГруппыАналогов.ИдентификаторГруппы
	|ИЗ
	|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
	|ГДЕ
	|	ГруппыАналогов.АртикулДляПоиска = &АртикулДляПоиска И
	|	ГруппыАналогов.Производитель = &Производитель";
	Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
	Запрос.УстановитьПараметр("Производитель"   , Производитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ИдентификаторГруппы = РезультатЗапроса.Выгрузить()[0].ИдентификаторГруппы;
	Иначе
		ИдентификаторГруппы = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Обработка изменения номенклатуры
//
Процедура ОбработкаИзмененияНоменклатуры() Экспорт
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Номенклатура, "Артикул,Наименование,Производитель,АртикулДляПоиска");
	
	ИдентификаторГруппыНоменклатуры();
	
КонецПроцедуры // ОбработкаИзмененияНоменклатуры()

// Обработка изменения артикула
//
Процедура ОбработкаИзмененияАртикула() Экспорт
	
	АртикулДляПоиска = ОбщегоНазначенияАвтосалонКлиентСервер.ВАртикулДляПоиска(Артикул);
	ИдентификаторГруппыНоменклатуры();
	
КонецПроцедуры

// Обработка изменения Производителя
//
Процедура ОбработкаИзмененияПроизводителя() Экспорт
	
	ИдентификаторГруппыНоменклатуры();
	
КонецПроцедуры


// Заполнение реквизитов по идентификатору группы
//
Процедура ЗаполнитьПоИдентификаторуГруппы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ГруппыАналогов.Артикул,
	|	ГруппыАналогов.АртикулДляПоиска,
	|	ГруппыАналогов.Наименование,
	|	ГруппыАналогов.Производитель,
	|	Номенклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО ГруппыАналогов.АртикулДляПоиска = Номенклатура.АртикулДляПоиска
	|			И ГруппыАналогов.Производитель = Номенклатура.Производитель
	|ГДЕ
	|	ГруппыАналогов.ИдентификаторГруппы = &ИдентификаторГруппы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппыАналогов.ГлавныйПоГруппе УБЫВ";
	Запрос.УстановитьПараметр("ИдентификаторГруппы", ИдентификаторГруппы);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ИдентификаторГруппы = "";
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатЗапроса.Выгрузить()[0]);
	
КонецПроцедуры

#КонецОбласти