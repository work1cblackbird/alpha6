#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ДокументОбъект Экспорт; // Документ объект

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определение интервала прошлого периода.
// ВидПлана - Вид плана
// ДатаНачала - Дата начала интервала
// ДатаКонца - Дата конца интервала.
//
Процедура ПолучитьИнтервалПрошлогоПериода(ВидПлана,ДатаНачала,ДатаКонца)
	ДатаНачалаПланирования = ДокументОбъект.ДатаНачала;
	ДатаКонцаПланирования = ДокументОбъект.ДатаКонца;
	Если ВидПлана=Неопределено Тогда
		ДатаНачала=Неопределено;
		ДатаКонца=Неопределено;
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.День Тогда
		ДатаНачала = НачалоДня(ДатаНачалаПланирования-1);
		ДатаКонца = КонецДня(ДатаКонцаПланирования-1);
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Неделя Тогда
		ДатаНачала = НачалоНедели(ДатаНачалаПланирования-7);
		ДатаКонца = КонецНедели(ДатаКонцаПланирования-7);
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Месяц Тогда
		ДатаНачала = НачалоМесяца(ДобавитьМесяц(ДатаНачалаПланирования,-1));
		ДатаКонца = КонецМесяца(ДобавитьМесяц(ДатаКонцаПланирования,-1));
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Квартал Тогда
		ДатаНачала = НачалоКвартала(ДобавитьМесяц(ДатаНачалаПланирования,-3));
		ДатаКонца = КонецКвартала(ДобавитьМесяц(ДатаКонцаПланирования,-3));
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Год Тогда
		ДатаНачала = НачалоГода(ДобавитьМесяц(ДатаНачалаПланирования,-12));
		ДатаКонца = КонецГода(ДобавитьМесяц(ДатаКонцаПланирования,-12));
	КонецЕсли;
КонецПроцедуры

// Заполнение документа план операция: планирование.
Процедура ЗаполнитьПлан() Экспорт
	ДокументОбъект.Показатели.Очистить();
	Если НЕ ДокументОбъект <> Документы.ПланФакт.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	// Кэшируем параметры запросов.
	ВидПлана = Справочники.ВидыПлановКомпании.ПланПродажПоТоварнымКатегориямМесячный;
	ПодразделениеКомпании=ДокументОбъект.ПодразделениеКомпании;
	ТипНоменклатуры=ДокументОбъект.ОбъектПланирования;
	ДатаНачала = Неопределено;
	ДатаКонца = Неопределено;
	ПолучитьИнтервалПрошлогоПериода(ВидПлана,ДатаНачала,ДатаКонца);
	
	// Определим необходимость ограничения по ассортименту.	
	ШиринаАссортимента=Документы.ПланФакт.ПолучитьПараметрПлана(ДокументОбъект, НСтр("ru = 'Ширина ассортимента'"));
	ОграничиватьПоШиринеАссортимента=(ШиринаАссортимента<>Неопределено И ШиринаАссортимента>0);
	
	// Получим результат планирования за предыдущий период по указанному типу номенклатуры.
	Запрос=Новый Запрос("ВЫБРАТЬ
	|	СУММА(ПланОбороты.ПоказательПлана1Оборот) КАК СуммаПлана
	|ИЗ
	|	РегистрНакопления.Планирование.Обороты(&ДатаНачала, &ДатаКонца, , ВидАналитикиПланирования1 = &ТипНоменклатуры И ВидПлана = &ВидПлана И ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)) КАК ПланОбороты
	|СГРУППИРОВАТЬ ПО
	|	ПланОбороты.ВидАналитикиПланирования1");
	
	Запрос.УстановитьПараметр("ДатаНачала",ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца",ДатаКонца);
	Запрос.УстановитьПараметр("ТипНоменклатуры",ТипНоменклатуры);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ВидПлана",ВидПлана);
	
	РезультатЗапроса=Запрос.Выполнить();
	ВыборкаИзРезультатовЗапроса=РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатовЗапроса.Следующий();
	СуммаПлана=ВыборкаИзРезультатовЗапроса.СуммаПлана;
	
	// Получим результаты продаж за предыдущий период по данному типу номенклатуры.
	ТекстЗапроса="ВЫБРАТЬ ";
	Если ОграничиватьПоШиринеАссортимента Тогда
		ТекстЗапроса=ТекстЗапроса+"ПЕРВЫЕ "+ШиринаАссортимента+" ";
	КонецЕсли;
	
	ТекстЗапроса=ТекстЗапроса+"
	|	ПродажиОбороты.Номенклатура КАК ВидАналитикиПланирования1,
	|	СУММА(ПродажиОбороты.КоличествоОборот) КАК ПоказательПлана1,
	|	СУММА(ПродажиОбороты.СуммаУпрОборот) КАК ПоказательПлана3,
	|	СУММА(ПродажиОбороты.СуммаУпрОборот) / СУММА(ПродажиОбороты.КоличествоОборот) КАК ПоказательПлана2
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца, , Номенклатура.ТипНоменклатуры=&ТипНоменклатуры И ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)) КАК ПродажиОбороты
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура";
	
	Если ОграничиватьПоШиринеАссортимента Тогда
		ТекстЗапроса=ТекстЗапроса+"
		| УПОРЯДОЧИТЬ ПО ПоказательПлана3 УБЫВ";
	КонецЕсли;
	
	Запрос=Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ДатаНачала",ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца",ДатаКонца);
	Запрос.УстановитьПараметр("ТипНоменклатуры",ТипНоменклатуры);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПодразделениеКомпании);
	
	// Заполняем документ
	ДокументОбъект.Показатели.Загрузить(Запрос.Выполнить().Выгрузить());
	
	СуммаПродаж=ДокументОбъект.Показатели.Итог("ПоказательПлана3");
	
	Если ОграничиватьПоШиринеАссортимента Тогда
		Если ДокументОбъект.Показатели.Количество()<ШиринаАссортимента Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'ВНИМАНИЕ! Заданная ширина ассортимента превосходит количество видов товаров, проданных за предыдущий период.'")
			);
		КонецЕсли;
	КонецЕсли;
	
	// Предотвращаем исключительные ситуации
	Если СуммаПродаж=0 Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Отсутствуют данные о суммах продаж. Автоматическое планирование невозможно...'")
		);
		Возврат;
	КонецЕсли;
	
	Если СуммаПлана=null ИЛИ СуммаПлана=0 ИЛИ СуммаПлана=Неопределено Тогда
		СуммаПлана=СуммаПродаж;
	КонецЕсли;
	
	// Подгоняем количества и суммы под план.		
	КоэффициентПланирования=СуммаПлана/СуммаПродаж;
	
	Для Каждого ТекущаяСтрока Из ДокументОбъект.Показатели Цикл
		ТекущаяСтрока.ПоказательПлана1=ТекущаяСтрока.ПоказательПлана1*КоэффициентПланирования;
		ТекущаяСтрока.ПоказательПлана3=ТекущаяСтрока.ПоказательПлана3*КоэффициентПланирования;
	КонецЦикла;
	
КонецПроцедуры

// Заполнение документа план операция: фактически.
Процедура ЗаполнитьФакт() Экспорт
	ДокументОбъект.Показатели.Очистить();
	Если НЕ ДокументОбъект <> Документы.ПланФакт.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	// Кэшируем параметры запроса
	ВидПлана = Справочники.ВидыПлановКомпании.ПланПродажПоТоварнымКатегориямМесячный;
	ТипНоменклатуры=ДокументОбъект.ОбъектПланирования;
	ПодразделениеКомпании=ДокументОбъект.ПодразделениеКомпании;
	ДатаНачала = Неопределено;
	ДатаКонца = Неопределено;
	ПолучитьИнтервалПрошлогоПериода(ВидПлана,ДатаНачала,ДатаКонца);
	
	// Определим необходимость ограничения по ассортименту	
	ШиринаАссортимента=Документы.ПланФакт.ПолучитьПараметрПлана(ДокументОбъект, НСтр("ru = 'Ширина ассортимента'"));
    ОграничиватьПоШиринеАссортимента=(ШиринаАссортимента<>Неопределено И ШиринаАссортимента>0);
	
	// Получим результаты продаж за предыдущий период по данному типу номенклатуры.
		ТекстЗапроса="ВЫБРАТЬ ";
	Если ОграничиватьПоШиринеАссортимента Тогда
		ТекстЗапроса=ТекстЗапроса+"ПЕРВЫЕ "+ШиринаАссортимента+" ";
	КонецЕсли;
	
	ТекстЗапроса=ТекстЗапроса+"
	|	ПродажиОбороты.Номенклатура КАК ВидАналитикиПланирования1,
	|	СУММА(ПродажиОбороты.КоличествоОборот) КАК ПоказательПлана1,
	|	СУММА(ПродажиОбороты.СуммаУпрОборот) КАК ПоказательПлана3,
	|	СУММА(ПродажиОбороты.СуммаУпрОборот) / СУММА(ПродажиОбороты.КоличествоОборот) КАК ПоказательПлана2
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца, , Номенклатура.ТипНоменклатуры=&ТипНоменклатуры И ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)) КАК ПродажиОбороты
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура";
	
	Если ОграничиватьПоШиринеАссортимента Тогда
		ТекстЗапроса=ТекстЗапроса+"
		| УПОРЯДОЧИТЬ ПО ПоказательПлана3 УБЫВ";
	КонецЕсли;
	
	Запрос=Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ДатаНачала",ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца",ДатаКонца);
	Запрос.УстановитьПараметр("ТипНоменклатуры",ТипНоменклатуры);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПодразделениеКомпании);
	
	// Заполняем документ
	ДокументОбъект.Показатели.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если ОграничиватьПоШиринеАссортимента Тогда
		Если ДокументОбъект.Показатели.Количество()<ШиринаАссортимента Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'ВНИМАНИЕ! Заданная ширина ассортимента превосходит количество видов товаров, проданных за предыдущий период.'")
			);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли