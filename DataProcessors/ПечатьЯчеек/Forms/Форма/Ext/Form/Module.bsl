///////////////////////////////////////////////////////////////////////////////
// Модуль формы обработки "Печать ячеек"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обновляем отображение элементов формы
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события возникающего на клиенте при изменении данных реквизита "Весь склад".
//
// Параметры:
//  Элемент - ПолеФормы - Элемент управления, в котором возникло данное событие.
//
&НаКлиенте
Процедура ВесьСкладПриИзменении(Элемент)
	
	// Обновляем отображение элементов формы
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры // ВесьСкладПриИзменении()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события возникающего на клиенте при нажатии на кнопку "Сформировать" в контексте сервера.
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаСервере
Процедура СформироватьПечатьЯчеекНаСервере(ТабличныйДокумент)
	
	Если Объект.ВесьСклад Тогда
		Если НЕ ЗначениеЗаполнено(Объект.СкладКомпании) Тогда
			Возврат;
		КонецЕсли;
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЯчейкиХранения.Ссылка КАК Ячейка
		|ИЗ
		|	Справочник.ЯчейкиХранения КАК ЯчейкиХранения
		|ГДЕ
		|	НЕ ЯчейкиХранения.ЭтоГруппа
		|	И ЯчейкиХранения.Владелец = &Склад
		|АВТОУПОРЯДОЧИВАНИЕ";
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Склад", Объект.СкладКомпании);
		Результат = Запрос.Выполнить();
		Таблица = Результат.Выгрузить();
		Обработки.ПечатьЯчеек.Печать(Объект, ТабличныйДокумент, Таблица);
		Возврат;
	КонецЕсли;
	
	Если Объект.ТаблицаЯчеек.Количество() > 0 Тогда
		Обработки.ПечатьЯчеек.Печать(Объект, ТабличныйДокумент, Объект.ТаблицаЯчеек);
	КонецЕсли;
	
КонецПроцедуры // СформироватьПечатьЯчеекНаСервере()

// Обработчик события возникающего на клиенте при нажатии на кнопку "Сформировать".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура СформироватьПечатьЯчеек(Команда)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	// Обработаем событие в контексте сервера
	СформироватьПечатьЯчеекНаСервере(ТабличныйДокумент);
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.ОтображатьСетку = Ложь;
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ПолеСверху = 2;
	ТабличныйДокумент.ПолеСлева = 2;
	ТабличныйДокумент.ПолеСнизу = 2;
	ТабличныйДокумент.ПолеСправа = 2;
	ТабличныйДокумент.Показать("Печать");
	
КонецПроцедуры // СформироватьПечатьЯчеек()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Производит настройку параметров отображения элементов управления диалога в зависимости от значений реквизитов
// объекта.
//
&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	Элементы.ТаблицаЯчеек.Доступность = НЕ Объект.ВесьСклад;
	
КонецПроцедуры // УправлениеДиалогомНаСервере()

#КонецОбласти

