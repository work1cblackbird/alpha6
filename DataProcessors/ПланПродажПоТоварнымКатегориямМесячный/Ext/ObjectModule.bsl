#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ДокументОбъект Экспорт; // Документ объект

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определение интервала прошлого периода
// ВидПлана - Вид плана
// ДатаНачала - Дата начала интервала
// ДатаКонца - Дата конца интервала.
//
Процедура ПолучитьИнтервалПрошлогоПериода(ВидПлана,ДатаНачала,ДатаКонца)
	ДатаНачалаПланирования = ДокументОбъект.ДатаНачала;
	ДатаКонцаПланирования = ДокументОбъект.ДатаКонца;
	Если ВидПлана=Неопределено Тогда
		ДатаНачала=Неопределено;
		ДатаКонца=Неопределено;
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.День Тогда
		ДатаНачала = НачалоДня(ДатаНачалаПланирования-1);
		ДатаКонца = КонецДня(ДатаКонцаПланирования-1);
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Неделя Тогда
		ДатаНачала = НачалоНедели(ДатаНачалаПланирования-7);
		ДатаКонца = КонецНедели(ДатаКонцаПланирования-7);
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Месяц Тогда
		ДатаНачала = НачалоМесяца(ДобавитьМесяц(ДатаНачалаПланирования,-1));
		ДатаКонца = КонецМесяца(ДобавитьМесяц(ДатаКонцаПланирования,-1));
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Квартал Тогда
		ДатаНачала = НачалоКвартала(ДобавитьМесяц(ДатаНачалаПланирования,-3));
		ДатаКонца = КонецКвартала(ДобавитьМесяц(ДатаКонцаПланирования,-3));
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Год Тогда
		ДатаНачала = НачалоГода(ДобавитьМесяц(ДатаНачалаПланирования,-12));
		ДатаКонца = КонецГода(ДобавитьМесяц(ДатаКонцаПланирования,-12));
	КонецЕсли;
КонецПроцедуры

// Заполнение документа план операция: планирование
Процедура ЗаполнитьПлан() Экспорт
	ДокументОбъект.Показатели.Очистить();
	Если НЕ ДокументОбъект <> Документы.ПланФакт.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;	
	Запрос=Новый Запрос("ВЫБРАТЬ
	|	ПродажиОбороты.Номенклатура.ТипНоменклатуры КАК ВидАналитикиПланирования1,
	|	СУММА(ПродажиОбороты.СуммаУпрОборот*(100+&КоэффициентРоста)/100) КАК ПоказательПлана1
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура.ТипНоменклатуры
	|
	|");
	
	ВидПлана = ДокументОбъект.ВидПлана;
	ДатаНачала = Неопределено;
	ДатаКонца = Неопределено;
	ПолучитьИнтервалПрошлогоПериода(ВидПлана,ДатаНачала,ДатаКонца);
	
	// получим коэффициент роста
	ПараметрКоэффициентРоста = ПланыВидовХарактеристик.ПараметрыПланирования.НайтиПоНаименованию("КоэффициентРоста",Истина);
	СтрокаПараметра = ДокументОбъект.ПараметрыПлана.Найти(ПараметрКоэффициентРоста,"ВидПараметра");
	Если СтрокаПараметра = Неопределено Тогда
		КоэффициентРоста = 1;
	Иначе
		КоэффициентРоста = СтрокаПараметра.ЗначениеПараметра;
	КонецЕсли;
	Если КоэффициентРоста = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указан коэффициент роста'"));
		Возврат;
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаНачала",ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца",ДатаКонца);
	Запрос.УстановитьПараметр("КоэффициентРоста",КоэффициентРоста);
	// заполняем документ
	ДокументОбъект.Показатели.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

// Заполнение документа план операция: фактически
Процедура ЗаполнитьФакт() Экспорт
	ДокументОбъект.Показатели.Очистить();
	Если НЕ ДокументОбъект <> Документы.ПланФакт.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	Запрос=Новый Запрос("ВЫБРАТЬ
	|	ПродажиОбороты.Номенклатура.ТипНоменклатуры КАК ВидАналитикиПланирования1,
	|	СУММА(ПродажиОбороты.СуммаУпрОборот) КАК ПоказательПлана1
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура.ТипНоменклатуры
	|");
	
	ВидПлана = ДокументОбъект.ВидПлана;
	ДатаНачала = ДокументОбъект.ДатаНачала;
	ДатаКонца = ДокументОбъект.ДатаКонца;
	Запрос.УстановитьПараметр("ДатаНачала",ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца",ДатаКонца);
	// заполняем документ
	ДокументОбъект.Показатели.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

#КонецОбласти

#КонецЕсли