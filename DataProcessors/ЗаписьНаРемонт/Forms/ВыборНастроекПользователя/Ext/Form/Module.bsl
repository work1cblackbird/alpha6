///////////////////////////////////////////////////////////////////////////////
// Модуль формы "Форма выбора настроек пользователя"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокПользователей();
	
	РезультатПоиска = ЭтотОбъект.Пользователи.НайтиСтроки(Новый Структура("Пользователь", ИмяПользователя()));
	
	Если РезультатПоиска.Количество() > 0 Тогда
		Элементы.Пользователи.ТекущаяСтрока = РезультатПоиска[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПользователиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	НастройкиВыбор(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиВыбор(Команда)
	
	ТекДанные = Элементы.Пользователи.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеНастройки = Новый Соответствие;
	ТекущиеНастройки.Вставить("КлючНастроек", ТекДанные.Пользователь);
	Закрыть(ТекущиеНастройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Загрузить список пользователей
//
&НаСервере
Процедура ЗаполнитьСписокПользователей()
	
	УстановитьПривилегированныйРежим(Истина);
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого Пользователь Из ПользователиИБ Цикл
		
		НоваяСтрока = Пользователи.Добавить();
		НоваяСтрока.Пользователь = Пользователь.Имя;
		НоваяСтрока.Представление = Пользователь.ПолноеИмя;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

