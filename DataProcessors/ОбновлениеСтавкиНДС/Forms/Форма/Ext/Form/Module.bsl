
#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоАдминистратор = ПравоДоступа("Администрирование", Метаданные);
	
	Если НЕ ЭтоАдминистратор Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Для работы с данной обработкой необходимо наличе прав администратора.'")
		);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаИнформация;
	НеПоказыватьПредупреждение = Ложь;
	
	ЗаполнитьСтавкиНДС();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НеПоказыватьПредупреждение Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация Тогда
		Отказ = Истина;
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		
		ТекстВопроса = НСтр("ru = 'Перезаполнение ставок НДС еще не закончено.
		|Закрытие обработки приведет к некорректному заполнению объектов.
		|Прервать?'");
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Прервать);
		Кнопки.Добавить(КодВозвратаДиалога.Пропустить, НСтр("ru = 'Не прерывать'"));
		Обработчик = Новый ОписаниеОповещения("Подключаемый_ОбработкиПрерыванияРаботы", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки, 60, КодВозвратаДиалога.Пропустить);
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытием()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события возникающего на клиенте при нажатии реквизита "Надпись закон".
//
&НаКлиенте
Процедура НадписьЗаконНажатие(Элемент)
	
	ЗапуститьПриложение("http://publication.pravo.gov.ru/Document/View/0001201808030106");
	
КонецПроцедуры // НадписьЗаконНажатие()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события возникающего на клиенте при выполнении команды "ОК".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	Если НЕ ЗначениеЗаполнено(СтавкаНДС20) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указана ставка НДС 20%'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтавкаНДСРасчетная20) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указана расчетная ставка НДС 20%'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтавкаНДС20) И СтавкаНДС20 = СтавкаНДСРасчетная20 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Ставка НДС 20% совпадает с выбранной расчетной ставкой НДС'")
		);
		Отказ = Истина;
	КонецЕсли;
	
	Если РежимУстановкиСтавкиНДС = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указан режим перерасчета суммовых показателей.'")
		);
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	Элементы.ОК.Доступность           = Ложь;
	
	Задание = ЗапуститьФоновоеЗадание();
	Если Задание = Неопределено Тогда
		Возврат; // Удаление не требуется.
	КонецЕсли;
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	НастройкиОжидания.ВыводитьПрогрессВыполнения = Истина;
	НастройкиОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ПриОбновленииПрогрессаФоновогоЗадания", ЭтотОбъект);
	
	Обработчик = Новый ОписаниеОповещения("ПослеЗавершенияФоновогоЗадания", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Задание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры // ОК()

&НаКлиенте
Процедура ДобавитьСтавкуНДС(Команда)
	
	РасчетаяСтавка = (Команда.Имя = "ДобавитьСтавкуНДСРасчетная");
	
	ТекущаяСтавкаНДС = ?(РасчетаяСтавка, Элементы.СтавкаНДСРасчетная20, Элементы.СтавкаНДС20);
	
	СтрукутраДанных = Новый Структура();
	СтрукутраДанных.Вставить("Ставка", 20);
	
	ФормаОбъекта = ОткрытьФорму(
		"Справочник.СтавкиНДС.ФормаОбъекта",
		Новый Структура("ЗначенияЗаполнения,РежимВыбора", СтрукутраДанных,Истина),
		ТекущаяСтавкаНДС,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ФормаОбъекта.Объект.Наименование = ?(РасчетаяСтавка, "20/120", "20%");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСтавкиНДС()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СтавкиНДС.Ссылка КАК Ссылка,
	               |	СтавкиНДС.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.СтавкиНДС КАК СтавкиНДС
	               |ГДЕ
	               |	СтавкиНДС.Ставка = 20
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СтавкиНДС.Ссылка КАК Ссылка,
	               |	СтавкиНДС.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.СтавкиНДС КАК СтавкиНДС
	               |ГДЕ
	               |	СтавкиНДС.Ставка = 20
	               |	И СтавкиНДС.Наименование ПОДОБНО ""%20/120%""";
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = ПакетЗапроса[0].Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		СтавкаНДС20 = Выборка.Ссылка;
	КонецЕсли;
	
	Выборка = ПакетЗапроса[1].Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		СтавкаНДСРасчетная20 = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтавкиНДС()

&НаКлиенте
Процедура ПриОбновленииПрогрессаФоновогоЗадания(Задание, ДополнительныеПараметры) Экспорт
	Если Задание.Прогресс <> Неопределено Тогда
		ФоновоеЗаданиеСостояние = Задание.Прогресс.Текст;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияФоновогоЗадания(Задание, ДополнительныеПараметры) Экспорт
	
	Активизировать();
	
	Если Задание.Статус = "Выполнено" Тогда
		
		АдресВременногоХранилища = Задание.АдресРезультата;
		Если АдресВременногоХранилища = Неопределено Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не получен результат перезаполнения ставки НДС'"));
			Возврат;
		КонецЕсли;
		
		// 3. Сформируем результат
		// Установим параметры для печати
		МенеджерПечати = "Обработка.ОбновлениеСтавкиНДС";
		
		ИменаМакетов = Новый Массив;
		ИменаМакетов.Добавить("Отчет");
		
		// Установим параметры команды
		ПараметрКоманды = Новый Массив;
		ПараметрыПечати = Новый Структура("ЗаголовокФормы", НСтр("ru = 'Результат перезаполнения ставки НДС'"));
		ПараметрыПечати.Вставить("АдресВременногоХранилища",АдресВременногоХранилища);
		
		// Добавляем пустую ссылку, так как этот параметр должен иметь ссылочный тип, но абсолютно не влияет на поведение печатной формы.
		ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.СтавкиНДС.ПустаяСсылка"));
		
		// Выведем на печатную форму
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(МенеджерПечати,ИменаМакетов,ПараметрКоманды,Неопределено,ПараметрыПечати);
		
		НеПоказыватьПредупреждение = Истина;
		
		Закрыть();
		
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(Задание.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновоеЗадание()
	
	// Определение параметров запуска.
	ИмяМетода = "Обработки.ОбновлениеСтавкиНДС.ВыполнитьПерезаполнениеСтавокНДС";
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Перезаполнение ставок НДС'");
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ПерерасчетСумм",       РежимУстановкиСтавкиНДС);
	ПараметрыПроцедуры.Вставить("СтавкаНДС20",          СтавкаНДС20);
	ПараметрыПроцедуры.Вставить("СтавкаНДСРасчетная20", СтавкаНДСРасчетная20);
	
	Задание = ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыПроцедуры, НастройкиЗапуска);
	Задание.Вставить("ОшибкаПриУстановкеМонопольногоРежима", Ложь);
	Задание.Вставить("ТекстОшибкиУстановкиМонопольногоРежима", Неопределено);
	
	Возврат Задание;
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбработкиПрерыванияРаботы(РезультатОповещения, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если РезультатОповещения = КодВозвратаДиалога.Прервать Тогда
		НеПоказыватьПредупреждение = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // Подключаемый_ОбработкиПрерыванияРаботы()

#КонецОбласти

