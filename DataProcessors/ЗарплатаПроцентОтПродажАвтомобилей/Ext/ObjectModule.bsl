
#Область ОписаниеПеременных

Перем ДатаНачала Экспорт; // Дата начала интервала расчета
Перем ДатаКонца Экспорт;  // Дата конца интервала расчета
Перем Сотрудник Экспорт;  // Ссылка на сотрудника по которому производится расчет

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Фукнция расчета базовой суммы
Функция БазоваяСумма() Экспорт
	БазоваяСумма=0;
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ТекстЗапроса ="ВЫБРАТЬ
			|	ЕСТЬNULL(СУММА(тбл.СуммаКупленныеАвто), 0) КАК СуммаКупленныеАвто,
			|	ЕСТЬNULL(СУММА(тбл.СуммаКупленныеТовары), 0) КАК СуммаКупленныеТовары,
			|	ЕСТЬNULL(СУММА(тбл.СуммаПринятыеАвто), 0) КАК СуммаПринятыеАвто,
			|	ЕСТЬNULL(СУММА(тбл.СуммаПринятыеТовары), 0) КАК СуммаПринятыеТовары,
			|	ЕСТЬNULL(СУММА(тбл.СуммаОтданныеАвто), 0) КАК СуммаОтданныеАвто,
			|	ЕСТЬNULL(СУММА(тбл.СуммаОтданныеТовары), 0) КАК СуммаОтданныеТовары
			|ИЗ
			|	(ВЫБРАТЬ
			|		ЕСТЬNULL(ПродажиАвтомобилейОбороты.СуммаУпрОборот, 0) КАК СуммаКупленныеАвто,
			|		0 КАК СуммаКупленныеТовары,
			|		0 КАК СуммаПринятыеАвто,
			|		0 КАК СуммаПринятыеТовары,
			|		0 КАК СуммаОтданныеАвто,
			|		0 КАК СуммаОтданныеТовары
			|	ИЗ
			|		РегистрНакопления.ПродажиАвтомобилей.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, СтатусПартии = &СтатусПартииКупленный) КАК ПродажиАвтомобилейОбороты
			|	ГДЕ
			|		ПродажиАвтомобилейОбороты.Регистратор ССЫЛКА Документ.РеализацияАвтомобилей
			|		И ПродажиАвтомобилейОбороты.Регистратор.Менеджер = &Менеджер
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		0,
			|		ЕСТЬNULL(ПродажиОбороты.СуммаУпрОборот, 0),
			|		0,
			|		0,
			|		0,
			|		0
			|	ИЗ
			|		РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, СтатусПартии = &СтатусПартииКупленный) КАК ПродажиОбороты
			|	ГДЕ
			|		ПродажиОбороты.Регистратор ССЫЛКА Документ.РеализацияАвтомобилей
			|		И ПродажиОбороты.Регистратор.Менеджер = &Менеджер
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		0,
			|		0,
			|		ЕСТЬNULL(РеализованныеАвтомобили1.СуммаПродажиПриход, 0),
			|		0,
			|		0,
			|		0
			|	ИЗ
			|		РегистрНакопления.РеализованныеАвтомобили.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ) КАК РеализованныеАвтомобили
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РеализованныеАвтомобили.Обороты(, , Регистратор, ) КАК РеализованныеАвтомобили1
			|			ПО РеализованныеАвтомобили.ДокументПередачи = РеализованныеАвтомобили1.ДокументПередачи
			|				И РеализованныеАвтомобили.Автомобиль = РеализованныеАвтомобили1.Автомобиль
			|				И РеализованныеАвтомобили.Контрагент = РеализованныеАвтомобили1.Контрагент
			|				И РеализованныеАвтомобили.ДоговорВзаиморасчетов = РеализованныеАвтомобили1.ДоговорВзаиморасчетов
			|				И (РеализованныеАвтомобили.Регистратор ССЫЛКА Документ.ОтчетКомитентуЗаАвтомобили)
			|				И (РеализованныеАвтомобили1.Регистратор ССЫЛКА Документ.РеализацияАвтомобилей)
			|				И (РеализованныеАвтомобили1.Регистратор.Менеджер = &Менеджер)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		0,
			|		0,
			|		0,
			|		ЕСТЬNULL(РеализованныеТовары1.СуммаПродажиПриход, 0),
			|		0,
			|		0
			|	ИЗ
			|		РегистрНакопления.РеализованныеТовары.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ) КАК РеализованныеТовары
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РеализованныеТовары.Обороты(, , Регистратор, ) КАК РеализованныеТовары1
			|			ПО РеализованныеТовары.ДокументПередачи = РеализованныеТовары1.ДокументПередачи
			|				И РеализованныеТовары.Номенклатура = РеализованныеТовары1.Номенклатура
			|				И РеализованныеТовары.ХарактеристикаНоменклатуры = РеализованныеТовары1.ХарактеристикаНоменклатуры
			|				И РеализованныеТовары.Контрагент = РеализованныеТовары1.Контрагент
			|				И РеализованныеТовары.ДоговорВзаиморасчетов = РеализованныеТовары1.ДоговорВзаиморасчетов
			|				И (РеализованныеТовары.Регистратор ССЫЛКА Документ.ОтчетКомитенту)
			|				И (РеализованныеТовары1.Регистратор ССЫЛКА Документ.РеализацияАвтомобилей)
			|				И (РеализованныеТовары1.Регистратор.Менеджер = &Менеджер)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		0,
			|		0,
			|		0,
			|		0,
			|		ЕСТЬNULL(АвтомобилиОтданныеОбороты.СуммаУпрРасход, 0),
			|		0
			|	ИЗ
			|		РегистрНакопления.АвтомобилиОтданные.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ДокументПередачи.Менеджер = &Менеджер) КАК АвтомобилиОтданныеОбороты
			|	ГДЕ
			|		АвтомобилиОтданныеОбороты.Регистратор ССЫЛКА Документ.ОтчетКомиссионераЗаАвтомобили
			|		И АвтомобилиОтданныеОбороты.ДокументПередачи ССЫЛКА Документ.РеализацияАвтомобилей
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		0,
			|		0,
			|		0,
			|		0,
			|		0,
			|		ЕСТЬNULL(ПартииТоваровОтданныеОбороты.СуммаУпрРасход, 0)
			|	ИЗ
			|		РегистрНакопления.ПартииТоваровОтданные.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ДокументПередачи.Менеджер = &Менеджер) КАК ПартииТоваровОтданныеОбороты
			|	ГДЕ
			|		ПартииТоваровОтданныеОбороты.Регистратор ССЫЛКА Документ.ОтчетКомиссионераЗаАвтомобили
			|		И ПартииТоваровОтданныеОбороты.ДокументПередачи ССЫЛКА Документ.РеализацияАвтомобилей) КАК тбл";
		Запрос = Новый Запрос();
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ДатаНачала", Новый Граница(НачалоДня(ДатаНачала),ВидГраницы.Включая));
		Запрос.УстановитьПараметр("ДатаКонца", Новый Граница(КонецДня(ДатаКонца),ВидГраницы.Включая));
		Запрос.УстановитьПараметр("Менеджер", Сотрудник);
		Запрос.УстановитьПараметр("СтатусПартииКупленный", Перечисления.СтатусыПартий.ТоварКупленный);
		// выполняем запрос
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			БазоваяСумма = Выборка.СуммаКупленныеАвто + Выборка.СуммаКупленныеТовары
						+ Выборка.СуммаПринятыеАвто + Выборка.СуммаПринятыеТовары
						+ Выборка.СуммаОтданныеАвто + Выборка.СуммаОтданныеТовары;
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат БазоваяСумма;
КонецФункции

#КонецОбласти