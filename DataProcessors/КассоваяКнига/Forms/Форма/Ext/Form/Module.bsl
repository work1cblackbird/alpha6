///////////////////////////////////////////////////////////////////////////////
// Модуль основной формы обработки "Кассовая книга"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Создадим список месяцев
	МассивМесяцев.Вставить(0,"");
	МассивМесяцев.Вставить(1,"январь");
	МассивМесяцев.Вставить(2,"февраль");
	МассивМесяцев.Вставить(3,"март");
	МассивМесяцев.Вставить(4,"апрель");
	МассивМесяцев.Вставить(5,"май");
	МассивМесяцев.Вставить(6,"июнь");
	МассивМесяцев.Вставить(7,"июль");
	МассивМесяцев.Вставить(8,"август");
	МассивМесяцев.Вставить(9,"сентябрь");
	МассивМесяцев.Вставить(10,"октябрь");
	МассивМесяцев.Вставить(11,"ноябрь");
	МассивМесяцев.Вставить(12,"декабрь");
	
	// Добавим список выбора
	Элементы.Лист.СписокВыбора.Добавить(0,"Обычный");
	Элементы.Лист.СписокВыбора.Добавить(1, НСтр("ru = 'Последний в месяце'"));
	Элементы.Лист.СписокВыбора.Добавить(2, НСтр("ru = 'Последний в году'"));
	
	// Установим значение по умолчанию
	Объект.Лист = 0;
	
	// Установим значения по умолчанию для периода.
	Объект.Период.ДатаНачала = НачалоДня(ТекущаяДатаСеанса());
	Объект.Период.ДатаОкончания  = КонецДня(ТекущаяДатаСеанса());
	
	// Обновляем отображение элементов формы.
	УправлениеДиалогомНаСервере();
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события возникающего при нажатии на кнопку "Сформировать".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура Сформировать(Команда)
	
	// Очистим сообщения на форме
	ОчиститьСообщения();
	
	// Проверим корректность заполнения
	Если Объект.Период.ДатаНачала > Объект.Период.ДатаОкончания И Объект.Период.ДатаОкончания <> '00010101000000' Тогда
		ТекстСообщения = НСтр("ru='Дата начала периода не может быть больше даты конца периода'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,  ,"Период");
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Организация"" не заполнено'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"Организация");
	Иначе
		
		// Установим параметры для печати
		МенеджерПечати = "Обработка.КассоваяКнига";
		
		ИменаМакетов = Новый Массив;
		ИменаМакетов.Добавить("КассоваяКнига");
		
		// Если необходимо напечатать обложку добавим макет.
		Если Объект.ПечатьОбложки Тогда
			ИменаМакетов.Добавить("Обложка");
		КонецЕсли;
		
		ОписаниеОбъекта = Новый Структура();
		ОписаниеОбъекта.Вставить("КассаКомпании", Объект.КассаКомпании);
		ОписаниеОбъекта.Вставить("ПересчитыватьНомера", Объект.ПересчитыватьНомера);
		ОписаниеОбъекта.Вставить("ВыводитьОснования", Объект.ВыводитьОснования);
		ОписаниеОбъекта.Вставить("ПечатьОбложки", Объект.ПечатьОбложки);
		ОписаниеОбъекта.Вставить("Организация", Объект.Организация);
		ОписаниеОбъекта.Вставить("Лист", Объект.Лист);
		ОписаниеОбъекта.Вставить("Период", Объект.Период);
		
		// Установим параметры команды
		ПараметрКоманды = Новый Массив;
		ПараметрыПечати = Новый Структура("ЗаголовокФормы", НСтр("ru = 'Кассовая книга'"));
		ПараметрыПечати.Вставить("ОбъектКассоваяКнига", ОписаниеОбъекта);
		ПараметрыПечати.Вставить("МассивМесяцев", МассивМесяцев);
		ПараметрыПечати.Вставить("ФиксированныйКомплект", Истина);
		
		// Добавляем пустую ссылку, так как этот параметр должен иметь ссылочный тип,
		// но абсолютно не влияет на поведение печатной формы.
		ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.КассыКомпании.ПустаяСсылка"));
		
		// Выведем на печатную форму
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			МенеджерПечати,
			ИменаМакетов,
			ПараметрКоманды,
			ЭтотОбъект,
			ПараметрыПечати);
		
		
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Производит настройку параметров отображения элементов управления диалога в зависимости от значений реквизитов объекта.
//
&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	// Установим отметку незаполненного
	Элементы.Организация.АвтоОтметкаНезаполненного = Истина;
	Элементы.Организация.ОтметкаНезаполненного     = Истина;
	Элементы.Период.АвтоОтметкаНезаполненного      = Истина;
	Элементы.Период.ОтметкаНезаполненного          = Истина;
	
КонецПроцедуры // УправлениеДиалогомНаСервере()

#КонецОбласти

