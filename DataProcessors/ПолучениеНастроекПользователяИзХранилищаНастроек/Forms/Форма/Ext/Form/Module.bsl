
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	пользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	
	Если пользователиИБ.Количество() = 0 Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого пользователь Из пользователиИБ Цикл
		новаяСтрока = СписокДоступныхПользователей.Добавить();
		новаяСтрока.Представление   = пользователь.ПолноеИмя;
		новаяСтрока.ИмяПользователя = пользователь.Имя;
	КонецЦикла;
	
	Параметры.Свойство("Настройки", Настройки);
	Параметры.Свойство("КлючОбъектВХранилищеНастроек", КлючОбъектВХранилищеНастроек);
	Параметры.Свойство("ИмяХранилищаНастроек", ИмяХранилищаНастроек);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.СписокДоступныхПользователей.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьНастройки(Элементы.СписокДоступныхПользователей.ТекущаяСтрока);
	
	Закрыть(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройки(текущаяСтрока)
	
	текущиеДанные = СписокДоступныхПользователей.НайтиПоИдентификатору(текущаяСтрока);
	
	Для Каждого Настройка Из Настройки Цикл
		Если ИмяХранилищаНастроек = "ХранилищеОбщихНастроек" Тогда
			данные = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъектВХранилищеНастроек, Настройка.Ключ, Неопределено,, текущиеДанные.ИмяПользователя);
		Иначе
			данные = Неопределено;
		КонецЕсли;
		
		Если данные <> Неопределено Тогда
			Настройки[Настройка.Ключ] = ПоместитьВоВременноеХранилище(данные, Новый УникальныйИдентификатор);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
