///////////////////////////////////////////////////////////////////////////////
// Модуль формы обработки "Персональные настройки"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Получим расширенные параметры подбора.
	ПараметрыПодбора = ПолучитьИзВременногоХранилища(Параметры.ПараметрыРасширеннойФормыПодбора);
	
	// Составим список фильтруемых пользователей.
	Если ТипЗнч(ПараметрыПодбора.ВыбранныеПользователи)=Тип("ТаблицаЗначений") Тогда
		ГрупповаяНастройкаПользователиГруппы.Загрузить(ПараметрыПодбора.ВыбранныеПользователи);
	КонецЕсли;
	
	МассивТипов = Новый Массив;
	Если Параметры.ВыборПользователей Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
		Заголовок = НСтр("ru = 'Редактирование состава пользователей для групповой настройки прав и настроек'");
	Иначе
		МассивТипов.Добавить(Тип("СправочникСсылка.ГруппыПравИНастроек"));
		Заголовок = НСтр("ru = 'Редактирование состава групп прав и настроек для групповой настройки прав и настроек'");
	КонецЕсли;
	
	Типы = Новый ОписаниеТипов(МассивТипов);
	Элементы.ГрупповаяНастройкаПользователиГруппыПользователь.ОграничениеТипа = Типы;
	ВыборПользователей = Параметры.ВыборПользователей;
	
КонецПроцедуры //ПриСозданииНаСервере()

// Обработчик события возникающего на клиенте при открытии формы, до показа окна пользователю.
//
// Параметры:
//  Отказ - Булево - Признак отказа от создания формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗащищенныеФункцииКлиент.НастроитьЭлементФормыТабличнойЧасти(ЭтотОбъект,"ГрупповаяНастройкаПользователиГруппы");
	ИдФормыВладельца = ВладелецФормы.УникальныйИдентификатор;
	
КонецПроцедуры //ПриОткрытии()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события возникающего на клиенте при выполнении команды "Подобрать пользователей/группы".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура ПодобратьПользователейГруппы(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("РасширенныйПодбор", Истина);  
	ПараметрыФормы.Вставить("ПараметрыРасширеннойФормыПодбора", ПараметрыРасширеннойФормыПодбора());
	
	Если ВыборПользователей Тогда
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
		ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы,Элементы.ГрупповаяНастройкаПользователиГруппы);
	Иначе
		ОткрытьФорму(
			"Справочник.ГруппыПравИНастроек.ФормаВыбора",
			ПараметрыФормы,
			Элементы.ГрупповаяНастройкаПользователиГруппы);
	КонецЕсли;
	
КонецПроцедуры //ПодобратьПользователейГруппы()

&НаСервере
Функция ПараметрыРасширеннойФормыПодбора()
	
	ЗаголовокФормыПодбора = НСтр("ru = 'Подбор участников группы доступа'");
	ПараметрыРасширеннойФормыПодбора = Новый Структура("ЗаголовокФормыПодбора, ВыбранныеПользователи",
	                                                   ЗаголовокФормыПодбора, ГрупповаяНастройкаПользователиГруппы.Выгрузить());
	АдресХранилища = ПоместитьВоВременноеХранилище(ПараметрыРасширеннойФормыПодбора);
	Возврат АдресХранилища;
	
КонецФункции //ПараметрыРасширеннойФормыПодбора()

// Обработчик события возникающего на клиенте при выполнении команды "Применить.
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура Применить(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("РезультатПодбора",РезультатПодбора());
	Результат.Вставить("Модифицированность",Модифицированность);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Функция РезультатПодбора()
	
	ПараметрыРасширеннойФормыПодбора = Новый Структура("ВыбранныеПользователи",ГрупповаяНастройкаПользователиГруппы.Выгрузить());
	АдресХранилища = ПоместитьВоВременноеХранилище(ПараметрыРасширеннойФормыПодбора,ИдФормыВладельца);
	Возврат АдресХранилища;
	
КонецФункции //ВыбранныеПользователиГруппы()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыГрупповаяНастройкаПользователиГруппы

&НаКлиенте
Процедура ГрупповаяНастройкаПользователиГруппыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		
		Для Каждого Значение Из ВыбранноеЗначение Цикл
			НовыйЭлемент = ГрупповаяНастройкаПользователиГруппы.Добавить();
			НовыйЭлемент.Пользователь = Значение;
			Модифицированность = Истина;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры //ГрупповаяНастройкаПользователиГруппыОбработкаВыбора()

#КонецОбласти
