
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаказНаряд = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ЗаказНаряд");
	Номенклатура = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Номенклатура");
	Характеристика = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ХарактеристикаНоменклатуры");
	КоличествоНоменклатуры = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Количество");
	
	Если НЕ ЗначениеЗаполнено(ЗаказНаряд) ИЛИ НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Заполним остатком кодов маркировок
	СформироватьСписокКодовМаркировокЗаказНаряда(ЗаказНаряд, Номенклатура, Характеристика);
	
	// Список пустой - нечего переносить.
	Если СписокКодовМаркировки.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Расставим ранее вы
	ВыбранныеКодыМаркировки = ПолучитьЗначениеПараметраСтруктуры(Параметры, "ВыбранныеКодыМаркировки");
	
	Для Каждого ТекущаяСтрока Из СписокКодовМаркировки Цикл
		Если ВыбранныеКодыМаркировки.Найти(ТекущаяСтрока.Значение) <> Неопределено Тогда
			ТекущаяСтрока.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Оформим заголовок формы
	Если ЗначениеЗаполнено(Характеристика) Тогда
		ЭтотОбъект.Заголовок = СтрШаблон(
			НСтр("ru = '%1 с характеристикой %2'"),
			СокрЛП(Номенклатура),
			СокрЛП(Характеристика));
	Иначе
		ЭтотОбъект.Заголовок = СокрЛП(Номенклатура);
	КонецЕсли;
	
	МаркировкаОбязательна = МаркировкаТоваровСервер.МаркировкаНоменклатурыОбязательная(Номенклатура, ТекущаяДатаСеанса());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	// Получим список выбранных маркировок
	СписокМаркировокПереноса = Новый Массив;
	
	Для Каждого ТекущаяСтрока Из СписокКодовМаркировки Цикл
		Если ТекущаяСтрока.Пометка Тогда
			СписокМаркировокПереноса.Добавить(ТекущаяСтрока.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ВыбраноМаркировок = СписокМаркировокПереноса.Количество();
	
	// Проверим, что обязательным маркировкам нашли соответствие.
	Если МаркировкаОбязательна И КоличествоНоменклатуры <> ВыбраноМаркировок Тогда
		ПоказатьПредупреждение(
			,
			СтрШаблон(НСтр("ru = 'Выбрано кодов маркировки %1. Необходимо выбрать %2 кодов'"),
				ВыбраноМаркировок,
				КоличествоНоменклатуры));
		Возврат;
	КонецЕсли;
	
	// Запретим выбирать больше необходимого
	Если ВыбраноМаркировок > КоличествоНоменклатуры Тогда
		ПоказатьПредупреждение(
			,
			СтрШаблон(НСтр("ru = 'Выбрано кодов маркировки %1 больше, чем для переноса. Необходимо выбрать %2 кодов'"),
				ВыбраноМаркировок,
				КоличествоНоменклатуры));
		Возврат;
	КонецЕсли;
	
	// Сформируем вывод итога.
	Закрыть(СписокМаркировокПереноса);
	
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	
	ОбработчикОповещения = Новый ОписаниеОповещения("Подключаемый_ЗакрытиеФормыПодбора", ЭтотОбъект);
	
	ПоказатьВопрос(
		ОбработчикОповещения,
		НСтр("ru = 'Отмена выбора кодов маркировки может привести к ошибкам при переносе. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьСписокКодовМаркировокЗаказНаряда(ЗаказНаряд, Номенклатура, Характеристика)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	МаркировкаТоваровВПроизводствеОстатки.Номенклатура КАК Номенклатура,
	               |	МаркировкаТоваровВПроизводствеОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	МаркировкаТоваровВПроизводствеОстатки.GTIN КАК GTIN,
	               |	МаркировкаТоваровВПроизводствеОстатки.СерийныйНомер КАК СерийныйНомер
	               |ПОМЕСТИТЬ КодыМаркировкиПроизводства
	               |ИЗ
	               |	РегистрНакопления.МаркировкаТоваровВПроизводстве.Остатки(
	               |			,
	               |			ЗаказНаряд = &ЗаказНаряд
	               |				И Номенклатура = &Номенклатура
	               |				%УсловиеПоХарактеристике%) КАК МаркировкаТоваровВПроизводствеОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	КодыМаркировкиПроизводства.GTIN КАК GTIN,
	               |	КодыМаркировкиПроизводства.СерийныйНомер КАК СерийныйНомер,
	               |	ЕСТЬNULL(СостоянияКодовМаркировкиСрезПоследних.КодМаркировки, """") КАК КодМаркировки
	               |ИЗ
	               |	КодыМаркировкиПроизводства КАК КодыМаркировкиПроизводства
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияКодовМаркировки.СрезПоследних(
	               |				,
	               |				(Номенклатура, ХарактеристикаНоменклатуры, GTIN, СерийныйНомер) В
	               |					(ВЫБРАТЬ
	               |						КодыМаркировкиПроизводства.Номенклатура КАК Номенклатура,
	               |						КодыМаркировкиПроизводства.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |						КодыМаркировкиПроизводства.GTIN КАК GTIN,
	               |						КодыМаркировкиПроизводства.СерийныйНомер КАК СерийныйНомер
	               |					ИЗ
	               |						КодыМаркировкиПроизводства КАК КодыМаркировкиПроизводства)) КАК СостоянияКодовМаркировкиСрезПоследних
	               |		ПО КодыМаркировкиПроизводства.Номенклатура = СостоянияКодовМаркировкиСрезПоследних.Номенклатура
	               |			И КодыМаркировкиПроизводства.ХарактеристикаНоменклатуры = СостоянияКодовМаркировкиСрезПоследних.ХарактеристикаНоменклатуры
	               |			И КодыМаркировкиПроизводства.GTIN = СостоянияКодовМаркировкиСрезПоследних.GTIN
	               |			И КодыМаркировкиПроизводства.СерийныйНомер = СостоянияКодовМаркировкиСрезПоследних.СерийныйНомер";
	
	УсловиеПоХарактеристике = "";
	Если ЗначениеЗаполнено(Характеристика) Тогда
		УсловиеПоХарактеристике = "И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоХарактеристике%", УсловиеПоХарактеристике);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ЗаказНаряд", ЗаказНаряд);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", Характеристика);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СписокКодовМаркировки.Добавить(
			?(ПустаяСтрока(Выборка.КодМаркировки),
				СтрШаблон("(01)%1(21)%2", Выборка.GTIN, Выборка.СерийныйНомер),
				Выборка.КодМаркировки));
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗакрытиеФормыПодбора(Ответ, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // Подключаемый_ЗакрытиеФормыПодбора()

#КонецОбласти

