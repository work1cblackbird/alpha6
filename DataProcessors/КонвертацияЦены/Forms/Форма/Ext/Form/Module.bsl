
#Область ОбработчикиКомандФормы

// Обработчик события возникающего при нажатии кнопки "Выполнить пересчет" в контексте сервера.
//
&НаСервере
Процедура ВыполнитьПересчетНаСервере()
	
	Запрос = Новый Запрос;
	
	Для Каждого Док Из Метаданные.Документы Цикл
		Если СписокДокументовИсключений(Док.Имя) Тогда
			Для Каждого ТЧ Из Док.ТабличныеЧасти Цикл
				Если ЕстьРеквизит(Док,"Цена",ТЧ.Имя) ИЛИ ЕстьРеквизит(Док,"ЦенаРозничная",ТЧ.Имя) Тогда
					Запрос.Текст = Запрос.Текст + ?(Запрос.Текст = "", "ВЫБРАТЬ ", "
					|ОБЪЕДИНИТЬ ВСЕ
					|ВЫБРАТЬ") + " 
					|""" + ТЧ.Имя + """ КАК ИмяТЧ, 
					|""" + Док.Имя + """ КАК ДокИмя, 
					|Ссылка КАК Ссылка 
					|ИЗ Документ." + Док.Имя + "
					|";
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИзменяемыйДокумент = Выборка.Ссылка.ПолучитьОбъект();
		
		Если ЕстьРеквизит(Выборка.Ссылка,"Цена",Выборка.ИмяТЧ) Тогда
			Для Каждого Строка Из ИзменяемыйДокумент[Выборка.ИмяТЧ] Цикл
				Строка.Цена = Строка.Цена * Строка.Коэффициент;
			КонецЦикла;
		КонецЕсли;
		
		Если ЕстьРеквизит(Выборка.Ссылка,"ЦенаРозничная",Выборка.ИмяТЧ) Тогда
			Для Каждого Строка Из ИзменяемыйДокумент[Выборка.ИмяТЧ] Цикл
				Строка.ЦенаРозничная = Строка.ЦенаРозничная * Строка.Коэффициент;
			КонецЦикла;
		КонецЕсли;
		
		Если Выборка.ДокИмя = "Переоценка" Тогда
			Для Каждого Строка Из ИзменяемыйДокумент[Выборка.ИмяТЧ] Цикл
				Строка.ЦенаСтарая = Строка.ЦенаСтарая * Строка.Коэффициент;
			КонецЦикла;
		КонецЕсли;
		
		Если Выборка.ДокИмя = "ПересортицаТоваров" Тогда
			Для Каждого Строка Из ИзменяемыйДокумент[Выборка.ИмяТЧ] Цикл
				Строка.ЦенаРозничнаяПриход = Строка.ЦенаРозничнаяПриход * Строка.КоэффициентПриход;
			КонецЦикла;
		КонецЕсли;
		
		ИзменяемыйДокумент.ОбменДанными.Загрузка = Истина;
		Попытка
			ИзменяемыйДокумент.Записать();
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(НСтр("ru='Обновлен документ ""%1"".'"), СокрЛП(ИзменяемыйДокумент.Ссылка))
			);
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Конвертация цены. Не удалось обновить документ'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				,
				ИзменяемыйДокумент.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры // ВыполнитьПересчетНаСервере()

// Обработчик события нажатия кнопки "Выполнить пересчет".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура ВыполнитьПересчет(Команда)
	
	// Обработаем событие в контексте сервера
	ВыполнитьПересчетНаСервере();
	
КонецПроцедуры //ВыполнитьПересчет()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СписокДокументовИсключений(ИмяДокумента)
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить("ИзменениеАссортиментаПодразделения");
	МассивДокументов.Добавить("БюджетЗакупок");
	МассивДокументов.Добавить("БюджетПродаж");
	
	Возврат (МассивДокументов.Найти(ИмяДокумента)=Неопределено);
	
КонецФункции

#КонецОбласти