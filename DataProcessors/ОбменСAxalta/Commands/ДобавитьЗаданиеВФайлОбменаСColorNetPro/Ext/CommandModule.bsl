#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПроверитьВозможностьДобавления(ПараметрКоманды) Тогда
		ДобавитьЗаписьВФайл(ПараметрКоманды);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПроверитьВозможностьДобавленияНаСервере(ЗаказНаряд, КодОшибки, ТекстОшибки)
	КодОшибки = 0; ТекстОшибки = "";
	
	Если НЕ ЗначениеЗаполнено(ЗаказНаряд) Тогда
		КодОшибки = 1;
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не указан заказ-наряд.'"));
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗаказНаряд.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен ИЛИ ЗаказНаряд.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
		КодОшибки = 1;
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Нельзя добавить заказ-наряд находящийся в состоянии <%1>.'"), ЗаказНаряд.Состояние);
		Возврат Ложь;
	КонецЕсли;
	
	СохраненныеНастройки =
		ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить("ОбменСAxalta","Режим"+Строка(Перечисления.ОбменСAxaltaРежимыЗагрузки.ColorNetPro), Новый Структура);
		
	ПутьКФайлуЗаданий = ПолучитьЗначениеПараметраСтруктуры(СохраненныеНастройки, "ФайлЗаданий", "");
	Если ПустаяСтрока(ПутьКФайлуЗаданий) Тогда
		КодОшибки = 2;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция ДанныеДляДобавления(ЗаказНаряд, Путь)
	Данные = Новый Структура();
	
	Данные.Вставить("НомерЗаказНаряда", СтроковыеФункции.СтрокаЛатиницей(ЗаказНаряд.Номер));
	Если ПустаяСтрока(Путь) Тогда
		СохраненныеНастройки =
		ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить("ОбменСAxalta","Режим"+Строка(Перечисления.ОбменСAxaltaРежимыЗагрузки.ColorNetPro), Новый Структура);
		
		Данные.Вставить("Путь", ПолучитьЗначениеПараметраСтруктуры(СохраненныеНастройки, "ФайлЗаданий", ""));
	Иначе
		Данные.Вставить("Путь", Путь);
	КонецЕсли;
	
	Данные.Вставить("Автомобиль", ЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль);
	Данные.Вставить("Модель"    , СтроковыеФункции.СтрокаЛатиницей(Данные.Автомобиль.Модель.Наименование));
	
	ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Данные.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер, ТекущаяДатаСеанса());
	Данные.Вставить("ГосНомер", ?(ЗначениеЗаполнено(ГосНомер), СтроковыеФункции.СтрокаЛатиницей(ГосНомер), ""));
	
	Возврат Данные;
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПослеВводаПутиКФайлуЗаданий(Путь, ЗаказНаряд) Экспорт
	
	Если ЗначениеЗаполнено(Путь) Тогда
		ДобавитьЗаписьВФайл(ЗаказНаряд, Путь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДобавитьЗаписьВФайл(ЗаказНаряд, ПутьКФайлу = "")
	
	#Если ВебКлиент Тогда
		ВызватьИсключение Нстр("ru = 'Операция не поддерживается в веб-клиенте.'");		
	#КонецЕсли
	
	ДанныеДляДобавления = ДанныеДляДобавления(ЗаказНаряд, ПутьКФайлу);
	
	//@skip-check type-not-defined
	ЧтениеТекста = Новый ЧтениеТекста(ДанныеДляДобавления.Путь);
	СтрокаФайла = ЧтениеТекста.ПрочитатьСтроку();
	ЕстьЗапись = Ложь;
	Пока СтрокаФайла <> Неопределено Цикл
		ЕстьЗапись = СтрНайти(СтрокаФайла, ДанныеДляДобавления.НомерЗаказНаряда);
		Если ЕстьЗапись Тогда
			Прервать;
		КонецЕсли;
		
		СтрокаФайла = ЧтениеТекста.ПрочитатьСтроку();
	КонецЦикла;
	ЧтениеТекста.Закрыть();
	
	Если ЕстьЗапись Тогда
		ПоказатьПредупреждение(, СтрШаблон(НСтр("ru = 'Запись о заказ-наряде <%1> уже есть в файле заданий.'"), ЗаказНаряд));
		Возврат Ложь;
	КонецЕсли;
	
	//@skip-check type-not-defined
	ЗаписьТекста = Новый ЗаписьТекста;
	ЗаписьТекста.Открыть(ДанныеДляДобавления.Путь,,, Истина);
	
	Попытка
		ЗаписьТекста.ЗаписатьСтроку(СтрШаблон("%1, %2, %3",
			ДанныеДляДобавления.НомерЗаказНаряда,
			ДанныеДляДобавления.Модель,
			ДанныеДляДобавления.ГосНомер)
		);
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Успех.'"),
			,
			НСтр("ru = 'Добавление записи в файл заданий прошло успешно.'")
		);
	Исключение
		ПоказатьПредупреждение(, СтрШаблон(
			НСтр("ru = 'При добавление данных в файл обмена произошла ошибка. %1.'"),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()))
		);
	КонецПопытки;
	ЗаписьТекста.Закрыть();
КонецФункции

&НаКлиенте
Функция ПроверитьВозможностьДобавления(ЗаказНаряд)
	КодОшибки = Неопределено; ТекстОшибки = Неопределено;
	
	Если НЕ ПроверитьВозможностьДобавленияНаСервере(ЗаказНаряд, КодОшибки, ТекстОшибки) Тогда
		Если КодОшибки = 1 Тогда
			ПоказатьПредупреждение(, ТекстОшибки);
			Возврат Ложь;
		ИначеЕсли КодОшибки = 2 Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ПослеВводаПутиКФайлуЗаданий", ЭтотОбъект, ЗаказНаряд);
			ОткрытьФорму("Обработка.ОбменСAxalta.Форма.ФормаВыбораПути",,, Новый УникальныйИдентификатор,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

#КонецОбласти