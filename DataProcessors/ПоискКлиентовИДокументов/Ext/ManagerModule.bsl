
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура получения контрагентов по параметрам поиска.
//
// Параметры:
//  Параметры      - Струкутра - Структура с параметрами поиска контрагента.
//  АдресХранилища - Строка    - Адрес хранения результата поиска.
//
Процедура ВыполнитьПоискКонтрагента(Параметры, АдресХранилища) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ТекстОшибки", "");
	
	ТаблицаКонтрагентов = Новый ТаблицаЗначений;
	ТаблицаКонтрагентов.Колонки.Добавить("Контрагент");
	ТаблицаКонтрагентов.Колонки.Добавить("НомерКартинки", Новый ОписаниеТипов("Число"));
	ТаблицаКонтрагентов.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
	ТаблицаКонтрагентов.Колонки.Добавить("ПоНаименованию", Новый ОписаниеТипов("Булево"));
	ТаблицаКонтрагентов.Колонки.Добавить("ПоНомеруТелефона", Новый ОписаниеТипов("Булево"));
	ТаблицаКонтрагентов.Колонки.Добавить("ПоАдресуЭлектроннойПочты", Новый ОписаниеТипов("Булево"));
	
	Клиент = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Контрагент", "");
	
	ПоискПоНаименованию   = Ложь;
	ПоискПоТелефону       = Ложь;
	ПоискПоАдресуЭлПочты  = Ложь;
	ПоискПоВсемПараметрам = Ложь;
	
	ВыполнениеЗапроса = ФормированиеЗапросаПоискаКонтрагентов(
		Параметры,
		ПоискПоНаименованию,
		ПоискПоТелефону,
		ПоискПоАдресуЭлПочты,
		ПоискПоВсемПараметрам);
	
	Выборка = ВыполнениеЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	МаксимальноеЧилоРезультатаПоиска = ПолучитьЗначениеПараметраСтруктуры(
		Параметры,
		"МаксимальноеЧилоРезультатаПоиска",
		50);
	
	НоваяСтрока = ТаблицаКонтрагентов.Добавить();
	НоваяСтрока.НомерКартинки = 1;
	НоваяСтрока.Порядок       = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Если ПоискПоВсемПараметрам И
			(ПоискПоНаименованию И НЕ Выборка.ПоНаименованию
			ИЛИ ПоискПоТелефону И НЕ Выборка.ПоНомеруТелефона
			ИЛИ ПоискПоАдресуЭлПочты И НЕ Выборка.ПоАдресуЭлектроннойПочты) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Выборка.Контрагент) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаКонтрагентов.Добавить();
		
		Если ТаблицаКонтрагентов.Количество() > МаксимальноеЧилоРезультатаПоиска Тогда
			Результат.ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Найдено более %1 клиентов. Уточните параметры поиска.'"),
				МаксимальноеЧилоРезультатаПоиска);
			Прервать;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонтрагентИзСправочника = ТипЗнч(НоваяСтрока.Контрагент) = Тип("СправочникСсылка.Контрагенты");
		НоваяСтрока.НомерКартинки = ?(КонтрагентИзСправочника, 0, 1);
		
		Если КонтрагентИзСправочника Тогда
			Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты")
				И НоваяСтрока.ПоНаименованию Тогда
				
				НоваяСтрока.Порядок = 1;
				
			ИначеЕсли НоваяСтрока.ПоНаименованию
				И НоваяСтрока.ПоНомеруТелефона
				И НоваяСтрока.ПоАдресуЭлектроннойПочты Тогда
				
				НоваяСтрока.Порядок = 2;
				
			ИначеЕсли (НоваяСтрока.ПоНаименованию И НоваяСтрока.ПоНомеруТелефона)
				ИЛИ (НоваяСтрока.ПоНаименованию И НоваяСтрока.ПоАдресуЭлектроннойПочты)
				ИЛИ (НоваяСтрока.ПоНомеруТелефона И НоваяСтрока.ПоАдресуЭлектроннойПочты) Тогда
				
				НоваяСтрока.Порядок = 4;
				
			Иначе
				
				НоваяСтрока.Порядок = 6;
				
			КонецЕсли;
		Иначе
			
			Если НоваяСтрока.ПоНаименованию
				И НоваяСтрока.ПоНомеруТелефона
				И НоваяСтрока.ПоАдресуЭлектроннойПочты Тогда
				
				НоваяСтрока.Порядок = 3;
				
			ИначеЕсли (НоваяСтрока.ПоНаименованию И НоваяСтрока.ПоНомеруТелефона)
				ИЛИ (НоваяСтрока.ПоНаименованию И НоваяСтрока.ПоАдресуЭлектроннойПочты)
				ИЛИ (НоваяСтрока.ПоНомеруТелефона И НоваяСтрока.ПоАдресуЭлектроннойПочты) Тогда
				
				НоваяСтрока.Порядок = 5;
				
			Иначе
				
				НоваяСтрока.Порядок = 7;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаКонтрагентов.Сортировать("Порядок");
	
	Результат.Вставить("ТаблицаКонтрагентов", ТаблицаКонтрагентов);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Процедура получения контрагентов по параметрам поиска.
//
// Параметры:
//  Параметры      - Струкутра - Структура с параметрами поиска документов.
//  АдресХранилища - Строка    - Адрес хранения результата поиска.
//
Процедура ПолучитьОтборыПоДокументам(Параметры, АдресХранилища) Экспорт
	
	ТаблицаРезультата = ПолучитьДокументовКонтрагентовСервиса(Параметры);
	
	ПоместитьВоВременноеХранилище(ТаблицаРезультата, АдресХранилища);
	
КонецПроцедуры // ПолучитьОтборыПоДокументам()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ФормированиеЗапросаПоискаКонтрагентов(
		СтруктураПоиска,
		ПоискПоНаименованию,
		ПоискПоТелефону,
		ПоискПоАдресуЭлПочты,
		ПоискПоВсемПараметрам)
	
	Клиент                = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "Контрагент", "");
	НомерТелефона         = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "НомерТелефона", "");
	АдресЭлектроннойПочты = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "АдресЭлектроннойПочты", "");
	НомерТелефонаИсходный = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "НомерТелефонаИсходный", "");
	ВнутреннийНомер       = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "Добавочный", "");
	
	ПоискПоНаименованию   = ЗначениеЗаполнено(Клиент);
	ПоискПоТелефону       = ЗначениеЗаполнено(НомерТелефона);
	ПоискПоАдресуЭлПочты  = ЗначениеЗаполнено(АдресЭлектроннойПочты);
	ПоискПоВсемПараметрам = (ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "РежимПоиска", "") = "Полный");
	
	// Ограничим поиск телефона кодом региона и номером.
	ДлинаТелефона = 10;
	НомерТелефонаПоиска = ?(СтрДлина(НомерТелефона) > ДлинаТелефона, Прав(НомерТелефона, ДлинаТелефона), НомерТелефона);
	ПредставлениеКлиента = ?(ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты"), Клиент.Наименование, Клиент);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Клиент", Клиент);
	Запрос.УстановитьПараметр("ПредставлениеКлиента", ПредставлениеКлиента);
	Запрос.УстановитьПараметр("НомерТелефона", НомерТелефонаПоиска);
	Запрос.УстановитьПараметр("НомерТелефонаИсходный", НомерТелефонаИсходный);
	Запрос.УстановитьПараметр("ВнутреннийНомер", ВнутреннийНомер);
	Запрос.УстановитьПараметр("АдресЭлПочты",  АдресЭлектроннойПочты);
	
	ТекстЗапроса = Новый Массив;
	
	МаксимальноеЧислоПоиска = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "МасимальноеЧилоРезультатаПоиска", 50);
	ДобавлениеКВыборке = 1;
	МаксимальноеЧислоПоиска = МаксимальноеЧислоПоиска + ДобавлениеКВыборке;
	ОграничениеВыборки = ?(ПоискПоВсемПараметрам, "", "ПЕРВЫЕ " + Строка(МаксимальноеЧислоПоиска));
	УсловиеПоКонтрагенту = "";
	Если ПоискПоНаименованию Тогда
		ТекстЗапросаКонтрагента = ЗапросПоСправочникуКонтрагенты(
			ПоискПоВсемПараметрам,
			МаксимальноеЧислоПоиска,
			УсловиеПоКонтрагенту);
		
		Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты") Тогда
			УсловиеОтбора = "Контрагенты.Ссылка = &Клиент";
		Иначе
			УсловиеОтбора = "Контрагенты.Наименование ПОДОБНО ""%"" + &Клиент + ""%""";
		КонецЕсли;
		
		ТекстЗапросаКонтрагента = СтрЗаменить(ТекстЗапросаКонтрагента, "%УсловиеОтбора%", УсловиеОтбора);
		ТекстЗапроса.Добавить(ТекстЗапросаКонтрагента);
	КонецЕсли;
	
	Если ПоискПоТелефону ИЛИ ПоискПоАдресуЭлПочты Тогда
		
		ТекстЗапросаПоКИ = ЗапросККонтактнойИнформацииКонтрагента(
			ПоискПоТелефону,
			ПоискПоАдресуЭлПочты,
			ОграничениеВыборки,
			УсловиеПоКонтрагенту);
		
		ТекстЗапроса.Добавить(ТекстЗапросаПоКИ);
	КонецЕсли;
	
	ЗапросПоискаКонтрагентаВДокументах(
		ТекстЗапроса,
		СтруктураПоиска,
		Клиент,
		ПоискПоТелефону,
		ПоискПоАдресуЭлПочты,
		ОграничениеВыборки,
		ПоискПоВсемПараметрам);
	
	Разделитель = "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	
	Запрос.Текст = СтрСоединить(ТекстЗапроса, Разделитель);
	
	Запрос.Текст = Запрос.Текст + "
		|ИТОГИ
		|	МАКСИМУМ(ПоНаименованию),
		|	МАКСИМУМ(ПоНомеруТелефона),
		|	МАКСИМУМ(ПоАдресуЭлектроннойПочты)
		|ПО
		|	Контрагент";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции // ФормированиеЗапросаПоискаКонтрагентов()

Функция ЗапросПоСправочникуКонтрагенты(ПоискПоВсемПараметрам, МаксимальноеЧислоПоиска, УсловиеПоКонтрагенту)
	
	Если ПоискПоВсемПараметрам Тогда
		ЗапросКонтрагента = "ВЫБРАТЬ
			|	Контрагенты.Ссылка КАК Контрагент
			|ПОМЕСТИТЬ СписокКонтрагентов
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	%УсловиеОтбора%
			|	И НЕ Контрагенты.ЭтоГруппа
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СписокКонтрагентов.Контрагент КАК Контрагент,
			|	ИСТИНА КАК ПоНаименованию,
			|	ЛОЖЬ КАК ПоНомеруТелефона,
			|	ЛОЖЬ КАК ПоАдресуЭлектроннойПочты
			|ИЗ
			|	СписокКонтрагентов КАК СписокКонтрагентов";
		УсловиеПоКонтрагенту = " И КонтрагентыКонтактнаяИнформация.Ссылка В (ВЫБРАТЬ
			|СписокКонтрагентов.Контрагент КАК Контрагент
			|ИЗ
			|СписокКонтрагентов КАК СписокКонтрагентов)";
	Иначе
		ЗапросКонтрагента = "ВЫБРАТЬ ПЕРВЫЕ " + Строка(МаксимальноеЧислоПоиска) + "
			|	Контрагенты.Ссылка КАК Контрагент,
			|	ИСТИНА КАК ПоНаименованию,
			|	ЛОЖЬ КАК ПоНомеруТелефона,
			|	ЛОЖЬ КАК ПоАдресуЭлектроннойПочты
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	%УсловиеОтбора%
			|	И НЕ Контрагенты.ЭтоГруппа";
	КонецЕсли;
	
	Возврат ЗапросКонтрагента;
	
КонецФункции // ЗапросПоСправочникуКонтрагенты()

Функция ЗапросККонтактнойИнформацииКонтрагента(
	ПоискПоТелефону,
	ПоискПоАдресуЭлПочты,
	ОграничениеВыборки,
	УсловиеПоКонтрагенту)
	
	ТекстЗапросаПоКИ = "ВЫБРАТЬ %ОграничениеВыборки%
		|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Контрагент,
		|	ЛОЖЬ КАК ПоНаименованию,
		|	КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон) КАК ПоНомеруТелефона,
		|	КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты) КАК ПоАдресуЭлектроннойПочты
		|ИЗ
		|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
		|ГДЕ
		|	ВЫБОР %УсловиеОтбора%
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ %ОтборПоКонтрагенту%";
	УсловиеОтбора = Новый Массив;
	Если ПоискПоТелефону Тогда
		УсловиеОтбора.Добавить("
		|			КОГДА КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
		|				ТОГДА КонтрагентыКонтактнаяИнформация.НомерТелефона ПОДОБНО ""%"" + &НомерТелефона + ""%""");
	КонецЕсли;
	
	Если ПоискПоАдресуЭлПочты Тогда
		УсловиеОтбора.Добавить("
		|			КОГДА КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
		|				ТОГДА КонтрагентыКонтактнаяИнформация.АдресЭП ПОДОБНО ""%"" + &АдресЭлПочты + ""%""");
	КонецЕсли;
	
	ТекстЗапросаПоКИ = СтрЗаменить(ТекстЗапросаПоКИ, "%УсловиеОтбора%", СтрСоединить(УсловиеОтбора, ""));
	ТекстЗапросаПоКИ = СтрЗаменить(ТекстЗапросаПоКИ, "%ОграничениеВыборки%", ОграничениеВыборки);
	ТекстЗапросаПоКИ = СтрЗаменить(ТекстЗапросаПоКИ, "%ОтборПоКонтрагенту%", УсловиеПоКонтрагенту);
	
	Возврат ТекстЗапросаПоКИ;
	
КонецФункции // ЗапросККонтактнойИнформацииКонтрагента()

Процедура ЗапросПоискаКонтрагентаВДокументах(
	ТекстЗапроса,
	СтруктураПоиска,
	Клиент,
	ПоискПоТелефону,
	ПоискПоАдресуЭлПочты,
	ОграничениеВыборки,
	ПоискПоВсемПараметрам)
	
	// Определим состав документов и поиск в них контрагента.
	МассивТиповОбъектов = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "МассивТиповОбъектов", Новый Массив);
	
	Для Каждого ТекущийДокумент Из МассивТиповОбъектов Цикл
		
		Если ПустаяСтрока(ТекущийДокумент) Тогда
			Продолжить;
		КонецЕсли;
		
		Документ =  Метаданные.Документы[ТекущийДокумент];
		
		УсловиеОбращения = "";
		Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты") Тогда
			РеквизитОбращения = Документ.Реквизиты.Найти("Заказчик");
			Если РеквизитОбращения = Неопределено Тогда
				РеквизитОбращения = Документ.Реквизиты.Найти("Контрагент");
			КонецЕсли;
			Если РеквизитОбращения = Неопределено Тогда
				РеквизитОбращения = Документ.Реквизиты.Найти("Клиент");
			КонецЕсли;
			Если РеквизитОбращения = Неопределено Тогда
				РеквизитОбращения = Документ.Реквизиты.Найти("Страхователь");
			КонецЕсли;
			Если РеквизитОбращения = Неопределено Тогда
				// Нет данных для вывода контрагента.
				Продолжить;
			КонецЕсли;
			ДополнительныйРеквизит = Документ.Реквизиты.Найти("ОбращениеККлиенту");
			Если ДополнительныйРеквизит = Неопределено Тогда 
				УсловиеОбращения = СтрШаблон(
					"%1.%2 = &Клиент",
					ТекущийДокумент,
					РеквизитОбращения.Имя);
				ИмяРеквизитаКонтрагент = СтрШаблон("%1.%2", ТекущийДокумент, РеквизитОбращения.Имя);
			Иначе
				Текст = "ВЫБОР КОГДА %1.%2 = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
						| (ВЫРАЗИТЬ(%1.%3 КАК Строка)) ПОДОБНО ""%"" + &ПредставлениеКлиента + ""%""
					| ИНАЧЕ %1.%2 = &Клиент КОНЕЦ";
				УсловиеОбращения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ТекущийДокумент,
					РеквизитОбращения.Имя, ДополнительныйРеквизит.Имя);
				Текст = "ВЫБОР КОГДА %1.%2 = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
						| %1.%3
					| ИНАЧЕ %1.%2 КОНЕЦ";
				ИмяРеквизитаКонтрагент = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ТекущийДокумент,
					РеквизитОбращения.Имя, ДополнительныйРеквизит.Имя);
			КонецЕсли;
		Иначе
			РеквизитОбращения = Документ.Реквизиты.Найти("ОбращениеККлиенту");
			Если РеквизитОбращения = Неопределено Тогда
				РеквизитОбращения = Документ.Реквизиты.Найти("Контрагент");
			КонецЕсли;
			Если РеквизитОбращения = Неопределено Тогда
				РеквизитОбращения = Документ.Реквизиты.Найти("Клиент");
			КонецЕсли;
			Если РеквизитОбращения = Неопределено Тогда
				РеквизитОбращения = Документ.Реквизиты.Найти("Страхователь");
			КонецЕсли;
			Если РеквизитОбращения = Неопределено ИЛИ НЕ РеквизитОбращения.Тип.СодержитТип(Тип("Строка")) Тогда
				// Нет данных для вывода контрагента.
				Продолжить;
			КонецЕсли;
			Если ЗначениеЗаполнено(Клиент) Тогда
				УсловиеОбращения = СтрШаблон("(ВЫРАЗИТЬ(%1.%2 КАК Строка)) ПОДОБНО %3",
					ТекущийДокумент, РеквизитОбращения.Имя, """%"" + &Клиент + ""%""");
			КонецЕсли;
			ИмяРеквизитаКонтрагент = СтрШаблон("%1.%2", ТекущийДокумент, РеквизитОбращения.Имя);
		КонецЕсли;
		
		УсловиеТелефона = "";
		УсловияПоТелефону = Новый Массив;
		Если ПоискПоТелефону Тогда
			Если Документ.Реквизиты.Найти("ПредставлениеТелефона") <> Неопределено Тогда
				ТекстУсловия = СтрШаблон("%1.%2 ПОДОБНО ""_"" + %3 + ""_""", ТекущийДокумент, "ПредставлениеТелефона", "&НомерТелефонаИсходный"); 
				УсловияПоТелефону.Добавить(СтрЗаменить(ТекстУсловия, "_", "%"));
			Иначе
				Если Документ.Реквизиты.Найти("КодГорода") <> Неопределено
					И Документ.Реквизиты.Найти("НомерТелефона") <> Неопределено Тогда
					УсловияПоТелефону.Добавить(
					СтрШаблон("%1.КодГорода + %1.НомерТелефона ПОДОБНО %2", ТекущийДокумент, """%"" + &НомерТелефона + ""%"""));
				КонецЕсли;
				Если Документ.Реквизиты.Найти("ВнутреннийНомер") <> Неопределено Тогда
					УсловияПоТелефону.Добавить(
					СтрШаблон("%1.%2 ПОДОБНО %3", ТекущийДокумент, "ВнутреннийНомер", """%"" + &ВнутреннийНомер + ""%"""));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если УсловияПоТелефону.Количество() > 0 Тогда
			УсловиеТелефона = СтрСоединить(УсловияПоТелефону, " И ");
		КонецЕсли;
		
		УсловиеЭлПочты = "";
		ДанныеЭлПочты = "";
		Если ПоискПоАдресуЭлПочты Тогда
			Если Документ.Реквизиты.Найти("АдресЭлектроннойПочты") <> Неопределено Тогда
				ДанныеЭлПочты = "АдресЭлектроннойПочты";
			ИначеЕсли Документ.Реквизиты.Найти("КонтактнаяИнформация") <> Неопределено Тогда
				ДанныеЭлПочты = "КонтактнаяИнформация";
			Иначе
				ДанныеЭлПочты = "";
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеЭлПочты) Тогда
			УсловиеЭлПочты = СтрШаблон("%1.%2 = &АдресЭлПочты", ТекущийДокумент, ДанныеЭлПочты);
		КонецЕсли;
		
		Если ПоискПоВсемПараметрам Тогда
			МассивУсловий = Новый Массив;
			Если НЕ ПустаяСтрока(УсловиеОбращения) Тогда
				МассивУсловий.Добавить(УсловиеОбращения);
			КонецЕсли;
			Если НЕ ПустаяСтрока(УсловиеТелефона) Тогда
				МассивУсловий.Добавить(УсловиеТелефона);
			КонецЕсли;
			Если НЕ ПустаяСтрока(УсловиеЭлПочты) Тогда
				МассивУсловий.Добавить(УсловиеЭлПочты);
			КонецЕсли;
			Если МассивУсловий.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаУсловия = СтрСоединить(МассивУсловий, " И ");
		Иначе
			МассивУсловий = Новый Массив;
			МассивУсловий.Добавить("ВЫБОР ");
			ШаблонУсловия = "КОГДА %1 ТОГДА ИСТИНА ";
			Если НЕ ПустаяСтрока(УсловиеОбращения) Тогда
				МассивУсловий.Добавить(СтрШаблон(ШаблонУсловия, УсловиеОбращения));
			КонецЕсли;
			Если НЕ ПустаяСтрока(УсловиеТелефона) Тогда
				МассивУсловий.Добавить(СтрШаблон(ШаблонУсловия, УсловиеТелефона));
			КонецЕсли;
			Если НЕ ПустаяСтрока(УсловиеЭлПочты) Тогда
				МассивУсловий.Добавить(СтрШаблон(ШаблонУсловия, УсловиеЭлПочты));
			КонецЕсли;
			Если МассивУсловий.Количество() > 1 Тогда
				МассивУсловий.Добавить(" ИНАЧЕ ЛОЖЬ КОНЕЦ");
				СтрокаУсловия = СтрСоединить(МассивУсловий, "");
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ПустаяСтрока(УсловиеОбращения) Тогда
			УсловиеОбращения = "ЛОЖЬ";
		КонецЕсли;
		Если ПустаяСтрока(УсловиеТелефона) Тогда
			УсловиеТелефона = "ЛОЖЬ";
		КонецЕсли;
		Если ПустаяСтрока(УсловиеЭлПочты) Тогда
			УсловиеЭлПочты = "ЛОЖЬ";
		КонецЕсли;
		
		ЗапросКонтрагента = "
		|ВЫБРАТЬ %ОграничениеВыборки%
		|	" + ИмяРеквизитаКонтрагент + " КАК Контрагент,
		|	" + УсловиеОбращения + " КАК ПоНаименованию,
		|	" + УсловиеТелефона + " КАК ПоНомеруТелефона,
		|	" + УсловиеЭлПочты + " КАК ПоАдресуЭлектроннойПочты
		|	
		|ИЗ
		|	Документ." + ТекущийДокумент + " КАК " + ТекущийДокумент + "
		|ГДЕ
		|	" + СтрокаУсловия;
		
		ЗапросКонтрагента = СтрЗаменить(ЗапросКонтрагента, "%ОграничениеВыборки%", ОграничениеВыборки);
		ТекстЗапроса.Добавить(ЗапросКонтрагента);
		
	КонецЦикла;
	
КонецПроцедуры // ЗапросПоискаКонтрагентаВДокументах()

// Функция формирования документов сервиса
//
// Параметры:
//  СтруктураПоиска - Струкутра - Структура с параметрами поиска документов.
//
Функция ПолучитьДокументовКонтрагентовСервиса(СтруктураПоиска)
	
	ПоискПоВсемПараметрам = (ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "РежимПоиска", "") = "Полный");
	Клиент                = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "Контрагент", "");
	НомерТелефона         = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "НомерТелефона", "");
	АдресЭлектроннойПочты = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "АдресЭлектроннойПочты", "");
	НомерТелефонаИсходный = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "НомерТелефонаИсходный", "");
	ИНН                   = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "ИНН", "");
	НомерДокумента        = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "НомерДокумента", "");
	ВладелецАвтомобиля    = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "ВладелецАвтомобиля", "");
	ГосНомер              = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "ГосНомер", "");
	VIN                   = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "VIN", "");
	Автомобиль            = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "Автомобиль", "");
	Комментарий           = ПолучитьЗначениеПараметраСтруктуры(СтруктураПоиска, "Комментарий", "");
	
	ШаблонПоЗнР =
		"ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК Документ,
		|	%2
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	%1 И ЗаявкаНаРемонт.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПланРемонта)";
	
	ШаблонПоЗН =
		"ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК Документ,
		|	%2
		|
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	%1";
	
	ШаблонПараметрыПоиска = "
		|	%3 КАК ПоКлиенту,
		|	%1 КАК ПоТелефону,
		|	%2 КАК ПоАдресуПочты,
		|	%4 КАК ПоИНН,
		|	%5 КАК ПоНомеруДокумента,
		|	%6 КАК ПоВладельцу,
		|	%7 КАК ПоГосНомеру,
		|	%8 КАК ПоVIN,
		|	%9 КАК ПоАвтомобилю,
		|	%10 КАК ПоКомментарию";
	
	ВременныеТаблицы = Новый Массив;
	ОбъединенияЗапроса = Новый Массив;
	
	УсловиеПоискаЗнР          = Новый Массив;
	УсловиеПоискаШаблонПоЗН   = Новый Массив;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("ВременныеТаблицы", ВременныеТаблицы);
	СтруктураПараметров.Вставить("ОбъединенияЗапроса", ОбъединенияЗапроса);
	СтруктураПараметров.Вставить("ПоискПоВсемПараметрам", ПоискПоВсемПараметрам);
	СтруктураПараметров.Вставить("УсловиеПоискаЗнР", УсловиеПоискаЗнР);
	СтруктураПараметров.Вставить("УсловиеПоискаШаблонПоЗН", УсловиеПоискаШаблонПоЗН);
	СтруктураПараметров.Вставить("ШаблонПоЗнР", ШаблонПоЗнР);
	СтруктураПараметров.Вставить("ШаблонПоЗН", ШаблонПоЗН);
	СтруктураПараметров.Вставить("ШаблонПараметрыПоиска", ШаблонПараметрыПоиска);
	
	Если ЗначениеЗаполнено(НомерТелефона) Тогда
		ЗапросПоНомеруТелефона(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(АдресЭлектроннойПочты) Тогда
		ЗапросПоАдресуЭлектроннойПочты(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(НомерДокумента) Тогда
		ЗапросПоНомеруДокумента(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(Клиент) Тогда
		СтруктураПараметров.Вставить("КонтрагентСтрокой", (ТипЗнч(Клиент) = Тип("Строка")));
		ЗапросПоЗаказчику(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		СтруктураПараметров.Вставить("АвтомобильСтрокой", (ТипЗнч(Автомобиль) = Тип("Строка")));
		ЗапросПоАвтомобилю(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(ГосНомер) Тогда
		ЗапросПоГосНомеру(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(VIN) Тогда
		ЗапросПоVIN(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(Комментарий) Тогда
		ЗапросПоКомментарию(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(ИНН) Тогда
		ЗапросПоИНН(СтруктураПараметров);
	КонецЕсли;
	Если ЗначениеЗаполнено(ВладелецАвтомобиля) Тогда
		ЗапросПоВладельцу(СтруктураПараметров);
	КонецЕсли;
	
	Если ПоискПоВсемПараметрам Тогда
		
		ПараметрыДокумента = СтрШаблон(
			ШаблонПараметрыПоиска,
			?(ЗначениеЗаполнено(НомерТелефона), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(АдресЭлектроннойПочты), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(Клиент), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(ИНН), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(НомерДокумента), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(ВладелецАвтомобиля), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(ГосНомер), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(VIN), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(Автомобиль), "ИСТИНА", "ЛОЖЬ"),
			?(ЗначениеЗаполнено(Комментарий), "ИСТИНА", "ЛОЖЬ"));
			
		ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ШаблонПоЗнР,
				СтрСоединить(УсловиеПоискаЗнР, " И "),
				ПараметрыДокумента));
		ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ШаблонПоЗН,
				СтрСоединить(УсловиеПоискаШаблонПоЗН, " И "),
				ПараметрыДокумента));
	КонецЕсли;
	
	РазделительВременныхТаблиц = "
	|;
	|";
	
	РазделительОбъединений = "
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	
	ТекстЗапроса = СтрСоединить(ВременныеТаблицы, РазделительВременныхТаблиц)
		+ ?(ВременныеТаблицы.Количество() > 0, РазделительВременныхТаблиц, "")
		+ СтрСоединить(ОбъединенияЗапроса, РазделительОбъединений);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты") Тогда
		Запрос.УстановитьПараметр("Клиент", Клиент);
	Иначе
		Запрос.УстановитьПараметр("Клиент", "%" + Клиент + "%");
	КонецЕсли;
	Запрос.УстановитьПараметр("НомерТелефона", "%" + НомерТелефона + "%");
	Запрос.УстановитьПараметр("АдресЭлектроннойПочты", "%" + АдресЭлектроннойПочты + "%");
	Запрос.УстановитьПараметр("НомерТелефонаИсходный", "%" + НомерТелефонаИсходный + "%");
	Запрос.УстановитьПараметр("ИНН", "%" + ИНН + "%");
	Запрос.УстановитьПараметр("НомерДокумента", "%" + НомерДокумента + "%");
	Запрос.УстановитьПараметр("ВладелецАвтомобиля", "%" + ВладелецАвтомобиля + "%");
	Запрос.УстановитьПараметр("ГосНомер", "%" + ГосНомер + "%");
	Запрос.УстановитьПараметр("VIN", "%" + VIN + "%");
	
	Если ТипЗнч(Автомобиль) = Тип("СправочникСсылка.Автомобили") Тогда
		Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	Иначе
		Запрос.УстановитьПараметр("Автомобиль", "%" + Автомобиль + "%");
	КонецЕсли;
	Запрос.УстановитьПараметр("Комментарий", "%" + Комментарий + "%");
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ТаблицаРезультат = РезультатЗапроса.Выгрузить();
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Процедура ЗапросПоНомеруТелефона(ПараметрыЗаполнения)
	
	ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
	"ВЫБРАТЬ
	|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Контрагент
	|ПОМЕСТИТЬ КонтрагентыПодходящиеПоТелефону
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|ГДЕ
	|	КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|	И КонтрагентыКонтактнаяИнформация.Представление ПОДОБНО &НомерТелефона");
	
	
	УсловиеДляЗнР = "ЗаявкаНаРемонт.Заказчик В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоТелефону)";
	УсловиеДляЗнРКонтрагент = "ЗаявкаНаРемонт.Контрагент В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоТелефону)";
	УсловиеДляЗнРТелефон = "ВЫРАЗИТЬ(ЗаявкаНаРемонт.ПредставлениеТелефона КАК Строка(30)) ПОДОБНО &НомерТелефонаИсходный";
	
	УсловиеДляЗНЗаказчик =
		"ЗаказНаряд.СводныйРемонтныйЗаказ.Заказчик В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоТелефону)";
	УсловиеДляЗНКонтрагент = "ЗаказНаряд.Контрагент В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоТелефону)";
	УсловиеДляЗНТелефон =
		"ВЫРАЗИТЬ(ЗаказНаряд.СводныйРемонтныйЗаказ.ПредставлениеТелефона КАК Строка(30)) ПОДОБНО &НомерТелефонаИсходный";
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		
		УсловияПоПоискуТелефона = Новый Массив;
		УсловияПоПоискуТелефона.Добавить(УсловиеДляЗнР);
		УсловияПоПоискуТелефона.Добавить(УсловиеДляЗнРКонтрагент);
		УсловияПоПоискуТелефона.Добавить(УсловиеДляЗнРТелефон);
		
		УсловиеТелефона = СтрШаблон("(%1)", СтрСоединить(УсловияПоПоискуТелефона, " ИЛИ "));
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеТелефона);
		
		УсловияПоПоискуТелефона = Новый Массив;
		УсловияПоПоискуТелефона.Добавить(УсловиеДляЗНЗаказчик);
		УсловияПоПоискуТелефона.Добавить(УсловиеДляЗНКонтрагент);
		УсловияПоПоискуТелефона.Добавить(УсловиеДляЗНТелефон);
		
		УсловиеТелефонаЗН = СтрШаблон("(%1)", СтрСоединить(УсловияПоПоискуТелефона, " ИЛИ "));
		
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить(УсловиеТелефонаЗН);
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнР,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнРКонтрагент,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнРТелефон,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗНЗаказчик,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗНКонтрагент,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗНТелефон,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапросПоАдресуЭлектроннойПочты(ПараметрыЗаполнения)
	
	ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
		"ВЫБРАТЬ
		|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Контрагент
		|ПОМЕСТИТЬ КонтрагентыПодходящиеПоЭлПочте
		|ИЗ
		|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
		|ГДЕ
		|	КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
		|	И КонтрагентыКонтактнаяИнформация.Представление ПОДОБНО &АдресЭлектроннойПочты");
	
	
	УсловиеДляЗнР = "ЗаявкаНаРемонт.Заказчик В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоЭлПочте)";
	УсловиеДляЗнРКонтрагент = "ЗаявкаНаРемонт.Контрагент В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоЭлПочте)";
	УсловиеДляЗнРЭлПочта = "ВЫРАЗИТЬ(ЗаявкаНаРемонт.КонтактнаяИнформация КАК Строка(200)) ПОДОБНО &АдресЭлектроннойПочты";
	
	УсловиеДляЗНЗаказчик =
		"ЗаказНаряд.СводныйРемонтныйЗаказ.Заказчик В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоЭлПочте)";
	УсловиеДляЗНКонтрагент = "ЗаказНаряд.Контрагент В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоЭлПочте)";
	УсловиеДляЗНЭлПочта =
		"ВЫРАЗИТЬ(ЗаказНаряд.СводныйРемонтныйЗаказ.КонтактнаяИнформация КАК Строка(50)) ПОДОБНО &АдресЭлектроннойПочты";
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		
		УсловияПоПоискуПочты = Новый Массив;
		УсловияПоПоискуПочты.Добавить(УсловиеДляЗнР);
		УсловияПоПоискуПочты.Добавить(УсловиеДляЗнРКонтрагент);
		УсловияПоПоискуПочты.Добавить(УсловиеДляЗнРЭлПочта);
		
		УсловиеАдресПочты = СтрШаблон("(%1)", СтрСоединить(УсловияПоПоискуПочты, " ИЛИ "));
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеАдресПочты);
		
		УсловияПоПоискуПочты = Новый Массив;
		УсловияПоПоискуПочты.Добавить(УсловиеДляЗНЗаказчик);
		УсловияПоПоискуПочты.Добавить(УсловиеДляЗНКонтрагент);
		УсловияПоПоискуПочты.Добавить(УсловиеДляЗНЭлПочта);
		
		УсловиеАдресПочтыЗН = СтрШаблон("(%1)", СтрСоединить(УсловияПоПоискуПочты, " ИЛИ "));
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить(УсловиеАдресПочтыЗН);
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнР,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнРКонтрагент,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнРЭлПочта,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН,УсловиеДляЗНЗаказчик,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗНКонтрагент,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗНЭлПочта,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапросПоНомеруДокумента(ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить("ЗаявкаНаРемонт.Номер ПОДОБНО &НомерДокумента");
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить("ЗаказНаряд.Номер ПОДОБНО &НомерДокумента");
		
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, "ЗаявкаНаРемонт.Номер ПОДОБНО &НомерДокумента",
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
			ПараметрыЗаполнения.ШаблонПоЗН, "ЗаказНаряд.Номер ПОДОБНО &НомерДокумента",
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
	КонецЕсли;
	
КонецПроцедуры // ЗапросПоНомеруДокумента()

Процедура ЗапросПоЗаказчику(ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.КонтрагентСтрокой Тогда
		ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
			"ВЫБРАТЬ
			|	Контрагенты.Ссылка КАК Контрагент
			|ПОМЕСТИТЬ КонтрагентыПодходящиеПоНаименованию
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Наименование ПОДОБНО &Клиент");
	Иначе
		ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
			"ВЫБРАТЬ
			|	Контрагенты.Ссылка КАК Контрагент
			|ПОМЕСТИТЬ КонтрагентыПодходящиеПоНаименованию
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Ссылка = &Клиент");
	КонецЕсли;
	
	УсловиеДляЗнРТекст = "ВЫРАЗИТЬ(ЗаявкаНаРемонт.ОбращениеККлиенту КАК Строка(50)) ПОДОБНО &Клиент";
	УсловиеДляЗнР = "ЗаявкаНаРемонт.Заказчик В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоНаименованию)";
	УсловиеДляЗН =
		"ЗаказНаряд.СводныйРемонтныйЗаказ.Заказчик В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоНаименованию)";
	
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		
		УсловияПоЗаказчику = Новый Массив;
		Если ПараметрыЗаполнения.КонтрагентСтрокой Тогда
			УсловияПоЗаказчику.Добавить(УсловиеДляЗнРТекст);
		КонецЕсли;
		УсловияПоЗаказчику.Добавить(УсловиеДляЗнР);
		
		УсловиеКонтрагента = СтрШаблон("(%1)", СтрСоединить(УсловияПоЗаказчику, " ИЛИ "));
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеКонтрагента);
		
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить(УсловиеДляЗН);
	Иначе
		Если ПараметрыЗаполнения.КонтрагентСтрокой Тогда
			ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнРТекст,
						СтрШаблон(
							ПараметрыЗаполнения.ШаблонПараметрыПоиска,
							"ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		КонецЕсли;
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнР,
					СтрШаблон(
						ПараметрыЗаполнения.ШаблонПараметрыПоиска,
						"ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗН,
					СтрШаблон(
						ПараметрыЗаполнения.ШаблонПараметрыПоиска,
						"ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ЗапросПоАвтомобилю(ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.АвтомобильСтрокой Тогда
		ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
			"ВЫБРАТЬ
			|	Автомобили.Ссылка КАК Автомобиль
			|ПОМЕСТИТЬ АвтомобилиПодходящиеПоНаименованию
			|ИЗ
			|	Справочник.Автомобили КАК Автомобили
			|ГДЕ
			|	Автомобили.Наименование ПОДОБНО &Автомобиль");
	Иначе
		ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
			"ВЫБРАТЬ
			|	Автомобили.Ссылка КАК Автомобиль
			|ПОМЕСТИТЬ АвтомобилиПодходящиеПоНаименованию
			|ИЗ
			|	Справочник.Автомобили КАК Автомобили
			|ГДЕ
			|	Автомобили.Ссылка = &Автомобиль");
	КонецЕсли;
	
	УсловиеДляЗнР = "ЗаявкаНаРемонт.Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ АвтомобилиПодходящиеПоНаименованию)";
	УсловиеДляЗН = 
		"ЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ АвтомобилиПодходящиеПоНаименованию)";
	
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеДляЗнР);
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить(УсловиеДляЗН);
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнР,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗН,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ")));
	КонецЕсли;
	
	
КонецПроцедуры // ЗапросПоАвтомобилю()

Процедура ЗапросПоГосНомеру(ПараметрыЗаполнения)
	
	ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
	"ВЫБРАТЬ
	|	АвтомобилиСрезПоследних.Автомобиль КАК Автомобиль
	|ПОМЕСТИТЬ АвтомобилиПодходящиеПоГосНомеру
	|ИЗ
	|	РегистрСведений.Автомобили.СрезПоследних(
	|			,
	|			ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)
	|				И ВЫРАЗИТЬ(Значение КАК СТРОКА(20)) ПОДОБНО &ГосНомер) КАК АвтомобилиСрезПоследних");
	
	УсловиеДляЗнР = "ЗаявкаНаРемонт.Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ АвтомобилиПодходящиеПоГосНомеру)";
	УсловиеДляЗН = 
	"ЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ АвтомобилиПодходящиеПоГосНомеру)";
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеДляЗнР);
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить(УсловиеДляЗН);
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнР,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗН,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
	КонецЕсли;
	
КонецПроцедуры // ЗапросПоГосНомеру()

Процедура ЗапросПоVIN(ПараметрыЗаполнения)
	
	ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
	"ВЫБРАТЬ
	|	Автомобили.Ссылка КАК Автомобиль
	|ПОМЕСТИТЬ АвтомобилиПодходящиеПоVIN
	|ИЗ
	|	Справочник.Автомобили КАК Автомобили
	|ГДЕ
	|	Автомобили.VIN ПОДОБНО &VIN");
	
	УсловиеДляЗнР = "ЗаявкаНаРемонт.Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ АвтомобилиПодходящиеПоVIN)";
	УсловиеДляЗнРВДокументе = "ЗаявкаНаРемонт.VIN ПОДОБНО &VIN";
	УсловиеДляЗН = "ЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ АвтомобилиПодходящиеПоVIN)";
	
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеДляЗнР);
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеДляЗнРВДокументе);
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить(УсловиеДляЗН);
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнР,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнРВДокументе,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗН,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ")));
	КонецЕсли;
	
КонецПроцедуры // ЗапросПоVIN()

Процедура ЗапросПоКомментарию(ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить("ЗаявкаНаРемонт.Комментарий ПОДОБНО &Комментарий");
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить("ЗаказНаряд.Комментарий ПОДОБНО &Комментарий");
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, "ЗаявкаНаРемонт.Комментарий ПОДОБНО &Комментарий",
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, "ЗаказНаряд.Комментарий ПОДОБНО &Комментарий",
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА")));
	КонецЕсли;
	
КонецПроцедуры // ЗапросПоКомментарию()

Процедура ЗапросПоИНН(ПараметрыЗаполнения)
	
	ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент
		|ПОМЕСТИТЬ КонтрагентыПодходящиеПоИНН
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН ПОДОБНО &ИНН");
	
	УсловиеДляЗнР = "ЗаявкаНаРемонт.Заказчик В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоИНН)";
	УсловиеДляЗН = "ЗаказНаряд.СводныйРемонтныйЗаказ.Заказчик В (ВЫБРАТЬ Контрагент ИЗ КонтрагентыПодходящиеПоИНН)";
	
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеДляЗнР);
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить(УсловиеДляЗН);
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнР,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗН,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
	КонецЕсли;
	
КонецПроцедуры // ЗапросПоИНН()

Процедура ЗапросПоВладельцу(ПараметрыЗаполнения)
	
	ПараметрыЗаполнения.ВременныеТаблицы.Добавить(
		"ВЫБРАТЬ
		|	АвтомобилиСрезПоследних.Автомобиль КАК Автомобиль
		|ПОМЕСТИТЬ АвтомобилиПодходящиеПоВладельцу
		|ИЗ
		|	РегистрСведений.Автомобили.СрезПоследних(
		|		,
		|		ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)
		|		И ВЫРАЗИТЬ(Значение КАК Справочник.Контрагенты).Наименование
		|			ПОДОБНО &ВладелецАвтомобиля) КАК АвтомобилиСрезПоследних");
	
	УсловиеДляЗнР = "ЗаявкаНаРемонт.Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ АвтомобилиПодходящиеПоВладельцу)";
	УсловиеДляЗН = 
		"ЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ АвтомобилиПодходящиеПоВладельцу)";
	Если ПараметрыЗаполнения.ПоискПоВсемПараметрам Тогда
		ПараметрыЗаполнения.УсловиеПоискаЗнР.Добавить(УсловиеДляЗнР);
		ПараметрыЗаполнения.УсловиеПоискаШаблонПоЗН.Добавить(УсловиеДляЗН);
	Иначе
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗнР, УсловиеДляЗнР,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
		ПараметрыЗаполнения.ОбъединенияЗапроса.Добавить(
			СтрШаблон(
				ПараметрыЗаполнения.ШаблонПоЗН, УсловиеДляЗН,
				СтрШаблон(
					ПараметрыЗаполнения.ШаблонПараметрыПоиска,
					"ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ИСТИНА", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ", "ЛОЖЬ")));
	КонецЕсли;
	
КонецПроцедуры // ПараметрыЗаполнения()

#КонецОбласти

#КонецЕсли


