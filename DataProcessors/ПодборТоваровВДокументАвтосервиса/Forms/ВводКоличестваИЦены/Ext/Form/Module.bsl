
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.УказыватьКоличество Тогда
		
		Элементы.Количество.Видимость =Ложь;
		
	КонецЕсли;
	
	Если НЕ Параметры.УказыватьЦену Тогда
		
		Элементы.Цена.Видимость =Ложь;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "УказыватьКоличество,УказыватьЦену");
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Очистить();
	
	Если УказыватьКоличество Тогда
		
		ПроверяемыеРеквизиты.Добавить("Количество");
		
	КонецЕсли;
	
	Если УказыватьЦену Тогда
		
		ПроверяемыеРеквизиты.Добавить("Цена");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьВвод(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		Закрыть(ВведенныеДанные());
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВведенныеДанные()
	
	Возврат Новый Структура("Количество,Цена", Количество, Цена);
	
КонецФункции

#КонецОбласти