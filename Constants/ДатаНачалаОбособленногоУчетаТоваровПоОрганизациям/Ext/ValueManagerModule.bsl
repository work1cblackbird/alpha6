
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьНастройкиИнтеркампаниРанееУказаннойДаты(ЭтотОбъект.Значение) Тогда
		
		Отказ = Истина;
		
		ТекстОшибки = СтрШаблон(НСтр(
			"ru = 'Есть настройки передачи товаров между организациями ранее указанной даты.
				  |Изменение заблокировано, так как для работы интеркампани требуется обособленный учет товаров по организациям.'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗапасыТоваров.НачальноеЗаполнениеОрганизацииВРегистрыУчетаТоваровПриОбновленииИБ() Тогда
		
		Отказ = Истина;
		
		ТекстОшибки = СтрШаблон(НСтр(
			"ru = 'Изменение заблокировано, так как выполняется начальное заполнение <Организации> в регистры <Партии товаров компании>, <ГТД партий товаров компании> и <Товары компании в производстве>, при отложенном обновлении программы.
				  |После завершения обновления, будет автоматически установлено новое значение.'"));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		
		Возврат;
	
	КонецЕсли;
	
КонецПроцедуры
	
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьНастройкиИнтеркампаниРанееУказаннойДаты(ДатаПроверки)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПередачиТоваровМеждуОрганизациями.ОрганизацияОтправитель КАК ОрганизацияОтправитель
		|ИЗ
		|	РегистрСведений.НастройкиПередачиТоваровМеждуОрганизациями КАК НастройкиПередачиТоваровМеждуОрганизациями
		|ГДЕ
		|	НастройкиПередачиТоваровМеждуОрганизациями.Период < &Период";
	
	Запрос.УстановитьПараметр("Период", ДатаПроверки);
	
	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
