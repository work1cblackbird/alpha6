
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПроверитьНеобходимостьБлокировкиОтключения(Значение) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТребуетсяБлокировка", Ложь);
	Результат.Вставить("ТекстОшибки", "");
	
	Если Значение Тогда
		
		РежимКонтроляОстатков = Константы.РежимКонтроляОстатковНаСкладах.Получить();
		Если РежимКонтроляОстатков = Перечисления.РежимыКонтроляОстатковНаСкладах.ПоКомпании Тогда
			
			Результат.ТекстОшибки = НСтр("ru = 'В базе установлен режим контроля остатков на складах ""По компании"".
				|Включение функционала ""Передача товаров между организациями"" невозможна.'"
			);
			Результат.ТребуетсяБлокировка = Истина;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецЕсли;
	
	ЕстьНастройки 	= Ложь;
	ЕстьДвижения 	= Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПередачиТоваровМеждуОрганизациями.Период КАК Период
		|ИЗ
		|	РегистрСведений.НастройкиПередачиТоваровМеждуОрганизациями КАК НастройкиПередачиТоваровМеждуОрганизациями";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		
		ЕстьНастройки = Истина;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТоварыОрганизацийКПередаче.Период КАК Период
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		
		ЕстьДвижения = Истина;
		
	КонецЕсли;
	
	Если ЕстьНастройки ИЛИ ЕстьДвижения Тогда
		
		ТекстОшибки = НСтр(
			"ru = 'В базе уже использовался функционал ""Передач товаров между организациями"", выключение опции заблокировано'"
		);
		
		Если ЕстьНастройки Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = ' - Есть ""Настройки передачи товаров между организациями""'");
		КонецЕсли;
		
		Если ЕстьДвижения Тогда
			ТекстОшибки = ТекстОшибки
				+ Символы.ПС
				+ НСтр("ru = ' - Есть движения по регистру накопления ""Товары организаций к передаче""'");
		КонецЕсли;
		
		Результат.ТребуетсяБлокировка = Истина;
		Результат.ТекстОшибки = ТекстОшибки;
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
