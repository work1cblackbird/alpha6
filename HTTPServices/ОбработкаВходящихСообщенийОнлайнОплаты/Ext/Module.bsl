#Область СлужебныеПроцедурыИФункции

Функция YooKassaMessageParsing(Запрос)
	
	ТелоКакСтрока = Запрос.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	
	ЧтениеУведомления = Новый ЧтениеJSON;
	ЧтениеУведомления.УстановитьСтроку(ТелоКакСтрока);
	
	Попытка
		
		ПараметрыУведомления = ПрочитатьJSON(ЧтениеУведомления, Истина);
		
		КодСостояния = ПлатежныйШлюзЮКасса.ОбработатьВходящееУведомление(ПараметрыУведомления);
		
	Исключение
		
		ИнфОбОшибке = ИнформацияОбОшибке();
		ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнфОбОшибке);
		ШаблонКомментария = НСтр("ru='Тело запроса:%1%2%1не в формате JSON%1Информация об ошибке:%3'");
		Комментарий = СтрШаблон(ШаблонКомментария, Символы.ПС, ТелоКакСтрока, ПредставлениеОшибки);
		РезультатВыполнения = ОнлайнОплата.ПолучитьСтруктуруРезультатВыполнения();
		ЗаписьЖурналаРегистрации(РезультатВыполнения.ИмяСобытияЖР, УровеньЖурналаРегистрации.Ошибка, , , Комментарий);
		
		КодСостояния = 200;
		
	КонецПопытки;
	
	
	Ответ = Новый HTTPСервисОтвет(КодСостояния);
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти