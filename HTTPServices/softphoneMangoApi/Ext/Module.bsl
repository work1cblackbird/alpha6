
#Область ОбработчикиМетодовHTTP

Функция EventsCallPOST(Запрос)
	
	ИмяСобытияДляЖурналаРегистрации = "/events/call";
		
	Если Не ПолучитьФункциональнуюОпцию("сфпИспользоватьОблачнуюТелефонию") Тогда
		Возврат СообщениеОбОшибке(
			500,
			ИмяСобытияДляЖурналаРегистрации,
			НСтр("ru='Использование телефонии Манго отключено в настройках';en='Use of telephony Mango turned off in settings'"));
	КонецЕсли;
	
	ТелоЗапроса = РаскодироватьСтроку(Запрос.ПолучитьТелоКакСтроку(), СпособКодированияСтроки.КодировкаURL);
	ПараметрыТела = ПолучитьПараметрыИзСтроки(ТелоЗапроса, "&");
	
	сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации(ИмяСобытияДляЖурналаРегистрации, ПараметрыТела.json);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПроверитьSign(ПараметрыТела) Тогда
		Возврат СообщениеОбОшибке(
			400,
			ИмяСобытияДляЖурналаРегистрации,
			сфпОбщегоНазначенияКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Неверно указана подпись запроса sign=%1';en='Invalid query signature sign=%1'"), ПараметрыТела.sign));
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ПараметрыТела.json);
	МассивИмен = Новый Массив;
	МассивИмен.Добавить("timestamp");
	ПараметрыЗапросаJSON = ПрочитатьJSON(ЧтениеJSON,,,,"ВосстановлениеJSON", сфпСофтФонПроСервер,, МассивИмен);
	ЧтениеJSON.Закрыть();
	
	ОбязательныеПараметры = Новый Массив;
	ОбязательныеПараметры.Добавить("entry_id");
	ОбязательныеПараметры.Добавить("call_id");
	ОбязательныеПараметры.Добавить("timestamp");
	ОбязательныеПараметры.Добавить("seq");
	ОбязательныеПараметры.Добавить("call_state");
	ОбязательныеПараметры.Добавить("location");
	ОбязательныеПараметры.Добавить("to");
	ОбязательныеПараметры.Добавить("from");
	
	Для Каждого ОбязательныйПараметр Из ОбязательныеПараметры Цикл
		Если Не ПараметрыЗапросаJSON.Свойство(ОбязательныйПараметр) Тогда
			Возврат СообщениеОбОшибке(
				400,
				ИмяСобытияДляЖурналаРегистрации,
				сфпОбщегоНазначенияКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отсутствует обязательный параметр %1.';en='Missing required parameter %1.'"), ОбязательныйПараметр));
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		Если НРег(ПараметрыЗапросаJSON.location) = "ivr" Тогда
			НомерКонтакта = ПараметрыЗапросаJSON.from.number;
			
			ПараметрыПереадресации = сфпСофтФонПроСервер.сфпПараметрыПереадресации(НомерКонтакта);
			Попытка    ВнутреннийНомер = ПараметрыПереадресации.ВнутреннийНомер;
			Исключение ВнутреннийНомер = "";
			КонецПопытки;
			
			Если ЗначениеЗаполнено(ВнутреннийНомер) Тогда
				ВходящийИдентификаторЗвонка = ПараметрыЗапросаJSON.entry_id;
				ИдентификаторЗвонка = ПараметрыЗапросаJSON.call_id;
				
				РезультатЗапроса = сфпЛицензированиеСервер.ВыполнитьЗапросОблачнойАТС("ПереводНеотвеченногоЗвонка", ИдентификаторЗвонка, ВнутреннийНомер, ВнутреннийНомер, Ложь);
			КонецЕсли;
			
		ИначеЕсли НРег(ПараметрыЗапросаJSON.location) = "abonent" Тогда
			ВходящийИдентификаторЗвонка = ПараметрыЗапросаJSON.entry_id;
			ИдентификаторЗвонка = ПараметрыЗапросаJSON.call_id;
				
			Если ПараметрыЗапросаJSON.call_state = "Appeared" Тогда
				Если ПараметрыЗапросаJSON.from.Свойство("extension") И ПараметрыЗапросаJSON.to.Свойство("extension") Тогда
					// Внутренний звонок
					сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации("Внутренний", "" + ИдентификаторЗвонка);
					
				ИначеЕсли ПараметрыЗапросаJSON.from.Свойство("extension") И ПараметрыЗапросаJSON.to.Свойство("number") Тогда
					НомерКонтакта = ПараметрыЗапросаJSON.to.number;
					ВнутреннийНомер = ПараметрыЗапросаJSON.from.extension;
					
					Если ЗначениеЗаполнено(НомерКонтакта) И ЗначениеЗаполнено(ВнутреннийНомер) И ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
						сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации("Исходящий", "" + ИдентификаторЗвонка + "; Контакт: " + НомерКонтакта + "; Вн.номер: " + ВнутреннийНомер);
						
						ДатаСобытия = ПараметрыЗапросаJSON.timestamp;
						сфпСофтФонПроСервер.ОбработатьИсходящийЗвонок(ДатаСобытия, ВнутреннийНомер,, НомерКонтакта, ИдентификаторЗвонка);
					КонецЕсли;	
					
				ИначеЕсли ПараметрыЗапросаJSON.from.Свойство("number") И ПараметрыЗапросаJSON.to.Свойство("extension")
					И НЕ ПараметрыЗапросаJSON.Свойство("command_id") Тогда

					НомерКонтакта = ПараметрыЗапросаJSON.from.number;
					ВнутреннийНомер = ПараметрыЗапросаJSON.to.extension;
					
					Если НЕ ПустаяСтрока(НомерКонтакта) И НЕ ПустаяСтрока(ВнутреннийНомер) И НЕ ПустаяСтрока(ИдентификаторЗвонка) Тогда
						сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации("Входящий", "" + ИдентификаторЗвонка + "; Контакт: " + НомерКонтакта + "; Вн.номер: " + ВнутреннийНомер);
						
						ДатаСобытия = ПараметрыЗапросаJSON.timestamp;
						сфпСофтФонПроСервер.ОбработатьВходящийЗвонок(НомерКонтакта, ВнутреннийНомер, ДатаСобытия, ИдентификаторЗвонка);
					КонецЕсли;
					
				Иначе
					// Пропускаем звонок
					сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации("Пропуск", "" + ИдентификаторЗвонка);
				КонецЕсли;
				
			ИначеЕсли ПараметрыЗапросаJSON.call_state = "Connected" Тогда
				Пользователь = Неопределено;
				Если ПараметрыЗапросаJSON.to.Свойство("extension") Тогда
					Пользователь = ПараметрыЗапросаJSON.to.extension;
				ИначеЕсли ПараметрыЗапросаJSON.from.Свойство("extension") Тогда
					Пользователь = ПараметрыЗапросаJSON.from.extension;
				КонецЕсли;

				Если ЗначениеЗаполнено(Пользователь) Тогда
					сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации("Соединено", "" + ИдентификаторЗвонка + "; Пользователь: " + Пользователь);
					
					ДатаСобытия = ПараметрыЗапросаJSON.timestamp;
					сфпСофтФонПроСервер.ОбработатьИзменениеЗвонка(ИдентификаторЗвонка, ДатаСобытия, Пользователь);
				КонецЕсли;	
				
			ИначеЕсли ПараметрыЗапросаJSON.call_state = "OnHold" Тогда
				Пользователь = Неопределено;
				Если ПараметрыЗапросаJSON.to.Свойство("extension") Тогда
					Пользователь = ПараметрыЗапросаJSON.to.extension;
				ИначеЕсли ПараметрыЗапросаJSON.from.Свойство("extension") Тогда
					Пользователь = ПараметрыЗапросаJSON.from.extension;
				КонецЕсли;
			
				Если ЗначениеЗаполнено(ИдентификаторЗвонка) И ЗначениеЗаполнено(Пользователь) Тогда
					сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации("Удержание", "" + ИдентификаторЗвонка + "; Пользователь: " + Пользователь);
					
					сфпСофтФонПроСервер.ОбработатьУдержаниеЗвонка(ИдентификаторЗвонка, Пользователь);
				КонецЕсли;
				
			ИначеЕсли ПараметрыЗапросаJSON.call_state = "Disconnected" Тогда
				Пользователь = Неопределено;
				Если ПараметрыЗапросаJSON.to.Свойство("extension") Тогда
					Пользователь = ПараметрыЗапросаJSON.to.extension;
				ИначеЕсли ПараметрыЗапросаJSON.from.Свойство("extension") Тогда
					Пользователь = ПараметрыЗапросаJSON.from.extension;
				КонецЕсли;
			
				Если ЗначениеЗаполнено(ИдентификаторЗвонка) И ЗначениеЗаполнено(Пользователь) Тогда
					сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации("Завершено", "" + ИдентификаторЗвонка + "; Пользователь: " + Пользователь);
					
					ДатаСобытия = ПараметрыЗапросаJSON.timestamp;
					сфпСофтФонПроСервер.ОбработатьЗавершениеЗвонка(,,, ДатаСобытия,,,,, ИдентификаторЗвонка,, Пользователь);
				КонецЕсли;	
			КонецЕсли;			
		КонецЕсли;
		
	Исключение
		Возврат СообщениеОбОшибке(
			500,
			ИмяСобытияДляЖурналаРегистрации,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Ответ = Новый HTTPСервисОтвет(200);
	Возврат Ответ;
	
КонецФункции

Функция EventsRecordingPOST(Запрос)
	
	ИмяСобытияДляЖурналаРегистрации = "/events/recording";
		
	Если Не ПолучитьФункциональнуюОпцию("сфпИспользоватьОблачнуюТелефонию") Тогда
		Возврат СообщениеОбОшибке(
			500,
			ИмяСобытияДляЖурналаРегистрации,
			НСтр("ru='Использование телефонии Манго отключено в настройках';en='Use of telephony Mango turned off in settings'"));
	КонецЕсли;
	
	ТелоЗапроса = РаскодироватьСтроку(Запрос.ПолучитьТелоКакСтроку(), СпособКодированияСтроки.КодировкаURL);
	ПараметрыТела = ПолучитьПараметрыИзСтроки(ТелоЗапроса, "&");
	
	сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации(ИмяСобытияДляЖурналаРегистрации, ПараметрыТела.json);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПроверитьSign(ПараметрыТела) Тогда
		Возврат СообщениеОбОшибке(
			400,
			ИмяСобытияДляЖурналаРегистрации,
			сфпОбщегоНазначенияКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Неверно указана подпись запроса sign=%1';en='Invalid query signature sign=%1'"), ПараметрыТела.sign));
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ПараметрыТела.json);
	МассивИмен = Новый Массив;
	МассивИмен.Добавить("timestamp");
	ПараметрыЗапросаJSON = ПрочитатьJSON(ЧтениеJSON,,,,"ВосстановлениеJSON", сфпСофтФонПроСервер,, МассивИмен);
	ЧтениеJSON.Закрыть();
	
	ОбязательныеПараметры = Новый Массив;
	ОбязательныеПараметры.Добавить("recording_id");
	ОбязательныеПараметры.Добавить("recording_state");
	ОбязательныеПараметры.Добавить("seq");
	ОбязательныеПараметры.Добавить("call_id");
	ОбязательныеПараметры.Добавить("timestamp");
	
	Для Каждого ОбязательныйПараметр Из ОбязательныеПараметры Цикл
		Если Не ПараметрыЗапросаJSON.Свойство(ОбязательныйПараметр) Тогда
			Возврат СообщениеОбОшибке(
				400,
				ИмяСобытияДляЖурналаРегистрации,
				сфпОбщегоНазначенияКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отсутствует обязательный параметр %1.';en='Missing required parameter %1.'"), ОбязательныйПараметр));
		КонецЕсли;
	КонецЦикла;
	
	НеобходимоОбработатьУведомление =
		ВРег(ПараметрыЗапросаJSON.recording_state) = ВРег("Completed")
		И ПараметрыЗапросаJSON.completion_code = 1000; // 1000 - Действие успешно выполнено
		
	Если НЕ НеобходимоОбработатьУведомление Тогда
		Ответ = Новый HTTPСервисОтвет(200);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		сфпСофтФонПроСервер.ОбработатьЗаписьЗвонка(ПараметрыЗапросаJSON.call_id, ПараметрыЗапросаJSON.recording_id);
	Исключение
		Возврат СообщениеОбОшибке(500,
			ИмяСобытияДляЖурналаРегистрации,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Ответ = Новый HTTPСервисОтвет(200);
	Возврат Ответ;
	
КонецФункции

Функция ResultCallbackPOST(Запрос)
	
	ИмяСобытияДляЖурналаРегистрации = "/result/callback";
		
	Если Не ПолучитьФункциональнуюОпцию("сфпИспользоватьОблачнуюТелефонию") Тогда
		Возврат СообщениеОбОшибке(
			500,
			ИмяСобытияДляЖурналаРегистрации,
			НСтр("ru='Использование телефонии отключено в настройках';en='Use of telephony is disabled in settings'"));
	КонецЕсли;
	
	Ответ = Новый HTTPСервисОтвет(501); // Not implemented
	Возврат Ответ;
	
КонецФункции

Функция ResultRoutePOST(Запрос)
	
	ИмяСобытияДляЖурналаРегистрации = "/result/route";
		
	Если Не ПолучитьФункциональнуюОпцию("сфпИспользоватьОблачнуюТелефонию") Тогда
		Возврат СообщениеОбОшибке(
			500,
			ИмяСобытияДляЖурналаРегистрации,
			НСтр("ru='Использование телефонии отключено в настройках';en='Use of telephony is disabled in settings'"));
	КонецЕсли;
	
	Ответ = Новый HTTPСервисОтвет(501); // Not implemented
	Возврат Ответ;
	
КонецФункции

Функция ResultStatsPOST(Запрос)
	
	ИмяСобытияДляЖурналаРегистрации = "/result/stats";
		
	Если Не ПолучитьФункциональнуюОпцию("сфпИспользоватьОблачнуюТелефонию") Тогда
		Возврат СообщениеОбОшибке(
			500,
			ИмяСобытияДляЖурналаРегистрации,
			НСтр("ru='Использование телефонии отключено в настройках';en='Use of telephony is disabled in settings'"));
	КонецЕсли;
	
	Ответ = Новый HTTPСервисОтвет(501); // Not implemented
	Возврат Ответ;
	
КонецФункции

Функция pingGET(Запрос)
	
	ИмяСобытияДляЖурналаРегистрации = "/ping";
		
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки("ok");
	Возврат Ответ;
	
КонецФункции

Функция ResultCallHangUpPOST(Запрос)
	
	ИмяСобытияДляЖурналаРегистрации = "/result/call/hangup";
	
	Если Не ПолучитьФункциональнуюОпцию("сфпИспользоватьОблачнуюТелефонию") Тогда
		Возврат СообщениеОбОшибке(
			500,
			ИмяСобытияДляЖурналаРегистрации,
			НСтр("ru='Использование телефонии отключено в настройках';en='Use of telephony is disabled in settings'"));
	КонецЕсли;
	
	ТелоЗапроса = РаскодироватьСтроку(Запрос.ПолучитьТелоКакСтроку(), СпособКодированияСтроки.КодировкаURL);
	ПараметрыТела = ПолучитьПараметрыИзСтроки(ТелоЗапроса, "&");
	
	сфпСофтФонПроСерверПереопределяемый.ЗаписатьЗапросВЖурналРегистрации(ИмяСобытияДляЖурналаРегистрации, ПараметрыТела.json);
	
	Ответ = Новый HTTPСервисОтвет(200);
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПараметрыИзСтроки(Знач СтрокаПараметров, Знач Разделитель = ";") Экспорт
	Результат = Новый Структура;
	
	ОписаниеПараметра = "";
	НайденоНачалоСтроки = Ложь;
	НомерПоследнегоСимвола = СтрДлина(СтрокаПараметров);
	Для НомерСимвола = 1 По НомерПоследнегоСимвола Цикл
		Символ =Сред(СтрокаПараметров, НомерСимвола, 1);
		Если Символ = """" Тогда
			НайденоНачалоСтроки = Не НайденоНачалоСтроки;
		КонецЕсли;
		Если Символ <> Разделитель Или НайденоНачалоСтроки Тогда
			ОписаниеПараметра = ОписаниеПараметра + Символ;
		КонецЕсли;
		Если Символ = Разделитель И Не НайденоНачалоСтроки Или НомерСимвола = НомерПоследнегоСимвола Тогда
			Позиция = Найти(ОписаниеПараметра, "=");
			Если Позиция > 0 Тогда
				ИмяПараметра = СокрЛП(Лев(ОписаниеПараметра, Позиция - 1));
				ЗначениеПараметра = СокрЛП(Сред(ОписаниеПараметра, Позиция + 1));
				ЗначениеПараметра = сфпОбщегоНазначенияКлиентСервер.сфпСократитьДвойныеКавычки(ЗначениеПараметра);
				Попытка
					Результат.Вставить(ИмяПараметра, ЗначениеПараметра);
				Исключение
				КонецПопытки;
			КонецЕсли;
			ОписаниеПараметра = "";
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция СообщениеОбОшибке(КодСостояния, ВложенноеИмяСобытия, Комментарий)
	
	ЗаписьЖурналаРегистрации(
		сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации() + "." + ВложенноеИмяСобытия,
		УровеньЖурналаРегистрации.Ошибка,,,
		Комментарий);
	
	Ответ = Новый HTTPСервисОтвет(КодСостояния);
	Возврат Ответ;
	
КонецФункции

Функция ПроверитьSign(ПараметрыТела)
	
	НастройкиТелефонии = сфпСофтФонПроСервер.ПолучитьНастройкиТелефонии();
	РассчитанныйSign = сфпСофтФонПроСервер.ПолучитьSign(
		НастройкиТелефонии.vpbx_api_key,
		ПараметрыТела.json,
		НастройкиТелефонии.vpbx_api_salt
	);
	
	Возврат РассчитанныйSign = ПараметрыТела.sign;
	
КонецФункции

#КонецОбласти