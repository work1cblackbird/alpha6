#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено ИЛИ ПараметрКоманды.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	// фильтры
	ОтчетОтбор = ПараметрыОткрытияОтчета(ПараметрКоманды);
	
	ОткрытьФорму("Отчет.ОтчетПоТабельномуВремени.Форма", Новый Структура("Отбор,КлючВарианта,СформироватьПриОткрытии", ОтчетОтбор, "Основной", Истина), ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыОткрытияОтчета(ДокументТабель)
	
	СтруктураПараметров = Новый Структура;
	
	// Найдем период.
	ДатаПланирования  = ДокументТабель.ДатаПланирования;
	РежимПланировщика = ДокументТабель.РежимПланировщика;
	Если РежимПланировщика = 0 Тогда
		// Период - месяц.
		НачалоПериода = НачалоМесяца(ДатаПланирования);
		КонецПериода  = КонецМесяца(ДатаПланирования);
	ИначеЕсли РежимПланировщика = 1 Тогда
		// Период - 2 недели.
		НачалоПериода = НачалоНедели(ДатаПланирования);
		КонецПериода  = КонецНедели(ДатаПланирования + 7*24*3600);
	Иначе
		// Период - неделя.
		НачалоПериода = НачалоНедели(ДатаПланирования);
		КонецПериода  = КонецНедели(ДатаПланирования);
	КонецЕсли;
	
	СтруктураПараметров.Вставить("НачалоПериода", НачалоПериода);
	СтруктураПараметров.Вставить("КонецПериода",  КонецПериода);
	
	СписокРесурсов = Новый СписокЗначений;
	ТаблицаРесурсов = ДокументТабель.Ресурсы.Выгрузить();
	ТаблицаРесурсов.Свернуть("Ресурс");
	
	Для Каждого ТекущаяСтрока Из ТаблицаРесурсов Цикл
		СписокРесурсов.Добавить(ТекущаяСтрока.Ресурс);
	КонецЦикла;
	
	СтруктураПараметров.Вставить("Ресурс", СписокРесурсов);
	
	
	Возврат СтруктураПараметров;
	
КонецФункции

#КонецОбласти