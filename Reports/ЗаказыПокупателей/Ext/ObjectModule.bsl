#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает параметр построителя отчета "СписокЗаказов"
//
// Без параметров
//
Процедура УстановитьПараметры() Экспорт
	
	НеПроверятьЗаказы = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("НеПроверятьЗаказы", КомпоновщикНастроек.Настройки);
	Если НеПроверятьЗаказы = Истина Тогда
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", Новый Массив);
	Иначе
		ПоляОграничения = Новый Массив;
		ПоляОграничения.Добавить("Контрагент");
		ПоляОграничения.Добавить("Заказ");
		ПоляОграничения.Добавить("Номенклатура");
		ПоляОграничения.Добавить("ХарактеристикаНоменклатуры");
		
		Запрос = Новый Запрос;
		
		ОтборКомпоновки = КомпоновщикНастроек.Настройки.Отбор;
		ПоляИсключения = ОтчетыПлатформаСервер.ПолучитьПоляИсключения(ПоляОграничения,ОтборКомпоновки);

		ТекстОтбора = ОтчетыПлатформаСервер.КомпоновщикПолучитьТекстОтбора(
			ОтборКомпоновки.Элементы,
			ОтборКомпоновки.ДоступныеПоляОтбора,
			"",
			Запрос.Параметры,, 
			ПоляОграничения,
			ПоляИсключения
		);
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(&ДатаКонГраница,"+ТекстОтбора+") КАК ЗаказыПокупателейОстатки
		|ГДЕ
		|	(НЕ ЗаказыПокупателейОстатки.ЗаказаноОстаток = 0)
		|";
		
		ДатаКон = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
		Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКон);
		
		Заказы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Заказ");
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", Заказы);
	КонецЕсли;
	
КонецПроцедуры // УстановитьПараметры()

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПараметры();
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли