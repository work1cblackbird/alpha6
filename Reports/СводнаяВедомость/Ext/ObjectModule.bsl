#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Стандартная процедура настройки схемы компоновщика данных
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ФорматПредставленияГодаВыпускаАвтомобиля = АвтомобилиСервер.ПолучитьФорматПредставленияГодаВыпускаАвтомобиля(Неопределено);
	Для Каждого ТекНабор Из СхемаКомпоновкиДанных.НаборыДанных Цикл
		Для Каждого ТекПоле Из ТекНабор.Поля Цикл
			Если НЕ ТипЗнч(ТекПоле) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			ИмяПоля = СокрЛП(ТекПоле.Поле);
			Если ИмяПоля = "ГодВыпуска" Тогда
				ТекПоле.ПараметрыРедактирования.УстановитьЗначениеПараметра("ФорматРедактирования", ФорматПредставленияГодаВыпускаАвтомобиля);
				ТекПоле.Оформление.УстановитьЗначениеПараметра("Формат", ФорматПредставленияГодаВыпускаАвтомобиля);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПараметры();
	
	ФорматПредставления = ПраваИНастройкиПользователя.Значение("ФорматПредставленияГодаВыпускаАвтомобиля");
	
	Если ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.MMyyyy ИЛИ 
		ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.yyyy Тогда
		
		ПолеПоиска      = Новый ПолеКомпоновкиДанных("ГодВыпуска");
		НайденныеОтборы = Новый Массив;
		ПолучитьОтборСКД(ПолеПоиска, КомпоновщикНастроек.Настройки.Отбор.Элементы, НайденныеОтборы);
		Для Каждого ТекОтбор Из НайденныеОтборы Цикл
			Если ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.MMyyyy Тогда
				ТекОтбор.ПравоеЗначение = НачалоМесяца(ТекОтбор.ПравоеЗначение);
			ИначеЕсли ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.yyyy Тогда
				ТекОтбор.ПравоеЗначение = НачалоГода(ТекОтбор.ПравоеЗначение);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

// Проверка заполнения
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПараметрВалютаОтчета = Новый ПараметрКомпоновкиДанных("ВалютаОтчета");
	ВалютаОтчета         = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрВалютаОтчета).Значение;
	
	Если НЕ ЗначениеЗаполнено(ВалютаОтчета) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не указана валюта отчета.'");
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриИзмененииНастроек(Форма) Экспорт
	
	ПараметрыДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных;
	
	ВалютаОтчета = ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВалютаОтчета"));
	
	Если НЕ ЗначениеЗаполнено(ВалютаОтчета.Значение) Тогда
		ВалютаОтчета.Значение = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьОсновныеПараметры() Экспорт 
	
	СтруктураОсновныхПараметров = Новый Структура;
	СтруктураОсновныхПараметров.Вставить("ВалютаОтчета");
	
	Возврат СтруктураОсновныхПараметров;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает основные параметры
//
Процедура УстановитьПараметры()
	
	ПараметрВалютаОтчета  = Новый ПараметрКомпоновкиДанных("ВалютаОтчета");
	ВалютаОтчета          = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрВалютаОтчета).Значение;
	ПараметрДатаОкончания = Новый ПараметрКомпоновкиДанных("КонецПериода");
	КонецПериода         = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрДатаОкончания).Значение;
	
	КурсВалюты = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(КонецПериода, Новый Структура("Валюта",ВалютаОтчета));
	КурсВалютыОтчета = КурсВалюты.Курс / ?(КурсВалюты.Кратность = 0,1,КурсВалюты.Кратность);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КурсВалютыОтчета", КурсВалютыОтчета);
	
КонецПроцедуры

Процедура ПолучитьОтборСКД(ПолеКомпоновки, Отбор, НайденныеОтборы)
	
	Для Каждого ТекОтбор Из Отбор Цикл
		Если ТекОтбор.Использование Тогда
			Если ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				ПолучитьОтборСКД(ПолеКомпоновки, ТекОтбор.Элементы, НайденныеОтборы);
			ИначеЕсли ТипЗнч(ТекОтбор) = Тип("ЭлементОтбораКомпоновкиДанных") И ТекОтбор.ЛевоеЗначение = ПолеКомпоновки Тогда
				НайденныеОтборы.Добавить(ТекОтбор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли