
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РежимВыборРоли = Параметры.ВыборРоли;
	
	СписокЗначений.ТипЗначения       = Параметры.ТипЗначения;
	СписокЗначений.ДоступныеЗначения = Параметры.ДоступныеЗначения;
	Если ТипЗнч(Параметры.СписокВыбора) = Тип("СписокЗначений") Тогда
		СписокЗначений.ЗагрузитьЗначения(Параметры.СписокВыбора.ВыгрузитьЗначения());
	ИначеЕсли ЗначениеЗаполнено(Параметры.СписокВыбора) Тогда
		СписокЗначений.Добавить(Параметры.СписокВыбора);
	КонецЕсли;
	ТолькоГруппы                              = Параметры.ТолькоГруппы;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокЗначенийЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВыборРоли", РежимВыборРоли);
	
	ОписаниеОповещенияВыбораЗначения = Новый ОписаниеОповещения("Подключаемый_ОповещениеВыбораЗначенияОтбора", ЭтотОбъект);
	ОткрытьФорму("Отчет.ОтчетПоПравамДоступа.Форма.ФормаВыбораДанных", ПараметрыФормы, Элемент,,,, ОписаниеОповещенияВыбораЗначения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОк(Команда)
	
	Закрыть(СписокЗначений);
	
КонецПроцедуры

// Обработчик события возникающего при выполнении оповещения данной формы о прекращении работы подчиненной.
//
// Параметры:
//  РезультатЗакрытия     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
&НаКлиенте
Процедура Подключаемый_ОповещениеВыбораЗначенияОтбора(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = Элементы.СписокЗначений.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено ИЛИ РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Данные = СписокЗначений.НайтиПоИдентификатору(Элементы.СписокЗначений.ТекущаяСтрока);
	Данные.Значение = РезультатЗакрытия;
	Данные.Представление = РезультатЗакрытия;
	
КонецПроцедуры //Подключаемый_ОповещениеВыбораЗначенияОтбора()

#КонецОбласти

