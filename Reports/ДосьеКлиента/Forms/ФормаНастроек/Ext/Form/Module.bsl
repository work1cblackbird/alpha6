
#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет.Контрагент = ПолучитьЗначениеПараметраСтруктуры(Параметры, "Клиент");
	
	Если Отчет.Контрагент.ЭтоГруппа Тогда
		Отчет.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Обработчик события возникающего на клиенте при открытии формы, до показа окна пользователю.
//
// Параметры:
//  Отказ - Булево - Признак отказа от создания формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Если не выбраны разделы, то установим их
	Если НеВыбраныРазделы() Тогда
		Отчет.ОбщееОКлиенте  = Истина;
		Отчет.Взаимодействия = Истина;
		Отчет.Взаиморасчеты  = Истина;
		Отчет.ОбАвтомобилях  = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция НеВыбраныРазделы()
	
	Возврат НЕ Отчет.ОбщееОКлиенте И НЕ Отчет.Взаимодействия
		    И НЕ Отчет.Взаиморасчеты И НЕ Отчет.ОбАвтомобилях;
	
КонецФункции // НеВыбраныРазделы()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события возникающего на клиенте при нажатии кнопок "Установить все".
//
&НаКлиенте
Процедура УстановитьВсе(Команда)
	
	ЗначениеФлага = (Команда.Имя = "УстановитьВсе");
	Отчет.ОбщееОКлиенте  = ЗначениеФлага;
	Отчет.Взаимодействия = ЗначениеФлага;
	Отчет.Взаиморасчеты  = ЗначениеФлага;
	Отчет.ОбАвтомобилях  = ЗначениеФлага;
	
КонецПроцедуры // УстановитьВсе()

// Обработчик события возникающего на клиенте при нажатии кнопок "Сформировать".
//
&НаКлиенте
Процедура Сформировать(Команда)
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Отчет.Контрагент) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан клиент, для которого сформировать отчет.'"),,,, Отказ);
	КонецЕсли;
	
	Если НеВыбраныРазделы() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Необходимо указать разделы для вывода.'"),,,, Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		// Установим параметры для печати
		МенеджерПечати = "Отчет.ДосьеКлиента";
		
		ИменаМакетов = Новый Массив;
		ИменаМакетов.Добавить("ФормаДосьеКлиента");
		
		ОписаниеОтчета = Новый Структура("Контрагент, ОбщееОКлиенте, Взаимодействия, Взаиморасчеты, ОбАвтомобилях");
		ЗаполнитьЗначенияСвойств(ОписаниеОтчета, Отчет);
		
		// Установим параметры команды
		ПараметрКоманды = Новый Массив;
		ПараметрыПечати = Новый Структура("ЗаголовокФормы", "Досье клиента");
		ПараметрыПечати.Вставить("ОбъектОтчета", ОписаниеОтчета);
		ПараметрыПечати.Вставить("ФиксированныйКомплект", Истина);
		
		// Добавляем пустую ссылку, так как этот параметр должен иметь ссылочный тип, но абсолютно не влияет на поеведение печатной формы
		ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
		
		// Выведем на печатную форму
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			МенеджерПечати,
			ИменаМакетов,
			ПараметрКоманды,
			ЭтотОбъект,
			ПараметрыПечати);
		
	КонецЕсли;
	
КонецПроцедуры // Сформировать()

#КонецОбласти
