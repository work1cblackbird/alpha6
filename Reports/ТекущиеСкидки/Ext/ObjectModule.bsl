#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Стандартная процедура настройки компоновщика данных
//
// Параметры:
//	ФормаОтчета - УправляемаяФорма - Форма, в которой возникло событие.
//
Процедура ПриИзмененииНастроек(ФормаОтчета) Экспорт
	
	Для Каждого Строка Из ФормаОтчета.ЭтотОбъект.ГруппировкиСтрок Цикл
		Если Строка.Заголовок = НСтр("ru = 'Период действия'") Тогда
			Поля = Строка.Выбор.ПолучитьЭлементы();
			ФормаОтчета.ЭтотОбъект.КолонкиПоля.Очистить();
			Для Каждого Поле Из Поля Цикл
				НовыйЭлемент = ФормаОтчета.ЭтотОбъект.КолонкиПоля.Добавить();
				НовыйЭлемент.Использование = Поле.Использование;
				НовыйЭлемент.Заголовок = Поле.Заголовок;
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПараметры();
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет основные параметры отчета
//
// Без параметров
//
Процедура УстановитьПараметры()
	
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СтрокаПонедельник", ", " + НСтр("ru = 'Пн'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СтрокаВторник",     ", " + НСтр("ru = 'Вт'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СтрокаСреда",       ", " + НСтр("ru = 'Ср'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СтрокаЧетверг",     ", " + НСтр("ru = 'Чт'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СтрокаПятница",     ", " + НСтр("ru = 'Пт'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СтрокаСуббота",     ", " + НСтр("ru = 'Сб'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СтрокаВоскресение", ", " + НСтр("ru = 'Вс'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Строка0",           НСтр("ru = '0'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Строка1",           НСтр("ru = '1'"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НадписьДни",        НСтр("ru = 'Дни'") + ": ");
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НадписьВремяС", Символы.НПП + НСтр("ru = 'Время: С'") + Символы.НПП);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли