#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	
	ШаблонТекстЗапроса = "ВЫБРАТЬ
	|    Док"+"%1"+".Автор,
	|    Док"+"%1"+".Организация,
	|    Док"+"%1"+".Ссылка КАК Документ,
	|    Док"+"%1"+".ПодразделениеКомпании КАК Подразделение,
	|    ЕСТЬNULL (Док"+"%1"+".%3, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК Дата,
	|    Док"+"%1"+".ДатаСоздания КАК ДатаСоздания,
	|    Док"+"%1"+".ДатаОперации КАК ДатаОперации,
	|    %4 КАК ХозОперация,
	|    %2
	|ИЗ
	|    Документ."+"%1"+" КАК Док"+"%1 " + "
	|ГДЕ Док"+"%1"+".%3 <> Док"+"%1"+".ДатаОперации
	|    И Док"+"%1"+".Проведен = ИСТИНА
	|    И Док"+"%1"+".Дата Между &ДатаНач и &ДатаКон
	|" + "%5";
	Первый = Истина;
	ТекстЗапроса = "";
	
	СтрокаТипыДокументов = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ТипыДокументов");
	
	Если СтрокаТипыДокументов = Неопределено
		ИЛИ Не ЗначениеЗаполнено(СтрокаТипыДокументов.Значение) Тогда
		
		МассивТиповДокументов = КомпоновщикНастроек.Настройки.ДополнительныеСвойства.МассивТиповДокументов;
		
	Иначе
		
		МассивТиповДокументов = ЗначениеИзСтрокиВнутр(СтрокаТипыДокументов.Значение);
		
	КонецЕсли;
	
	Для Каждого ВидДок Из МассивТиповДокументов Цикл
		
		Если НЕ ВидДок.Использовать Тогда
			Продолжить;
		КонецЕсли;
		
		Имя = ВидДок.ТипДокумента;
		
		Если Имя = "ЗаказНаряд" Тогда
			ТекстДата = "ДатаЗакрытия";
			ТекстДопУсловие = "И ДокЗаказНаряд.Состояние = Значение(Справочник.ВидыСостоянийЗаказНарядов.Закрыт)";
		Иначе
			ТекстДата = "Дата";
			ТекстДопУсловие = "";
		КонецЕсли;
		
		Если ВидДок.ЕстьСуммаДокумента Тогда
			ТекстСуммаДокумента = "Док" + Имя + ".СуммаДокумента КАК СуммаДокумента";
		Иначе
			ТекстСуммаДокумента = "0 КАК СуммаДокумента";
		КонецЕсли;
		
		
		ТекстХозОперация    = ?(ВидДок.ЕстьХозОперация, "Док" + Имя + ".ХозОперация", """""");
		ТекстСуммаДокумента = ? (ВидДок.ЕстьСуммаДокумента,
								 "Док" + Имя + ".СуммаДокумента КАК СуммаДокумента",
								 "0 КАК СуммаДокумента");
		
		Если Первый Тогда
			ТекстЗапроса = СтрШаблон(ШаблонТекстЗапроса, Имя, ТекстСуммаДокумента,
										ТекстДата, ТекстХозОперация, ТекстДопУсловие);
			Первый = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|";
			ТекстЗапроса = ТекстЗапроса
							+ СтрШаблон(ШаблонТекстЗапроса, Имя, ТекстСуммаДокумента, ТекстДата, ТекстХозОперация, ТекстДопУсловие);
		КонецЕсли;
	
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстЗапроса) Тогда	
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбраны типы документов.'"));
		Возврат;
	КонецЕсли;
	
	// Получаем схему
	Схема = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	// и настройки
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы[0].Значение));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы[1].Значение));
	
	Выборка = Запрос.Выполнить().Выгрузить();
	
	// Создаем компоновщик макета и получаем макет компоновки
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, ДанныеРасшифровки);
	
	ВнешнийНаборДанных = Новый Структура;
	ВнешнийНаборДанных.Вставить("ТаблицаДанных", Выборка);
	
	// Инициализируем процессор компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки, Истина);
	
	// Очищаем документ результата
	ДокументРезультат.Очистить();
	
	// Выводим отчет в документ
	ПроцессорВывода = Новый
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли