
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	НачальныеЗначенияКоличественныеГруппы = ПараметрыВыполненияКоманды.Источник.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("КоличественныеГруппы"));
	НачальныеЗначенияСуммовыеГруппы       = ПараметрыВыполненияКоманды.Источник.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("СуммовыеГруппы"));
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("НачальныеЗначенияКоличественныеГруппы", НачальныеЗначенияКоличественныеГруппы.Значение);
	ПараметрыФормы.Вставить("НачальныеЗначенияСуммовыеГруппы", НачальныеЗначенияСуммовыеГруппы.Значение);
	
	ОбработкаОповещения = Новый ОписаниеОповещения("Подключаемый_ОбработкаРедактированияИнтервалов", ЭтотОбъект, ПараметрыВыполненияКоманды);
	
	ОткрытьФорму("Отчет.АнализПотерянныхКлиентов.Форма.НастройкаИнтервалов", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность,,, ОбработкаОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ОбработкаРедактированияИнтервалов(РезультатОповещения, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатОповещения = Неопределено Тогда
		КоличественныеГруппы = ДополнительныеПараметры.Источник.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("КоличественныеГруппы"));
		СуммовыеГруппы       = ДополнительныеПараметры.Источник.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("СуммовыеГруппы"));
		КоличественныеГруппы.Значение = РезультатОповещения.КоличественныеГруппы;
		СуммовыеГруппы.Значение       = РезультатОповещения.СуммовыеГруппы;
	КонецЕсли;
	
КонецПроцедуры // Подключаемый_ОбработкаРедактированияИнтервалов()

#КонецОбласти