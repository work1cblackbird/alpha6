///////////////////////////////////////////////////////////////////////////////
// Модуль произвольной формы отчета "Анализ работы пользователей"
//
///////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события возникающего на сервере при создании формы.
//
// Параметры:
//  Отказ                - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальныйСписокДокументов = ПолучитьЗначениеПараметраСтруктуры(Параметры, "НачальныеЗначенияСпискаДокументов", Неопределено);
	
	ЗаполнитьДеревоДокументов();
	
	Если НЕ НачальныйСписокДокументов = Неопределено И ТипЗнч(НачальныйСписокДокументов) = Тип("ХранилищеЗначения") Тогда
		ЗаполнитьСписокДокументов(НачальныйСписокДокументов.Получить());
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Обработчик события возникающего на клиенте при открытии формы, до показа окна пользователю.
//
// Параметры:
//  Отказ - Булево - Признак отказа от создания формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтотОбъект.ВладелецФормы = Неопределено Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Запрещен самостоятельный вызов.'"),,
			НСтр("ru = 'Данная форма используется другими объектами конфигурации.'"),
			БиблиотекаКартинок.Предупреждение32,
			СтатусОповещенияПользователя.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события возникающегопри изменении данных реквизита "Режим выбора" в контексте сервера.
//
&НаСервере
Процедура РежимВыбораДокументовПриИзмененииНаСервере()
	
	УправлениеДиалогомСервер();
	
КонецПроцедуры // РежимВыбораДокументовПриИзмененииНаСервере()

// Обработчик события возникающего на клиенте при изменении данных реквизита "Режим выбора".
//
// Параметры:
//  Элемент - ПолеФормы - Элемент управления, в котором возникло данное событие.
//
&НаКлиенте
Процедура РежимВыбораДокументовПриИзменении(Элемент)
	
	РежимВыбораДокументовПриИзмененииНаСервере();
	
КонецПроцедуры // РежимВыбораДокументовПриИзменении()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик события нажатия кнопки "Применить".
//
// Параметры:
//  Команда - КомандаФормы - Команда, в которой возникло данное событие.
//
&НаКлиенте
Процедура Применить(Команда)
	
	ВозвращаемоеЗначение = ПолучитьСтруктуруДляВозврата();
	
	Если ВозвращаемоеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(ВозвращаемоеЗначение);
	
КонецПроцедуры // Применить()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеДиалогомСервер()
	
	Элементы.СписокДокументов.ТолькоПросмотр = (РежимВыбораДокументов = 0);
	
КонецПроцедуры // УправлениеДиалогомСервер()

&НаСервере
Процедура ЗаполнитьСписокДокументов(НачальныйСписокДокументов)
	
	// Проставим переключатель
	ВыводитьВсеДокументы = НачальныйСписокДокументов.НайтиПоЗначению("ВсеДокументы");
	Если НЕ ВыводитьВсеДокументы = Неопределено Тогда
		РежимВыбораДокументов = 0;
		Для Каждого ТекущаяСтрока Из СписокДокументов Цикл
			ТекущаяСтрока.Пометка = Истина;
		КонецЦикла;
		УправлениеДиалогомСервер();
		Возврат;
	Иначе
		РежимВыбораДокументов = 1;
		УправлениеДиалогомСервер();
	КонецЕсли;
	
	// установим выделенные пометки
	Для Каждого ТекущаяСтрока Из СписокДокументов Цикл
		НайденноеЗначение = НачальныйСписокДокументов.НайтиПоЗначению(ТекущаяСтрока.Значение);
		Если НЕ НайденноеЗначение = Неопределено Тогда
			ТекущаяСтрока.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьСписокДокументов()

&НаСервере
Процедура ЗаполнитьДеревоДокументов()
	МетаданныеДок = Метаданные.Документы;
	СписокДокументов.Очистить();
	Для Каждого Документ Из МетаданныеДок Цикл
		НоваяСтрока = СписокДокументов.Добавить();
		НоваяСтрока.Пометка = Ложь;
		НоваяСтрока.Представление = Документ.Синоним;
		НоваяСтрока.Значение = Документ.Имя;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьДеревоДокументов()

&НаСервере
Функция ПолучитьСтруктуруДляВозврата()
	
	Результат = Новый СписокЗначений;
	Если РежимВыбораДокументов = 0 Тогда
		Результат.Добавить("ВсеДокументы", "<Все документы>");
	Иначе
		Для Каждого ТекущаяСтрока Из СписокДокументов Цикл
			Если ТекущаяСтрока.Пометка Тогда
				Результат.Добавить(ТекущаяСтрока.Значение, ТекущаяСтрока.Представление);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Результат.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбран ни один из видов документов.'"));
		Возврат Неопределено;
	Иначе
		Возврат Новый ХранилищеЗначения(Результат);
	КонецЕсли;
	
КонецФункции // ПолучитьСтруктуруДляВозврата()

#КонецОбласти

