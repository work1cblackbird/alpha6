#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПараметры();
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьОсновныеПараметры() Экспорт 
	
	СтруктураОсновныхПараметров = Новый Структура;
	СтруктураОсновныхПараметров.Вставить("ВыводитьЗакрытыеЗаказы");
	
	Возврат СтруктураОсновныхПараметров;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Установить параметры отчета
Процедура УстановитьПараметры()
	
	Запрос = Новый Запрос;
	
	ПоляОграничения = Новый Массив;
	ПоляОграничения.Добавить("Контрагент");
	ПоляОграничения.Добавить("ДоговорВзаиморасчетов");
	ПоляОграничения.Добавить("Заказ");
	ПоляОграничения.Добавить("Автомобиль");
	ПоляОграничения.Добавить("Модель");
	ПоляОграничения.Добавить("ВариантКомплектации");
	
	СоответствиеЗамен = Новый Соответствие;
	СоответствиеЗамен.Вставить("Контрагент",            "Заказ.Контрагент");
	СоответствиеЗамен.Вставить("ДоговорВзаиморасчетов", "Заказ.ДоговорВзаиморасчетов");
	СоответствиеЗамен.Вставить("Модель",                "Автомобиль.Модель");
	СоответствиеЗамен.Вставить("ВариантКомплектации",   "Автомобиль.ВариантКомплектации");
	
	ОтборКомпоновки = КомпоновщикНастроек.Настройки.Отбор; 
	ПоляИсключения = ОтчетыПлатформаСервер.ПолучитьПоляИсключения(ПоляОграничения,ОтборКомпоновки);
	
	ТекстОтбора = ОтчетыПлатформаСервер.КомпоновщикПолучитьТекстОтбора(
		ОтборКомпоновки.Элементы,
		ОтборКомпоновки.ДоступныеПоляОтбора,
		"ЗаказыАвтомобилей", Запрос.Параметры,
		СоответствиеЗамен, 
		ПоляОграничения, 
		ПоляИсключения
	);
	ТекстОтбора = ?(ТекстОтбора = "", "", " И " + Символы.ПС + ТекстОтбора);
	
	ДатаНач = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("НачалоПериода", КомпоновщикНастроек.Настройки);
	ДатаКон = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("КонецПериода",  КомпоновщикНастроек.Настройки);
	
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	
	ВыводитьЗакрытыеЗаказы = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("ВыводитьЗакрытыеЗаказы", КомпоновщикНастроек.Настройки);
	
	Если ВыводитьЗакрытыеЗаказы Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыАвтомобилей.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыАвтомобилей
		|ГДЕ
		|	ЗаказыАвтомобилей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора + "
		|";
	Иначе
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыАвтомобилей.Заказ КАК Заказ
		|{ВЫБРАТЬ
		|	Заказ КАК Заказ}
		|ПОМЕСТИТЬ
		|	ТаблицаЗаказов
		|ИЗ
		|	РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыАвтомобилей
		|ГДЕ
		|	ЗаказыАвтомобилей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора + "
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|ВЫБРАТЬ
		|	ЗаказыАвтомобилейОстатки.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(&ДатаКонГраница, Заказ В (ВЫБРАТЬ Заказ ИЗ ТаблицаЗаказов)) КАК ЗаказыАвтомобилейОстатки
		|ГДЕ
		|	(НЕ ЗаказыАвтомобилейОстатки.КоличествоОстаток = 0)
		|";
		
		ДатаКон = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
		Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКон);
	КонецЕсли;
	
	СписокЗаказов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Заказ");
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", СписокЗаказов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли