#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура помогает настроить отчет непосредственно перед выводом
//
// Параметры:
//   ПараметрСхемаКомпоновкиДанных - СхемаКомпоновкиДанных - Схема компоновки данных.
//   Настройки - НастройкиКомпоновкиДанных - Копия настроек компоновки данных.
//
Процедура ПередФормированиемМакетаКомпоновки(ПараметрСхемаКомпоновкиДанных, Настройки) Экспорт
	
	Поля = Новый Массив;
	Поля.Добавить("ПериодРегистратор");
	
	ОтчетыПлатформаСервер.КомпоновщикУстановитьОтборПустыхГруппировок(Настройки.Структура, Поля);
	
КонецПроцедуры // ПередФормированиемМакетаКомпоновки()

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	КлючВарианта = Неопределено;
	КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("КлючВарианта", КлючВарианта);
	
	Если КлючВарианта = "НезакрытыеСделки" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстатки.Контрагент КАК Контрагент,
		|	ВзаиморасчетыКомпанииОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&КонецПериода, ) КАК ВзаиморасчетыКомпанииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетыСКонтрагентамиОстатки.Контрагент КАК Контрагент,
		|	РасчетыСКонтрагентамиОстатки.СуммаОстаток КАК СуммаОстаток,
		|	РасчетыСКонтрагентамиОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		|ПОМЕСТИТЬ ОстаткиПоРасчетамСКонтрагентами
		|ИЗ
		|	РегистрНакопления.РасчетыСКонтрагентами.Остатки(&КонецПериода, ) КАК РасчетыСКонтрагентамиОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстатки.Сделка КАК Сделка
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
		|			&КонецПериода,
		|			НЕ ДоговорВзаиморасчетов В
		|					(ВЫБРАТЬ
		|						ВТ.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		|					ИЗ
		|						ВТ КАК ВТ)) КАК ВзаиморасчетыКомпанииОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасчетыСКонтрагентамиОстатки.ДокументРасчетов
		|ИЗ
		|	РегистрНакопления.РасчетыСКонтрагентами.Остатки(
		|			&КонецПериода,
		|			НЕ ДоговорВзаиморасчетов В
		|					(ВЫБРАТЬ
		|						ОстаткиПоРасчетамСКонтрагентами.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		|					ИЗ
		|						ОстаткиПоРасчетамСКонтрагентами КАК ОстаткиПоРасчетамСКонтрагентами)) КАК РасчетыСКонтрагентамиОстатки";
		
		Запрос.УстановитьПараметр("КонецПериода",  КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Конецпериода").Значение);
		РезультатЗапроса = Запрос.Выполнить();
		Результат =РезультатЗапроса.Выгрузить();
		Сделки = Новый СписокЗначений;
		Сделки.ЗагрузитьЗначения(Результат.ВыгрузитьКолонку("Сделка"));
		ОтчетыПлатформаСервер.КомпоновщикУстановитьОтбор(КомпоновщикНастроек.Настройки, "Сделка", Сделки, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли