#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обработчик при изменении настроек.
//
// Параметры:
//  ФормаОтчета - УправляемаяФорма - Форма, в которой возникло событие.
//
Процедура ПриИзмененииНастроек(ФормаОтчета) Экспорт
	
	Параметр = Новый ПараметрКомпоновкиДанных("СценарийПланирования");
	НайденныеПараметры = ФормаОтчета.ПараметрыДанных.НайтиСтроки(Новый Структура("Параметр", Параметр));
	Если НайденныеПараметры.Количество()>0 Тогда
	
		ДанныеСтроки = НайденныеПараметры[0];
		
		ПриИзмененииПараметра(ФормаОтчета, ДанныеСтроки);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТекСценарийПланирования = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("СценарийПланирования")).Значение;
	Если НЕ ЗначениеЗаполнено(ТекСценарийПланирования) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не установлено значение параметра ""Сценарий планирования"".'"),,,,Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриИзмененииПараметра(ФормаОтчета, ДанныеСтроки)
	
	ИмяПараметра = СокрЛП(ДанныеСтроки.Параметр);
	
	Если НЕ ИмяПараметра = "СценарийПланирования" Тогда
		Возврат;
	КонецЕсли;
	
	СценарийПланирования = ДанныеСтроки.Значение;
	Если СценарийПланирования.Периодичность = Перечисления.ПериодичностьПланирования.Месяц Тогда
		
		ФормаОтчета.ВидПериода = "Месяц";
		ОтчетыПлатформаКлиентСервер.ОбновитьВариантыПериодов(ФормаОтчета);
		ОтчетыПлатформаКлиентСервер.УстановитьПометкуНаПериод(ФормаОтчета, "ПериодМесяц");
		ОтчетыПлатформаСервер.ВидимостьПериода(ФормаОтчета);
		
	ИначеЕсли СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Квартал Тогда
	
		ФормаОтчета.ВидПериода = "Квартал";
		ОтчетыПлатформаКлиентСервер.ОбновитьВариантыПериодов(ФормаОтчета);
		ОтчетыПлатформаКлиентСервер.УстановитьПометкуНаПериод(ФормаОтчета, "ПериодКвартал");
		ОтчетыПлатформаСервер.ВидимостьПериода(ФормаОтчета);
	
	ИначеЕсли СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Год Тогда
	
		ФормаОтчета.ВидПериода = "Год";
		ОтчетыПлатформаКлиентСервер.ОбновитьВариантыПериодов(ФормаОтчета);
		ОтчетыПлатформаКлиентСервер.УстановитьПометкуНаПериод(ФормаОтчета, "ПериодГод");
		ОтчетыПлатформаСервер.ВидимостьПериода(ФормаОтчета);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли