#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПараметры();
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает параметр СписокЗаказов в построителе отчета ПостроительОтчета
//
// Без параметров
//
Процедура УстановитьПараметры()
	
	Запрос = Новый Запрос;
	
	ПоляОграничения = Новый Массив;
	ПоляОграничения.Добавить("Контрагент");
	ПоляОграничения.Добавить("ЗаказПоставщику");
	ПоляОграничения.Добавить("Номенклатура");
	ПоляОграничения.Добавить("ХарактеристикаНоменклатуры");
	
	ОтборКомпоновки = КомпоновщикНастроек.Настройки.Отбор;
	
	ПоляИсключения = ОтчетыПлатформаСервер.ПолучитьПоляИсключения(ПоляОграничения,ОтборКомпоновки);
	
	ТекстОтбора = ОтчетыПлатформаСервер.КомпоновщикПолучитьТекстОтбора(
		ОтборКомпоновки.Элементы, 
		ОтборКомпоновки.ДоступныеПоляОтбора,
		"ЗаказыПоставщикам",
		Запрос.Параметры, , 
		ПоляОграничения,
		ПоляИсключения
	);
	ТекстОтбора = ?(ТекстОтбора = "", "", " И " + Символы.ПС + ТекстОтбора);
	
	ДатаНач = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("НачалоПериода", КомпоновщикНастроек.Настройки);
	ДатаКон = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("КонецПериода", КомпоновщикНастроек.Настройки);
	
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	
	ВыводитьЗакрытыеЗаказы = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("ВыводитьЗакрытыеЗаказы", КомпоновщикНастроек.Настройки);
	
	Если ВыводитьЗакрытыеЗаказы Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПоставщикам.ЗаказПоставщику КАК ЗаказПоставщику
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|ГДЕ
		|	ЗаказПоставщику.Дата МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора + "
		|";
	Иначе
		
		ТекстПараметров = ОтчетыПлатформаСервер.КомпоновщикПолучитьТекстОтбора(
			ОтборКомпоновки.Элементы, 
			ОтборКомпоновки.ДоступныеПоляОтбора, 
			"",
			Запрос.Параметры, , 
			ПоляОграничения, 
			ПоляИсключения
		);
		ТекстПараметров = ?(ТекстПараметров = "", "", " И " + Символы.ПС + ТекстПараметров);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПоставщикам.ЗаказПоставщику КАК ЗаказПоставщику
		|ПОМЕСТИТЬ
		|	ТаблицаЗаказов
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|ГДЕ
		|	ЗаказПоставщику.Дата МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора + "
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказПоставщику
		|;
		|
		|ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику
		|{ВЫБРАТЬ
		|	ЗаказПоставщику КАК ЗаказПоставщику}
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&ДатаКон,
		|	ЗаказПоставщику В (ВЫБРАТЬ ЗаказПоставщику ИЗ ТаблицаЗаказов)" + ТекстПараметров + ") КАК ЗаказыПоставщикамОстатки
		|ГДЕ
		|	(НЕ ЗаказыПоставщикамОстатки.ЗаказаноОстаток = 0)
		|";
	КонецЕсли;
	
	МассивЗаказов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗаказПоставщику");
	СписокЗаказов = Новый СписокЗначений;
	СписокЗаказов.ЗагрузитьЗначения(МассивЗаказов);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", СписокЗаказов);
	
КонецПроцедуры // УстановитьПараметры()

#КонецОбласти

#КонецЕсли