#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура помогает настроить отчет непосредственно перед выводом
//
// Параметры:
//  ПараметрСхемаКомпоновкиДанных - СхемаКомпоновкиДанных - Схема компоновки данных.
//  Настройки - НастройкиКомпоновкиДанных - Копия настроек компоновки данных.
//
Процедура ПередФормированиемМакетаКомпоновки(ПараметрСхемаКомпоновкиДанных, Настройки) Экспорт
	
	ИнтервалыХранения = Новый ТаблицаЗначений;
	ИнтервалыХранения.Колонки.Добавить("ЧислоДней");
	ИнтервалыХранения.Колонки.Добавить("Представление");
	
	НовыйПериод = ИнтервалыХранения.Добавить();
	НовыйПериод.ЧислоДней		= 0;
	НовыйПериод.Представление	= "Сегодня";
	НовыйПериод = ИнтервалыХранения.Добавить();
	НовыйПериод.ЧислоДней		= 7;
	НовыйПериод.Представление	= "Хранения меньше недели";
	НовыйПериод = ИнтервалыХранения.Добавить();
	НовыйПериод.ЧислоДней		= 30;
	НовыйПериод.Представление	= "Хранения меньше месяца";
	НовыйПериод = ИнтервалыХранения.Добавить();
	НовыйПериод.ЧислоДней		= 5000;
	НовыйПериод.Представление	= "Хранения больше месяца";
	
	ПросроченныеИнтервалыХранения = Новый ТаблицаЗначений;
	ПросроченныеИнтервалыХранения.Колонки.Добавить("ЧислоДней");
	ПросроченныеИнтервалыХранения.Колонки.Добавить("Представление");
	
	НовыйПериод = ПросроченныеИнтервалыХранения.Добавить();
	НовыйПериод.ЧислоДней		= 7;
	НовыйПериод.Представление	= "Срок просрочки до недели";
	НовыйПериод = ПросроченныеИнтервалыХранения.Добавить();
	НовыйПериод.ЧислоДней		= 30;
	НовыйПериод.Представление	= "Срок просрочки до месяца";
	НовыйПериод = ПросроченныеИнтервалыХранения.Добавить();
	НовыйПериод.ЧислоДней		= 5000;
	НовыйПериод.Представление	= "Срок просрочки больше месяца";
	
	ПревЧислоДней = -1;
	Порядок       = 0;
	
	ИнтервалыХранения.Сортировать("ЧислоДней ВОЗР");
	ТекстИнтервалыХранения = "";
	Для Каждого ТекИнтервал Из ИнтервалыХранения Цикл
		ТекстИнтервалыХранения = ТекстИнтервалыХранения + "ОБЪЕДИНИТЬ ВСЕ 
			|
			|ВЫБРАТЬ
			|	"+Формат(ПревЧислоДней, "ЧРД=.; ЧН=0; ЧГ=")+",
			|	"+Формат(ТекИнтервал.ЧислоДней, "ЧРД=.; ЧН=0; ЧГ=")+",
			|	"""+ТекИнтервал.Представление+""",
			|	"+Формат(Порядок, "ЧН=0; ЧГ=")+"
			|";
		
		ПревЧислоДней = ТекИнтервал.ЧислоДней;
		Порядок       = Порядок+1;
	КонецЦикла;
	
	ПревЧислоДней = 0;
	
	ПросроченныеИнтервалыХранения.Сортировать("ЧислоДней ВОЗР");
	ТекстПросроченныеИнтервалыХранения = "";
	Для Каждого ТекИнтервал Из ПросроченныеИнтервалыХранения Цикл
		
		ТекстПросроченныеИнтервалыХранения = ТекстПросроченныеИнтервалыХранения + "ОБЪЕДИНИТЬ ВСЕ 
			|
			|ВЫБРАТЬ
			|	"+Формат(ПревЧислоДней, "ЧРД=.; ЧН=0; ЧГ=")+",
			|	"+Формат(ТекИнтервал.ЧислоДней, "ЧРД=.; ЧН=0; ЧГ=")+",
			|	"""+ТекИнтервал.Представление+""",
			|	"+Формат(Порядок, "ЧН=0; ЧГ=")+"
			|";
		
		ПревЧислоДней = ТекИнтервал.ЧислоДней;
		Порядок       = Порядок+1;
	КонецЦикла;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, СхемаКомпоновкиДанных, "dataCompositionScheme", "http://v8.1c.ru/8.1/data-composition-system/scheme");
	ТекстЗапроса = ЗаписьXML.Закрыть();
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПродолжениеИнтервалыХранения//", ТекстИнтервалыХранения);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПродолжениеПросроченныеИнтервалыХранения//", ТекстПросроченныеИнтервалыХранения);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстЗапроса);
	ПараметрСхемаКомпоновкиДанных = СериализаторXDTO.ПрочитатьXML(ЧтениеXML, Тип("СхемаКомпоновкиДанных"));
	
КонецПроцедуры // ПередФормированиемМакетаКомпоновки()

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли