#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - Настройкм отчета
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Если ТипЗнч(Настройки) = Тип("Структура") И НЕ Настройки.Свойство("Отбор") Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("НачалоПериода", НачалоДня(ТекущаяДатаСеанса()));
		СтруктураОтбора.Вставить("КонецПериода",  КонецДня(ТекущаяДатаСеанса()));
		Настройки.Вставить("Отбор", СтруктураОтбора);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ВариантОтчета = КомпоновщикНастроек.Настройки.ДополнительныеСвойства.КлючВарианта;
	
	ВнешниеНаборыДанных = Новый Структура("ТаблицаРабот", ОсновнойЗапрос(ВариантОтчета).Выполнить());
	ОтчетыПлатформаСервер
		.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, ВнешниеНаборыДанных);
	
КонецПроцедуры // ПриКомпоновкеРезультата()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ГраницыОтчета()
	
	Возврат Новый Структура(
		"Начало,Конец",
		НачалоДня(ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("НачалоПериода", КомпоновщикНастроек.Настройки)),
		КонецДня(ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("КонецПериода", КомпоновщикНастроек.Настройки)));
	
КонецФункции

Функция ДатыВнутриГраниц(Границы)
	
	ТипДата = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
	ДатыОтчета = Новый ТаблицаЗначений;
	ДатыОтчета.Колонки.Добавить("НачалоДня", ТипДата);
	ДатыОтчета.Колонки.Добавить("КонецДня", ТипДата);
	
	ТекущийДень = Границы.Начало;
	
	Пока ТекущийДень < Границы.Конец Цикл
		
		НоваяСтрока = ДатыОтчета.Добавить();
		НоваяСтрока.НачалоДня = ТекущийДень;
		НоваяСтрока.КонецДня = КонецДня(ТекущийДень);
		ТекущийДень = ТекущийДень + 24*60*60;
		
	КонецЦикла;
	
	Возврат ДатыОтчета;
	
КонецФункции

Функция ОсновнойЗапрос(ВариантОтчета)
	
	ГраницыОтчета = ГраницыОтчета();
	
	ТолькоЗаявкиНаРемонт = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра(
		"ТолькоЗаявкиНаРемонт", 
		КомпоновщикНастроек.Настройки);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Т.НачалоДня КАК НачалоДня,
	|	Т.КонецДня КАК КонецДня
	|ПОМЕСТИТЬ ДатыОтчета
	|ИЗ
	|	&ДатыОтчета КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПланированиеРабот.Документ КАК Документ,
	|	ПланированиеРабот.Начало КАК Начало,
	|	ПланированиеРабот.Конец КАК Конец,
	|	ПланированиеРабот.РабочееМесто КАК РабочееМесто,
	|	ПланированиеРабот.Исполнитель КАК Исполнитель,
	|	ВЫБОР
	|		КОГДА ПланированиеРабот.Объект ССЫЛКА Справочник.Автоработы
	|			ТОГДА ПланированиеРабот.Объект
	|		ИНАЧЕ ""-""
	|	КОНЕЦ КАК Работа
	|ПОМЕСТИТЬ Запланировано
	|ИЗ
	|	РегистрСведений.ПланированиеРабот КАК ПланированиеРабот
	|ГДЕ
	|	(ПланированиеРабот.Начало МЕЖДУ &Начало И &Конец
	|			ИЛИ ПланированиеРабот.Конец МЕЖДУ &Начало И &Конец
	|			ИЛИ &Начало МЕЖДУ ПланированиеРабот.Начало И ПланированиеРабот.Конец)" + ?(ТолькоЗаявкиНаРемонт, "
	|	И ТипЗначения(ПланированиеРабот.Документ) = Тип(Документ.ЗаявкаНаРемонт)", "
	|	И НЕ ПланированиеРабот.НеАктуален") + "
	|	И ПланированиеРабот.Документ <> НЕОПРЕДЕЛЕНО // FIXME: Фильтруем битые записи содержащиеся в некоторых базах.
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкаНаРемонт.Ссылка КАК Документ,
	|	ЗаявкаНаРемонт.Автомобиль КАК Автомобиль,
	|	ЗаявкаНаРемонт.Модель КАК Модель,
	|	ЗаявкаНаРемонт.Организация КАК Организация,
	|	ЗаявкаНаРемонт.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаявкаНаРемонт.ПодразделениеКомпании.ФилиалКомпании КАК Филиал,
	|	ЗаявкаНаРемонт.Мастер КАК Мастер,
	|	ВЫБОР
	|		КОГДА ЗаявкаНаРемонт.Заказчик <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ЗаявкаНаРемонт.Заказчик
	|		ИНАЧЕ ЗаявкаНаРемонт.ОбращениеККлиенту
	|	КОНЕЦ КАК Заказчик,
	|	ЗаявкаНаРемонт.ПредставлениеТелефона КАК Телефон,
	|	ЗаявкаНаРемонт.ОписаниеПричиныОбращения КАК ПричинаОбращения
	|ПОМЕСТИТЬ ДанныеДокументов
	|ИЗ
	|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
	|ГДЕ
	|	ЗаявкаНаРемонт.Ссылка В
	|			(ВЫБРАТЬ
	|				Запланировано.Документ КАК Документ
	|			ИЗ
	|				Запланировано КАК Запланировано)" + ?(ТолькоЗаявкиНаРемонт, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказНаряд.Ссылка,
	|	ЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль,
	|	ЗаказНаряд.СводныйРемонтныйЗаказ.Автомобиль.Модель,
	|	ЗаказНаряд.СводныйРемонтныйЗаказ.Организация,
	|	ЗаказНаряд.СводныйРемонтныйЗаказ.ПодразделениеКомпании,
	|	ЗаказНаряд.СводныйРемонтныйЗаказ.ПодразделениеКомпании.ФилиалКомпании,
	|	ЗаказНаряд.Мастер,
	|	ЗаказНаряд.СводныйРемонтныйЗаказ.Заказчик,
	|	ЗаказНаряд.СводныйРемонтныйЗаказ.ПредставлениеТелефона,
	|	ЗаказНаряд.ОписаниеПричиныОбращения
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Ссылка В
	|			(ВЫБРАТЬ
	|				Запланировано.Документ КАК Документ
	|			ИЗ
	|				Запланировано КАК Запланировано)", "") + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АвтомобилиСрезПоследних.Автомобиль КАК Автомобиль,
	|	АвтомобилиСрезПоследних.Значение КАК ГосНомер
	|ПОМЕСТИТЬ ГосНомера
	|ИЗ
	|	РегистрСведений.Автомобили.СрезПоследних(
	|			,
	|			Автомобиль В
	|					(ВЫБРАТЬ
	|						ДанныеДокументов.Автомобиль КАК Автомобиль
	|					ИЗ
	|						ДанныеДокументов КАК ДанныеДокументов)
	|				И ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)) КАК АвтомобилиСрезПоследних
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Документ КАК Документ,
	|	ДанныеДокументов.Автомобиль КАК Автомобиль,
	|	ДанныеДокументов.Модель КАК Модель,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ДанныеДокументов.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ДанныеДокументов.Филиал КАК Филиал,
	|	ДанныеДокументов.Мастер КАК Мастер,
	|	ДанныеДокументов.Заказчик КАК Заказчик,
	|	ДанныеДокументов.Телефон КАК Телефон,
	|	ДанныеДокументов.ПричинаОбращения КАК ПричинаОбращения,
	|	ЕСТЬNULL(ГосНомера.ГосНомер, ""-"") КАК ГосНомер
	|ПОМЕСТИТЬ ДанныеДокументовСГосНомерами
	|ИЗ
	|	ДанныеДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГосНомера КАК ГосНомера
	|		ПО ДанныеДокументов.Автомобиль = ГосНомера.Автомобиль
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ГосНомера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запланировано.Документ КАК Документ,
	|	Запланировано.Начало КАК Начало,
	|	Запланировано.Конец КАК Конец,
	|	Запланировано.РабочееМесто КАК РабочееМесто,
	|	Запланировано.Исполнитель КАК Исполнитель,
	|	Запланировано.Работа КАК Работа,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.Автомобиль, ""-"") КАК Автомобиль,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.Модель, ""-"") КАК Модель,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.Организация, ""-"") КАК Организация,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.ПодразделениеКомпании, ""-"") КАК ПодразделениеКомпании,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.Филиал, ""-"") КАК Филиал,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.Мастер, ""-"") КАК Мастер,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.Заказчик, ""-"") КАК Заказчик,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.Телефон, ""-"") КАК Телефон,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.ПричинаОбращения, ""-"") КАК ПричинаОбращения,
	|	ЕСТЬNULL(ДанныеДокументовСГосНомерами.ГосНомер, ""-"") КАК ГосНомер
	|ПОМЕСТИТЬ ЗапланированоСДанными
	|ИЗ
	|	Запланировано КАК Запланировано
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументовСГосНомерами КАК ДанныеДокументовСГосНомерами
	|		ПО Запланировано.Документ = ДанныеДокументовСГосНомерами.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Запланировано
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеДокументовСГосНомерами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыОтчета.НачалоДня КАК Дата,
	|	ЗапланированоСДанными.Документ КАК Документ,
	|	ВЫБОР
	|		КОГДА ЗапланированоСДанными.Начало < ДатыОтчета.НачалоДня
	|			ТОГДА ДатыОтчета.НачалоДня
	|		ИНАЧЕ ЗапланированоСДанными.Начало
	|	КОНЕЦ КАК НачалоРабот,
	|	ВЫБОР
	|		КОГДА ЗапланированоСДанными.Конец > ДатыОтчета.КонецДня
	|			ТОГДА ДатыОтчета.КонецДня
	|		ИНАЧЕ ЗапланированоСДанными.Конец
	|	КОНЕЦ КАК КонецРабот,
	|	ЗапланированоСДанными.РабочееМесто КАК РабочееМесто,
	|	ЗапланированоСДанными.Исполнитель КАК Исполнитель,
	|	ЗапланированоСДанными.Работа КАК Работа,
	|	ЗапланированоСДанными.Автомобиль КАК Автомобиль,
	|	ЗапланированоСДанными.Модель КАК Модель,
	|	ЗапланированоСДанными.Организация КАК Организация,
	|	ЗапланированоСДанными.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗапланированоСДанными.Филиал КАК Филиал,
	|	ЗапланированоСДанными.Мастер КАК Мастер,
	|	ЗапланированоСДанными.Заказчик КАК Заказчик,
	|	ЗапланированоСДанными.Телефон КАК Телефон,
	|	ЗапланированоСДанными.ПричинаОбращения КАК ПричинаОбращения,
	|	ЗапланированоСДанными.ГосНомер КАК ГосНомер
	|ИЗ
	|	ДатыОтчета КАК ДатыОтчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗапланированоСДанными КАК ЗапланированоСДанными
	|		ПО (ДатыОтчета.НачалоДня <= ЗапланированоСДанными.Начало
	|					И ДатыОтчета.КонецДня >= ЗапланированоСДанными.Начало
	|				ИЛИ ДатыОтчета.НачалоДня <= ЗапланированоСДанными.Конец
	|					И ДатыОтчета.КонецДня >= ЗапланированоСДанными.Конец
	|				ИЛИ ЗапланированоСДанными.Начало <= ДатыОтчета.НачалоДня
	|					И ЗапланированоСДанными.Конец >= ДатыОтчета.НачалоДня)";
	Запрос.УстановитьПараметр("ДатыОтчета", ДатыВнутриГраниц(ГраницыОтчета));
	Запрос.УстановитьПараметр("Начало", ГраницыОтчета.Начало);
	Запрос.УстановитьПараметр("Конец", ГраницыОтчета.Конец);
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#КонецЕсли