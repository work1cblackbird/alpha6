#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
	Маркер = "/////";
	СкрытьСтрокиВТабличномДокументе(ДокументРезультат, Маркер);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ЭлементОтбора.Использование Тогда
			Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодразделениеКомпании")
				И (ЭлементОтбора.ПравоеЗначение = Неопределено 
				ИЛИ ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.ПодразделенияКомпании.ПустаяСсылка")) Тогда
				ЭлементОтбора.Использование = Ложь;
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Отбор по подразделению компании не установлен. Формируется отчет для компании в целом.'"));
			ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация")
				И (ЭлементОтбора.ПравоеЗначение = Неопределено 
				ИЛИ ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка")) Тогда
				ЭлементОтбора.Использование = Ложь;
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Отбор по организации компании не установлен. Формируется отчет для компании в целом.'"));
			ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Документ") 
				И ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.Использование = Ложь;
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Отбор по документу не установлен. Формируется отчет для всех документов.'"));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СкрытьСтрокиВТабличномДокументе(ТабличныйДокумент, МаркерУдаления)
	
	НайденныеОбласти = НайтиОбластиТабличногоДокументаПоВхождениюПодстроки(ТабличныйДокумент, МаркерУдаления);
	НайденныеОбласти.Сортировать("Верх УБЫВ");
	
	Для каждого Строка Из НайденныеОбласти Цикл
		Область = ТабличныйДокумент.Область(Строка.Верх,, Строка.Верх);
		ТабличныйДокумент.УдалитьОбласть(Область,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиОбластиТабличногоДокументаПоВхождениюПодстроки(ТабличныйДокумент, ПодстрокаПоиска)
	
	НайденныеОбласти = Новый ТаблицаЗначений;
	НайденныеОбласти.Колонки.Добавить("Область");
	НайденныеОбласти.Колонки.Добавить("Текст");
	НайденныеОбласти.Колонки.Добавить("Верх");
	НайденныеОбласти.Колонки.Добавить("Лево");
	
	НайденнаяОбласть = ТабличныйДокумент.НайтиТекст(ПодстрокаПоиска);
	
	Пока НЕ НайденнаяОбласть = Неопределено Цикл
		
		НоваяСтрока = НайденныеОбласти.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяОбласть);
		НоваяСтрока.Область = НайденнаяОбласть;
		
		НайденнаяОбласть = ТабличныйДокумент.НайтиТекст(ПодстрокаПоиска, НайденнаяОбласть);
		
	КонецЦикла;
	
	Возврат НайденныеОбласти;
	
КонецФункции

#КонецОбласти

#КонецЕсли
