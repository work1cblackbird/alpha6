#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - НастройкиКомпоновкиДанных - Копия настроек компоновки данных.
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Если НЕ Форма.Параметры.Свойство("Схема") Тогда
		ТекстСообщения = НСтр("ru='Данный отчет используется только объектами конфигурации.
		                          |Запрещен самостоятельный вызов.'");
		Форма.НастройкиОтчета.Вставить("ОтменитьОткрытие", ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(Форма.Параметры.Схема);
	КомпоновщикНастроек.ЗагрузитьНастройки(Форма.Параметры.Настройки);
	
	Если СхемаКомпоновкиДанных.НаборыДанных.Найти("НаборДанныхДинамическогоСписка") <> Неопределено Тогда
		Поле = СхемаКомпоновкиДанных.НаборыДанных.НаборДанныхДинамическогоСписка.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		Поле.Заголовок      = "СуммаСписания";
		Поле.ПутьКДанным    = "СуммаСписания";
		Поле.Поле           = "СуммаСписания";
		Поле.ТипЗначения    = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 2)); 
		ПолеРесурса = СхемаКомпоновкиДанных.ПоляИтога.Добавить(); 
		ПолеРесурса.Выражение   = "СУММА(СуммаСписания)"; 
		ПолеРесурса.ПутьКДанным = "СуммаСписания";
		
		Поле = СхемаКомпоновкиДанных.НаборыДанных.НаборДанныхДинамическогоСписка.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		Поле.Заголовок      = "СуммаСписанияБезНДС";
		Поле.ПутьКДанным    = "СуммаСписанияБезНДС";
		Поле.Поле           = "СуммаСписанияБезНДС";
		Поле.ТипЗначения    = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 2)); 
		ПолеРесурса = СхемаКомпоновкиДанных.ПоляИтога.Добавить(); 
		ПолеРесурса.Выражение   = "СУММА(СуммаСписанияБезНДС)"; 
		ПолеРесурса.ПутьКДанным = "СуммаСписанияБезНДС";
	КонецЕсли;
	
	// Заполним данные для показателей
	Показатели = КомпоновщикНастроек.Настройки.Структура[0];
	Если КомпоновщикНастроек.Настройки.Структура[0].Структура.Количество() > 0 Тогда
		Показатели = КомпоновщикНастроек.Настройки.Структура[0].Структура[0];
	КонецЕсли;
	
	Для Каждого Строка Из Показатели.Выбор.Элементы Цикл
		НовыйЭлемент = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(ТипЗнч(Строка));
		Если ТипЗнч(Строка) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, Строка,, "Поле");
			ЗаполнитьЗаголовок(НовыйЭлемент, Показатели, Строка.Элементы);
			ЗаполнитьЭлементыГруппы(НовыйЭлемент, Строка.Элементы);
		Иначе
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	Показатели.Выбор.Элементы.Очистить();
	Если НЕ КомпоновщикНастроек.Настройки.Структура[0] = Показатели Тогда
		КомпоновщикНастроек.Настройки.Структура[0].Выбор.Элементы.Очистить();
	КонецЕсли;
	
	КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВертикальноеРасположениеОбщихИтогов").Использование 
																												= Истина;
	КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВертикальноеРасположениеОбщихИтогов").Значение      
																				= РасположениеИтоговКомпоновкиДанных.Нет;
	
КонецПроцедуры // ОпределитьНастройкиФормы()

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//  СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - Схема компоновки данных.
//  Настройки - НастройкиКомпоновкиДанных - Копия настроек компоновки данных.
//
Процедура ПередФормированиемМакетаКомпоновки(СхемаКомпоновкиДанных, Настройки) Экспорт
	
	Для каждого Строка Из Настройки.Выбор.Элементы Цикл
		Если ТипЗнч(Строка) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			Строка.Заголовок = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПередФормированиемМакетаКомпоновки()

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб        = Истина;
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьЗаголовок(НовыйЭлемент, Показатели, Настройка)
	
	Заголовок = "";
	Если НовыйЭлемент.Заголовок = "" Тогда
		Для Каждого Строка Из Настройка Цикл
			
			ДоступноеПоле = Показатели.Выбор.ДоступныеПоляВыбора.Элементы.Найти(Строка.Поле);
			Если НЕ ДоступноеПоле = Неопределено И НЕ ДоступноеПоле.Заголовок = "" Тогда
				Заголовок = Заголовок + ?(Заголовок = "", ДоступноеПоле.Заголовок, ", " + ДоступноеПоле.Заголовок);
			КонецЕсли;
			
		КонецЦикла;
		НовыйЭлемент.Заголовок = Заголовок;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьЗаголовок()

Процедура ЗаполнитьЭлементыГруппы(НовыйЭлемент, Настройка)
	
	Для Каждого Строка Из Настройка Цикл
		НовыйЭлементГруппы = НовыйЭлемент.Элементы.Добавить(ТипЗнч(Строка));
		Если ТипЗнч(Строка) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			ЗаполнитьЗначенияСвойств(НовыйЭлементГруппы, Строка,, "Поле");
			ЗаполнитьЭлементыГруппы(НовыйЭлементГруппы, Строка.Элементы);
		Иначе
			ЗаполнитьЗначенияСвойств(НовыйЭлементГруппы, Строка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПолучитьПредставлениеПоля()

#КонецОбласти

#КонецЕсли