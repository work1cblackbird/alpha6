#Область ОбработчикиСобытий

// Обработчик события нажатия кнопки "История аренды автомобиля".
//
// Параметры:
//  ПараметрКоманды - Произвольный - В параметр передается значение от источника, в котором реализована команда
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды - В обработчике команды можно изменить
//  значение свойств параметра Окно и Уникальность. 
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено ИЛИ ПараметрКоманды.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Автомобиль = Новый СписокЗначений;
	Если ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Автомобили") Тогда
		Автомобиль.Добавить(ПараметрКоманды);
	Иначе
		// выгрузим все автомобили из ТЧ
		Автомобиль.ЗагрузитьЗначения(ЗначениеРеквизитаОбъекта(ПараметрКоманды, "Автомобили.Автомобиль"));
	КонецЕсли;
	
	// фильтры
	ОтчетОтбор = Новый Структура;
	ОтчетОтбор.Вставить("ЗаявкаНаАренду_Автомобиль", Автомобиль);
	ОтчетОтбор.Вставить("ДоговорАренды_Автомобиль", Автомобиль);
	
	ОтчетыПлатформаКлиент.ОткрытьОтчет(
			"Отчет.ИсторияАренды",
			"Основной", , , ,
			ОтчетОтбор,
			'20200101',
			Дата(1, 1, 1));
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗначениеРеквизитаОбъекта(ПараметрКоманды, Реквизит)
	
	ТЗ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрКоманды, Реквизит).Выгрузить();
	ТЗ.Свернуть("Автомобиль");
	
	Возврат ТЗ.ВыгрузитьКолонку("Автомобиль");
	
КонецФункции

#КонецОбласти
