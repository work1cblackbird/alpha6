#Область ОбработчикиСобытий

// Обработчик события нажатия кнопки "Наличие в прайс-листах контрагентов".
//
// Параметры:
//  ПараметрКоманды - Произвольный - В параметр передается значение от источника, в котором реализована команда
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды - В обработчике команды можно
//  изменить значение свойств параметра Окно и Уникальность. 
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено ИЛИ ПараметрКоманды.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	// фильтры
	ОтчетОтбор = Новый Структура;
	ОтчетОтбор.Вставить("Артикул", ?(ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.СводныйРемонтныйЗаказ"), ПолучитьСписокТоваровЗаказНарядов(ПараметрКоманды), ПолучитьСписокАртикуловТовара(ПараметрКоманды)));
	
	ОткрытьФорму("Отчет.ПрайсЛистыКонтрагентов.Форма", Новый Структура("Отбор,КлючВарианта,СформироватьПриОткрытии", ОтчетОтбор, "НаличиеВПрайсЛистахКонтрагентов", Истина), ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность);
	
КонецПроцедуры //ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает список артикулов табличной части "Товары"
//
&НаСервере
Функция ПолучитьСписокАртикуловТовара(Документ)
	
	СписокТоваров = Новый СписокЗначений;
	Если Документ.Товары.Количество() <> 0 Тогда
		Для Каждого Товар Из Документ.Товары Цикл
			СписокТоваров.Добавить(Товар.Номенклатура.Артикул);
		КонецЦикла;
	КонецЕсли;
	Возврат СписокТоваров;
	
КонецФункции

// Функция возвращает список заказ-нарядов выбранного сводного ремонтного заказа
//
&НаСервере
Функция ПолучитьСписокТоваровЗаказНарядов(Документ)
	
	СписокТоваровЗаказНарядов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказНарядТовары.Номенклатура.Артикул КАК Артикул
		|ИЗ
		|	Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
		|ГДЕ
		|	ЗаказНарядТовары.Ссылка.СводныйРемонтныйЗаказ = &Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНарядТовары.Номенклатура.Артикул";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		СписокТоваровЗаказНарядов.Добавить(РезультатЗапроса.Артикул);
	КонецЦикла;
	
	Возврат СписокТоваровЗаказНарядов;
	
КонецФункции

#КонецОбласти