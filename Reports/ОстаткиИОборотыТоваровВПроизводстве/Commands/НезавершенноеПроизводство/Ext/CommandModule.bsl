#Область ОбработчикиСобытий

// Обработчик события нажатия кнопки "Незавершенное производство".
//
// Параметры:
//  ПараметрКоманды - Произвольный - В параметр передается значение от источника, в котором реализована команда
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды - В обработчике команды
//  можно изменить значение свойств параметра Окно и Уникальность. 
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено ИЛИ ПараметрКоманды.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	// фильтры
	ОтчетОтбор = Новый Структура;
	ОтчетОтбор.Вставить("НачалоПериода", Дата('00010101'));
	ОтчетОтбор.Вставить("КонецПериода",  ОбщегоНазначенияКлиент.ДатаСеанса());
	ОтчетОтбор.Вставить("ЗаказНаряд",    ?(ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЗаказНаряд"), ПараметрКоманды, ПолучитьСписокЗаказНарядов(ПараметрКоманды)));
	
	ОткрытьФорму("Отчет.ОстаткиИОборотыТоваровВПроизводстве.Форма", Новый Структура("Отбор,КлючВарианта,СформироватьПриОткрытии", ОтчетОтбор, "Обороты", Истина), ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность);
	
КонецПроцедуры //ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает список заказ-нарядов выбранного сводного ремонтного заказа
//
&НаСервере
Функция ПолучитьСписокЗаказНарядов(Документ)
	
	СписокЗаказНарядов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ЗаказНаряд
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.СводныйРемонтныйЗаказ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		СписокЗаказНарядов.Добавить(РезультатЗапроса.ЗаказНаряд);
	КонецЦикла;
	
	Возврат СписокЗаказНарядов;
	
КонецФункции

#КонецОбласти