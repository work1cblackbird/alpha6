#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено ИЛИ ПараметрКоманды.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = ПолучитьПараметрыПользователя();
	
	// фильтры
	ОтчетОтбор = Новый Структура;
	
	Если ПраваИНастройкиПользователяКлиент.Значение("УстанавливатьОтборОрганизацияВОтчетах") Тогда
		ОтчетОтбор.Вставить("Организация", ПараметрыОтбора.Организация);
	КонецЕсли;
	
	ОтчетОтбор.Вставить("Номенклатура",  ПараметрКоманды);
	ОтчетОтбор.Вставить("НачалоПериода", НачалоГода(ДобавитьМесяц(ПараметрыОтбора.ДатаОтчета, -60)));
	ОтчетОтбор.Вставить("КонецПериода",  ПараметрыОтбора.ДатаОтчета);
	
	ОткрытьФорму("Отчет.ОстаткиИОборотыПартийТоваров.Форма", Новый Структура("Отбор,КлючВарианта,СформироватьПриОткрытии", ОтчетОтбор, "ДвиженияПоСкладам", Истина), ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность,ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПараметрыПользователя()
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", ПараметрыСеанса.Организация);
	Результат.Вставить("ДатаОтчета",  ТекущаяДатаСеанса());
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти