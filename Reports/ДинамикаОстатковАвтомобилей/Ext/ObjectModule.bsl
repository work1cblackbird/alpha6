#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем СписокПериодов Экспорт;                          // хранится список периодов
Перем СтруктураТипаКолонки;                            // структура типа колонки
Перем СтруктураСвязиСвойств Экспорт;                   // структура связи свойств

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТаблицаПериодов = ПолучитьТаблицуПериодов();
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаПериодов", ТаблицаПериодов);
	
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, ВнешниеНаборыДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// формирует текст запроса отчета
Функция ПолучитьТаблицуПериодов()
	
	ИмяСтроки = ИзмерениеСтроки;
	ИмяКолонки = ИзмерениеКолонки;
	
	ДатаНачала = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы[1].Значение;
	ДатаКонца = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы[0].Значение;
	
	ТаблицаПериодов = Новый ТаблицаЗначений;
	ТаблицаПериодов.Колонки.Добавить("ПериодСтроки", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("ПериодКолонки", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("ПериодНижнийПредел", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("ПериодВерхнийПредел", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("НомерКолонки", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(2));
	
	ТекДата = ДатаНачала;
	МинимальныйПределПериода = 1;
	Если ИзмерениеСтроки="ПериодГод" Тогда // Год
		МинимальныйПределМесяца = 29;
		МинимальныйПределПериода = 366;
		ТекДата	= НачалоГода(ДатаНачала);
	ИначеЕсли ИзмерениеСтроки="ПериодКвартал" Тогда // Квартал
		МинимальныйПределМесяца = 29;
		МинимальныйПределПериода = 92;
		ТекДата = НачалоКвартала(ДатаНачала);
	ИначеЕсли ИзмерениеСтроки="ПериодМесяц" Тогда // Месяц
		МинимальныйПределМесяца = 31;
		МинимальныйПределПериода = 31;
		ТекДата = НачалоМесяца(ДатаНачала);
	ИначеЕсли ИзмерениеСтроки="ПериодНеделя" Тогда // Неделя
		МинимальныйПределМесяца = 29;
		МинимальныйПределПериода = 7;
		ТекДата	= НачалоНедели(ДатаНачала);
	КонецЕсли;
	
	Шаг = 0;
	Если ИзмерениеКолонки = "ПериодКвартал" Тогда
		Шаг = 3;
	ИначеЕсли ИзмерениеКолонки = "ПериодМесяц" Тогда
		Шаг = 1;
	ИначеЕсли ИзмерениеКолонки = "ПериодНеделя" Тогда
		Шаг = 60*60*24*7;
	ИначеЕсли ИзмерениеКолонки = "ПериодДень" Тогда
		Шаг = 60*60*24;
	КонецЕсли;
	
	КолонкаДата = (СтруктураТипаКолонки[ИмяСтроки+"_"+ИмяКолонки]="Дата");
	Если ИзмерениеКолонки = "ПериодНеделя" Тогда // Неделя
		ПустаяДата = Дата(Год(ДатаНачала), 1, 1);
		МинимальныйПределПериода = Окр(МинимальныйПределПериода/7+0.5, 0, 0)+?(ИзмерениеСтроки="ПериодМесяц", 1, 0);
		Пока ТекДата<ДатаКонца Цикл // Период от начальной даты до конечной даты
			Если ИзмерениеСтроки="ПериодГод" Тогда
				ПериодСтроки = НачалоГода(ТекДата);
				ТекКонДата = КонецГода(ТекДата);
			ИначеЕсли ИзмерениеСтроки="ПериодКвартал" Тогда
				ПериодСтроки = НачалоКвартала(ТекДата);
				ТекКонДата = КонецКвартала(ТекДата);
			ИначеЕсли ИзмерениеСтроки="ПериодМесяц" Тогда
				ПериодСтроки = НачалоМесяца(ТекДата);
				ТекКонДата = КонецМесяца(ТекДата);
			КонецЕсли;
			
			Счетчик = 1;
			ТекДата	= НачалоНедели(ТекДата);
			ТекКонДата = Мин(ТекКонДата, ДатаКонца);
			Пока ТекДата<ТекКонДата Цикл // период по группировке строки
				НоваяСтрока = ТаблицаПериодов.Добавить();
				НоваяСтрока.ПериодСтроки = ПериодСтроки;
				НоваяСтрока.ПериодКолонки = ТекДата;
				НоваяСтрока.НомерКолонки = Счетчик;
				НоваяСтрока.ПериодНижнийПредел = Макс(ТекДата, ПериодСтроки);
				ТекДата = ТекДата + Шаг;
				НоваяСтрока.ПериодВерхнийПредел = ТекДата;
				Счетчик = Счетчик + 1;
			КонецЦикла;
			
			// добавим пустышки до последнего дня
			Если ТекДата<ДатаКонца И Счетчик <= МинимальныйПределПериода Тогда
				ТекДатаКолонки = ТекДата - Шаг;
				Пока Счетчик <= МинимальныйПределПериода Цикл
					НоваяСтрока = ТаблицаПериодов.Добавить();
					НоваяСтрока.ПериодСтроки = ПериодСтроки;
					НоваяСтрока.ПериодКолонки = ТекДатаКолонки;
					НоваяСтрока.НомерКолонки = Счетчик;
					НоваяСтрока.ПериодНижнийПредел = ПустаяДата;
					НоваяСтрока.ПериодВерхнийПредел = ПустаяДата;
					Счетчик = Счетчик + 1;
				КонецЦикла;
			Иначе
				НоваяСтрока.ПериодВерхнийПредел = ТекКонДата+1;
			КонецЕсли;
			ТекДата = ТекКонДата+1;
		КонецЦикла;
	
	ИначеЕсли ИзмерениеСтроки <> "ПериодНеделя" И ИзмерениеКолонки = "ПериодДень" Тогда // день
		
		Пока ТекДата<ДатаКонца Цикл // Период от начальной даты до конечной даты
			Если ИзмерениеСтроки="ПериодГод" Тогда
				ТекКонДата = КонецГода(ТекДата);
			ИначеЕсли ИзмерениеСтроки="ПериодКвартал" Тогда
				ТекКонДата = КонецКвартала(ТекДата);
			ИначеЕсли ИзмерениеСтроки="ПериодМесяц" Тогда
				ТекКонДата = КонецМесяца(ТекДата);
			КонецЕсли;
			
			Счетчик = 1;
			ПериодСтроки = ТекДата;
			НетПустышек = Истина;
			ТекКонДата = Мин(ТекКонДата, ДатаКонца);
			Пока ТекДата<ТекКонДата Цикл // период по группировке строки
				
				Если МинимальныйПределМесяца = 29 Тогда
					ТекДатаКолонки = Дата(Год(ДатаНачала), Месяц(ТекДата), День(ТекДата));
				Иначе
					ТекДатаКолонки = Дата(Год(ДатаНачала), 1, День(ТекДата));
				КонецЕсли;
				ТекДень = День(ТекДата);
				ПоследнийДень = День(КонецМесяца(ТекДата));
				Пока ТекДень <= ПоследнийДень Цикл // период по месяцу
					НоваяСтрока = ТаблицаПериодов.Добавить();
					НоваяСтрока.ПериодСтроки = ПериодСтроки;
					НоваяСтрока.ПериодКолонки = ТекДатаКолонки;
					НоваяСтрока.НомерКолонки = Счетчик;
					НоваяСтрока.ПериодНижнийПредел = ТекДата;
					ТекДатаКолонки = ТекДатаКолонки + Шаг;
					ТекДата = ТекДата + Шаг;
					НоваяСтрока.ПериодВерхнийПредел = ТекДата;
					ТекДень = ТекДень + 1;
					Счетчик = Счетчик + 1;
					Если ТекДата>ДатаКонца Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если ТекДата>ДатаКонца Тогда
					Прервать;
				КонецЕсли;
				
				// добавим пустышки до последнего дня
				НетПустышек = Истина;
				Если ТекДень <= МинимальныйПределМесяца Тогда
					ТекДатаКолонки = Дата(Год(ДатаНачала), Месяц(ТекДатаКолонки), ПоследнийДень+1);
					Пока ТекДень <= МинимальныйПределМесяца Цикл
						НоваяСтрока = ТаблицаПериодов.Добавить();
						НоваяСтрока.ПериодСтроки = ПериодСтроки;
						НоваяСтрока.ПериодКолонки = ТекДатаКолонки;
						НоваяСтрока.НомерКолонки = Счетчик;
						НоваяСтрока.ПериодНижнийПредел = ТекДатаКолонки;
						НоваяСтрока.ПериодВерхнийПредел = ТекДатаКолонки;
						ТекДатаКолонки = ТекДатаКолонки + Шаг;
						ТекДень = ТекДень + 1;
						Счетчик = Счетчик + 1;
					КонецЦикла;
					НетПустышек = Ложь;
				КонецЕсли;
			КонецЦикла;
			
			// добавим пустышки до последнего дня
			Если ТекДата<ДатаКонца И Счетчик <= МинимальныйПределПериода Тогда
				ТекДатаКолонки = ТекДатаКолонки-Шаг;
				Пока Счетчик <= МинимальныйПределПериода Цикл
					НоваяСтрока = ТаблицаПериодов.Добавить();
					НоваяСтрока.ПериодСтроки = ПериодСтроки;
					НоваяСтрока.ПериодКолонки = ТекДатаКолонки;
					НоваяСтрока.НомерКолонки = Счетчик;
					НоваяСтрока.ПериодНижнийПредел = ТекДатаКолонки;
					НоваяСтрока.ПериодВерхнийПредел = ТекДатаКолонки;
					Счетчик = Счетчик + 1;
				КонецЦикла;
				НетПустышек = Ложь;
			КонецЕсли;
			ТекДата = ТекКонДата+1;
			Если НетПустышек Тогда
				НоваяСтрока.ПериодВерхнийПредел = ТекДата;
			КонецЕсли;
		КонецЦикла;

	Иначе // Год, Квартал, Месяц
		
		Пока ТекДата<ДатаКонца Цикл
			Если ИзмерениеСтроки="ПериодГод" Тогда
				ТекКонДата = КонецГода(ТекДата);
			ИначеЕсли ИзмерениеСтроки="ПериодКвартал" Тогда
				ТекКонДата = КонецКвартала(ТекДата);
			ИначеЕсли ИзмерениеСтроки="ПериодНеделя" Тогда
				ТекКонДата = КонецНедели(ТекДата);
			КонецЕсли;
			ПериодСтроки = ТекДата;
			ТекКонДата = Мин(ТекКонДата, ДатаКонца);
			Счетчик = 1;
			Пока ТекДата < ТекКонДата Цикл
				НоваяСтрока = ТаблицаПериодов.Добавить();
				НоваяСтрока.ПериодСтроки = ПериодСтроки;
				НоваяСтрока.ПериодКолонки = Дата(Год(ДатаНачала), Месяц(ТекДата), День(ТекДата));
				НоваяСтрока.НомерКолонки = Счетчик;
				НоваяСтрока.ПериодНижнийПредел = ТекДата;
				Если ИзмерениеСтроки = "ПериодНеделя" Тогда
					ТекДата = ТекДата + Шаг;
				Иначе
					ТекДата = ДобавитьМесяц(ТекДата, Шаг);
				КонецЕсли;
				НоваяСтрока.ПериодВерхнийПредел = ТекДата;
				Счетчик = Счетчик+1;
			КонецЦикла;
			ТекДата = ТекКонДата+1;
			НоваяСтрока.ПериодВерхнийПредел = ТекДата;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаПериодов;
	
КонецФункции

#КонецОбласти

#Область Инициализация

СписокПериодов = Новый СписокЗначений();
СписокПериодов.Добавить("ПериодГод");
СписокПериодов.Добавить("ПериодКвартал");
СписокПериодов.Добавить("ПериодМесяц");
СписокПериодов.Добавить("ПериодНеделя");
СписокПериодов.Добавить("ПериодДень");

СтруктураТипаКолонки = Новый Структура;
СтруктураТипаКолонки.Вставить("ПериодГод_ПериодКвартал",	"Число");
СтруктураТипаКолонки.Вставить("ПериодГод_ПериодМесяц",		"Дата");
//СтруктураТипаКолонки.Вставить("ПериодГод_ПериодНеделя",		"Дата");
СтруктураТипаКолонки.Вставить("ПериодГод_ПериодНеделя",		"Число");
СтруктураТипаКолонки.Вставить("ПериодГод_ПериодДень",		"Дата");
СтруктураТипаКолонки.Вставить("ПериодКвартал_ПериодМесяц",  "Число");
СтруктураТипаКолонки.Вставить("ПериодКвартал_ПериодНеделя", "Число");
СтруктураТипаКолонки.Вставить("ПериодКвартал_ПериодДень",  	"Число");
СтруктураТипаКолонки.Вставить("ПериодМесяц_ПериодНеделя",  	"Число");
СтруктураТипаКолонки.Вставить("ПериодМесяц_ПериодДень",  	"Число");
СтруктураТипаКолонки.Вставить("ПериодНеделя_ПериодДень",  	"Число");

ПоследовательностиОтчета = Новый Структура();
ПоследовательностиОтчета.Вставить("ГраницыАвтомобили");

#КонецОбласти

#КонецЕсли