#Область ОбработчикиСобытий

// Обработчик события нажатия кнопки "Отчет по состоянию работ".
//
// Параметры:
//  ПараметрКоманды - Произвольный - В параметр передается значение от источника, в котором реализована команда
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды - В обработчике команды можно
//  изменить значение свойств параметра Окно и Уникальность. 
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено ИЛИ ПараметрКоманды.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета  = ПолучитьПараметрыОтчета(ПараметрКоманды);
	
	// фильтры
	ОтчетОтбор = Новый Структура;
	ОтчетОтбор.Вставить("ЗаказНаряд", ПараметрыОтчета.ЗаказНаряд);
	
	ОтчетыПлатформаКлиент.ОткрытьОтчет(
		"Отчет.СостояниеРабот",
		"Основной",,,,
		ОтчетОтбор,
		НачалоМесяца(ПараметрыОтчета.Дата),
		Дата(1,1,1));
	
КонецПроцедуры //ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает список заказ-нарядов выбранного сводного ремонтного заказа
//
&НаСервере
Функция ПолучитьСписокЗаказНарядов(Документ)
	
	СписокЗаказНарядов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ЗаказНаряд
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.СводныйРемонтныйЗаказ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		СписокЗаказНарядов.Добавить(РезультатЗапроса.ЗаказНаряд);
	КонецЦикла;
	
	Возврат СписокЗаказНарядов;
	
КонецФункции

// Функция получения параметров отчета
//
&НаСервере
Функция ПолучитьПараметрыОтчета(Документ)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Дата", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Дата"));
	ПараметрыОтчета.Вставить("ЗаказНаряд", ?(ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказНаряд"), Документ, ПолучитьСписокЗаказНарядов(Документ)));
	
	Возврат ПараметрыОтчета;
	
КонецФункции // ПолучитьПараметрыОтчета()

#КонецОбласти
