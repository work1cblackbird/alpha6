#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ВнешниеНаборыДанных = Новый Структура("ВыборкаЖурналаРегистрации", ВыбратьИзЖурналаРегистрации());
	ОтчетыПлатформаСервер
		.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, ВнешниеНаборыДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыбратьИзЖурналаРегистрации()
	
	НачалоПериода = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НачалоПериода");
	Если НачалоПериода = Неопределено Тогда
		НачалоПериода = Дата("00010101");
	Иначе
		НачалоПериода = НачалоПериода.Значение;
	КонецЕсли;
	
	КонецПериода = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("КонецПериода");
	Если КонецПериода = Неопределено Тогда
		КонецПериода = ТекущаяДатаСеанса();
	Иначе
		КонецПериода = КонецПериода.Значение;
	КонецЕсли;
	
	СобытияВостановления = Новый Массив;
	СобытияВостановления.Добавить(НСтр("ru = 'Восстановление последовательностей. Последовательности восстановлены'"));
	СобытияВостановления.Добавить(НСтр("ru = 'Восстановление последовательностей. Документ проведен'"));
	СобытияВостановления.Добавить(НСтр("ru = 'Восстановление последовательностей. Ошибка'"));
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Метаданные",    Метаданные.Обработки.ВосстановлениеПоследовательностей);
	ПараметрыОтбора.Вставить("ДатаНачала",    НачалоПериода);
	ПараметрыОтбора.Вставить("ДатаОкончания", КонецПериода);
	ПараметрыОтбора.Вставить("Событие", СобытияВостановления);
	
	СобытияЖурнала = Новый ТаблицаЗначений;
	ВыгрузитьЖурналРегистрации(СобытияЖурнала, ПараметрыОтбора, "Дата,Событие,ИмяПользователя,Данные,Комментарий");
	Возврат СобытияЖурнала;
	
КонецФункции

#КонецОбласти

#КонецЕсли