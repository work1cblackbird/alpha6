#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПараметры();
	ОтчетыПлатформаСервер.ВывестиОтчет(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает параметр построителя отчета "СписокЗаказов"
//
// Без параметров
//
Процедура УстановитьПараметры() Экспорт
	
	// РежимВывода
	//  Выводить все
	//  Не выводить закрытые заказы
	//  Выводить заказы поступившие, но не отгруженные.

	Запрос = Новый Запрос;
	
	ПоляОграничения = Новый Массив;
	ПоляОграничения.Добавить("Контрагент");
	ПоляОграничения.Добавить("Заказ");
	ПоляОграничения.Добавить("Номенклатура");
	ПоляОграничения.Добавить("ХарактеристикаНоменклатуры");
	
	ОтборКомпоновки = КомпоновщикНастроек.Настройки.Отбор; 
	ПоляИсключения = ОтчетыПлатформаСервер.ПолучитьПоляИсключения(ПоляОграничения,ОтборКомпоновки);
			
	ТекстОтбора = ОтчетыПлатформаСервер.КомпоновщикПолучитьТекстОтбора(
		ОтборКомпоновки.Элементы, 
		ОтборКомпоновки.ДоступныеПоляОтбора, 
		"ЗаказыПокупателей", 
		Запрос.Параметры,,
		ПоляОграничения,
		ПоляИсключения
	);
	ТекстОтбора = ?(ТекстОтбора = "", "", " И " + Символы.ПС + ТекстОтбора);
	
	ДатаКон = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("КонецПериода", КомпоновщикНастроек.Настройки);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	ДатаНач = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("НачалоПериода", КомпоновщикНастроек.Настройки);
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	
	РежимВывода = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("РежимВывода", КомпоновщикНастроек.Настройки);

	Если РежимВывода = 0 Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПокупателей.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|ГДЕ
		|	ЗаказыПокупателей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора +"
		|";
	ИначеЕсли РежимВывода = 3 Тогда
		
		ТекстПараметров = ОтчетыПлатформаСервер.КомпоновщикПолучитьТекстОтбора(
			ОтборКомпоновки.Элементы, 
			ОтборКомпоновки.ДоступныеПоляОтбора,
			"",
			Запрос.Параметры, ,
			ПоляОграничения,
			ПоляИсключения
		);
		ТекстПараметров = ?(ТекстПараметров = "", "", " И " + Символы.ПС + ТекстПараметров);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПокупателей.Заказ КАК Заказ
		|ПОМЕСТИТЬ
		|	ТаблицаЗаказов
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|ГДЕ
		|	ЗаказыПокупателей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора +"
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателейОстатки.Заказ КАК Заказ,
		|		ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|		ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		ВЫБОР
		|			КОГДА ЗаказыПокупателейОстатки.ЗаказаноОстаток = ЗаказыПокупателейОстатки.РезервОстаток ТОГДА
		|				1
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК СовпадаетЗаказИРезерв
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей.Остатки(&ДатаКонГраница,
		|		Заказ В (ВЫБРАТЬ Заказ ИЗ ТаблицаЗаказов)" + ТекстПараметров + ") КАК ЗаказыПокупателейОстатки
		|	ГДЕ
		|		ЗаказыПокупателейОстатки.ЗаказаноОстаток>0) КАК ВложенныйЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Заказ
		|ИМЕЮЩИЕ
		|	МИНИМУМ(ВложенныйЗапрос.СовпадаетЗаказИРезерв)>0
		|";
		
		ДатаКон = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
		Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКон);
		
	Иначе
		
		Если РежимВывода = 1 Тогда
			СтрокаОтбора = "(НЕ ЗаказыПокупателейОстатки.ЗаказаноОстаток = 0)";
		Иначе
			СтрокаОтбора = "(НЕ ЗаказыПокупателейОстатки.РезервОстаток = 0)";
		КонецЕсли;
		
		ТекстПараметров = ОтчетыПлатформаСервер.КомпоновщикПолучитьТекстОтбора(
			ОтборКомпоновки.Элементы,
			ОтборКомпоновки.ДоступныеПоляОтбора,
			"",
			Запрос.Параметры, ,
			ПоляОграничения,
			ПоляИсключения
		);
		ТекстПараметров = ?(ТекстПараметров = "", "", " И " + Символы.ПС + ТекстПараметров);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПокупателей.Заказ КАК Заказ
		|ПОМЕСТИТЬ
		|	ТаблицаЗаказов
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|ГДЕ
		|	ЗаказыПокупателей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора +"
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(&ДатаКонГраница,
		|	Заказ В (ВЫБРАТЬ Заказ ИЗ ТаблицаЗаказов)" + ТекстПараметров + ") КАК ЗаказыПокупателейОстатки
		|ГДЕ
		|	"+СтрокаОтбора+"
		|";
		
		ДатаКон = ОтчетыПлатформаСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
		Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКон);
		
	КонецЕсли;
	
	СписокЗаказов = Новый СписокЗначений;
	СписокЗаказов.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Заказ"));
	
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", СписокЗаказов);
	
КонецПроцедуры // УстановитьПараметры()

#КонецОбласти   


#КонецЕсли