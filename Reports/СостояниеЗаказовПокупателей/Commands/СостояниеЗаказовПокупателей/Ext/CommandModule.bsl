#Область ОбработчикиСобытий

// Обработчик события нажатия кнопки "Наличие на складах".
//
// Параметры:
//  ПараметрКоманды - Произвольный - В параметр передается значение от источника, в котором реализована команда
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды - В обработчике команды можно изменить значение свойств
//                                                            параметра Окно и Уникальность.
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено ИЛИ ПараметрКоманды.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета  = ПолучитьПараметрыОтчета(ПараметрКоманды);
	
	// фильтры
	ОтчетОтбор = Новый Структура;
	Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЗаказНаряд")
		ИЛИ ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.СводныйРемонтныйЗаказ") Тогда
		ОтчетОтбор.Вставить("ДокументОснование", ПараметрыОтчета.ДокументОснование);
		
		КлючВарианта = "ДляЗаказНаряда";
		НачалоПериода = ОбщегоНазначенияКлиент.ДатаСеанса() - 86400 * 365;
	Иначе
		ОтчетОтбор.Вставить("Заказ",      ПараметрКоманды);
		ОтчетОтбор.Вставить("Контрагент", ПараметрыОтчета.Контрагент);
		
		КлючВарианта = "ДляЗаказа";
		НачалоПериода = НачалоМесяца(ПараметрыОтчета.Дата);
	КонецЕсли;
	
	ОтчетыПлатформаКлиент.ОткрытьОтчет(
		"Отчет.СостояниеЗаказовПокупателей",
		КлючВарианта,,,,
		ОтчетОтбор,
		НачалоПериода,
		Дата(1,1,1));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция получения параметров отчета
//
&НаСервере
Функция ПолучитьПараметрыОтчета(Документ)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Дата", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Дата"));
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		ПараметрыОтчета.Вставить("ДокументОснование", Документ);
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.СводныйРемонтныйЗаказ") Тогда
		ПараметрыОтчета.Вставить("ДокументОснование", ПолучитьСписокЗаказНарядов(Документ));
	Иначе
		ИмяРеквизитаКонтрагент = ?(ЕстьРеквизит(Документ, "Контрагент"), "Контрагент", "ПодразделениеПолучатель");
		ПараметрыОтчета.Вставить("Контрагент", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, ИмяРеквизитаКонтрагент));
	КонецЕсли;
	
	Возврат ПараметрыОтчета;
	
КонецФункции // ПолучитьПараметрыОтчета()

// Функция возвращает список заказ-нарядов выбранного сводного ремонтного заказа
//
&НаСервере
Функция ПолучитьСписокЗаказНарядов(Документ)
	
	СписокЗаказНарядов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказНаряд.Ссылка КАК ЗаказНаряд
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.СводныйРемонтныйЗаказ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		СписокЗаказНарядов.Добавить(РезультатЗапроса.ЗаказНаряд);
	КонецЦикла;
	
	Возврат СписокЗаказНарядов;
	
КонецФункции

#КонецОбласти