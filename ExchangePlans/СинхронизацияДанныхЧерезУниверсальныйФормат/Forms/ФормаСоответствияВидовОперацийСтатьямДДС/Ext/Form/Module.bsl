#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.СоответствиеОперацийИСтатейДДС.Количество() > 0  Тогда
		Операция = Параметры.СоответствиеОперацийИСтатейДДС; 
		
		Для Каждого Строка Из Операция Цикл
			
			СоответствиеОпераций = ЭтотОбъект.ОперацияСтатья.Добавить();
			СоответствиеОпераций.Операция = Строка.Операция;
			СоответствиеОпераций.СтатьяДДС = Строка.СтатьяДДС;
			
		КонецЦикла;

	Иначе 
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыОперацийДвиженияДенежныхСредств.Ссылка КАК Ссылка
		|ИЗ
		|	Перечисление.ВидыОперацийДвиженияДенежныхСредств КАК ВидыОперацийДвиженияДенежныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатьиДДС.Ссылка КАК Ссылка,
		|	СтатьиДДС.Операция КАК Операция
		|ИЗ
		|	Справочник.СтатьиДДС КАК СтатьиДДС
		|ГДЕ
		|	СтатьиДДС.Предопределенный = ИСТИНА
		|	И СтатьиДДС.ЭтоГруппа = ЛОЖЬ
		|	И НЕ СтатьиДДС.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийДвиженияДенежныхСредств.ПустаяСсылка)";
		
		Запросы = Запрос.ВыполнитьПакет();
		ВыборкаОперация = Запросы[0].Выбрать(); 
		ВыборкаСтатья = Запросы[1].Выгрузить();
		
		Операция = Новый ТаблицаЗначений(); 
		
		Операция.Колонки.Добавить("Операция", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийДвиженияДенежныхСредств"));
		Операция.Колонки.Добавить("СтатьяДДС", Новый ОписаниеТипов("СправочникСсылка.СтатьиДДС"));

		Пока ВыборкаОперация.Следующий() Цикл  
			
			Строка = Операция.Добавить();
			Строка.Операция = ВыборкаОперация.Ссылка;
			Статья =ВыборкаСтатья.Найти(ВыборкаОперация.Ссылка, "Операция");
			Строка.СтатьяДДС = ?(Статья <> Неопределено, Статья.Ссылка, Справочники.СтатьиДДС.ПустаяСсылка());
			
		КонецЦикла;
		ЗначениеВРеквизитФормы(Операция, "ОперацияСтатья");
			
	КонецЕсли; 
				
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗакрытияФормы = Новый Структура();
	ПараметрыЗакрытияФормы.Вставить("АдресТаблицыВоВременномХранилище", СформироватьТаблицу());
	ПараметрыЗакрытияФормы.Вставить("ИмяТаблицыДляЗаполнения", "СоответствиеОперацииИСтатейДДС");
	
	ОповеститьОВыборе(ПараметрыЗакрытияФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьТаблицу()

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Операция");
	Таблица.Колонки.Добавить("СтатьяДДС"); 
	
	Для Каждого Строка из ЭтотОбъект.ОперацияСтатья Цикл 
		СтрокаОперации = Таблица.Добавить();
		СтрокаОперации.Операция = Строка.Операция; 
		СтрокаОперации.СтатьяДДС = Строка.СтатьяДДС;
	КонецЦикла;	

	Возврат ПоместитьВоВременноеХранилище(Таблица, УникальныйИдентификатор);

КонецФункции

#КонецОбласти