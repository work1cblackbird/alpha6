// Модуль менеджера регистра накоплений "Маркировка товаров в производстве"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбработчикиОбновления

// Отрезает криптохвосты кодов маркировки.
//
// Возвращаемое значение:
//  Булево - Истина, если произошли ошибки в процессе выполнения, Ложь - в противном случае.
//
Функция ОтрезатьКриптохвосты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МаркировкаТоваровВПроизводстве.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.МаркировкаТоваровВПроизводстве КАК МаркировкаТоваровВПроизводстве";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ЕстьОшибки = Ложь;
	// Заполним новые реквизиты по заказ-нарядам
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыНакопления.МаркировкаТоваровВПроизводстве.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для Каждого ТекущаяЗапись Из НаборЗаписей Цикл
			ТекущаяЗапись.КодМаркировки = МаркировкаТоваровКлиентСервер.ПолучитьКодМаркировки(ТекущаяЗапись.КодМаркировки);
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ЕстьОшибки = Истина;
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Обновление ИБ.Очистка криптохвостов Маркировка товаров в производстве'",
					ОбщегоНазначения.КодОсновногоЯзыка()
				),
				УровеньЖурналаРегистрации.Ошибка,
				,
				Выборка.Регистратор,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			);
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат ЕстьОшибки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли