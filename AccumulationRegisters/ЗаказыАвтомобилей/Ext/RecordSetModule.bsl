// Модуль набора записей регистра накоплений "Заказы автомбилей"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем РежимПроведения Экспорт;               // Режим проведения документа. Если проведение неоперативное - проверок остатков нет

Перем ДокументОбъект Экспорт;                //Документ, осуществляющий движение по регистру
Перем РезультатЗапросаПоАвтомобилям Экспорт; //Выборка табличной части или таблица значений табличной части автомобилей
Перем Заказ Экспорт;                         //Ссылка на документ ЗаказНаАвтомобиль. Если значение неопределено заказ находится в табличной части

Перем Приходовать Экспорт;                   // Булево. Истина - надо приходовать заказ на автомобиль
Перем Резервировать Экспорт;                 // Булево. Истина - надо резервировать автомобиль под заказ

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует движения по регистру приход (регистрируем заказ)
// Возвращаемое значение: Булево. Истина - все хорошо, Ложь - чего-то не так.
Функция Приход() Экспорт
	ВсеОК = Истина;

	Приходовать   = ?(Приходовать = Неопределено, Истина, Приходовать);
	Резервировать = ?(Резервировать = Неопределено, Ложь, Резервировать);
	
	// получим таблицу автомобилей
	Если (РезультатЗапросаПоАвтомобилям = Неопределено) ИЛИ
		(ТипЗнч(РезультатЗапросаПоАвтомобилям) <> Тип("РезультатЗапроса")) И
		(ТипЗнч(РезультатЗапросаПоАвтомобилям) <> Тип("ТаблицаЗначений")) Тогда
		
		РезультатЗапросаПоАвтомобилям = ПолучитьТаблицуАвтомобилей();
		
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗапросаПоАвтомобилям) = Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоАвтомобилям = РезультатЗапросаПоАвтомобилям.Выгрузить();
	КонецЕсли;

	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
	Если ЕстьРеквизит(ДокументОбъект, "КурсВалютыУпр") Тогда
		КурсУпр = ДокументОбъект.КурсВалютыУпр;
	Иначе
		КурсУпр = Неопределено;
	КонецЕсли;
	
	Если КурсУпр = Неопределено ИЛИ НЕ ЗначениеЗаполнено(КурсУпр) Тогда
		СтруктураКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаУпр,ДокументОбъект.Дата);
		КурсУпр        = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;

	// Перебираем строки товаров
	Для каждого СтрокаАвтомобилей Из РезультатЗапросаПоАвтомобилям Цикл
		ЗаказЗаказа = ?(Заказ = Неопределено, СтрокаАвтомобилей.Заказ, Заказ);
		СуммаУпр    = РаботаСКурсамиВалютПлатформа.ПересчетПоВалюте(СтрокаАвтомобилей.Сумма, ДокументОбъект.ВалютаДокумента, ДокументОбъект.КурсДокумента, ВалютаУпр, КурсУпр);
		
		// Создаем запись регистра заказов
		НоваяЗапись = Добавить();
		
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период      = ДокументОбъект.Дата;
		НоваяЗапись.Регистратор = ДокументОбъект.Ссылка;
		НоваяЗапись.Заказ       = ЗаказЗаказа;
		НоваяЗапись.Количество  = ?(Приходовать,СтрокаАвтомобилей.Количество,0);
		НоваяЗапись.Резерв      = ?(Резервировать,СтрокаАвтомобилей.Резерв,0);
		НоваяЗапись.СуммаУпр    = Окр(СуммаУпр,2);
		НоваяЗапись.ХозОперация = ДокументОбъект.ХозОперация;
	КонецЦикла; 
	
	// запись движений
	Если ВсеОК Тогда
		Записать();
	КонецЕсли;
	
	РезультатЗапросаПоАвтомобилям = Неопределено;
	Заказ                         = Неопределено;
	ДокументОбъект                = Неопределено;
	
	Возврат ВсеОК;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает результат запроса по таблице автомобилей
Функция ПолучитьТаблицуАвтомобилей()
	// получим результат запроса по таблице автомобилей
	ВидДок = ДокументОбъект.Метаданные().Имя;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДокументАвтомобили.СуммаВсего КАК Сумма,
	|	"+?(Приходовать,"ДокументАвтомобили.Количество КАК Количество,","")+"
	|	"+?(Резервировать,"ДокументАвтомобили.Резерв КАК Резерв,","")+"
	|	"+?(Заказ=Неопределено,"ДокументАвтомобили.Заказ","&Заказ")+" КАК Заказ
	|ИЗ
	|	Документ."+ВидДок+".Автомобили КАК ДокументАвтомобили
	|ГДЕ
	|	ДокументАвтомобили.Ссылка=&Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Ссылка" , ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("Заказ"  , Заказ);
	
	Возврат Запрос.Выполнить();
КонецФункции

#КонецОбласти

#Область Инициализация

////////////////////////////////////////////////////////////////////////
// Инициализация переменных модуля набора записей

РежимПроведения               = РежимПроведенияДокумента.Оперативный;
ДокументОбъект                = Неопределено; // Обязательный к заполнению перед началом проведения
РезультатЗапросаПоАвтомобилям = Неопределено;
Заказ                         = Неопределено;

#КонецОбласти

#КонецЕсли