// Модуль набора записей регистра накоплений "Бюджет доходов и расходов"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ОписаниеПеременных

Перем ПодразделениеКомпании Экспорт; // Подразделение компании
Перем ДокументОбъект Экспорт;        // Документ объект (Регистратор)
Перем РежимПроведения Экспорт;		 // Режим проведения документа оперативный/неоперативный

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция выполняющая приход
Функция Приход() Экспорт
	Результат=Истина;
	
	// Получим документ, по которому будем делать записи в регистр
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	&ДатаПланирования КАК Период,
	|	РасходыИДоходы.Ссылка.СценарийПланирования,
	|	РасходыИДоходы.Ссылка.ПодразделениеКомпании,
	|	РасходыИДоходы.СтатьяРасходовИДоходов КАК СтатьяДоходовИРасходов,
	|	РасходыИДоходы.СуммаДоход КАК СуммаДоходУпр,
	|	РасходыИДоходы.СуммаДоход * &Коэффициент КАК СуммаДоходРег,
	|	NULL КАК СуммаРасходУпр,
	|	NULL КАК СуммаРасходРег
	|ИЗ
	|	Документ.БюджетДоходовИРасходов.РасходыИДоходы КАК РасходыИДоходы
	|ГДЕ
	|	РасходыИДоходы.Ссылка = &Ссылка
	|	И РасходыИДоходы.СуммаДоход > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаПланирования,
	|	РасходыИДоходы.Ссылка.СценарийПланирования,
	|	РасходыИДоходы.Ссылка.ПодразделениеКомпании,
	|	РасходыИДоходы.СтатьяРасходовИДоходов,
	|	NULL,
	|	NULL,
	|	РасходыИДоходы.СуммаРасход,
	|	РасходыИДоходы.СуммаРасход * &Коэффициент
	|ИЗ
	|	Документ.БюджетДоходовИРасходов.РасходыИДоходы КАК РасходыИДоходы
	|ГДЕ
	|	РасходыИДоходы.Ссылка = &Ссылка
	|	И РасходыИДоходы.СуммаРасход > 0";
	
	// Найдем коэффициент пересчета из управленческой валюты в валюту регл.учета		
	Коэффициент = ПланированиеСервер.ПолучитьКоэффициентПересчетаВалют(ДокументОбъект.Дата);
	
	Запрос.УстановитьПараметр("Ссылка",           ДокументОбъект);	
	Запрос.УстановитьПараметр("Коэффициент",      Коэффициент);
	Запрос.УстановитьПараметр("ДатаПланирования", ДокументОбъект.ДатаПланирования);
    	
	Загрузить(Запрос.Выполнить().Выгрузить());
		
	ДокументОбъект=Неопределено;	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли