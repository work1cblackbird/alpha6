// Модуль набора записей регистра накоплений "Бюджет продаж"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ПодразделениеКомпании Экспорт; // Подразделение компании
Перем ДокументОбъект Экспорт;        // Документ объект (Регистратор)
Перем РежимПроведения Экспорт;		 // Режим проведения документа оперативный/неоперативный

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция выполняющая приход
Функция Приход() Экспорт
	Результат=Истина;
	
	// Получим документ, по которому будем делать записи в регистр
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	БюджетПродажТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	БюджетПродажТовары.Ссылка.СценарийПланирования КАК СценарийПланирования,
	|	БюджетПродажТовары.Ссылка.ХозОперация КАК ХозОперация,
	|	БюджетПродажТовары.Номенклатура КАК Номенклатура,
	|	БюджетПродажТовары.Количество КАК Количество,
	|	БюджетПродажТовары.СуммаВсегоУпр КАК СуммаУпр,
	|	БюджетПродажТовары.СуммаВсегоУпр * &Коэффициент КАК СуммаРег,
	|	БюджетПродажТовары.СебестоимостьУпр КАК СебестоимостьУпр,
	|	БюджетПродажТовары.СуммаНДС КАК СуммаНДС,
	|	БюджетПродажТовары.Ссылка.ДатаПланирования КАК Период
	|ИЗ
	|	Документ.БюджетПродаж.Товары КАК БюджетПродажТовары
	|ГДЕ
	|	БюджетПродажТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БюджетПродажУслуги.Ссылка.ПодразделениеКомпании,
	|	БюджетПродажУслуги.Ссылка.СценарийПланирования,
	|	БюджетПродажУслуги.Ссылка.ХозОперация,
	|	БюджетПродажУслуги.Номенклатура,
	|	БюджетПродажУслуги.Количество,
	|	БюджетПродажУслуги.СуммаВсегоУпр,
	|	БюджетПродажУслуги.СуммаВсегоУпр * &Коэффициент,
	|	0,
	|	БюджетПродажУслуги.СуммаНДС,
	|	БюджетПродажУслуги.Ссылка.ДатаПланирования
	|ИЗ
	|	Документ.БюджетПродаж.Услуги КАК БюджетПродажУслуги
	|ГДЕ
	|	БюджетПродажУслуги.Ссылка = &Ссылка";
	
	// Найдем коэффициент пересчета из управленческой валюты в валюту регл.учета
	УпрВал = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ДокументОбъект.Дата);
	РегВал = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), ДокументОбъект.Дата);  	
	Коэффициент = (УпрВал.Курс/УпрВал.Кратность) / (РегВал.Курс/РегВал.Кратность);
	
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект);	
	Запрос.УстановитьПараметр("Коэффициент",Коэффициент);	

	Загрузить(Запрос.Выполнить().Выгрузить());
		
	ДокументОбъект=Неопределено;	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли