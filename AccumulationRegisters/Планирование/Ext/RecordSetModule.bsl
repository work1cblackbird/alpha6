// Модуль набора записей регистра накоплений "Планирование"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ОписаниеПеременных

Перем Дата Экспорт, 
ДокументОбъект Экспорт, 
ВидПлана Экспорт, 
ДатаНачала Экспорт, 
ДатаКонца Экспорт,
ПодразделениеКомпании Экспорт;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Проведение по регистрам документа с видом операции "План"
Процедура План() Экспорт
	ТЧПоказатели=ПолучитьТаблицуПоказатели();
	
	Для Каждого Показатель Из ТЧПоказатели Цикл
		НовоеДвижение=Добавить();
		
		// Стандартные атрибуты
		НовоеДвижение.Период=Дата;
		НовоеДвижение.Регистратор=ДокументОбъект;
		
		// Атрибуты шапки
		НовоеДвижение.ВидПлана=ВидПлана;
		НовоеДвижение.ДатаНачала=ДатаНачала;
		НовоеДвижение.ДатаКонца=ДатаКонца;
		НовоеДвижение.ПодразделениеКомпании=ПодразделениеКомпании;
		
		// Атрибуты ТЧ "Показатели"
		НовоеДвижение.ВидАналитикиПланирования1=Показатель.ВидАналитикиПланирования1;
		НовоеДвижение.ВидАналитикиПланирования2=Показатель.ВидАналитикиПланирования2;
		НовоеДвижение.ПоказательПлана1=Показатель.ПланФакт1;
		НовоеДвижение.ПоказательПлана2=Показатель.ПланФакт2;
		НовоеДвижение.ПоказательПлана3=Показатель.ПланФакт3;
	КонецЦикла;
	
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
КонецПроцедуры

// Проведение по регистрам документа с видом операции "Факт"
Процедура Факт() Экспорт
	
	ТЧПоказатели=ПолучитьТаблицуПоказатели();
	
	Для Каждого Показатель Из ТЧПоказатели Цикл
		НовоеДвижение=Добавить();
		
		// Стандартные атрибуты
		НовоеДвижение.Период=Дата;
		НовоеДвижение.Регистратор=ДокументОбъект;
		
		// Атрибуты шапки
		НовоеДвижение.ВидПлана=ВидПлана;
		НовоеДвижение.ДатаНачала=ДатаНачала;
		НовоеДвижение.ДатаКонца=ДатаКонца;
		НовоеДвижение.ПодразделениеКомпании=ПодразделениеКомпании;
		
		// Атрибуты ТЧ "Показатели"
		НовоеДвижение.ВидАналитикиПланирования1=Показатель.ВидАналитикиПланирования1;
		НовоеДвижение.ВидАналитикиПланирования2=Показатель.ВидАналитикиПланирования2;
		НовоеДвижение.ПоказательФакта1=Показатель.ПланФакт1;
		НовоеДвижение.ПоказательФакта2=Показатель.ПланФакт2;
		НовоеДвижение.ПоказательФакта3=Показатель.ПланФакт3;
	КонецЦикла;
	
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает результат запроса по ТЧ "Показатели"
Функция ПолучитьТаблицуПоказатели()
	Запрос=Новый Запрос();
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокументПоказатели.ВидАналитикиПланирования1 КАК ВидАналитикиПланирования1,
	|	ДокументПоказатели.ВидАналитикиПланирования2 КАК ВидАналитикиПланирования2,
	|	ДокументПоказатели.ПоказательПлана1 КАК ПланФакт1,
	|	ДокументПоказатели.ПоказательПлана2 КАК ПланФакт2,
	|	ДокументПоказатели.ПоказательПлана3 КАК ПланФакт3				
	|ИЗ
	|	Документ.ПланФакт.Показатели КАК ДокументПоказатели
	|ГДЕ
	|	ДокументПоказатели.Ссылка=&Ссылка	
	|";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Запрос.Текст=ТекстЗапроса;
	Возврат Запрос.Выполнить().Выгрузить();		
КонецФункции

#КонецОбласти

#КонецЕсли