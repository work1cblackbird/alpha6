// Модуль менеджера регистра накоплений "Доходы и расходы"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗаполнитьСуммовыеРазницы(
		Объект,
		СуммовыеРазницы,
		ДоходыИРасходы,
		ПодразделениеКомпании = Неопределено,
		Знач СтатьяДоходовИРасходов = Неопределено) Экспорт
	
	Если СуммовыеРазницы.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если СтатьяДоходовИРасходов = Неопределено Тогда
		
		СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		
	КонецЕсли;
	
	// БАЛАНС: Подразделение банковского счета и подразделения договоров из табличной части могут
	// находится в разных балансовых "ветках". Поэтому, если в системе ведется баланс по подразделениям, то возможен
	// "разрыв" баланса. Следовательно, придется создавать корректирующие движения по регистру "Доходы и расходы".
	БалансВедетсяПоПодразделениям = БалансВедетсяПоПодразделениям(Объект.Дата);
	
	// Создаем движения по суммовым разницам
	СуммовыеРазницы.Свернуть("Подразделение", "СуммаУпр");
	
	Для Каждого ТекСтрока Из СуммовыеРазницы Цикл
		
		НоваяЗапись = ДоходыИРасходы.Добавить();
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период = Объект.Дата;
		НоваяЗапись.Регистратор = Объект.Ссылка;
		Если БалансВедетсяПоПодразделениям Тогда
			НоваяЗапись.ПодразделениеКомпании = ТекСтрока.Подразделение;
		Иначе
			НоваяЗапись.ПодразделениеКомпании = ПодразделениеКомпании;
		КонецЕсли;
		НоваяЗапись.СтатьяДоходовИРасходов = СтатьяДоходовИРасходов;
		
		Если ТекСтрока.СуммаУпр < 0 Тогда
			НоваяЗапись.РасходУпр = -ТекСтрока.СуммаУпр;
		Иначе
			НоваяЗапись.ДоходУпр  = ТекСтрока.СуммаУпр;
		КонецЕсли;
		
		НоваяЗапись.ХозОперация = Объект.ХозОперация;
		НоваяЗапись.Активность = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли

