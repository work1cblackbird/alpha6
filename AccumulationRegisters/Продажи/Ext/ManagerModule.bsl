// Модуль менеджера регистра накоплений "Продажи"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИБ

// Обработчик обновления на версию 6.1.03.20
//
// Перебираются все записи регистра Продажи с отбором по регистратору. В качестве регистратора
// выступают документы "Реализация товаров", "Корректировка реализации товаров", "Корректировка движений" и "Перенос истории".
// Затем устанавливается значение Неопределено для реквизита "СкладКомпании", если выполняются условия:
//  * Значение Вида номенклатуры у реквизита "Номенклатура" равно Услуга;
//  * Заполнено значение для реквизита "СкладКомпани" и Тип значения равен Типу "СправочникСсылка.СкладыКомпании"
//
Процедура ОчиститьСкладыДляЗаписейУслуг(Параметры) Экспорт
	
	РазмерПорции = 1000;
	ДокументыТребующиеОбработкуДвижений = ДокументыТребующиеОчисткуСкладаДляДвиженийУслуг(РазмерПорции);
		
	Если ДокументыТребующиеОбработкуДвижений.Количество() = 0 Тогда
		
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
		
	КонецЕсли;
	
	ВидНоменклатурыУслуга = Перечисления.ВидыНоменклатуры.Услуга;
	ТипСкладыКомпании = Тип("СправочникСсылка.СкладыКомпании");
	
	// Обойдем регистраторы
	Для Каждого ДокументТребующийОбработки Из ДокументыТребующиеОбработкуДвижений Цикл
	
		ОчиститьСкладыДляУслуг(ДокументТребующийОбработки, ВидНоменклатурыУслуга, ТипСкладыКомпании);
	
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
КонецПроцедуры

// Функция для проверки объектов при открытии форм и перед записью.
// Предназначен для совместной работы обработчика обновления РегистрыНакопления.Продажи.ОчиститьСкладыДляЗаписейУслуг.
//
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным.
//
// Возвращаемое значение:
//  Булево - Истина, если объект обновлен и доступен для изменения.
//
Функция ВыполненаОчисткаСкладовДляЗаписейУслуг(Параметры) Экспорт
	
	Возврат Параметры.ЭтоНовый ИЛИ УДокументаОтсутсвуютСкладыКомпанииДляУслугВДвиженияхПоПродажам(Параметры.Отбор);
	
КонецФункции
	
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновлений

Функция ДокументыТребующиеОчисткуСкладаДляДвиженийУслуг(РазмерПорции)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Продажи.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.Продажи КАК Продажи
		|ГДЕ
		|	(Продажи.Регистратор ССЫЛКА Документ.РеализацияТоваров
		|			ИЛИ Продажи.Регистратор ССЫЛКА Документ.КорректировкаРеализации
		|			ИЛИ Продажи.Регистратор ССЫЛКА Документ.КорректировкаДвижений
		|			ИЛИ Продажи.Регистратор ССЫЛКА Документ.ПереносИстории)
		|	И Продажи.СкладКомпании ССЫЛКА Справочник.СкладыКомпании
		|	И Продажи.СкладКомпании <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|	И Продажи.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|	И Продажи.Активность = ИСТИНА";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ РАЗЛИЧНЫЕ", "ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ " + Формат(РазмерПорции, "ЧРГ=' '; ЧГ=0"));
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Новый Массив;
		
	КонецЕсли;
	
	Результат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	Возврат Результат;
	
КонецФункции

Процедура ОчиститьСкладыДляУслуг(Регистратор, ВидНоменклатурыУслуга, ТипСкладыКомпании)
	
	// Выполним изменение набора записей для регистратора
	НаборЗаписей = РегистрыНакопления.Продажи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Значение = Регистратор;
	НаборЗаписей.Прочитать();
	
	ВидыНоменклатурыЗаписей = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(НаборЗаписей.ВыгрузитьКолонку("Номенклатура"), "ВидНоменклатуры");
	
	Для Каждого Запись Из НаборЗаписей Цикл
		
		// Выбираем только услуги
		Если НЕ ВидыНоменклатурыЗаписей[Запись.Номенклатура] = ВидНоменклатурыУслуга Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		// Проверяем на заполненность склада
		Если ЗначениеЗаполнено(Запись.СкладКомпании)
			И ТипЗнч(Запись.СкладКомпании) = ТипСкладыКомпании Тогда
			
			Запись.СкладКомпании = Неопределено;
			
		КонецЕсли;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция УДокументаОтсутсвуютСкладыКомпанииДляУслугВДвиженияхПоПродажам(Документ)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Продажи.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.Продажи КАК Продажи
		|ГДЕ
		|	(Продажи.Регистратор ССЫЛКА Документ.РеализацияТоваров
		|			ИЛИ Продажи.Регистратор ССЫЛКА Документ.КорректировкаРеализации
		|			ИЛИ Продажи.Регистратор ССЫЛКА Документ.КорректировкаДвижений
		|			ИЛИ Продажи.Регистратор ССЫЛКА Документ.ПереносИстории)
		|	И Продажи.СкладКомпании ССЫЛКА Справочник.СкладыКомпании
		|	И Продажи.СкладКомпании <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|	И Продажи.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|	И Продажи.Активность = ИСТИНА
		|	И Продажи.Регистратор = &Регистратор";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Регистратор", Документ);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли