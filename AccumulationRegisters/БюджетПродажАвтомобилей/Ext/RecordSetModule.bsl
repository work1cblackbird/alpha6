// Модуль набора записей регистра накоплений "Бюджет продаж автомобилей"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ПодразделениеКомпании Экспорт; // Ссылка на подразделение компании
Перем ДокументОбъект Экспорт;        // документ объект выполняющий движения
Перем РежимПроведения Экспорт;		 // Режим проведения документа оперативный/неоперативный

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция выполняет приход
Функция Приход() Экспорт
	Результат = Истина;
	
	// Получим документ, по которому будем делать записи в регистр
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	БюджетПродажАвтомобилейАвтомобили.Ссылка.ПодразделениеКомпании,
	|	БюджетПродажАвтомобилейАвтомобили.Ссылка.СценарийПланирования,
	|	БюджетПродажАвтомобилейАвтомобили.Ссылка.ХозОперация,
	|	БюджетПродажАвтомобилейАвтомобили.Модель,
	|	БюджетПродажАвтомобилейАвтомобили.ВариантКомплектации,
	|	БюджетПродажАвтомобилейАвтомобили.Количество,
	|	БюджетПродажАвтомобилейАвтомобили.СуммаВсегоУпр КАК СуммаУпр,
	|	БюджетПродажАвтомобилейАвтомобили.СуммаВсегоУпр * &Коэффициент КАК СуммаРег,
	|	БюджетПродажАвтомобилейАвтомобили.СебестоимостьУпр КАК СебестоимостьУпр,
	|	БюджетПродажАвтомобилейАвтомобили.СуммаНДС КАК СуммаНДС,
	|	БюджетПродажАвтомобилейАвтомобили.Ссылка.ДатаПланирования КАК Период
	|ИЗ
	|	Документ.БюджетПродажАвтомобилей.Автомобили КАК БюджетПродажАвтомобилейАвтомобили
	|ГДЕ                                                                      
	|	БюджетПродажАвтомобилейАвтомобили.Ссылка = &Ссылка";
	
	// Найдем коэффициент пересчета из управленческой валюты в валюту регл.учета
	УпрВал = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ДокументОбъект.Дата);
	РегВал = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), ДокументОбъект.Дата);	
	Коэффициент = (УпрВал.Курс/УпрВал.Кратность)/(РегВал.Курс/РегВал.Кратность);
	
	Запрос.УстановитьПараметр("Ссылка"      , ДокументОбъект);
	Запрос.УстановитьПараметр("Коэффициент" , Коэффициент);

	Загрузить(Запрос.Выполнить().Выгрузить());
		
	// почистим циклические ссылки
	ДокументОбъект = Неопределено;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли