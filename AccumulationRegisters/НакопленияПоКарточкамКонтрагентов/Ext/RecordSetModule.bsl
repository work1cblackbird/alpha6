// Модуль набора записей регистра накоплений "Накопления по карточкам контрагентов"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ОписаниеПеременных

Перем ДокументСсылка Экспорт; // Ссылка на документ.
Перем РезультатЗапроса Экспорт; // Результат запроса.

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ВыполнитьДвижения() Экспорт
	Если НЕ ЗначениеЗаполнено(ДокументСсылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗапроса) = Тип("РезультатЗапроса") Тогда
		РезультатЗапроса = РезультатЗапроса.Выгрузить();
	КонецЕсли;
	
	ВалютаНакоплений = Константы.ВалютаНакопительныхСумм.Получить();
	
	Для Каждого Строка Из РезультатЗапроса Цикл
		
		Если Строка.Сумма = 0 И Строка.КоличествоНоменклатуры = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = Добавить();
		
		НоваяЗапись.Период      = Строка.ПериодНакопления;
		НоваяЗапись.Регистратор = ДокументСсылка;
		
		НоваяЗапись.Карточка              = Строка.Карточка;
		НоваяЗапись.Контрагент            = Строка.Контрагент;
		НоваяЗапись.ПодразделениеКомпании = ДокументСсылка.ПодразделениеКомпании;
		
		НоваяЗапись.Сумма = РаботаСКурсамиВалютПлатформа.ПересчетПоВалюте(Строка.Сумма,
																 ДокументСсылка.ВалютаДокумента,
																 ДокументСсылка.КурсДокумента,
																 ВалютаНакоплений,
																 Строка.ПериодНакопления);
																 
		НоваяЗапись.КоличествоНоменклатуры = Строка.КоличествоНоменклатуры;
		НоваяЗапись.КоличествоЧеков        = Строка.КоличествоЧеков;
		
		НоваяЗапись.ХозОперация = ДокументСсылка.ХозОперация;
	КонецЦикла;
	
	Записать(Истина);
КонецФункции

#КонецОбласти

#КонецЕсли