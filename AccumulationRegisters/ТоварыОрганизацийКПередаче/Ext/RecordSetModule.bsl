// Модуль набора записей регистра накоплений "Товары организаций к передаче"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОписаниеПеременных

Перем ДокументОбъект Экспорт;
Перем СкладКомпании Экспорт;
Перем ОрганизацияОтправитель Экспорт;
Перем ОрганизацияПолучатель Экспорт;
Перем ШапкаДокумента Экспорт; // Выборка или строка таблицы значений, в которой содержаться необходимые данные о шапке документа
Перем ТаблицаДвиженийПартииТоваровКомпании Экспорт;
Перем ТаблицаДвиженийГТДПартийТоваровКомпании Экспорт;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует движения по регистру приход
// 
// Возвращаемое значение:
//  Булево - Истина - все ОК, Ложь - ошибка.
Функция Приход() Экспорт
	
	ВсеОК = Истина;
	
	ПериодДвижения = ШапкаДокумента.Дата;
	
	ТаблицаПартийПоИнтеркампани = ТаблицаДвиженийПартииТоваровКомпании.Скопировать(Новый Структура("ЗапасыДругойОрганизацииПоИнтеркампани", Истина));
	
	КолонкиГруппировок	= "СкладКомпании,Организация,Номенклатура,ХарактеристикаНоменклатуры,СтатусПартии,Партия,СтавкаНДС";
	КолонкиСуммирования	= "Количество,Сумма,СуммаНДС,СуммаБезНДС,СуммаУпр,СуммаНДСУпр,СуммаБезНДСУпр"; 
	
	ТаблицаПартийПоИнтеркампани.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
	
	ТаблицаГТДпоИнтеркампани = ТаблицаДвиженийГТДПартийТоваровКомпании.Скопировать(Новый Структура("ЗапасыДругойОрганизацииПоИнтеркампани", Истина));
	
	// Перебираем строки товаров
	Для каждого СтрокаТоваров Из ТаблицаПартийПоИнтеркампани Цикл
		СуммаПоПартии = 0;
		СуммаНДСПоПартии = 0;
		СуммаБезНДСПоПартии = 0;
		СуммаУпрПоПартии = 0;
		СуммаНДСУпрПоПартии = 0;
		СуммаБезНДСУпрПоПартии = 0;
		Количество = СтрокаТоваров.Количество;

		СтруктураОтбора = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Партия", СтрокаТоваров.Номенклатура, СтрокаТоваров.ХарактеристикаНоменклатуры, СтрокаТоваров.Партия);
		МассивНайденныхСтрокГТД = ТаблицаГТДпоИнтеркампани.НайтиСтроки(СтруктураОтбора);
		
		Для СчГТД = 0 По МассивНайденныхСтрокГТД.ВГраница() Цикл
			
			// Создаем запись регистра партий товаров в производстве
			ТекСтрокаГТД = МассивНайденныхСтрокГТД[СчГТД];
			НоваяЗапись = Добавить();
			НоваяЗапись.ВидДвижения					= ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период						= ПериодДвижения;
			НоваяЗапись.Регистратор					= ШапкаДокумента.Ссылка;
			НоваяЗапись.СкладКомпании				= СтрокаТоваров.СкладКомпании;
			НоваяЗапись.ОрганизацияОтправитель		= СтрокаТоваров.Организация;
			НоваяЗапись.ОрганизацияПолучатель		= ОрганизацияПолучатель;
			НоваяЗапись.Номенклатура				= ТекСтрокаГТД.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры	= ТекСтрокаГТД.ХарактеристикаНоменклатуры;
			НоваяЗапись.СтатусПартии				= СтрокаТоваров.СтатусПартии;
			НоваяЗапись.Партия						= ТекСтрокаГТД.Партия;
			НоваяЗапись.ГТД							= ТекСтрокаГТД.ГТД;
			НоваяЗапись.ХозОперация					= ШапкаДокумента.ХозОперация;
			НоваяЗапись.СтавкаНДС 					= СтрокаТоваров.СтавкаНДС;
			НоваяЗапись.Количество = Мин(ТекСтрокаГТД.Количество, Количество);
			КолВо = НоваяЗапись.Количество / СтрокаТоваров.Количество;
			
			// Пересчитаем сумму товара в валюту управленческого учета
			НоваяЗапись.Сумма	 = Окр(СтрокаТоваров.Сумма * КолВо, 2);
			НоваяЗапись.СуммаНДС = Окр(СтрокаТоваров.СуммаНДС * КолВо, 2);
			СуммаУпр = СтрокаТоваров.СуммаУпр;
			СуммаУпр = СуммаУпр * КолВо;
			НоваяЗапись.СуммаУпр = Окр(СуммаУпр, 2);
			СуммаНДСУпр = СтрокаТоваров.СуммаНДСУпр;
			СуммаНДСУпр = СуммаНДСУпр * КолВо;
			НоваяЗапись.СуммаНДСУпр = Окр(СуммаНДСУпр, 2);
			СуммаБезНДС = СтрокаТоваров.СуммаБезНДС;
			СуммаБезНДС = СуммаБезНДС * КолВо;
			НоваяЗапись.СуммаБезНДС = Окр(СуммаБезНДС, 2);
			СуммаБезНДСУпр = СтрокаТоваров.СуммаБезНДСУпр;
			СуммаБезНДСУпр = СуммаБезНДСУпр * КолВо;
			НоваяЗапись.СуммаБезНДСУпр	= Окр(СуммаБезНДСУпр, 2);
			
			ТекСтрокаГТД.Количество		= ТекСтрокаГТД.Количество - НоваяЗапись.Количество;
			Количество					= Количество - НоваяЗапись.Количество;
			СуммаПоПартии				= СуммаПоПартии + НоваяЗапись.Сумма;
			СуммаНДСПоПартии			= СуммаНДСПоПартии + НоваяЗапись.СуммаНДС;
			СуммаБезНДСПоПартии			= СуммаБезНДСПоПартии + НоваяЗапись.СуммаБезНДС;
			СуммаУпрПоПартии			= СуммаУпрПоПартии + НоваяЗапись.СуммаУпр;
			СуммаНДСУпрПоПартии			= СуммаНДСУпрПоПартии + НоваяЗапись.СуммаНДСУпр;
			СуммаБезНДСУпрПоПартии		= СуммаБезНДСУпрПоПартии + НоваяЗапись.СуммаБезНДСУпр;
			
			Если ТекСтрокаГТД.Количество = 0 Тогда
				ТаблицаГТДпоИнтеркампани.Удалить(ТекСтрокаГТД);
			КонецЕсли;
			
			Если Количество = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Количество > 0 Тогда
			
			НоваяЗапись = Добавить();
			НоваяЗапись.ВидДвижения					= ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период						= ПериодДвижения;
			НоваяЗапись.Регистратор					= ШапкаДокумента.Ссылка;
			НоваяЗапись.СкладКомпании				= СтрокаТоваров.СкладКомпании;
			НоваяЗапись.ОрганизацияОтправитель		= СтрокаТоваров.Организация;
			НоваяЗапись.ОрганизацияПолучатель		= ОрганизацияПолучатель;
			НоваяЗапись.Номенклатура				= СтрокаТоваров.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры	= СтрокаТоваров.ХарактеристикаНоменклатуры;
			НоваяЗапись.СтатусПартии				= СтрокаТоваров.СтатусПартии;
			НоваяЗапись.Партия						= СтрокаТоваров.Партия;
			НоваяЗапись.ГТД							= Справочники.ГТД.ПустаяСсылка();
			НоваяЗапись.ХозОперация					= ШапкаДокумента.ХозОперация;
			НоваяЗапись.СтавкаНДС 					= СтрокаТоваров.СтавкаНДС;
			НоваяЗапись.Количество = Количество;
			КолВо = НоваяЗапись.Количество / СтрокаТоваров.Количество;
			
			// Пересчитаем сумму товара в валюту управленческого учета
			НоваяЗапись.Сумма	 = Окр(СтрокаТоваров.Сумма * КолВо, 2);
			НоваяЗапись.СуммаНДС = Окр(СтрокаТоваров.СуммаНДС * КолВо, 2);
			СуммаУпр = СтрокаТоваров.СуммаУпр;
			СуммаУпр = СуммаУпр * КолВо;
			НоваяЗапись.СуммаУпр = Окр(СуммаУпр, 2);
			СуммаНДСУпр = СтрокаТоваров.СуммаНДСУпр;
			СуммаНДСУпр = СуммаНДСУпр * КолВо;
			НоваяЗапись.СуммаНДСУпр = Окр(СуммаНДСУпр, 2);

			СуммаБезНДС = СтрокаТоваров.СуммаБезНДС;
			СуммаБезНДС = СуммаБезНДС * КолВо;
			НоваяЗапись.СуммаБезНДС = Окр(СуммаБезНДС, 2);
			СуммаБезНДСУпр = СтрокаТоваров.СуммаБезНДСУпр;
			СуммаБезНДСУпр = СуммаБезНДСУпр * КолВо;
			НоваяЗапись.СуммаБезНДСУпр	= Окр(СуммаБезНДСУпр, 2);
			
			СуммаПоПартии				= СуммаПоПартии + НоваяЗапись.Сумма;
			СуммаНДСПоПартии			= СуммаНДСПоПартии + НоваяЗапись.СуммаНДС;
			СуммаБезНДСПоПартии			= СуммаБезНДСПоПартии + НоваяЗапись.СуммаБезНДС;
			СуммаУпрПоПартии			= СуммаУпрПоПартии + НоваяЗапись.СуммаУпр;
			СуммаНДСУпрПоПартии			= СуммаНДСУпрПоПартии + НоваяЗапись.СуммаНДСУпр;
			СуммаБезНДСУпрПоПартии		= СуммаБезНДСУпрПоПартии + НоваяЗапись.СуммаБезНДСУпр;
			
		КонецЕсли;
		
		НоваяЗапись.Сумма			= НоваяЗапись.Сумма + (СтрокаТоваров.Сумма - СуммаПоПартии);
		НоваяЗапись.СуммаНДС		= НоваяЗапись.СуммаНДС + (СтрокаТоваров.СуммаНДС - СуммаНДСПоПартии);
		НоваяЗапись.СуммаБезНДС 	= НоваяЗапись.СуммаБезНДС + (СтрокаТоваров.СуммаБезНДС - СуммаБезНДСПоПартии);
		НоваяЗапись.СуммаУпр 		= НоваяЗапись.СуммаУпр + (СтрокаТоваров.СуммаУпр - СуммаУпрПоПартии);
		НоваяЗапись.СуммаНДСУпр 	= НоваяЗапись.СуммаНДСУпр + (СтрокаТоваров.СуммаНДСУпр - СуммаНДСУпрПоПартии);
		НоваяЗапись.СуммаБезНДСУпр 	= НоваяЗапись.СуммаБезНДСУпр + (СтрокаТоваров.СуммаБезНДСУпр - СуммаБезНДСУпрПоПартии);
		
	КонецЦикла; 
	
	// Запись движений
	Если ВсеОК Тогда
		Записать();
	КонецЕсли;
	
	ДокументОбъект = Неопределено;
	ШапкаДокумента = Неопределено;
	
	Возврат ВсеОК;

КонецФункции

// Формирует движения по регистру расход
// 
// Возвращаемое значение:
//  Булево - Истина - все ОК, Ложь - ошибка.
Функция Расход() Экспорт
	
	ВсеОК = Истина;
	
	НачалоПериода 	= НачалоДня(ДокументОбъект.НачалоПериода);
	КонецПериода 	= КонецДня(ДокументОбъект.КонецПериода);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("ОрганизацияОтправитель", ОрганизацияОтправитель);
	Запрос.УстановитьПараметр("ОрганизацияПолучатель", ОрганизацияПолучатель);
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	
	Запрос.Текст = ТекстЗапросаОстатковДляПередачаТоваровМеждуОрганизациями();
	
	// Наложим блокировку на считываемые данные
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(НачалоПериода, КонецПериода)); 
	ЗначенияБлокировки.Вставить("ОрганизацияОтправитель", ОрганизацияОтправитель);
	ЗначенияБлокировки.Вставить("ОрганизацияПолучатель", ОрганизацияПолучатель);
	ЗначенияБлокировки.Вставить("СкладКомпании", СкладКомпании);
	
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ТоварыОрганизацийКПередаче");
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ДокументОбъект.Товары);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	
	ОбработкаСобытийДокументаСервер.УстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	ТоварыБезПартий = МассивРезультатовЗапроса[3].Выгрузить();
	
	ФорматЧисла = "ЧДЦ=3; ЧН=0,00";
	
	// Выполним контроль остатков
	Для Каждого Строка Из ТоварыБезПартий Цикл
		
		НадоСписать = Строка.КоличествоБазовоеПоДокументу;
		
		Превышение = НадоСписать - Строка.Количество;
		Если Превышение > 0 Тогда
			
			ЗначениеКолонкиКода = УправлениеПечатьюПлатформа.ПолучитьЗначениеКолонкиКода(Строка.Номенклатура);
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = '[%1] Товар <%2>%3%4. Требуется к передаче %5. По документу %6. Превышение %7'"),
				ЗначениеКолонкиКода,
				СокрЛП(Строка.Номенклатура),
				?(ЗначениеЗаполнено(Строка.ХарактеристикаНоменклатуры), " с характеристикой <" + СокрЛП(Строка.ХарактеристикаНоменклатуры) + ">", ""),
				?(ЗначениеЗаполнено(Строка.ГТД), " с ГТД/РНПТ <" + СокрЛП(Строка.ГТД) + ">", " без ГТД/РНПТ"),
				Формат(Строка.Количество, ФорматЧисла),
				Формат(НадоСписать, ФорматЧисла),
				Формат(Превышение, ФорматЧисла)
			);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ДокументОбъект, , , Истина);
			
			ВсеОК = Ложь;
			
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Если НЕ ВсеОК Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ВыборкаИдентификаторТовара = МассивРезультатовЗапроса[4].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Выполним движения
	Пока ВыборкаИдентификаторТовара.Следующий() Цикл
		
		НадоСписать = ВыборкаИдентификаторТовара.КоличествоБазовоеПоДокументу;
	
		Выборка = ВыборкаИдентификаторТовара.Выбрать();

		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Количество > НадоСписать Тогда
				КоличествоСписания		= НадоСписать;
				СуммаСписания			= Окр(Выборка.Сумма / Выборка.Количество * НадоСписать, 2);
				СуммаНДССписания		= Окр(Выборка.СуммаНДС / Выборка.Количество * НадоСписать, 2);
				СуммаБезНДССписания		= СуммаСписания - СуммаНДССписания;
				СуммаУпрСписания		= Окр(Выборка.СуммаУпр / Выборка.Количество * НадоСписать, 2);
				СуммаНДСУпрСписания		= Окр(Выборка.СуммаНДСУпр / Выборка.Количество * НадоСписать, 2);
				СуммаБезНДСУпрСписания	= СуммаУпрСписания - СуммаНДСУпрСписания;
			Иначе
				КоличествоСписания		= Выборка.Количество;
				СуммаСписания			= Окр(Выборка.Сумма, 2);
				СуммаНДССписания		= Окр(Выборка.СуммаНДС, 2);
				СуммаБезНДССписания		= Окр(Выборка.СуммаБезНДС, 2);
				СуммаУпрСписания		= Окр(Выборка.СуммаУпр, 2);
				СуммаНДСУпрСписания		= Окр(Выборка.СуммаНДСУпр, 2);
				СуммаБезНДСУпрСписания	= Окр(Выборка.СуммаБезНДСУпр, 2);
			КонецЕсли;
			
			НоваяЗапись = Добавить();
			
			// Изменения
			НоваяЗапись.ВидДвижения 				= ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период 						= Выборка.ПериодДень;
			НоваяЗапись.Регистратор 				= ДокументОбъект.Ссылка;
			НоваяЗапись.ОрганизацияОтправитель 		= ОрганизацияОтправитель;
			НоваяЗапись.ОрганизацияПолучатель 		= ОрганизацияПолучатель;
			НоваяЗапись.СкладКомпании 				= СкладКомпании;
			НоваяЗапись.Номенклатура				= Выборка.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры 	= Выборка.ХарактеристикаНоменклатуры;
			НоваяЗапись.СтатусПартии				= Выборка.СтатусПартии;
			НоваяЗапись.Партия						= Выборка.Партия;
			НоваяЗапись.ГТД							= Выборка.ГТД;
			
			// Ресурсы
			НоваяЗапись.Количество					= КоличествоСписания;
			НоваяЗапись.Сумма						= СуммаСписания;
			НоваяЗапись.СуммаНДС					= СуммаНДССписания;
			НоваяЗапись.СуммаБезНДС					= СуммаБезНДССписания;
			НоваяЗапись.СуммаУпр					= СуммаУпрСписания;
			НоваяЗапись.СуммаНДСУпр					= СуммаНДСУпрСписания;
			НоваяЗапись.СуммаБезНДСУпр				= СуммаБезНДСУпрСписания;
			
			// Реквизиты
			НоваяЗапись.ХозОперация					= ДокументОбъект.ХозОперация;
			
			НадоСписать = НадоСписать - НоваяЗапись.Количество;
			
			Если НадоСписать = 0 Тогда
				Прервать;
			КонецЕсли;
	
		КонецЦикла;
			
	КонецЦикла;
	
	Возврат ВсеОК;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаОстатковДляПередачаТоваровМеждуОрганизациями()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура КАК Номенклатура,
	|	ПередачаТоваровМеждуОрганизациямиТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПередачаТоваровМеждуОрганизациямиТовары.ГТД КАК ГТД,
	|	СУММА(ПередачаТоваровМеждуОрганизациямиТовары.КоличествоБазовое) КАК КоличествоБазовоеПоДокументу
	|ПОМЕСТИТЬ втТоварыДокумента
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ПередачаТоваровМеждуОрганизациямиТовары
	|ГДЕ
	|	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура,
	|	ПередачаТоваровМеждуОрганизациямиТовары.ХарактеристикаНоменклатуры,
	|	ПередачаТоваровМеждуОрганизациямиТовары.ГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втТоварыДокумента.Номенклатура КАК Номенклатура,
	|	втТоварыДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	втТоварыДокумента.ГТД КАК ГТД,
	|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторТовара
	|ПОМЕСТИТЬ втИдентификаторыТовара
	|ИЗ
	|	втТоварыДокумента КАК втТоварыДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ТоварыОрганизацийКПередачеОбороты.Период, ДЕНЬ) КАК ПериодДень,
	|	втТоварыДокумента.Номенклатура КАК Номенклатура,
	|	втТоварыДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТоварыОрганизацийКПередачеОбороты.ГТД КАК ГТД,
	|	ТоварыОрганизацийКПередачеОбороты.СтатусПартии КАК СтатусПартии,
	|	ТоварыОрганизацийКПередачеОбороты.Партия КАК Партия,
	|	МАКСИМУМ(втТоварыДокумента.КоличествоБазовоеПоДокументу) КАК КоличествоБазовоеПоДокументу,
	|	СУММА(ЕСТЬNULL(ТоварыОрганизацийКПередачеОбороты.КоличествоОборот, 0)) КАК Количество,
	|	СУММА(ЕСТЬNULL(ТоварыОрганизацийКПередачеОбороты.СуммаОборот, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(ТоварыОрганизацийКПередачеОбороты.СуммаНДСОборот, 0)) КАК СуммаНДС,
	|	СУММА(ЕСТЬNULL(ТоварыОрганизацийКПередачеОбороты.СуммаБезНДСОборот, 0)) КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(ТоварыОрганизацийКПередачеОбороты.СуммаУпрОборот, 0)) КАК СуммаУпр,
	|	СУММА(ЕСТЬNULL(ТоварыОрганизацийКПередачеОбороты.СуммаНДСУпрОборот, 0)) КАК СуммаНДСУпр,
	|	СУММА(ЕСТЬNULL(ТоварыОрганизацийКПередачеОбороты.СуммаБезНДСУпрОборот, 0)) КАК СуммаБезНДСУпр
	|ПОМЕСТИТЬ втДанныеРегистра
	|ИЗ
	|	втТоварыДокумента КАК втТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизацийКПередаче.Обороты(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				День,
	|				ОрганизацияОтправитель = &ОрганизацияОтправитель
	|					И ОрганизацияПолучатель = &ОрганизацияПолучатель
	|					И СкладКомпании = &СкладКомпании
	|					И (Номенклатура, ХарактеристикаНоменклатуры) В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							втТоварыДокумента.Номенклатура КАК Номенклатура,
	|							втТоварыДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|						ИЗ
	|							втТоварыДокумента КАК втТоварыДокумента)) КАК ТоварыОрганизацийКПередачеОбороты
	|		ПО втТоварыДокумента.Номенклатура = ТоварыОрганизацийКПередачеОбороты.Номенклатура
	|			И втТоварыДокумента.ХарактеристикаНоменклатуры = ТоварыОрганизацийКПередачеОбороты.ХарактеристикаНоменклатуры
	|			И втТоварыДокумента.ГТД = ТоварыОрганизацийКПередачеОбороты.ГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	втТоварыДокумента.Номенклатура,
	|	втТоварыДокумента.ХарактеристикаНоменклатуры,
	|	ТоварыОрганизацийКПередачеОбороты.ГТД,
	|	ТоварыОрганизацийКПередачеОбороты.СтатусПартии,
	|	ТоварыОрганизацийКПередачеОбороты.Партия,
	|	КОНЕЦПЕРИОДА(ТоварыОрганизацийКПередачеОбороты.Период, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеРегистра.Номенклатура КАК Номенклатура,
	|	втДанныеРегистра.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	втДанныеРегистра.ГТД КАК ГТД,
	|	МАКСИМУМ(втДанныеРегистра.КоличествоБазовоеПоДокументу) КАК КоличествоБазовоеПоДокументу,
	|	СУММА(втДанныеРегистра.Количество) КАК Количество
	|ИЗ
	|	втДанныеРегистра КАК втДанныеРегистра
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеРегистра.Номенклатура,
	|	втДанныеРегистра.ХарактеристикаНоменклатуры,
	|	втДанныеРегистра.ГТД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры УБЫВ,
	|	ГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеРегистра.ПериодДень КАК ПериодДень,
	|	втДанныеРегистра.Номенклатура КАК Номенклатура,
	|	втДанныеРегистра.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	втДанныеРегистра.ГТД КАК ГТД,
	|	втДанныеРегистра.СтатусПартии КАК СтатусПартии,
	|	втДанныеРегистра.Партия КАК Партия,
	|	втДанныеРегистра.КоличествоБазовоеПоДокументу КАК КоличествоБазовоеПоДокументу,
	|	втДанныеРегистра.Количество КАК Количество,
	|	втДанныеРегистра.Сумма КАК Сумма,
	|	втДанныеРегистра.СуммаНДС КАК СуммаНДС,
	|	втДанныеРегистра.СуммаБезНДС КАК СуммаБезНДС,
	|	втДанныеРегистра.СуммаУпр КАК СуммаУпр,
	|	втДанныеРегистра.СуммаНДСУпр КАК СуммаНДСУпр,
	|	втДанныеРегистра.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
	|	втИдентификаторыТовара.ИдентификаторТовара КАК ИдентификаторТовара
	|ИЗ
	|	втДанныеРегистра КАК втДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИдентификаторыТовара КАК втИдентификаторыТовара
	|		ПО втДанныеРегистра.Номенклатура = втИдентификаторыТовара.Номенклатура
	|			И втДанныеРегистра.ХарактеристикаНоменклатуры = втИдентификаторыТовара.ХарактеристикаНоменклатуры
	|			И втДанныеРегистра.ГТД = втИдентификаторыТовара.ГТД
	|ГДЕ
	|	втДанныеРегистра.Количество <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодДень,
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры УБЫВ,
	|	втДанныеРегистра.Партия.МоментВремени,
	|	втДанныеРегистра.ГТД УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(КоличествоБазовоеПоДокументу)
	|ПО
	|	ИдентификаторТовара";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли