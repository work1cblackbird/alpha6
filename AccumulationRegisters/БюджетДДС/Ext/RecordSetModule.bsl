// Модуль набора записей регистра накопления "Бюджет ДДС".

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ОписаниеПеременных

Перем ПодразделениеКомпании Экспорт; // Подразделение компании
Перем ДокументОбъект Экспорт;        // Документ объект
Перем РежимПроведения Экспорт;		 // Режим проведения документа оперативный/неоперативный

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция выполняющая приход
Функция Приход() Экспорт
	Результат=Истина;
	
	// Получим документ, по которому будем делать записи в регистр
	Запрос = Новый Запрос;
	Если ДокументОбъект.ИспользоватьРаздельныеГрафикиПогашения Тогда 
		Запрос.Текст =
		"ВЫБРАТЬ
		|	БюджетДДСГрафикРаспределенияДС.Ссылка.СценарийПланирования,
		|	БюджетДДСГрафикРаспределенияДС.Ссылка.ПодразделениеКомпании,
		|	БюджетДДСГрафикРаспределенияДС.СтатьяДДС КАК СтатьяДДС,
		|	БюджетДДСГрафикРаспределенияДС.СуммаПогашенияУпр КАК СуммаУпр,
		|	БюджетДДСГрафикРаспределенияДС.СуммаПогашенияУпр * &Коэффициент КАК СуммаРег,
		|	ВЫБОР
		|		КОГДА &Периодичность = ""год""
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период), ГОД)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""квартал""
		|					ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период), КВАРТАЛ)
		|				ИНАЧЕ КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период), МЕСЯЦ)
		|			КОНЕЦ
		|	КОНЕЦ КАК Период
		|ИЗ
		|	Документ.БюджетДДС.ГрафикРаспределенияДС КАК БюджетДДСГрафикРаспределенияДС
		|ГДЕ
		|	БюджетДДСГрафикРаспределенияДС.Ссылка = &Ссылка
		|	И БюджетДДСГрафикРаспределенияДС.СуммаПогашенияУпр <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтатьяДДС,
		|	Период";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	БюджетДДССтатьиДДС.Ссылка.СценарийПланирования,
		|	БюджетДДССтатьиДДС.Ссылка.ПодразделениеКомпании,
		|	БюджетДДССтатьиДДС.СтатьяДДС КАК СтатьяДДС,
		|	ВЫБОР
		|		КОГДА &Периодичность = ""год""
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период), ГОД)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""квартал""
		|					ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период), КВАРТАЛ)
		|				ИНАЧЕ КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период), МЕСЯЦ)
		|			КОНЕЦ
		|	КОНЕЦ КАК Период,
		|	БюджетДДССтатьиДДС.БазоваяСуммаУпр * БюджетДДСГрафикРаспределенияДС.Процент / 100 КАК СуммаУпр,
		|	БюджетДДССтатьиДДС.БазоваяСуммаУпр * БюджетДДСГрафикРаспределенияДС.Процент / 100 * &Коэффициент КАК СуммаРег
		|ИЗ
		|	Документ.БюджетДДС.СтатьиДДС КАК БюджетДДССтатьиДДС,
		|	Документ.БюджетДДС.ГрафикРаспределенияДС КАК БюджетДДСГрафикРаспределенияДС
		|ГДЕ
		|	БюджетДДССтатьиДДС.Ссылка = &Ссылка
		|	И БюджетДДССтатьиДДС.БазоваяСуммаУпр <> 0
		|	И БюджетДДСГрафикРаспределенияДС.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтатьяДДС,
		|	Период"; 		
		
	КонецЕсли;
	
	
	// Найдем коэффициент пересчета из управленческой валюты в валюту регл.учета
	Коэффициент = ПланированиеСервер.ПолучитьКоэффициентПересчетаВалют(ДокументОбъект.Дата);
	
	Запрос.УстановитьПараметр("Ссылка",           ДокументОбъект);	
	Запрос.УстановитьПараметр("Коэффициент",      Коэффициент);
	Запрос.УстановитьПараметр("ДатаПланирования", ДокументОбъект.ДатаПланирования);
    Запрос.УстановитьПараметр("Периодичность",    Строка(ДокументОбъект.СценарийПланирования.Периодичность));	
	
	Загрузить(Запрос.Выполнить().Выгрузить());
		
	ДокументОбъект=Неопределено;	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли