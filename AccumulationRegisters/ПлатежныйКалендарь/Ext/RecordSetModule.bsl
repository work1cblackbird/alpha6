// Модуль набора записей регистра накоплений "Платежный календарь"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ОписаниеПеременных

Перем Поступление Экспорт;           // Флаг поступления
Перем ДокументОбъект Экспорт;        // Документ объект (Регистратор)

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция План() Экспорт
	
	Результат = Истина;
	// Получим документ, в зависимости от планируемого поступления или списания ДС,
	// по которому будем делать записи в регистр.
	Запрос = Новый Запрос;
	
	ИмяРеквизитаСделка = ?(ЕстьРеквизит(ДокументОбъект, "Сделка"), "Сделка", "ДокументОснование");
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПланируемоеДвижениеДС.Ссылка.ВалютаДокумента КАК Валюта,
	               |	ПланируемоеДвижениеДС.Ссылка.СтруктурнаяЕдиница,
	               |	ПланируемоеДвижениеДС.Ссылка.ДоговорВзаиморасчетов,
	               |	ПланируемоеДвижениеДС.Ссылка.СтатьяДДС,
	               |	ПланируемоеДвижениеДС.Ссылка.Проект,
	               |	ПланируемоеДвижениеДС.ДатаПлатежа КАК Период,
	               |	ПланируемоеДвижениеДС.Ссылка." + ИмяРеквизитаСделка + " КАК Сделка,
	               |	ПланируемоеДвижениеДС.Ссылка КАК ДокументПланирования,
	               |	ПланируемоеДвижениеДС.Сумма КАК " + ?(Поступление, "Поступление", "Расход") + "
	               |ИЗ
	               |	Документ." + ДокументОбъект.Метаданные().Имя + ".Платежи КАК ПланируемоеДвижениеДС
	               |ГДЕ
	               |	ПланируемоеДвижениеДС.Ссылка = &Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",     ДокументОбъект);
	Запрос.УстановитьПараметр("ПустаяДата", Дата("00010101"));
	
	Загрузить(Запрос.Выполнить().Выгрузить());
	
	ДокументОбъект = Неопределено;
	
	Возврат Результат;
	
КонецФункции

Функция Факт() Экспорт
	
	Результат = Истина;
	
	// Получим документ, в зависимости от планируемого поступления или списания ДС,
	// по которому будем делать записи в регистр.
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПланируемоеДвижениеДС.ВалютаДокумента КАК Валюта,
	               |	ПланируемоеДвижениеДС.КассаКомпании КАК СтруктурнаяЕдиница,
	               |	ПланируемоеДвижениеДС.ДоговорВзаиморасчетов,
	               |	ПланируемоеДвижениеДС.СтатьяДДС,
	               |	ПланируемоеДвижениеДС.Проект,
	               |	ПланируемоеДвижениеДС.Дата КАК Период,
	               |	ПланируемоеДвижениеДС.Сделка КАК Сделка,
	               |	ПланируемоеДвижениеДС.ДокументПланирования КАК ДокументПланирования,
	               |	ПланируемоеДвижениеДС.СуммаДокумента КАК " + ?(Поступление, "ПоступлениеФакт", "РасходФакт") + "
	               |ИЗ
	               |	Документ." + ДокументОбъект.Метаданные().Имя + " КАК ПланируемоеДвижениеДС
	               |ГДЕ
	               |	ПланируемоеДвижениеДС.Ссылка = &Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект);
	
	Загрузить(Запрос.Выполнить().Выгрузить());
	
	ДокументОбъект=Неопределено;
	
	Возврат Результат;
	
КонецФункции

Функция ФактПеремещение() Экспорт
	
	Результат = Истина;
	
	// Получим документ, в зависимости от планируемого поступления или списания ДС,
	// по которому будем делать записи в регистр.
	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ
				   |	ПланируемоеДвижениеДС.ВалютаДокумента КАК Валюта,
				   |	ПланируемоеДвижениеДС.КассаКомпанииОтправитель КАК СтруктурнаяЕдиница,
				   |	ПланируемоеДвижениеДС.СтатьяДДС КАК СтатьяДДС,
				   |	ПланируемоеДвижениеДС.Дата КАК Период,
				   |	ПланируемоеДвижениеДС.СуммаДокумента КАК РасходФакт,
				   |	0 КАК ПоступлениеФакт
				   |ИЗ
				   |	Документ.ПеремещениеДенежныхСредств КАК ПланируемоеДвижениеДС
				   |ГДЕ
				   |	ПланируемоеДвижениеДС.Ссылка = &Ссылка
				   |
				   |ОБЪЕДИНИТЬ ВСЕ
				   |
				   |ВЫБРАТЬ
				   |	ПланируемоеДвижениеДС.ВалютаДокумента,
				   |	ПланируемоеДвижениеДС.КассаКомпанииПолучатель,
				   |	ПланируемоеДвижениеДС.СтатьяДДС КАК СтатьяДДС,
				   |	ПланируемоеДвижениеДС.Дата,
				   |	0,
				   |	ПланируемоеДвижениеДС.СуммаДокумента
				   |ИЗ
				   |	Документ.ПеремещениеДенежныхСредств КАК ПланируемоеДвижениеДС
				   |ГДЕ
				   |	ПланируемоеДвижениеДС.Ссылка = &Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект);
	
	Загрузить(Запрос.Выполнить().Выгрузить());
	
	ДокументОбъект=Неопределено;
	
	Возврат Результат;
	
КонецФункции

Функция ФактВыписка() Экспорт
	
	Результат = Истина;
	
	// Получим документ, в зависимости от планируемого поступления или списания ДС,
	// по которому будем делать записи в регистр.
	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВыпискаСостав.Ссылка.ВалютаДокумента КАК Валюта,
	               |	ВыпискаСостав.Ссылка.БанковскийСчет КАК СтруктурнаяЕдиница,
	               |	ВыпискаСостав.СтатьяДДС КАК СтатьяДДС,
	               |	ВыпискаСостав.Ссылка.Дата КАК Период,
	               |	ВыпискаСостав.СуммаПриход КАК ПоступлениеФакт,
	               |	ВыпискаСостав.СуммаРасход КАК РасходФакт,
	               |	ВыпискаСостав.ДокументПланирования,
	               |	ВыпискаСостав.Сделка КАК Сделка,
	               |	ВыпискаСостав.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВыпискаСостав.Ссылка.Проект
	               |ИЗ
	               |	Документ.Выписка.Состав КАК ВыпискаСостав
	               |ГДЕ
	               |	ВыпискаСостав.Ссылка = &Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект);
	
	Загрузить(Запрос.Выполнить().Выгрузить());
	
	ДокументОбъект = Неопределено;
	Возврат Результат;
КонецФункции

Функция ФактВыплата() Экспорт
	
	Результат = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыплатаЗарплатыСотрудники.Ссылка.ВалютаДокумента КАК Валюта,
	               |	ВыплатаЗарплатыСотрудники.Ссылка.КассаКомпании КАК СтруктурнаяЕдиница,
	               |	ВыплатаЗарплатыСотрудники.Ссылка.СтатьяДДС КАК СтатьяДДС,
	               |	ВыплатаЗарплатыСотрудники.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВыплатаЗарплатыСотрудники.Ссылка.ДокументОснование КАК Сделка,
	               |	ВыплатаЗарплатыСотрудники.Ссылка.Дата КАК Период,
	               |	ВыплатаЗарплатыСотрудники.ДокументПланирования КАК ДокументПланирования,
	               |	ВыплатаЗарплатыСотрудники.Сумма КАК РасходФакт
	               |ИЗ
	               |	Документ.ВыплатаЗарплаты.Сотрудники КАК ВыплатаЗарплатыСотрудники
	               |ГДЕ
	               |	ВыплатаЗарплатыСотрудники.Ссылка = &Ссылка
	               |";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект);	
	
	Загрузить(Запрос.Выполнить().Выгрузить());
	
	ДокументОбъект = Неопределено;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли